*  PROGRAM TO COUNT THE OCCURRENCES OF KEYWORDS 
*  IN A PIECE OF TEXT. THE SEARCH FOR KEYWORDS 
*  IS EFFICIENTLY PERFORMED BY HASHING IN A TABLE
*
*  Reads keywords to index from file "keywords", and
*  main text from file "keytext".
*
*  Demonstration program by Robert B. K. Dewar
* 
	&ANCHOR = &TRIM = 1 
	INPUT(.KEYS,1,'keywords')		:F(NOFILE1) 
	INPUT(.INPUT,2,'keytext')		:F(NOFILE2) 

	PUNC = " .,;:'!" 
	WORDPAT	 = BREAK(PUNC) $ WORD SPAN(PUNC) 
	KEYTABLE = TABLE(1001) 
* 
*  LOOP TO READ AND HASH KEYS 
* 
KEYLOOP	KEYTABLE<KEYS> = 0			:S(KEYLOOP) 
* 
*  A SPACE IS ADDED TO LINES OF TEXT TO ENSURE MATCH SUCCESS 
* 
READLOOP  INP = INPUT ' '			:F(CONVERT) 
* 
*  INDIVIDUAL WORDS ARE EXTRACTED BY PATTERN MATCHING 
* 
WORDLOOP  INP ? WORDPAT =
*
*  ONLY INCREMENT IF IT IS A DESIRED KEYWORD
*
	KEYTABLE<WORD> = DIFFER(ENTRY = KEYTABLE<WORD>) ENTRY + 1 
	DIFFER(INP)				:S(WORDLOOP)F(READLOOP) 
* 
*  FAILURE POINT 
* 
NOFILE1	TERMINAL = 'Missing input file "Keywords"' :(END) 
NOFILE2	TERMINAL = 'Missing input file "Keytext"'	 :(END) 
CONVFL	TERMINAL = 'NO KEYWORDS FOUND'		 :(END) 
* 
*  EXTRACT THE ENTRIES FROM THE TABLE INTO AN ARRAY 
* 
CONVERT   A        = CONVERT(KEYTABLE,'ARRAY')	 :F(CONVFL) 
	OUTPUT  = '     KEYWORD    NUMBER OF OCCURRENCES' 
	OUTPUT  = '     -------    ---------------------' 
	OUTPUT	= 
* 
*  PRINT THE ENTRIES 
* 
	I = 1 
PRINT  OUTPUT = LPAD(A[I,1],13) DUPL(' . ',5) A[I,2] :F(END) 
	I = I + 1				  :(PRINT) 
END 
