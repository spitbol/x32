* Simple KWIC program.
*
* The program reads a keyword file (one word per line) to build a
* table of words to index.  The files is named KEYWORDS.
*
* The input file is then read, and each occurrence of the one of the
* keywords is displayed, with NCHARS characters displayed on each side.
*
* Sample usage:
*	SPITBOL KWIC2 <KEYTEXT >RESULTS
*
	&ANCHOR = 1

* Number of characters to display on either side of the matched word:
*
	NCHARS = 30

* Read keyword file and save words in a table in lower case form.
*
	INPUT(.KEYFILE,1,'KEYWORDS')			:F(END)
	T = TABLE(101)
READKEY	WORD = KEYFILE					:F(ENDKEY)
	T<REPLACE(WORD,&UCASE,&LCASE)> = 1		:(READKEY)
ENDKEY	ENDFILE(1)

* Establish the definition of characters that make up a word.
*
	LETTERS = &UCASE &LCASE "-'"

* Pattern to obtain the NCHARS preceeding and following a matched word.
*
	DPAT = TAB(*KSTART) LEN(NCHARS) . PREVIOUS TAB(*WEND)
+	 (LEN(NCHARS) | REM) . POST

* Pattern to obtain the next word.  Note that the deferred variable
* WEND is used to index through the subject.
*
	WPAT = TAB(*WEND) BREAK(LETTERS) @WSTART SPAN(LETTERS) . WORD @WEND

* Initialize variables.
*
	LINE = DUPL(" ",NCHARS)
   	WEND = NCHARS

* Append next input line to LINE.
*
MORE	LINE = LINE " " INPUT				:F(END)

* Find next word in LINE.
*
GTWORD	LINE ? WPAT					:F(MORE)

* Is it a keyword?
*
	DIFFER(T<REPLACE(WORD,&UCASE,&LCASE)>)		:F(GTWORD)

* Get cursor position of previous NCHARS.
*
	KSTART = WSTART - NCHARS

* If we need more characters to display the following NCHARS, obtain them.
*
CKSIZE	LE(WEND + NCHARS, SIZE(LINE))			:S(DISPLAY)
	LINE = LINE " " INPUT				:S(CKSIZE)

* Peel off the previous and following NCHARS, and display them.
*
DISPLAY	LINE ? DPAT
	OUTPUT = PREVIOUS "  " WORD "  " POST

* Now remove characters no longer needed from the beginning of LINE.
*
	LINE = SUBSTR(LINE, WEND + 1 - NCHARS)
	WEND = NCHARS					:(GTWORD)

END
