* Simple KWIC program, version 2.
*
* The program reads a keyword file (one word per line) to build a
* table of words to index.  The files is named KEYWORDS.
*
* The input file is then read, and each occurrence of the one of the
* keywords is captured to a table, with NCHARS characters on each side.
* The table is then sorted, and the keywords displayed in order.
*
* Input is read from "Standard Input" and results are
* written to "Standard Output".
*
* A total word count and keyword count is also displayed.
*
* Sample usage:
*	SPITBOL KWIC2 <KEYTEXT >RESULTS
*
*
	&ANCHOR = 1

* Number of characters to display on either side of the matched word:
*
	NCHARS = 30

* Marker to use between sentences in the capture table.
*
	MARKER = CHAR(0)
	CAPTURE = TABLE(101)
	WORDCNTS = TABLE(101)
	CPAT = BREAK(MARKER) . DISPLAY MARKER

* Read keyword file and save words in a table in lower case form.
*
	INPUT(.KEYFILE,1,"keywords")			:F(END)
	T = TABLE(101)
READKEY	WORD = KEYFILE					:F(ENDKEY)
	T<REPLACE(WORD,&UCASE,&LCASE)> = 1		:(READKEY)
ENDKEY	ENDFILE(1)

* Establish the definition of characters that make up a word.
*
	LETTERS = &UCASE &LCASE "-'"

* Pattern to obtain the NCHARS preceeding and following a matched word.
*
	DPAT = TAB(*KSTART) LEN(NCHARS) . PREVIOUS TAB(*WEND)
+	 (LEN(NCHARS) | REM) . POST

* Pattern to obtain the next word.  Note that the deferred variable
* WEND is used to index through the subject.
*
	WPAT = TAB(*WEND) BREAK(LETTERS) @WSTART SPAN(LETTERS) . WORD @WEND

* Initialize variables.
*
	LINE = DUPL(" ",NCHARS)
   	WEND = NCHARS
	KCOUNT = WCOUNT = 0

* Append next input line to LINE.
*
MORE	LINE = LINE " " INPUT				:F(DISPLAY)

* Find next word in LINE.
*
GTWORD	LINE ? WPAT					:F(MORE)

* Count it.  Is it a keyword?
*
	WCOUNT = WCOUNT + 1
	DIFFER(T<REPLACE(WORD,&UCASE,&LCASE)>)		:F(GTWORD)

* Get cursor position of previous NCHARS.
*
	KSTART = WSTART - NCHARS

* If we need more characters to display the following NCHARS, obtain them.
*
CKSIZE	LE(WEND + NCHARS, SIZE(LINE))			:S(CAPTURE)
	LINE = LINE " " INPUT				:S(CKSIZE)

* Peel off the previous and following NCHARS, and record them.
*
CAPTURE	LINE ? DPAT
	CAPTURE<WORD> = CAPTURE<WORD> PREVIOUS "  " WORD "  " POST MARKER

* Count occurrence of keyword and accumulate total.
*
	WORDCNTS<WORD> = WORDCNTS<WORD> + 1
	KCOUNT = KCOUNT + 1

* Now remove characters no longer needed from the beginning of LINE.
*
	LINE = SUBSTR(LINE, WEND + 1 - NCHARS)
	WEND = NCHARS					:(GTWORD)

* Now convert the table to a sorted array, and print it out.
*
DISPLAY	OUTPUT = WCOUNT " total words read"
	OUTPUT = KCOUNT " occurrences of keywords found"
	CAPTURE = SORT(CAPTURE)
	I = 0
D1	S = CAPTURE<I = I + 1, 2>			:F(END)
	OUTPUT = ""
	WORD = CAPTURE<I,1>
	COUNT = WORDCNTS<WORD>
	OUTPUT = WORD ": " COUNT " occurrence"
+	 (GT(COUNT,1) "s","") " --"
D2	S ? CPAT = ""					:F(D1)
	OUTPUT = DISPLAY				:(D2)
END
