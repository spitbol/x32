       TTL  L I C E N S E -- SOFTWARE LICENSE FOR THIS PROGRAM
*
*     Copyright 1983-2012 Robert B. K. Dewar
*     Copyright 2012-2013 David Shields
* 
*     This file is part of Macro SPITBOL.
* 
*     Macro SPITBOL is free software: you can redistribute it and/or modify
*     it under the terms of the GNU General Public License as published by
*     the Free Software Foundation, either version 2 of the License, or
*     (at your option) any later version.
* 
*     Macro SPITBOL is distributed in the hope that it will be useful,
*     but WITHOUT ANY WARRANTY; without even the implied warranty of
*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
*     GNU General Public License for more details.
* 
*     You should have received a copy of the GNU General Public License
*     along with Macro SPITBOL.  If not, see <http://www.gnu.org/licenses/>.
*
       TTL  S P I T B O L -- NOTES TO IMPLEMENTORS
*
*      M A C R O   S P I T B O L     V E R S I O N   13.01
*      ---------------------------------------------------
*
*      DATE OF RELEASE  -  JANUARY 2013
*
*      MACRO SPITBOL IS MAINTAINED BY
*           DR. DAVID SHIELDS
*           260 GARTH RD APT 3H4
*           SCARSDALE, NY 10583
*      E-MAIL - THEDAVESHIELDS AT GMAIL DOT COM
*
*      VERSION 3.7 WAS MAINTAINED BY
*           MARK EMMER
*           CATSPAW, INC.
*           P.O. BOX 1123
*           SALIDA, COLORADO 81021
*           U.S.A
*      E-MAIL - MARKE AT SNOBOL4 DOT COM
*
*      VERSIONS 2.6 THROUGH 3.4 WERE MAINTAINED BY
*           DR. A. P. MCCANN (DECEASED)
*           DEPARTMENT OF COMPUTER STUDIES
*           UNIVERSITY OF LEEDS
*           LEEDS LS2 9JT
*           ENGLAND.
*
*      FROM 1979 THROUGH EARLY 1983 A NUMBER OF FIXES AND
*      ENHANCEMENTS WERE MADE BY STEVE DUFF AND ROBERT GOLDBERG.
*
*
       TTL  S P I T B O L - REVISION HISTORY
       EJC
*      R E V I S I O N   H I S T O R Y
*      -------------------------------
*
*      VERSION 13.01 (JANUARY 2013, DAVID SHIELDS)
*
*      THIS VERSION HAS THE SAME FUNCTIONALITY AS THE PREVIOUS RELEASE, BUT WITH
*      MANY INTERNAL CODE CHANGES.
*      SUPPORT FOR X86-64 HAS BEEN ADDED, BUT IS NOT CURRENTLY WORKING.
*      THE DESCRIPTION OF THE MINIMAL LANGUAGE FORMERLY FOUND HERE AS COMMENTS
*      IS NOW TO BE FOUND IN THE FILE MINIMAL-REFERENCE-MANUAL.HTML 
*
*      VERSION 3.8 (JUNE 2012, DAVID SHIELDS)
*      --------------------------------------
*
*	       THIS VERSION IS VERY CLOSE TO V3.7, WITH THE
*              SAME FUNCTIONALITY.
*
*              THE SOURCE IS NOW MAINTAINED USING GIT, SO GOING FORWARD
*              THE DETAILED REVISION HISTORY WILL BE RECORDED IN THE GIT
*              COMMIT LOGS, NOT IN THIS FILE.
*
       TTL  S P I T B O L  -- BASIC INFORMATION
       EJC
*
*      GENERAL STRUCTURE
*      -----------------
*
*      THIS PROGRAM IS A TRANSLATOR FOR A VERSION OF THE SNOBOL4
*      PROGRAMMING LANGUAGE. LANGUAGE DETAILS ARE CONTAINED IN
*      THE MANUAL MACRO SPITBOL BY DEWAR AND MCCANN, TECHNICAL
*      REPORT 90, UNIVERSITY OF LEEDS 1976.
*      THE IMPLEMENTATION IS DISCUSSED IN DEWAR AND MCCANN,
*      MACRO SPITBOL - A SNOBOL4 COMPILER, SOFTWARE PRACTICE AND
*      EXPERIENCE, 7, 95-113, 1977.
*      THE LANGUAGE IS AS IMPLEMENTED BY THE BTL TRANSLATOR
*      (GRISWOLD, POAGE AND POLONSKY, PRENTICE HALL, 1971)
*      WITH THE FOLLOWING PRINCIPAL EXCEPTIONS.
*
*      1)   REDEFINITION OF STANDARD SYSTEM FUNCTIONS AND
*           OPERATORS IS NOT PERMITTED.
*
*      2)   THE VALUE FUNCTION IS NOT PROVIDED.
*
*      3)   ACCESS TRACING IS PROVIDED IN ADDITION TO THE
*           OTHER STANDARD TRACE MODES.
*
*      4)   THE KEYWORD STFCOUNT IS NOT PROVIDED.
*
*      5)   THE KEYWORD FULLSCAN IS NOT PROVIDED AND ALL PATTERN
*           MATCHING TAKES PLACE IN FULLSCAN MODE (I.E. WITH NO
*           HEURISTICS APPLIED).
*
*      6)   A SERIES OF EXPRESSIONS SEPARATED BY COMMAS MAY
*           BE GROUPED WITHIN PARENTHESES TO PROVIDE A SELECTION
*           CAPABILITY. THE SEMANTICS ARE THAT THE SELECTION
*           ASSUMES THE VALUE OF THE FIRST EXPRESSION WITHIN IT
*           WHICH SUCCEEDS AS THEY ARE EVALUATED FROM THE LEFT.
*           IF NO EXPRESSION SUCCEEDS THE ENTIRE STATEMENT FAILS
*
*      7)   AN EXPLICIT PATTERN MATCHING OPERATOR IS PROVIDED.
*           THIS IS THE BINARY QUERY (SEE GIMPEL SIGPLAN OCT 74)
*
*      8)   THE ASSIGNMENT OPERATOR IS INTRODUCED AS IN THE
*           GIMPEL REFERENCE.
*
*      9)   THE EXIT FUNCTION IS PROVIDED FOR GENERATING LOAD
*           MODULES - CF. GIMPELS SITBOL.
*
*
*      THE METHOD USED IN THIS PROGRAM IS TO TRANSLATE THE
*      SOURCE CODE INTO AN INTERNAL PSEUDO-CODE (SEE FOLLOWING
*      SECTION). AN INTERPRETOR IS THEN USED TO EXECUTE THIS
*      GENERATED PSEUDO-CODE. THE NATURE OF THE SNOBOL4 LANGUAGE
*      IS SUCH THAT THE LATTER TASK IS MUCH MORE COMPLEX THAN
*      THE ACTUAL TRANSLATION PHASE. ACCORDINGLY, NEARLY ALL THE
*      CODE IN THE PROGRAM SECTION IS CONCERNED WITH THE ACTUAL
*      EXECUTION OF THE SNOBOL4 PROGRAM.
       EJC
*
*      INTERPRETIVE CODE FORMAT
*      ------------------------
*
*      THE INTERPRETIVE PSEUDO-CODE CONSISTS OF A SERIES OF
*      ADDRESS POINTERS. THE EXACT FORMAT OF THE CODE IS
*      DESCRIBED IN CONNECTION WITH THE CDBLK FORMAT. THE
*      PURPOSE OF THIS SECTION IS TO GIVE GENERAL INSIGHT INTO
*      THE INTERPRETIVE APPROACH INVOLVED.
*
*      THE BASIC FORM OF THE CODE IS RELATED TO REVERSE POLISH.
*      IN OTHER WORDS, THE OPERANDS PRECEDE THE OPERATORS WHICH
*      ARE ZERO ADDRESS OPERATORS. THERE ARE SOME EXCEPTIONS TO
*      THESE RULES, NOTABLY THE UNARY NOT OPERATOR AND THE
*      SELECTION CONSTRUCTION WHICH CLEARLY REQUIRE ADVANCE
*      KNOWLEDGE OF THE OPERATOR INVOLVED.
*
*      THE OPERANDS ARE MOVED TO THE TOP OF THE MAIN STACK AND
*      THE OPERATORS ARE APPLIED TO THE TOP STACK ENTRIES. LIKE
*      OTHER VERSIONS OF SPITBOL, THIS PROCESSOR DEPENDS ON
*      KNOWING WHETHER OPERANDS ARE REQUIRED BY NAME OR BY VALUE
*      AND MOVES THE APPROPRIATE OBJECT TO THE STACK. THUS NO
*      NAME/VALUE CHECKS ARE INCLUDED IN THE OPERATOR CIRCUITS.
*
*      THE ACTUAL POINTERS IN THE CODE POINT TO A BLOCK WHOSE
*      FIRST WORD IS THE ADDRESS OF THE INTERPRETOR ROUTINE
*      TO BE EXECUTED FOR THE CODE WORD.
*
*      IN THE CASE OF OPERATORS, THE POINTER IS TO A WORD WHICH
*      CONTAINS THE ADDRESS OF THE OPERATOR TO BE EXECUTED. IN
*      THE CASE OF OPERANDS SUCH AS CONSTANTS, THE POINTER IS TO
*      THE OPERAND ITSELF. ACCORDINGLY, ALL OPERANDS CONTAIN
*      A FIELD WHICH POINTS TO THE ROUTINE TO LOAD THE VALUE OF
*      THE OPERAND ONTO THE STACK. IN THE CASE OF A VARIABLE,
*      THERE ARE THREE SUCH POINTERS. ONE TO LOAD THE VALUE,
*      ONE TO STORE THE VALUE AND A THIRD TO JUMP TO THE LABEL.
*
*      THE HANDLING OF FAILURE RETURNS DESERVES SPECIAL COMMENT.
*      THE LOCATION FLPTR CONTAINS THE POINTER TO THE LOCATION
*      ON THE MAIN STACK WHICH CONTAINS THE FAILURE RETURN
*      WHICH IS IN THE FORM OF A BYTE OFFSET IN THE CURRENT
*      CODE BLOCK (CDBLK OR EXBLK). WHEN A FAILURE OCCURS, THE
*      STACK IS POPPED AS INDICATED BY THE SETTING OF FLPTR AND
*      CONTROL IS PASSED TO THE APPROPRIATE LOCATION IN THE
*      CURRENT CODE BLOCK WITH THE STACK POINTER POINTING TO THE
*      FAILURE OFFSET ON THE STACK AND FLPTR UNCHANGED.
       EJC
*
*      INTERNAL DATA REPRESENTATIONS
*      -----------------------------
*
*      REPRESENTATION OF VALUES
*
*      A VALUE IS REPRESENTED BY A POINTER TO A BLOCK WHICH
*      DESCRIBES THE TYPE AND PARTICULARS OF THE DATA VALUE.
*      IN GENERAL, A VARIABLE IS A LOCATION CONTAINING SUCH A
*      POINTER (ALTHOUGH IN THE CASE OF TRACE ASSOCIATIONS THIS
*      IS MODIFIED, SEE DESCRIPTION OF TRBLK).
*
*      THE FOLLOWING IS A LIST OF POSSIBLE DATATYPES SHOWING THE
*      TYPE OF BLOCK USED TO HOLD THE VALUE. THE DETAILS OF
*      EACH BLOCK FORMAT ARE GIVEN LATER.
*
*      DATATYPE              BLOCK TYPE
*      --------              ----------
*
*      ARRAY                 ARBLK OR VCBLK
*
*      CODE                  CDBLK
*
*      EXPRESSION            EXBLK OR SEBLK
*
*      INTEGER               ICBLK
*
*      NAME                  NMBLK
*
*      PATTERN               P0BLK OR P1BLK OR P2BLK
*
*      REAL                  RCBLK
*
*      STRING                SCBLK
*
*      TABLE                 TBBLK
*
*      PROGRAM DATATYPE      PDBLK
       EJC
*
*      REPRESENTATION OF VARIABLES
*      ---------------------------
*
*      DURING THE COURSE OF EVALUATING EXPRESSIONS, IT IS
*      NECESSARY TO GENERATE NAMES OF VARIABLES (FOR EXAMPLE
*      ON THE LEFT SIDE OF A BINARY EQUALS OPERATOR). THESE ARE
*      NOT TO BE CONFUSED WITH OBJECTS OF DATATYPE NAME WHICH
*      ARE IN FACT VALUES.
*
*      FROM A LOGICAL POINT OF VIEW, SUCH NAMES COULD BE SIMPLY
*      REPRESENTED BY A POINTER TO THE APPROPRIATE VALUE CELL.
*      HOWEVER IN THE CASE OF ARRAYS AND PROGRAM DEFINED
*      DATATYPES, THIS WOULD VIOLATE THE RULE THAT THERE MUST BE
*      NO POINTERS INTO THE MIDDLE OF A BLOCK IN DYNAMIC STORE.
*      ACCORDINGLY, A NAME IS ALWAYS REPRESENTED BY A BASE AND
*      OFFSET. THE BASE POINTS TO THE START OF THE BLOCK
*      CONTAINING THE VARIABLE VALUE AND THE OFFSET IS THE
*      OFFSET WITHIN THIS BLOCK IN BYTES. THUS THE ADDRESS
*      OF THE ACTUAL VARIABLE IS DETERMINED BY ADDING THE BASE
*      AND OFFSET VALUES.
*
*      THE FOLLOWING ARE THE INSTANCES OF VARIABLES REPRESENTED
*      IN THIS MANNER.
*
*      1)   NATURAL VARIABLE BASE IS PTR TO VRBLK
*                            OFFSET IS *VRVAL
*
*      2)   TABLE ELEMENT    BASE IS PTR TO TEBLK
*                            OFFSET IS *TEVAL
*
*      3)   ARRAY ELEMENT    BASE IS PTR TO ARBLK
*                            OFFSET IS OFFSET TO ELEMENT
*
*      4)   VECTOR ELEMENT   BASE IS PTR TO VCBLK
*                            OFFSET IS OFFSET TO ELEMENT
*
*      5)   PROG DEF DTP     BASE IS PTR TO PDBLK
*                            OFFSET IS OFFSET TO FIELD VALUE
*
*      IN ADDITION THERE ARE TWO CASES OF OBJECTS WHICH ARE
*      LIKE VARIABLES BUT CANNOT BE HANDLED IN THIS MANNER.
*      THESE ARE CALLED PSEUDO-VARIABLES AND ARE REPRESENTED
*      WITH A SPECIAL BASE POINTER AS FOLLOWS=
*
*      EXPRESSION VARIABLE   PTR TO EVBLK (SEE EVBLK)
*
*      KEYWORD VARIABLE      PTR TO KVBLK (SEE KVBLK)
*
*      PSEUDO-VARIABLES ARE HANDLED AS SPECIAL CASES BY THE
*      ACCESS PROCEDURE (ACESS) AND THE ASSIGNMENT PROCEDURE
*      (ASIGN). SEE THESE TWO PROCEDURES FOR DETAILS.
       EJC
*
*      ORGANIZATION OF DATA AREA
*      -------------------------
*
*      THE DATA AREA IS DIVIDED INTO TWO REGIONS.
*
*      STATIC AREA
*
*      THE STATIC AREA BUILDS UP FROM THE BOTTOM AND CONTAINS
*      DATA AREAS WHICH ARE ALLOCATED DYNAMICALLY BUT ARE NEVER
*      DELETED OR MOVED AROUND. THE MACRO-PROGRAM ITSELF
*      USES THE STATIC AREA FOR THE FOLLOWING.
*
*      1)   ALL VARIABLE BLOCKS (VRBLK).
*
*      2)   THE HASH TABLE FOR VARIABLE BLOCKS.
*
*      3)   MISCELLANEOUS BUFFERS AND WORK AREAS (SEE PROGRAM
*           INITIALIZATION SECTION).
*
*      IN ADDITION, THE SYSTEM PROCEDURES MAY USE THIS AREA FOR
*      INPUT/OUTPUT BUFFERS, EXTERNAL FUNCTIONS ETC. SPACE IN
*      THE STATIC REGION IS ALLOCATED BY CALLING PROCEDURE ALOST
*
*      THE FOLLOWING GLOBAL VARIABLES DEFINE THE CURRENT
*      LOCATION AND SIZE OF THE STATIC AREA.
*
*      STATB                 ADDRESS OF START OF STATIC AREA
*      STATE                 ADDRESS+1 OF LAST WORD IN AREA.
*
*      THE MINIMUM SIZE OF STATIC IS GIVEN APPROXIMATELY BY
*           12 + *E_HNB + *E_STS + SPACE FOR ALPHABET STRING
*           AND STANDARD PRINT BUFFER.
       EJC
*      DYNAMIC AREA
*
*      THE DYNAMIC AREA IS BUILT UPWARDS IN MEMORY AFTER THE
*      STATIC REGION. DATA IN THIS AREA MUST ALL BE IN STANDARD
*      BLOCK FORMATS SO THAT IT CAN BE PROCESSED BY THE GARBAGE
*      COLLECTOR (PROCEDURE GBCOL). GBCOL COMPACTS BLOCKS DOWN
*      IN THIS REGION AS REQUIRED BY SPACE EXHAUSTION AND CAN
*      ALSO MOVE ALL BLOCKS UP TO ALLOW FOR EXPANSION OF THE
*      STATIC REGION.
*      WITH THE EXCEPTION OF TABLES AND ARRAYS, NO SPITBOL
*      OBJECT ONCE BUILT IN DYNAMIC MEMORY IS EVER SUBSEQUENTLY
*      MODIFIED. OBSERVING THIS RULE NECESSITATES A COPYING
*      ACTION DURING STRING AND PATTERN CONCATENATION.
*
*      GARBAGE COLLECTION IS FUNDAMENTAL TO THE ALLOCATION OF
*      SPACE FOR VALUES. SPITBOL USES A VERY EFFICIENT GARBAGE
*      COLLECTOR WHICH INSISTS THAT POINTERS INTO DYNAMIC STORE
*      SHOULD BE IDENTIFIABLE WITHOUT USE OF BIT TABLES,
*      MARKER BITS ETC. TO SATISFY THIS REQUIREMENT, DYNAMIC
*      MEMORY MUST NOT START AT TOO LOW AN ADDRESS AND LENGTHS
*      OF ARRAYS, TABLES, STRINGS, CODE AND EXPRESSION BLOCKS
*      MAY NOT EXCEED THE NUMERICAL VALUE OF THE LOWEST DYNAMIC
*      ADDRESS.
*
*      TO AVOID EITHER PENALIZING USERS WITH MODEST
*      REQUIREMENTS OR RESTRICTING THOSE WITH GREATER NEEDS ON
*      HOST SYSTEMS WHERE DYNAMIC MEMORY IS ALLOCATED IN LOW
*      ADDRESSES, THE MINIMUM DYNAMIC ADDRESS MAY BE SPECIFIED
*      SUFFICIENTLY HIGH TO PERMIT ARBITRARILY LARGE SPITBOL
*      OBJECTS TO BE CREATED (WITH THE POSSIBILITY IN EXTREME
*      CASES OF WASTING LARGE AMOUNTS OF MEMORY BELOW THE
*      START ADDRESS). THIS MINIMUM VALUE IS MADE AVAILABLE
*      IN VARIABLE MXLEN BY A SYSTEM ROUTINE, SYSMX.
*      ALTERNATIVELY SYSMX MAY INDICATE THAT A
*      DEFAULT MAY BE USED IN WHICH DYNAMIC IS PLACED
*      AT THE LOWEST POSSIBLE ADDRESS FOLLOWING STATIC.
*
*      THE FOLLOWING GLOBAL WORK CELLS DEFINE THE LOCATION AND
*      LENGTH OF THE DYNAMIC AREA.
*
*      DNAMB                 START OF DYNAMIC AREA
*      DNAMP                 NEXT AVAILABLE LOCATION
*      DNAME                 LAST AVAILABLE LOCATION + 1
*
*      DNAMB IS ALWAYS HIGHER THAN STATE SINCE THE ALOST
*      PROCEDURE MAINTAINS SOME EXPANSION SPACE ABOVE STATE.
*      *** DNAMB MUST NEVER BE PERMITTED TO HAVE A VALUE LESS
*      THAN THAT IN MXLEN ***
*
*      SPACE IN THE DYNAMIC REGION IS ALLOCATED BY THE ALLOC
*      PROCEDURE. THE DYNAMIC REGION MAY BE USED BY SYSTEM
*      PROCEDURES PROVIDED THAT ALL THE RULES ARE OBEYED.
*      SOME OF THE RULES ARE SUBTLE SO IT IS PREFERABLE FOR
*      OSINT TO MANAGE ITS OWN MEMORY NEEDS. SPITBOL PROCS
*      OBEY RULES TO ENSURE THAT NO ACTION CAN CAUSE A GARBAGE
*      COLLECTION EXCEPT AT SUCH TIMES AS CONTENTS OF XL, XR
*      AND THE STACK ARE +CLEAN+ (SEE COMMENT BEFORE UTILITY
*      PROCEDURES AND IN GBCOL FOR MORE DETAIL). NOTE
*      THAT CALLS OF ALOST MAY CAUSE GARBAGE COLLECTION (SHIFT
*      OF MEMORY TO FREE SPACE). SPITBOL PROCS WHICH CALL
*      SYSTEM ROUTINES ASSUME THAT THEY CANNOT PRECIPITATE
*      COLLECTION AND THIS MUST BE RESPECTED.
       EJC
*
*      REGISTER USAGE
*      --------------
*
*      (CP)                  CODE POINTER REGISTER. USED TO
*                            HOLD A POINTER TO THE CURRENT
*                            LOCATION IN THE INTERPRETIVE PSEUDO
*                            CODE (I.E. PTR INTO A CDBLK).
*
*      (XL,XR)               GENERAL INDEX REGISTERS. USUALLY
*                            USED TO HOLD POINTERS TO BLOCKS IN
*                            DYNAMIC STORAGE. AN IMPORTANT
*                            RESTRICTION IS THAT THE VALUE IN
*                            XL MUST BE COLLECTABLE FOR
*                            A GARBAGE COLLECT CALL. A VALUE
*                            IS COLLECTABLE IF IT EITHER POINTS
*                            OUTSIDE THE DYNAMIC AREA, OR IF IT
*                            POINTS TO THE START OF A BLOCK IN
*                            THE DYNAMIC AREA.
*
*      (XS)                  STACK POINTER. USED TO POINT TO
*                            THE STACK FRONT. THE STACK MAY
*                            BUILD UP OR DOWN AND IS USED
*                            TO STACK SUBROUTINE RETURN POINTS
*                            AND OTHER RECURSIVELY SAVED DATA.
*
*      (XT)                  AN ALTERNATIVE NAME FOR XL DURING
*                            ITS USE IN ACCESSING STACKED ITEMS.
*
*      (WA,WB,WC)            GENERAL WORK REGISTERS. CANNOT BE
*                            USED FOR INDEXING, BUT MAY HOLD
*                            VARIOUS TYPES OF DATA.
*
*      (IA)                  USED FOR ALL SIGNED INTEGER
*                            ARITHMETIC, BOTH THAT USED BY THE
*                            TRANSLATOR AND THAT ARISING FROM
*                            USE OF SNOBOL4 ARITHMETIC OPERATORS
*
*      (RA)                  REAL ACCUMULATOR. USED FOR ALL
*                            FLOATING POINT ARITHMETIC.
       EJC
*
*      SPITBOL CONDITIONAL ASSEMBLY SYMBOLS
*      ------------------------------------
*
*      IN THE SPITBOL TRANSLATOR, THE FOLLOWING CONDITIONAL
*      ASSEMBLY SYMBOLS ARE REFERRED TO. TO INCORPORATE THE
*      FEATURES REFERRED TO, THE MINIMAL SOURCE SHOULD BE
*      PREFACED BY SUITABLE CONDITIONAL ASSEMBLY SYMBOL
*      DEFINITIONS.
*      IN ALL CASES IT IS PERMISSIBLE TO DEFAULT THE DEFINITIONS
*      IN WHICH CASE THE ADDITIONAL FEATURES WILL BE OMITTED
*      FROM THE TARGET CODE.
*
*      .CAEX                 DEFINE TO ALLOW UP ARROW FOR EXPON.
*      .CAHT                 DEFINE TO INCLUDE HORIZONTAL TAB
*      .CASL                 DEFINE TO INCLUDE 26 SHIFTED LETTRS
*      .CAVT                 DEFINE TO INCLUDE VERTICAL TAB
*      .CBYT                 DEFINE FOR STATISTICS IN BYTES
*      .CCMC                 DEFINE TO INCLUDE SYSCM FUNCTION
*      .CCMK                 DEFINE TO INCLUDE COMPARE KEYWORD
*      .CEPP                 DEFINE IF ENTRYS HAVE ODD PARITY
*      .CERA                 DEFINE TO INCLUDE SYSEA FUNCTION
*      .CEXP                 DEFINE IF SPITBOL POPS SYSEX ARGS
*      .CGBC                 DEFINE TO INCLUDE SYSGC FUNCTION
*      .CICC                 DEFINE TO IGNORE BAD CONTROL CARDS
*      .CINC                 DEFINE TO ADD -INCLUDE CONTROL CARD
*      .CIOD                 DEFINE TO NOT USE DEFAULT DELIMITER
*                              IN PROCESSING 3RD ARG OF INPUT()
*                              AND OUTPUT()
*      .CMTH                 DEFINE TO INCLUDE MATH FUNCTIONS
*      .CNBF                 DEFINE TO OMIT BUFFER EXTENSION
*      .CNBT                 DEFINE TO OMIT BATCH INITIALISATION
*      .CNCI                 DEFINE TO ENABLE SYSCI ROUTINE
*      .CNCR                 DEFINE TO ENABLE SYSCR ROUTINE
*      .CNEX                 DEFINE TO OMIT EXIT() CODE.
*      .CNLD                 DEFINE TO OMIT LOAD() CODE.
*      .CNLF                 DEFINE TO ADD FILE TYPE FOR LOAD()
*      .CNPF                 DEFINE TO OMIT PROFILE STUFF
*      .CNRA                 DEFINE TO OMIT ALL REAL ARITHMETIC
*      .CNSC                 DEFINE TO NO NUMERIC-STRING COMPARE
*      .CNSR                 DEFINE TO OMIT SORT, RSORT
*      .CPOL                 DEFINE IF INTERFACE POLLING DESIRED
*      .CREL                 DEFINE TO INCLUDE RELOC ROUTINES
*      .CRPP                 DEFINE IF RETURNS HAVE ODD PARITY
*      .CS16                 DEFINE TO INITIALIZE STLIM TO 32767
*      .CS32                 DEFINE TO INIT STLIM TO 2147483647
*                            OMIT TO TAKE DEFAULT OF 50000
*      .CSAX                 DEFINE IF SYSAX IS TO BE CALLED
*      .CSED                 DEFINE TO USE SEDIMENT IN GBCOL
*      .CSFN                 DEFINE TO TRACK SOURCE FILE NAMES
*      .CSLN                 DEFINE IF LINE NUMBER IN CODE BLOCK
*      .CSN5                 DEFINE TO PAD STMT NOS TO 5 CHARS
*      .CSN6                 DEFINE TO PAD STMT NOS TO 6 CHARS
*      .CSN8                 DEFINE TO PAD STMT NOS TO 8 CHARS
*      .CSOU                 DEFINE IF OUTPUT, TERMINAL TO SYSOU
*      .CTET                 DEFINE TO TABLE ENTRY TRACE WANTED
*      .CTMD                 DEFINE IF SYSTM UNIT IS DECISECOND
*      .CUCF                 DEFINE TO INCLUDE CFP_U
*      .CUEJ                 DEFINE TO SUPPRESS NEEDLESS EJECTS
*      .CULK                 DEFINE TO INCLUDE &L/UCASE KEYWORDS
*      .CULC                 DEFINE TO INCLUDE &CASE (LC NAMES)
*                            IF CUCL DEFINED, MUST SUPPORT
*                            MINIMAL OP FLC WREG THAT FOLDS
*                            ARGUMENT TO UPPER CASE
*      .CUST                 DEFINE TO INCLUDE SET() CODE
*
*                            CONDITIONAL OPTIONS
*                            SINCE .UNDEF NOT ALLOWED IF SYMBOL
*                            NOT DEFINED, A FULL COMMENT LINE
*                            INDICATES SYMBOL INITIALLY NOT
*                            DEFINED.
*
.DEF   .CAEX                 DEFINE TO ALLOW UP ARROW FOR EXPON.
.DEF   .CAHT                 DEFINE TO INCLUDE HORIZONTAL TAB
.DEF   .CASL                 DEFINE TO INCLUDE 26 SHIFTED LETTRS
.DEF   .CAVT                 DEFINE TO INCLUDE VERTICAL TAB
*      .CBYT                 DEFINE FOR STATISTICS IN BYTES
*      .CCMC                 DEFINE TO INCLUDE SYSCM FUNCTION
*      .CCMK                 DEFINE TO INCLUDE COMPARE KEYWORD
*      .CEPP                 DEFINE IF ENTRYS HAVE ODD PARITY
*      .CERA                 DEFINE TO INCLUDE SYSEA FUNCTION
*      .CEXP                 DEFINE IF SPITBOL POPS SYSEX ARGS
.DEF   .CGBC                 DEFINE TO INCLUDE SYSGC FUNCTION
*      .CICC                 DEFINE TO IGNORE BAD CONTROL CARDS
*      .CINC                 DEFINE TO ADD -INCLUDE CONTROL CARD
.DEF   .CIOD                 DEFINE TO NOT USE DEFAULT DELIMITER
*                            IN PROCESSING 3RD ARG OF INPUT()
*                            AND OUTPUT()
*      .CMTH                 DEFINE TO INCLUDE MATH FUNCTIONS
.DEF   .CNBF                 DEFINE TO OMIT BUFFER EXTENSION
.DEF   .CNBT                 DEFINE TO OMIT BATCH INITIALISATION
*      .CNCI                 DEFINE TO ENABLE SYSCI ROUTINE
*      .CNCR                 DEFINE TO ENABLE SYSCR ROUTINE
*      .CNEX                 DEFINE TO OMIT EXIT() CODE.
.DEF   .CNLD                 DEFINE TO OMIT LOAD() CODE.
*      .CNLF                 DEFINE TO ADD FILE TYPE TO LOAD()
*      .CNPF                 DEFINE TO OMIT PROFILE STUFF
*      .CNRA                 DEFINE TO OMIT ALL REAL ARITHMETIC
*      .CNSC                 DEFINE IF NO NUMERIC-STRING COMPARE
*      .CNSR                 DEFINE TO OMIT SORT, RSORT
*      .CPOL                 DEFINE IF INTERFACE POLLING DESIRED
*      .CREL                 DEFINE TO INCLUDE RELOC ROUTINES
*      .CRPP                 DEFINE IF RETURNS HAVE ODD PARITY
*      .CS16                 DEFINE TO INITIALIZE STLIM TO 32767
*      .CS32                 DEFINE TO INIT STLIM TO 2147483647
.DEF   .CSAX                 DEFINE IF SYSAX IS TO BE CALLED
*      .CSED                 DEFINE TO USE SEDIMENT IN GBCOL
*      .CSFN                 DEFINE TO TRACK SOURCE FILE NAMES
*      .CSLN                 DEFINE IF LINE NUMBER IN CODE BLOCK
*      .CSN5                 DEFINE TO PAD STMT NOS TO 5 CHARS
*      .CSN6                 DEFINE TO PAD STMT NOS TO 6 CHARS
.DEF   .CSN8                 DEFINE TO PAD STMT NOS TO 8 CHARS
*      .CSOU                 DEFINE IF OUTPUT, TERMINAL TO SYSOU
.DEF   .CTET                 DEFINE TO TABLE ENTRY TRACE WANTED
*      .CTMD                 DEFINE IF SYSTM UNIT IS DECISECOND
.DEF   .CUCF                 DEFINE TO INCLUDE CFP_U
.DEF   .CUEJ                 DEFINE TO SUPPRESS NEEDLESS EJECTS
.DEF   .CULK                 DEFINE TO INCLUDE &L/UCASE KEYWORDS
.DEF   .CULC                 DEFINE TO INCLUDE &CASE (LC NAMES)
.DEF   .CUST                 DEFINE TO INCLUDE SET() CODE
*
*      FORCE DEFINITION OF .CCMK IF .CCMC IS DEFINED
*
.IF    .CCMC
.DEF   .CCMK
.FI
       TTL  S P I T B O L -- PROCEDURES SECTION
*
*      THIS SECTION STARTS WITH DESCRIPTIONS OF THE OPERATING
*      SYSTEM DEPENDENT PROCEDURES WHICH ARE USED BY THE SPITBOL
*      TRANSLATOR. ALL SUCH PROCEDURES HAVE FIVE LETTER NAMES
*      BEGINNING WITH SYS. THEY ARE LISTED IN ALPHABETICAL
*      ORDER.
*      ALL PROCEDURES HAVE A  SPECIFICATION CONSISTING OF A
*      MODEL CALL, PRECEDED BY A POSSIBLY EMPTY LIST OF REGISTER
*      CONTENTS GIVING PARAMETERS AVAILABLE TO THE PROCEDURE AND
*      FOLLOWED BY A POSSIBLY EMPTY LIST OF REGISTER CONTENTS
*      REQUIRED ON RETURN FROM THE CALL OR WHICH MAY HAVE HAD
*      THEIR CONTENTS DESTROYED. ONLY THOSE REGISTERS EXPLICITLY
*      MENTIONED IN THE LIST AFTER THE CALL MAY HAVE THEIR
*      VALUES CHANGED.
*      THE SEGMENT OF CODE PROVIDING THE EXTERNAL PROCEDURES IS
*      CONVENIENTLY REFERRED TO AS OSINT (OPERATING SYSTEM
*      INTERFACE). THE SYSXX PROCEDURES IT CONTAINS PROVIDE
*      FACILITIES NOT USUALLY AVAILABLE AS PRIMITIVES IN
*      ASSEMBLY LANGUAGES. FOR PARTICULAR TARGET MACHINES,
*      IMPLEMENTORS MAY CHOOSE FOR SOME MINIMAL OPCODES WHICH
*      DO NOT HAVE REASONABLY DIRECT TRANSLATIONS, TO USE CALLS
*      OF ADDITIONAL PROCEDURES WHICH THEY PROVIDE IN OSINT.
*      E.G. MWB OR TRC MIGHT BE TRANSLATED AS JSR SYSMB,
*      JSR SYSTC IN SOME IMPLEMENTATIONS.
*
*      IN THE DESCRIPTIONS, REFERENCE IS MADE TO --BLK
*      FORMATS (-- = A PAIR OF LETTERS). SEE THE SPITBOL
*      DEFINITIONS SECTION FOR DETAILED DESCRIPTIONS OF ALL
*      SUCH BLOCK FORMATS EXCEPT FCBLK FOR WHICH SYSFC SHOULD
*      BE CONSULTED.
*
*      SECTION 0 CONTAINS INP,INR SPECIFICATIONS OF INTERNAL
*      PROCEDURES,ROUTINES. THIS GIVES A SINGLE PASS TRANSLATOR
*      INFORMATION MAKING IT EASY TO GENERATE ALTERNATIVE CALLS
*      IN THE TRANSLATION OF JSR-S FOR PROCEDURES OF DIFFERENT
*      TYPES IF THIS PROVES NECESSARY.
*
       SEC                   START OF PROCEDURES SECTION
.IF    .CSAX
       EJC
*
*      SYSAX -- AFTER EXECUTION
*
SYSAX  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      IF THE CONDITIONAL ASSEMBLY SYMBOL .CSAX IS DEFINED,
*      THIS ROUTINE IS CALLED IMMEDIATELY AFTER EXECUTION AND
*      BEFORE PRINTING OF EXECUTION STATISTICS OR DUMP OUTPUT.
*      PURPOSE OF CALL IS FOR IMPLEMENTOR TO DETERMINE AND
*      IF THE CALL IS NOT REQUIRED IT WILL BE OMITTED IF .CSAX
*      IS UNDEFINED. IN THIS CASE SYSAX NEED NOT BE CODED.
*
*      JSR  SYSAX            CALL AFTER EXECUTION
.ELSE
.FI
       EJC
.IF    .CBSP
*
*      SYSBS -- BACKSPACE FILE
*
SYSBS  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSBS IS USED TO IMPLEMENT THE SNOBOL4 FUNCTION BACKSPACE
*      IF THE CONDITIONAL ASSEMBLY SYMBOL .CBSP IS DEFINED.
*      THE MEANING IS SYSTEM DEPENDENT.  IN GENERAL, BACKSPACE
*      REPOSITIONS THE FILE ONE RECORD CLOSER TO THE BEGINNING
*      OF FILE, SUCH THAT A SUBSEQUENT READ OR WRITE WILL
*      OPERATE ON THE PREVIOUS RECORD.
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      (XR)                  BACKSPACE ARGUMENT (SCBLK PTR)
*      JSR  SYSBS            CALL TO BACKSPACE
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF BACKSPACE NOT ALLOWED
*      PPM  LOC              RETURN HERE IF I/O ERROR
*      (WA,WB)               DESTROYED
*
*      THE SECOND ERROR RETURN IS USED FOR FILES FOR WHICH
*      BACKSPACE IS NOT PERMITTED. FOR EXAMPLE, IT MAY BE EXPECTED
*      FILES ON CHARACTER DEVICES ARE IN THIS CATEGORY.
       EJC
.FI
*
*      SYSBX -- BEFORE EXECUTION
*
SYSBX  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      CALLED AFTER INITIAL SPITBOL COMPILATION AND BEFORE
*      COMMENCING EXECUTION IN CASE OSINT NEEDS
*      TO ASSIGN FILES OR PERFORM OTHER NECESSARY SERVICES.
*      OSINT MAY ALSO CHOOSE TO SEND A MESSAGE TO ONLINE
*      TERMINAL (IF ANY) INDICATING THAT EXECUTION IS STARTING.
*
*      JSR  SYSBX            CALL BEFORE EXECUTION STARTS
       EJC
.IF    .CNCI
*
*      SYSCI -- CONVERT INTEGER
*
SYSCI  EXP
*
*      SYSCI IS AN OPTIONAL OSINT ROUTINE THAT CAUSES SPITBOL TO
*      CALL SYSCI TO CONVERT INTEGER VALUES TO STRINGS, RATHER
*      THAN USING THE INTERNAL SPITBOL CONVERSION CODE.  THIS
*      CODE MAY BE LESS EFFICIENT ON MACHINES WITH HARDWARE
*      CONVERSION INSTRUCTIONS AND IN SUCH CASES, IT MAY BE AN
*      ADVANTAGE TO INCLUDE SYSCI.  THE SYMBOL .CNCI MUST BE
*      DEFINED IF THIS ROUTINE IS TO BE USED.
*
*      THE RULES FOR CONVERTING INTEGERS TO STRINGS ARE THAT
*      POSITIVE VALUES ARE REPRESENTED WITHOUT ANY SIGN, AND
*      THERE ARE NEVER ANY LEADING BLANKS OR ZEROS, EXCEPT IN
*      THE CASE OF ZERO ITSELF WHICH IS REPRESENTED AS A SINGLE
*      ZERO DIGIT.  NEGATIVE NUMBERS ARE REPRESENTED WITH A
*      PRECEEDING MINUS SIGN.  THERE ARE NEVER ANY TRAILING
*      BLANKS, AND CONVERSION CANNOT FAIL.
*
*      (IA)                  VALUE TO BE CONVERTED
*      JSR  SYSCI            CALL TO CONVERT INTEGER VALUE
*      (XL)                  POINTER TO PSEUDO-SCBLK WITH STRING
       EJC
.FI
.IF    .CCMC
*
*      SYSCM -- GENERAL STRING COMPARISON FUNCTION
*
SYSCM  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      PROVIDES STRING COMPARISON DETERMINED BY INTERFACE.
*      USED FOR INTERNATIONAL STRING COMPARISON.
*
*
*      (XR)                  CHARACTER POINTER FOR FIRST STRING
*      (XL)                  CHARACTER POINTER FOR SECOND STRING
*      (WB)                  CHARACTER COUNT OF FIRST STRING
*      (WA)                  CHARACTER COUNT OF SECOND STRING
*      JSR  SYSCM            CALL TO SYSCM FUNCTION
*      PPM  LOC              STRING TOO LONG FOR SYSCM
*      PPM  LOC              FIRST STRING LEXICALLY GT SECOND
*      PPM  LOC              FIRST STRING LEXICALLY LT SECOND
*      ---                   STRINGS EQUAL
*      (XL)                  ZERO
*      (XR)                  DESTROYED
*
       EJC
.FI
.IF    .CNRA
.ELSE
.IF    .CNCR
*
*      SYSCR -- CONVERT REAL
*
SYSCR  EXP
*
*      SYSCR IS AN OPTIONAL OSINT ROUTINE THAT CAUSES SPITBOL TO
*      CALL SYSCR TO CONVERT REAL VALUES TO STRINGS, RATHER
*      THAN USING THE INTERNAL SPITBOL CONVERSION CODE.  THIS
*      CODE MAY BE DESIRED ON MACHINES WHERE THE INTEGER SIZE
*      IS TOO SMALL TO ALLOW PRODUCTION OF A SUFFICIENT NUMBER
*      OF SIGNIFICANT DIGITS.  THE SYMBOL .CNCR MUST BE DEFINED
*      IF THIS ROUTINE IS TO BE USED.
*
*      THE RULES FOR CONVERTING REALS TO STRINGS ARE THAT
*      POSITIVE VALUES ARE REPRESENTED WITHOUT ANY SIGN, AND
*      THERE ARE NEVER ANY LEADING BLANKS OR ZEROS, EXCEPT IN
*      THE CASE OF ZERO ITSELF WHICH IS REPRESENTED AS A SINGLE
*      ZERO DIGIT.  NEGATIVE NUMBERS ARE REPRESENTED WITH A
*      PRECEEDING MINUS SIGN.  THERE ARE NEVER ANY TRAILING
*      BLANKS, OR TRAILING ZEROS IN THE FRACTIONAL PART.
*      CONVERSION CANNOT FAIL.
*
*      (RA)                  VALUE TO BE CONVERTED
*      (WA)                  NO. OF SIGNIFICANT DIGITS DESIRED
*      (WB)                  CONVERSION TYPE:
*                             NEGATIVE FOR E-TYPE CONVERSION
*                             ZERO FOR G-TYPE CONVERSION
*                             POSITIVE FOR F-TYPE CONVERSION
*      (WC)                  CHARACTER POSITIONS IN RESULT SCBLK
*      (XR)                  SCBLK FOR RESULT
*      JSR  SYSCR            CALL TO CONVERT REAL VALUE
*      (XR)                  RESULT SCBLK
*      (WA)                  NUMBER OF RESULT CHARACTERS
       EJC
.FI
.FI
*
*      SYSDC -- DATE CHECK
*
SYSDC  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSDC IS CALLED TO CHECK THAT THE EXPIRY DATE FOR A TRIAL
*      VERSION OF SPITBOL IS UNEXPIRED.
*
*      JSR  SYSDC            CALL TO CHECK DATE
*      RETURN ONLY IF DATE IS OK
       EJC
*
*      SYSDM  -- DUMP CORE
*
SYSDM  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSDM IS CALLED BY A SPITBOL PROGRAM CALL OF DUMP(N) WITH
*      N GE 4.  ITS PURPOSE IS TO PROVIDE A CORE DUMP.
*      N COULD HOLD AN ENCODING OF THE START ADRS FOR DUMP AND
*      AMOUNT TO BE DUMPED E.G.  N = 256*A + S , S = START ADRS
*      IN KILOWORDS,  A = KILOWORDS TO DUMP
*
*      (XR)                  PARAMETER N OF CALL DUMP(N)
*      JSR  SYSDM            CALL TO ENTER ROUTINE
       EJC
*
*      SYSDT -- GET CURRENT DATE
*
SYSDT  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSDT IS USED TO OBTAIN THE CURRENT DATE. THE DATE IS
*      RETURNED AS A CHARACTER STRING IN ANY FORMAT APPROPRIATE
*      TO THE OPERATING SYSTEM IN USE. IT MAY ALSO CONTAIN THE
*      CURRENT TIME OF DAY. SYSDT IS USED TO IMPLEMENT THE
*      SNOBOL4 FUNCTION DATE().
*
*      (XR)                  PARAMETER N OF CALL DATE(N)
*      JSR  SYSDT            CALL TO GET DATE
*      (XL)                  POINTER TO BLOCK CONTAINING DATE
*
*      THE FORMAT OF THE BLOCK IS LIKE AN SCBLK EXCEPT THAT
*      THE FIRST WORD NEED NOT BE SET. THE RESULT IS COPIED
*      INTO SPITBOL DYNAMIC MEMORY ON RETURN.
.IF    .CERA
       EJC
*
*      SYSEA -- INFORM OSINT OF COMPILATION AND RUNTIME ERRORS
*
SYSEA  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      PROVIDES MEANS FOR INTERFACE TO TAKE SPECIAL ACTIONS ON
*      ERRORS
*
*      (WA)                  ERROR CODE
*      (WB)                  LINE NUMBER
*      (WC)                  COLUMN NUMBER
*      (XR)                  SYSTEM STAGE
.IF    .CSFN
*      (XL)                  FILE NAME (SCBLK)
.FI
*      JSR  SYSEA            CALL TO SYSEA FUNCTION
*      PPM  LOC              SUPPRESS PRINTING OF ERROR MESSAGE
*      (XR)                  MESSAGE TO PRINT (SCBLK) OR 0
*
*      SYSEA MAY NOT RETURN IF INTERFACE CHOOSES TO RETAIN
*      CONTROL.  CLOSING FILES VIA THE FCB CHAIN WILL BE THE
*      RESPONSIBILITY OF THE INTERFACE.
*
*      ALL REGISTERS PRESERVED
.FI
       EJC
*
*      SYSEF -- EJECT FILE
*
SYSEF  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEF IS USED TO WRITE A PAGE EJECT TO A NAMED FILE. IT
*      MAY ONLY BE USED FOR FILES WHERE THIS CONCEPT MAKES
*      SENSE. NOTE THAT SYSEF IS NOT NORMALLY USED FOR THE
*      STANDARD OUTPUT FILE (SEE SYSEP).
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      (XR)                  EJECT ARGUMENT (SCBLK PTR)
*      JSR  SYSEF            CALL TO EJECT FILE
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF INAPPROPRIATE FILE
*      PPM  LOC              RETURN HERE IF I/O ERROR
       EJC
*
*      SYSEJ -- END OF JOB
*
SYSEJ  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEJ IS CALLED ONCE AT THE END OF EXECUTION TO
*      TERMINATE THE RUN. THE SIGNIFICANCE OF THE ABEND AND
*      CODE VALUES IS SYSTEM DEPENDENT. IN GENERAL, THE CODE
*      VALUE SHOULD BE MADE AVAILABLE FOR TESTING, AND THE
*      ABEND VALUE SHOULD CAUSE SOME POST-MORTEM ACTION SUCH AS
*      A DUMP. NOTE THAT SYSEJ DOES NOT RETURN TO ITS CALLER.
*      SEE SYSXI FOR DETAILS OF FCBLK CHAIN
*
*      (WA)                  VALUE OF ABEND KEYWORD
*      (WB)                  VALUE OF CODE KEYWORD
*      (XL)                  O OR PTR TO HEAD OF FCBLK CHAIN
*      JSR  SYSEJ            CALL TO END JOB
*
*      THE FOLLOWING SPECIAL VALUES ARE USED AS CODES IN (WB)
*      999  EXECUTION SUPPRESSED
*      998  STANDARD OUTPUT FILE FULL OR UNAVAILABLE IN A SYSXI
*           LOAD MODULE. IN THESE CASES (WA) CONTAINS THE NUMBER
*           OF THE STATEMENT CAUSING PREMATURE TERMINATION.
       EJC
*
*      SYSEM -- GET ERROR MESSAGE TEXT
*
SYSEM  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEM IS USED TO OBTAIN THE TEXT OF ERR, ERB CALLS IN THE
*      SOURCE PROGRAM GIVEN THE ERROR CODE NUMBER. IT IS ALLOWED
*      TO RETURN A NULL STRING IF THIS FACILITY IS UNAVAILABLE.
*
*      (WA)                  ERROR CODE NUMBER
*      JSR  SYSEM            CALL TO GET TEXT
*      (XR)                  TEXT OF MESSAGE
*
*      THE RETURNED VALUE IS A POINTER TO A BLOCK IN SCBLK
*      FORMAT EXCEPT THAT THE FIRST WORD NEED NOT BE SET. THE
*      STRING IS COPIED INTO DYNAMIC MEMORY ON RETURN.
*      IF THE NULL STRING IS RETURNED EITHER BECAUSE SYSEM DOES
*      NOT PROVIDE ERROR MESSAGE TEXTS OR BECAUSE WA IS OUT OF
*      RANGE, SPITBOL WILL PRINT THE STRING STORED IN ERRTEXT
*      KEYWORD.
       EJC
*
*      SYSEN -- ENDFILE
*
SYSEN  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEN IS USED TO IMPLEMENT THE SNOBOL4 FUNCTION ENDFILE.
*      THE MEANING IS SYSTEM DEPENDENT. IN GENERAL, ENDFILE
*      IMPLIES THAT NO FURTHER I/O OPERATIONS WILL BE PERFORMED,
*      BUT DOES NOT GUARANTEE THIS TO BE THE CASE. THE FILE
*      SHOULD BE CLOSED AFTER THE CALL, A SUBSEQUENT READ
*      OR WRITE MAY REOPEN THE FILE AT THE START OR IT MAY BE
*      NECESSARY TO REOPEN THE FILE VIA SYSIO.
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      (XR)                  ENDFILE ARGUMENT (SCBLK PTR)
*      JSR  SYSEN            CALL TO ENDFILE
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF ENDFILE NOT ALLOWED
*      PPM  LOC              RETURN HERE IF I/O ERROR
*      (WA,WB)               DESTROYED
*
*      THE SECOND ERROR RETURN IS USED FOR FILES FOR WHICH
*      ENDFILE IS NOT PERMITTED. FOR EXAMPLE, IT MAY BE EXPECTED
*      THAT THE STANDARD INPUT AND OUTPUT FILES ARE IN THIS
*      CATEGORY.
       EJC
*
*      SYSEP -- EJECT PRINTER PAGE
*
SYSEP  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEP IS CALLED TO PERFORM A PAGE EJECT ON THE STANDARD
*      PRINTER OUTPUT FILE (CORRESPONDING TO SYSPR OUTPUT).
*
*      JSR  SYSEP            CALL TO EJECT PRINTER OUTPUT
       EJC
*
*      SYSEX -- CALL EXTERNAL FUNCTION
*
SYSEX  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSEX IS CALLED TO PASS CONTROL TO AN EXTERNAL FUNCTION
*      PREVIOUSLY LOADED WITH A CALL TO SYSLD.
*
*      (XS)                  POINTER TO ARGUMENTS ON STACK
*      (XL)                  POINTER TO CONTROL BLOCK (EFBLK)
*      (WA)                  NUMBER OF ARGUMENTS ON STACK
*      JSR  SYSEX            CALL TO PASS CONTROL TO FUNCTION
*      PPM  LOC              RETURN HERE IF FUNCTION CALL FAILS
*      PPM  LOC              RETURN HERE IF INSUFFICIENT MEMORY
*      PPM  LOC              RETURN HERE IF BAD ARGUMENT TYPE
.IF    .CEXP
.ELSE
*      (XS)                  POPPED PAST ARGUMENTS
.FI
*      (XR)                  RESULT RETURNED
*
*      THE ARGUMENTS ARE STORED ON THE STACK WITH
*      THE LAST ARGUMENT AT 0(XS). ON RETURN, XS
*      IS POPPED PAST THE ARGUMENTS.
*
*      THE FORM OF THE ARGUMENTS AS PASSED IS THAT USED IN THE
*      SPITBOL TRANSLATOR (SEE DEFINITIONS AND DATA STRUCTURES
*      SECTION). THE CONTROL BLOCK FORMAT IS ALSO DESCRIBED
*      (UNDER EFBLK) IN THIS SECTION.
*
*      THERE ARE TWO WAYS OF RETURNING A RESULT.
*
*      1)   RETURN A POINTER TO A BLOCK IN DYNAMIC STORAGE. THIS
*           BLOCK MUST BE IN EXACTLY CORRECT FORMAT, INCLUDING
*           THE FIRST WORD. ONLY FUNCTIONS WRITTEN WITH INTIMATE
*           KNOWLEDGE OF THE SYSTEM WILL RETURN IN THIS WAY.
*
*      2)   STRING, INTEGER AND REAL RESULTS MAY BE RETURNED BY
*           POINTING TO A PSEUDO-BLOCK OUTSIDE DYNAMIC MEMORY.
*           THIS BLOCK IS IN ICBLK, RCBLK OR SCBLK FORMAT EXCEPT
*           THAT THE FIRST WORD WILL BE OVERWRITTEN
*           BY A TYPE WORD ON RETURN AND SO NEED NOT
*           BE CORRECTLY SET. SUCH A RESULT IS
*           COPIED INTO MAIN STORAGE BEFORE PROCEEDING.
*           UNCONVERTED RESULTS MAY SIMILARLY BE RETURNED IN A
*           PSEUDO-BLOCK WHICH IS IN CORRECT FORMAT INCLUDING
*           TYPE WORD RECOGNISABLE BY GARBAGE COLLECTOR SINCE
*           BLOCK IS COPIED INTO DYNAMIC MEMORY.
       EJC
*
*      SYSFC -- FILE CONTROL BLOCK ROUTINE
*
SYSFC  EXP  2                DEFINE EXTERNAL ENTRY POINT
*
*      SEE ALSO SYSIO
*      INPUT AND OUTPUT HAVE 3 ARGUMENTS REFERRED TO AS SHOWN
*           INPUT(VARIABLE NAME,FILE ARG1,FILE ARG2)
*           OUTPUT(VARIABLE NAME,FILE ARG1,FILE ARG2)
*      FILE ARG1 MAY BE AN INTEGER OR STRING USED TO IDENTIFY
*      AN I/O CHANNEL. IT IS CONVERTED TO A STRING FOR CHECKING.
*      THE EXACT SIGNIFICANCE OF FILE ARG2
*      IS NOT RIGOROUSLY PRESCRIBED BUT TO IMPROVE PORTABILITY,
*      THE SCHEME DESCRIBED IN THE SPITBOL USER MANUAL
*      SHOULD BE ADOPTED WHEN POSSIBLE. THE PREFERRED FORM IS
*      A STRING _F_,R_R_,C_C_,I_I_,...,Z_Z_  WHERE
*      _F_ IS AN OPTIONAL FILE NAME WHICH IS PLACED FIRST.
*       REMAINING ITEMS MAY BE OMITTED OR INCLUDED IN ANY ORDER.
*      _R_ IS MAXIMUM RECORD LENGTH
*      _C_ IS A CARRIAGE CONTROL CHARACTER OR CHARACTER STRING
*      _I_ IS SOME FORM OF CHANNEL IDENTIFICATION USED IN THE
*         ABSENCE OF _F_ TO ASSOCIATE THE VARIABLE
*         WITH A FILE ALLOCATED DYNAMICALLY BY JCL COMMANDS AT
*         SPITBOL LOAD TIME.
*      ,...,Z_Z_ ARE ADDITIONAL FIELDS.
*      IF , (COMMA) CANNOT BE USED AS A DELIMITER, .CIOD
*      SHOULD BE DEFINED TO INTRODUCE BY CONDITIONAL ASSEMBLY
*      ANOTHER DELIMITER (SEE
*        IODEL  EQU  *
*      EARLY IN DEFINITIONS SECTION).
*      SYSFC IS CALLED WHEN A VARIABLE IS INPUT OR OUTPUT
*      ASSOCIATED TO CHECK FILE ARG1 AND FILE ARG2 AND
*      TO  REPORT WHETHER AN FCBLK (FILE CONTROL
*      BLOCK) IS NECESSARY AND IF SO WHAT SIZE IT SHOULD BE.
*      THIS MAKES IT POSSIBLE FOR SPITBOL RATHER THAN OSINT TO
*      ALLOCATE SUCH A BLOCK IN DYNAMIC MEMORY IF REQUIRED
*      OR ALTERNATIVELY IN STATIC MEMORY.
*      THE SIGNIFICANCE OF AN FCBLK , IF ONE IS REQUESTED, IS
*      ENTIRELY UP TO THE SYSTEM INTERFACE. THE ONLY RESTRICTION
*      IS THAT IF THE FCBLK SHOULD APPEAR TO LIE IN DYNAMIC
*      MEMORY, POINTERS TO IT SHOULD BE PROPER POINTERS TO
*      THE START OF A RECOGNISABLE AND GARBAGE COLLECTABLE
*      BLOCK (THIS CONDITION WILL BE MET IF SYSFC REQUESTS
*      SPITBOL TO PROVIDE AN FCBLK).
*      AN OPTION IS PROVIDED FOR OSINT TO RETURN A POINTER IN
*      XL TO AN FCBLK WHICH IT PRIVATELY ALLOCATED. THIS PTR
*      WILL BE MADE AVAILABLE WHEN I/O OCCURS LATER.
*      PRIVATE FCBLKS MAY HAVE ARBITRARY CONTENTS AND SPITBOL
*      STORES NOTHING IN THEM.
       EJC
*      THE REQUESTED SIZE FOR AN FCBLK IN DYNAMIC MEMORY
*      SHOULD ALLOW A 2 WORD OVERHEAD FOR BLOCK TYPE AND
*      LENGTH FIELDS. INFORMATION SUBSEQUENTLY STORED IN THE
*      REMAINING WORDS MAY BE ARBITRARY IF AN XNBLK (EXTERNAL
*      NON-RELOCATABLE BLOCK) IS REQUESTED. IF THE REQUEST IS
*      FOR AN XRBLK (EXTERNAL RELOCATABLE BLOCK) THE
*      CONTENTS OF WORDS SHOULD BE COLLECTABLE (I.E. ANY
*      APPARENT POINTERS INTO DYNAMIC SHOULD BE GENUINE BLOCK
*      POINTERS). THESE RESTRICTIONS DO NOT APPLY IF AN FCBLK
*      IS ALLOCATED OUTSIDE DYNAMIC OR IS NOT ALLOCATED AT ALL.
*      IF AN FCBLK IS REQUESTED, ITS FIELDS WILL BE INITIALISED
*      TO ZERO BEFORE ENTRY TO SYSIO WITH THE EXCEPTION OF
*      WORDS 0 AND 1 IN WHICH THE BLOCK TYPE AND LENGTH
*      FIELDS ARE PLACED FOR FCBLKS IN DYNAMIC MEMORY ONLY.
*      FOR THE POSSIBLE USE OF SYSEJ AND SYSXI, IF FCBLKS
*      ARE USED, A CHAIN IS BUILT SO THAT THEY MAY ALL BE
*      FOUND - SEE SYSXI FOR DETAILS.
*      IF BOTH FILE ARG1 AND FILE ARG2 ARE NULL, CALLS OF SYSFC
*      AND SYSIO ARE OMITTED.
*      IF FILE ARG1 IS NULL (STANDARD INPUT/OUTPUT FILE), SYSFC
*      IS CALLED TO CHECK NON-NULL FILE ARG2 BUT ANY REQUEST
*      FOR AN FCBLK WILL BE IGNORED, SINCE SPITBOL HANDLES THE
*      STANDARD FILES SPECIALLY AND CANNOT READILY KEEP FCBLK
*      POINTERS FOR THEM.
*      FILEARG1 IS TYPE CHECKED BY SPITBOL SO FURTHER CHECKING
*      MAY BE UNNECCESSARY IN MANY IMPLEMENTATIONS.
*      FILE ARG2 IS PASSED SO THAT SYSFC MAY ANALYSE AND
*      CHECK IT. HOWEVER TO ASSIST IN THIS, SPITBOL ALSO PASSES
*      ON THE STACK THE COMPONENTS OF THIS ARGUMENT WITH
*      FILE NAME, _F_ (OTHERWISE NULL) EXTRACTED AND STACKED
*      FIRST.
*      THE OTHER FIELDS, IF ANY, ARE EXTRACTED AS SUBSTRINGS,
*      POINTERS TO THEM ARE STACKED AND A COUNT OF ALL ITEMS
*      STACKED IS PLACED IN WC. IF AN FCBLK WAS EARLIER
*      ALLOCATED AND POINTED TO VIA FILE ARG1, SYSFC IS ALSO
*      PASSED A POINTER TO THIS FCBLK.
*
*      (XL)                  FILE ARG1 SCBLK PTR (2ND ARG)
*      (XR)                  FILEARG2 (3RD ARG) OR NULL
*      -(XS)...-(XS)         SCBLKS FOR _F_,_R_,_C_,...
*      (WC)                  NO. OF STACKED SCBLKS ABOVE
*      (WA)                  EXISTING FILE ARG1 FCBLK PTR OR 0
*      (WB)                  0/3 FOR INPUT/OUTPUT ASSOCN
*      JSR  SYSFC            CALL TO CHECK NEED FOR FCBLK
*      PPM  LOC              INVALID FILE ARGUMENT
*      PPM  LOC              FCBLK ALREADY IN USE
*      (XS)                  POPPED (WC) TIMES
*      (WA NON ZERO)         BYTE SIZE OF REQUESTED FCBLK
*      (WA=0,XL NON ZERO)    PRIVATE FCBLK PTR IN XL
*      (WA=XL=0)             NO FCBLK WANTED, NO PRIVATE FCBLK
*      (WC)                  0/1/2 REQUEST ALLOC OF XRBLK/XNBLK
*                            /STATIC BLOCK FOR USE AS FCBLK
*      (WB)                  DESTROYED
.IF    .CGBC
       EJC
*
*      SYSGC -- INFORM INTERFACE OF GARBAGE COLLECTIONS
*
SYSGC  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      PROVIDES MEANS FOR INTERFACE TO TAKE SPECIAL ACTIONS
*      PRIOR TO AND AFTER A GARBAGE COLLECTION.
*
*      POSSIBLE USAGES-
*      1. PROVIDE VISIBLE SCREEN ICON OF GARBAGE COLLECTION
*         IN PROGRESS
*      2. INFORM VIRTUAL MEMORY MANAGER TO IGNORE PAGE ACCESS
*         PATTERNS DURING GARBAGE COLLECTION.  SUCH ACCESSES
*         TYPICALLY DESTROY THE PAGE WORKING SET ACCUMULATED
*         BY THE PROGRAM.
*      3. INFORM VIRTUAL MEMORY MANAGER THAT CONTENTS OF MEMORY
*         FREED BY GARBAGE COLLECTION CAN BE DISCARDED.
*
*      (XR)                  NON-ZERO IF BEGINNING GC
*                            =0 IF COMPLETING GC
*      (WA)                  DNAMB=START OF DYNAMIC AREA
*      (WB)                  DNAMP=NEXT AVAILABLE LOCATION
*      (WC)                  DNAME=LAST AVAILABLE LOCATION + 1
*      JSR  SYSGC            CALL TO SYSGC FUNCTION
*      ALL REGISTERS PRESERVED
.FI

       EJC
*
*      SYSHS -- GIVE ACCESS TO HOST COMPUTER FEATURES
*
SYSHS  EXP  8                DEFINE EXTERNAL ENTRY POINT
*
*      PROVIDES MEANS FOR IMPLEMENTING SPECIAL FEATURES
*      ON DIFFERENT HOST COMPUTERS. THE ONLY DEFINED ENTRY IS
*      THAT WHERE ALL ARGUMENTS ARE NULL IN WHICH CASE SYSHS
*      RETURNS AN SCBLK CONTAINING NAME OF COMPUTER,
*      NAME OF OPERATING SYSTEM AND NAME OF SITE SEPARATED BY
*      COLONS. THE SCBLK NEED NOT HAVE A CORRECT FIRST FIELD
*      AS THIS IS SUPPLIED ON COPYING STRING TO DYNAMIC MEMORY.
*      SPITBOL DOES NO ARGUMENT CHECKING BUT DOES PROVIDE A
*      SINGLE ERROR RETURN FOR ARGUMENTS CHECKED AS ERRONEOUS
*      BY OSINT. IT ALSO PROVIDES A SINGLE EXECUTION ERROR
*      RETURN. IF THESE ARE INADEQUATE, USE MAY BE MADE OF THE
*      MINIMAL ERROR SECTION DIRECT AS DESCRIBED IN MINIMAL
*      DOCUMENTATION, SECTION 10.
*      SEVERAL NON-ERROR RETURNS ARE PROVIDED. THE FIRST
*      CORRESPONDS TO THE DEFINED ENTRY OR, FOR IMPLEMENTATION
*      DEFINED ENTRIES, ANY STRING MAY BE RETURNED. THE OTHERS
*      PERMIT RESPECTIVELY,  RETURN A NULL RESULT, RETURN WITH A
*      RESULT TO BE STACKED WHICH IS POINTED AT BY XR, AND A
*      RETURN CAUSING SPITBOL STATEMENT FAILURE. IF A RETURNED
*      RESULT IS IN DYNAMIC MEMORY IT MUST OBEY GARBAGE
*      COLLECTOR RULES. THE ONLY RESULTS COPIED ON RETURN
*      ARE STRINGS RETURNED VIA PPM LOC3 RETURN.
*
*      (WA)                  ARGUMENT 1
*      (XL)                  ARGUMENT 2
*      (XR)                  ARGUMENT 3
*      (WB)                  ARGUMENT 4
*      (WC)                  ARGUMENT 5
*      JSR  SYSHS            CALL TO GET HOST INFORMATION
*      PPM  LOC1             ERRONEOUS ARG
*      PPM  LOC2             EXECUTION ERROR
*      PPM  LOC3             SCBLK PTR IN XL OR 0 IF UNAVAILABLE
*      PPM  LOC4             RETURN A NULL RESULT
*      PPM  LOC5             RETURN RESULT IN XR
*      PPM  LOC6             CAUSE STATEMENT FAILURE
*      PPM  LOC7             RETURN STRING AT XL, LENGTH WA
*      PPM  LOC8             RETURN COPY OF RESULT IN XR
       EJC
*
*      SYSID -- RETURN SYSTEM IDENTIFICATION
*
SYSID  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      THIS ROUTINE SHOULD RETURN STRINGS TO HEAD THE STANDARD
*      PRINTER OUTPUT. THE FIRST STRING WILL BE APPENDED TO
*      A HEADING LINE OF THE FORM
*           MACRO SPITBOL VERSION V.V
*      SUPPLIED BY SPITBOL ITSELF. V.V ARE DIGITS GIVING THE
*      MAJOR VERSION NUMBER AND GENERALLY AT LEAST A MINOR
*      VERSION NUMBER RELATING TO OSINT SHOULD BE SUPPLIED TO
*      GIVE SAY
*           MACRO SPITBOL VERSION V.V(M.M)
*      THE SECOND STRING SHOULD IDENTIFY AT LEAST THE MACHINE
*      AND OPERATING SYSTEM.  PREFERABLY IT SHOULD INCLUDE
*      THE DATE AND TIME OF THE RUN.
*      OPTIONALLY THE STRINGS MAY INCLUDE SITE NAME OF THE
*      THE IMPLEMENTOR AND/OR MACHINE ON WHICH RUN TAKES PLACE,
*      UNIQUE SITE OR COPY NUMBER AND OTHER INFORMATION AS
*      APPROPRIATE WITHOUT MAKING IT SO LONG AS TO BE A
*      NUISANCE TO USERS.
*      THE FIRST WORDS OF THE SCBLKS POINTED AT NEED NOT BE
*      CORRECTLY SET.
*
*      JSR  SYSID            CALL FOR SYSTEM IDENTIFICATION
*      (XR)                  SCBLK PTR FOR ADDITION TO HEADER
*      (XL)                  SCBLK PTR FOR SECOND HEADER
       EJC
.IF    .CINC
*
*      SYSIF -- SWITCH TO NEW INCLUDE FILE
*
SYSIF  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      SYSIF IS USED FOR INCLUDE FILE PROCESSING, BOTH TO INFORM
*      THE INTERFACE WHEN A NEW INCLUDE FILE IS DESIRED, AND
*      WHEN THE END OF FILE OF AN INCLUDE FILE HAS BEEN REACHED
*      AND IT IS DESIRED TO RETURN TO READING FROM THE PREVIOUS
*      NESTED FILE.
*
*      IT IS THE RESPONSIBILITY OF SYSIF TO REMEMBER THE FILE
*      ACCESS PATH TO THE PRESENT INPUT FILE BEFORE SWITCHING TO
*      THE NEW INCLUDE FILE.
*
*      (XL)                  PTR TO SCBLK OR ZERO
*      (XR)                  PTR TO VACANT SCBLK OF LENGTH CSWIN
*                            (XR NOT USED IF XL IS ZERO)
*      JSR  SYSIF            CALL TO CHANGE FILES
*      PPM  LOC              UNABLE TO OPEN FILE
*      (XR)                  SCBLK WITH FULL PATH NAME OF FILE
*                            (XR NOT USED IF INPUT XL IS ZERO)
*
*      REGISTER XL POINTS TO AN SCBLK CONTAINING THE NAME OF THE
*      INCLUDE FILE TO WHICH THE INTERFACE SHOULD SWITCH.  DATA
*      IS FETCHED FROM THE FILE UPON THE NEXT CALL TO SYSRD.
*
*      SYSIF MAY HAVE THE ABILITY TO SEARCH MULTIPLE LIBRARIES
*      FOR THE INCLUDE FILE NAMED IN (XL).  IT IS THEREFORE
*      REQUIRED THAT THE FULL PATH NAME OF THE FILE WHERE THE
*      FILE WAS FINALLY LOCATED BE RETURNED IN (XR).  IT IS THIS
*      NAME THAT IS RECORDED ALONG WITH THE SOURCE STATEMENTS,
*      AND WILL ACCOMPANY SUBSEQUENT ERROR MESSAGES.
*
*      REGISTER XL IS ZERO TO MARK CONCLUSION OF USE OF AN
*      INCLUDE FILE.
       EJC
.FI
*
*      SYSIL -- GET INPUT RECORD LENGTH
*
SYSIL  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSIL IS USED TO GET THE LENGTH OF THE NEXT INPUT RECORD
*      FROM A FILE PREVIOUSLY INPUT ASSOCIATED WITH A SYSIO
*      CALL. THE LENGTH RETURNED IS USED TO ESTABLISH A BUFFER
*      FOR A SUBSEQUENT SYSIN CALL.  SYSIL ALSO INDICATES TO THE
*      CALLER IF THIS IS A BINARY OR TEXT FILE.
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      JSR  SYSIL            CALL TO GET RECORD LENGTH
*      (WA)                  LENGTH OR ZERO IF FILE CLOSED
*      (WC)                  ZERO IF BINARY, NON-ZERO IF TEXT
*
*      NO HARM IS DONE IF THE VALUE RETURNED IS TOO LONG SINCE
*      UNUSED SPACE WILL BE RECLAIMED AFTER THE SYSIN CALL.
*
*      NOTE THAT IT IS THE SYSIL CALL (NOT THE SYSIO CALL) WHICH
*      CAUSES THE FILE TO BE OPENED AS REQUIRED FOR THE FIRST
*      RECORD INPUT FROM THE FILE.
       EJC
*
*      SYSIN -- READ INPUT RECORD
*
SYSIN  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSIN IS USED TO READ A RECORD FROM THE FILE WHICH WAS
*      REFERENCED IN A PRIOR CALL TO SYSIL (I.E. THESE CALLS
*      ALWAYS OCCUR IN PAIRS). THE BUFFER PROVIDED IS AN
*      SCBLK FOR A STRING OF LENGTH SET FROM THE SYSIL CALL.
*      IF THE ACTUAL LENGTH READ IS LESS THAN THIS, THE LENGTH
*      FIELD OF THE SCBLK MUST BE MODIFIED BEFORE RETURNING
*      UNLESS BUFFER IS RIGHT PADDED WITH ZEROES.
*      IT IS ALSO PERMISSIBLE TO TAKE ANY OF THE ALTERNATIVE
*      RETURNS AFTER SCBLK LENGTH HAS BEEN MODIFIED.
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      (XR)                  POINTER TO BUFFER (SCBLK PTR)
*      JSR  SYSIN            CALL TO READ RECORD
*      PPM  LOC              ENDFILE OR NO I/P FILE AFTER SYSXI
*      PPM  LOC              RETURN HERE IF I/O ERROR
*      PPM  LOC              RETURN HERE IF RECORD FORMAT ERROR
*      (WA,WB,WC)            DESTROYED
       EJC
*
*      SYSIO -- INPUT/OUTPUT FILE ASSOCIATION
*
SYSIO  EXP  2                DEFINE EXTERNAL ENTRY POINT
*
*      SEE ALSO SYSFC.
*      SYSIO IS CALLED IN RESPONSE TO A SNOBOL4 INPUT OR OUTPUT
*      FUNCTION CALL EXCEPT WHEN FILE ARG1 AND FILE ARG2
*      ARE BOTH NULL.
*      ITS CALL ALWAYS FOLLOWS IMMEDIATELY AFTER A CALL
*      OF SYSFC. IF SYSFC REQUESTED ALLOCATION
*      OF AN FCBLK, ITS ADDRESS WILL BE IN WA.
*      FOR INPUT FILES, NON-ZERO VALUES OF _R_ SHOULD BE
*      COPIED TO WC FOR USE IN ALLOCATING INPUT BUFFERS. IF _R_
*      IS DEFAULTED OR NOT IMPLEMENTED, WC SHOULD BE ZEROISED.
*      ONCE A FILE HAS BEEN OPENED, SUBSEQUENT INPUT(),OUTPUT()
*      CALLS IN WHICH THE SECOND ARGUMENT IS IDENTICAL WITH THAT
*      IN A PREVIOUS CALL, MERELY ASSOCIATE THE ADDITIONAL
*      VARIABLE NAME (FIRST ARGUMENT) TO THE FILE AND DO NOT
*      RESULT IN RE-OPENING THE FILE.
*      IN SUBSEQUENT ASSOCIATED ACCESSES TO THE FILE A POINTER
*      TO ANY FCBLK ALLOCATED WILL BE MADE AVAILABLE.
*
*      (XL)                  FILE ARG1 SCBLK PTR (2ND ARG)
*      (XR)                  FILE ARG2 SCBLK PTR (3RD ARG)
*      (WA)                  FCBLK PTR (0 IF NONE)
*      (WB)                  0 FOR INPUT, 3 FOR OUTPUT
*      JSR  SYSIO            CALL TO ASSOCIATE FILE
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN IF INPUT/OUTPUT NOT ALLOWED
*      (XL)                  FCBLK POINTER (0 IF NONE)
*      (WC)                  0 (FOR DEFAULT) OR MAX RECORD LNGTH
*      (WA,WB)               DESTROYED
*
*      THE SECOND ERROR RETURN IS USED IF THE FILE NAMED EXISTS
*      BUT INPUT/OUTPUT FROM THE FILE IS NOT ALLOWED. FOR
*      EXAMPLE, THE STANDARD OUTPUT FILE MAY BE IN THIS CATEGORY
*      AS REGARDS INPUT ASSOCIATION.
       EJC
*
*      SYSLD -- LOAD EXTERNAL FUNCTION
*
SYSLD  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSLD IS CALLED IN RESPONSE TO THE USE OF THE SNOBOL4
*      LOAD FUNCTION. THE NAMED FUNCTION IS LOADED (WHATEVER
*      THIS MEANS), AND A POINTER IS RETURNED. THE POINTER WILL
*      BE USED ON SUBSEQUENT CALLS TO THE FUNCTION (SEE SYSEX).
*
*      (XR)                  POINTER TO FUNCTION NAME (SCBLK)
*      (XL)                  POINTER TO LIBRARY NAME (SCBLK)
*      JSR  SYSLD            CALL TO LOAD FUNCTION
*      PPM  LOC              RETURN HERE IF FUNC DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF I/O ERROR
*      PPM  LOC              RETURN HERE IF INSUFFICIENT MEMORY
*      (XR)                  POINTER TO LOADED CODE
*
*      THE SIGNIFICANCE OF THE POINTER RETURNED IS UP TO THE
*      SYSTEM INTERFACE ROUTINE. THE ONLY RESTRICTION IS THAT
*      IF THE POINTER IS WITHIN DYNAMIC STORAGE, IT MUST BE
*      A PROPER BLOCK POINTER.
       EJC
*
*      SYSMM -- GET MORE MEMORY
*
SYSMM  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSMM IS CALLED IN AN ATTEMPT TO ALLOCATE MORE DYNAMIC
*      MEMORY. THIS MEMORY MUST BE ALLOCATED CONTIGUOUSLY WITH
*      THE CURRENT DYNAMIC DATA AREA.
*
*      THE AMOUNT ALLOCATED IS UP TO THE SYSTEM TO DECIDE. ANY
*      VALUE IS ACCEPTABLE INCLUDING ZERO IF ALLOCATION IS
*      IMPOSSIBLE.
*
*      JSR  SYSMM            CALL TO GET MORE MEMORY
*      (XR)                  NUMBER OF ADDITIONAL WORDS OBTAINED
       EJC
*
*      SYSMX -- SUPPLY MXLEN
*
SYSMX  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      BECAUSE OF THE METHOD OF GARBAGE COLLECTION, NO SPITBOL
*      OBJECT IS ALLOWED TO OCCUPY MORE BYTES OF MEMORY THAN
*      THE INTEGER GIVING THE LOWEST ADDRESS OF DYNAMIC
*      (GARBAGE COLLECTABLE) MEMORY. MXLEN IS THE NAME USED TO
*      REFER TO THIS MAXIMUM LENGTH OF AN OBJECT AND FOR MOST
*      USERS OF MOST IMPLEMENTATIONS, PROVIDED DYNAMIC MEMORY
*      STARTS AT AN ADDRESS OF AT LEAST A FEW THOUSAND WORDS,
*      THERE IS NO PROBLEM.
*      IF THE DEFAULT STARTING ADDRESS IS LESS THAN SAY 10000 OR
*      20000, THEN A LOAD TIME OPTION SHOULD BE PROVIDED WHERE A
*      USER CAN REQUEST THAT HE BE ABLE TO CREATE LARGER
*      OBJECTS. THIS ROUTINE INFORMS SPITBOL OF THIS REQUEST IF
*      ANY. THE VALUE RETURNED IS EITHER AN INTEGER
*      REPRESENTING THE DESIRED VALUE OF MXLEN (AND HENCE THE
*      MINIMUM DYNAMIC STORE ADDRESS WHICH MAY RESULT IN
*      NON-USE OF SOME STORE) OR ZERO IF A DEFAULT IS ACCEPTABLE
*      IN WHICH MXLEN IS SET TO THE LOWEST ADDRESS ALLOCATED
*      TO DYNAMIC STORE BEFORE COMPILATION STARTS.
*      IF A NON-ZERO VALUE IS RETURNED, THIS IS USED FOR KEYWORD
*      MAXLNGTH. OTHERWISE THE INITIAL LOW ADDRESS OF DYNAMIC
*      MEMORY IS USED FOR THIS KEYWORD.
*
*      JSR  SYSMX            CALL TO GET MXLEN
*      (WA)                  EITHER MXLEN OR 0 FOR DEFAULT
       EJC
*
*      SYSOU -- OUTPUT RECORD
*
SYSOU  EXP  2                DEFINE EXTERNAL ENTRY POINT
*
*      SYSOU IS USED TO WRITE A RECORD TO A FILE PREVIOUSLY
*      ASSOCIATED WITH A SYSIO CALL.
*
*      (WA)                  PTR TO FCBLK
.IF    .CSOU
*                            OR 0 FOR TERMINAL OR 1 FOR OUTPUT
.FI
.IF    .CNBF
*      (XR)                  RECORD TO BE WRITTEN (SCBLK)
.ELSE
*      (XR)                  RECORD TO WRITE (BCBLK OR SCBLK)
.FI
*      JSR  SYSOU            CALL TO OUTPUT RECORD
*      PPM  LOC              FILE FULL OR NO FILE AFTER SYSXI
*      PPM  LOC              RETURN HERE IF I/O ERROR
*      (WA,WB,WC)            DESTROYED
*
*      NOTE THAT IT IS THE SYSOU CALL (NOT THE SYSIO CALL) WHICH
*      CAUSES THE FILE TO BE OPENED AS REQUIRED FOR THE FIRST
*      RECORD OUTPUT TO THE FILE.
       EJC
*
*      SYSPI -- PRINT ON INTERACTIVE CHANNEL
*
SYSPI  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      IF SPITBOL IS RUN FROM AN ONLINE TERMINAL, OSINT CAN
*      REQUEST THAT MESSAGES SUCH AS COPIES OF COMPILATION
*      ERRORS BE SENT TO THE TERMINAL (SEE SYSPP). IF RELEVANT
*      REPLY WAS MADE BY SYSPP THEN SYSPI IS CALLED TO SEND SUCH
*      MESSAGES TO THE INTERACTIVE CHANNEL.
*      SYSPI IS ALSO USED FOR SENDING OUTPUT TO THE TERMINAL
*      THROUGH THE SPECIAL VARIABLE NAME, TERMINAL.
*
*      (XR)                  PTR TO LINE BUFFER (SCBLK)
*      (WA)                  LINE LENGTH
*      JSR  SYSPI            CALL TO PRINT LINE
*      PPM  LOC              FAILURE RETURN
*      (WA,WB)               DESTROYED
.IF    .CPOL
       EJC
*
*      SYSPL -- PROVIDE INTERACTIVE CONTROL OF SPITBOL
*
SYSPL  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      PROVIDES MEANS FOR INTERFACE TO TAKE SPECIAL ACTIONS,
*      SUCH AS INTERRUPTING EXECUTION, BREAKPOINTING, STEPPING,
*      AND EXPRESSION EVALUATION.  THESE LAST THREE OPTIONS ARE
*      NOT PRESENTLY IMPLEMENTED BY THE CODE CALLING SYSPL.
*
*
*      (WA)                  OPCODE AS FOLLOWS-
*                            =0 POLL TO ALLOW OSINT TO INTERRUPT
*                            =1 BREAKPOINT HIT
*                            =2 COMPLETION OF STATEMENT STEPPING
*                            =3 EXPRESSION EVALUATION RESULT
*      (WB)                  STATEMENT NUMBER
*      R_FCB                 O OR PTR TO HEAD OF FCBLK CHAIN
*      JSR  SYSPL            CALL TO SYSPL FUNCTION
*      PPM  LOC              USER INTERRUPTION
*      PPM  LOC              STEP ONE STATEMENT
*      PPM  LOC              EVALUATE EXPRESSION
*      ---                   RESUME EXECUTION
*                            (WA) = NEW POLLING INTERVAL
*
.FI
       EJC
*
*      SYSPP -- OBTAIN PRINT PARAMETERS
*
SYSPP  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSPP IS CALLED ONCE DURING COMPILATION TO OBTAIN
*      PARAMETERS REQUIRED FOR CORRECT PRINTED OUTPUT FORMAT
*      AND TO SELECT OTHER OPTIONS. IT MAY ALSO BE CALLED AGAIN
*      AFTER SYSXI WHEN A LOAD MODULE IS RESUMED. IN THIS
*      CASE THE VALUE RETURNED IN WA MAY BE LESS THAN OR EQUAL
*      TO THAT RETURNED IN INITIAL CALL BUT MAY NOT BE
*      GREATER.
*      THE INFORMATION RETURNED IS -
*      1.   LINE LENGTH IN CHARS FOR STANDARD PRINT FILE
*      2.   NO OF LINES/PAGE. 0 IS PREFERABLE FOR A NON-PAGED
*           DEVICE (E.G. ONLINE TERMINAL) IN WHICH CASE LISTING
*           PAGE THROWS ARE SUPPRESSED AND PAGE HEADERS
*           RESULTING FROM -TITLE,-STITL LINES ARE KEPT SHORT.
*      3.   AN INITIAL -NOLIST OPTION TO SUPPRESS LISTING UNLESS
*           THE PROGRAM CONTAINS AN EXPLICIT -LIST.
*      4.   OPTIONS TO SUPPRESS LISTING OF COMPILATION AND/OR
*           EXECUTION STATS (USEFUL FOR ESTABLISHED PROGRAMS) -
*           COMBINED WITH 3. GIVES POSSIBILITY OF LISTING
*           FILE NEVER BEING OPENED.
*      5.   OPTION TO HAVE COPIES OF ERRORS SENT TO AN
*           INTERACTIVE CHANNEL IN ADDITION TO STANDARD PRINTER.
*      6.   OPTION TO KEEP PAGE HEADERS SHORT (E.G. IF LISTING
*           TO AN ONLINE TERMINAL).
*      7.   AN OPTION TO CHOOSE EXTENDED OR COMPACT LISTING
*           FORMAT. IN THE FORMER A PAGE EJECT AND IN THE LATTER
*           A FEW LINE FEEDS PRECEDE THE PRINTING OF EACH
*           OF-- LISTING, COMPILATION STATISTICS, EXECUTION
*           OUTPUT AND EXECUTION STATISTICS.
*      8.   AN OPTION TO SUPPRESS EXECUTION AS THOUGH A
*           -NOEXECUTE CARD WERE SUPPLIED.
*      9.   AN OPTION TO REQUEST THAT NAME /TERMINAL/  BE PRE-
*           ASSOCIATED TO AN ONLINE TERMINAL VIA SYSPI AND SYSRI
*      10.  AN INTERMEDIATE (STANDARD) LISTING OPTION REQUIRING
*           THAT PAGE EJECTS OCCUR IN SOURCE LISTINGS. REDUNDANT
*           IF EXTENDED OPTION CHOSEN BUT PARTIALLY EXTENDS
*           COMPACT OPTION.
*      11.  OPTION TO SUPPRESS SYSID IDENTIFICATION.
*
*      JSR  SYSPP            CALL TO GET PRINT PARAMETERS
*      (WA)                  PRINT LINE LENGTH IN CHARS
*      (WB)                  NUMBER OF LINES/PAGE
*      (WC)                  BITS VALUE ...MLKJIHGFEDCBA WHERE
*                            A = 1 TO SEND ERROR COPY TO INT.CH.
*                            B = 1 MEANS STD PRINTER IS INT. CH.
*                            C = 1 FOR -NOLIST OPTION
*                            D = 1 TO SUPPRESS COMPILN. STATS
*
*                            E = 1 TO SUPPRESS EXECN. STATS
*                            F = 1/0 FOR EXTNDED/COMPACT LISTING
*                            G = 1 FOR -NOEXECUTE
*                            H = 1 PRE-ASSOCIATE /TERMINAL/
*
*                            I = 1 FOR STANDARD LISTING OPTION.
*                            J = 1 SUPPRESSES LISTING HEADER
*                            K = 1 FOR -PRINT
*                            L = 1 FOR -NOERRORS
.IF    .CULC
*
*                            M = 1 FOR -CASE 1
.FI
       EJC
*
*      SYSPR -- PRINT LINE ON STANDARD OUTPUT FILE
*
SYSPR  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      SYSPR IS USED TO PRINT A SINGLE LINE ON THE STANDARD
*      OUTPUT FILE.
*
*      (XR)                  POINTER TO LINE BUFFER (SCBLK)
*      (WA)                  LINE LENGTH
*      JSR  SYSPR            CALL TO PRINT LINE
*      PPM  LOC              TOO MUCH O/P OR NO FILE AFTER SYSXI
*      (WA,WB)               DESTROYED
*
*      THE BUFFER POINTED TO IS THE LENGTH OBTAINED FROM THE
*      SYSPP CALL AND IS FILLED OUT WITH TRAILING BLANKS. THE
*      VALUE IN WA IS THE ACTUAL LINE LENGTH WHICH MAY BE LESS
*      THAN THE MAXIMUM LINE LENGTH POSSIBLE. THERE IS NO SPACE
*      CONTROL ASSOCIATED WITH THE LINE, ALL LINES ARE PRINTED
*      SINGLE SPACED. NOTE THAT NULL LINES (WA=0) ARE POSSIBLE
*      IN WHICH CASE A BLANK LINE IS TO BE PRINTED.
*
*      THE ERROR EXIT IS USED FOR SYSTEMS WHICH LIMIT THE AMOUNT
*      OF PRINTED OUTPUT. IF POSSIBLE, PRINTING SHOULD BE
*      PERMITTED AFTER THIS CONDITION HAS BEEN SIGNALLED ONCE TO
*      ALLOW FOR DUMP AND OTHER DIAGNOSTIC INFORMATION.
*      ASSUMING THIS TO BE POSSIBLE, SPITBOL MAY MAKE MORE SYSPR
*      CALLS. IF THE ERROR RETURN OCCURS ANOTHER TIME, EXECUTION
*      IS TERMINATED BY A CALL OF SYSEJ WITH ENDING CODE 998.
       EJC
*
*      SYSRD -- READ RECORD FROM STANDARD INPUT FILE
*
SYSRD  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      SYSRD IS USED TO READ A RECORD FROM THE STANDARD INPUT
*      FILE. THE BUFFER PROVIDED IS AN SCBLK FOR A STRING THE
*      LENGTH OF WHICH IN CHARACTERS IS GIVEN IN WC, THIS
*      CORRESPONDING TO THE MAXIMUM LENGTH OF STRING WHICH
*      SPITBOL IS PREPARED TO RECEIVE. AT COMPILE TIME IT
*      CORRESPONDS TO XXX IN THE MOST RECENT -INXXX CARD
*      (DEFAULT 72) AND AT EXECUTION TIME TO THE MOST RECENT
*      ,R_R_ (RECORD LENGTH) IN THE THIRD ARG OF AN INPUT()
*      STATEMENT FOR THE STANDARD INPUT FILE (DEFAULT 80).
*      IF FEWER THAN (WC) CHARACTERS ARE READ, THE LENGTH
*      FIELD OF THE SCBLK MUST BE ADJUSTED BEFORE RETURNING
*      UNLESS THE BUFFER IS RIGHT PADDED WITH ZEROES.
*      IT IS ALSO PERMISSIBLE TO TAKE THE ALTERNATIVE RETURN
*      AFTER SUCH AN ADJUSTMENT HAS BEEN MADE.
*      SPITBOL MAY CONTINUE TO MAKE CALLS AFTER AN ENDFILE
*      RETURN SO THIS ROUTINE SHOULD BE PREPARED TO MAKE
*      REPEATED ENDFILE RETURNS.
*
*      (XR)                  POINTER TO BUFFER (SCBLK PTR)
*      (WC)                  LENGTH OF BUFFER IN CHARACTERS
*      JSR  SYSRD            CALL TO READ LINE
*      PPM  LOC              ENDFILE OR NO I/P FILE AFTER SYSXI
.IF    .CSFN
*                            OR INPUT FILE NAME CHANGE.  IF
*                            THE FORMER, SCBLK LENGTH IS ZERO.
*                            IF INPUT FILE NAME CHANGE, LENGTH
*                            IS NON-ZERO. CALLER SHOULD RE-ISSUE
*                            SYSRD TO OBTAIN INPUT RECORD.
.FI
*      (WA,WB,WC)            DESTROYED
       EJC
*
*      SYSRI -- READ RECORD FROM INTERACTIVE CHANNEL
*
SYSRI  EXP  1                DEFINE EXTERNAL ENTRY POINT
*
*      READS A RECORD FROM ONLINE TERMINAL FOR SPITBOL VARIABLE,
*      TERMINAL. IF ONLINE TERMINAL IS UNAVAILABLE THEN CODE THE
*      ENDFILE RETURN ONLY.
*      THE BUFFER PROVIDED IS OF LENGTH 258 CHARACTERS. SYSRI
*      SHOULD REPLACE THE COUNT IN THE SECOND WORD OF THE SCBLK
*      BY THE ACTUAL CHARACTER COUNT UNLESS BUFFER IS RIGHT
*      PADDED WITH ZEROES.
*      IT IS ALSO PERMISSIBLE TO TAKE THE ALTERNATIVE
*      RETURN AFTER ADJUSTING THE COUNT.
*      THE END OF FILE RETURN MAY BE USED IF THIS MAKES
*      SENSE ON THE TARGET MACHINE (E.G. IF THERE IS AN
*      EOF CHARACTER.)
*
*      (XR)                  PTR TO 258 CHAR BUFFER (SCBLK PTR)
*      JSR  SYSRI            CALL TO READ LINE FROM TERMINAL
*      PPM  LOC              END OF FILE RETURN
*      (WA,WB,WC)            MAY BE DESTROYED
       EJC
*
*      SYSRW -- REWIND FILE
*
SYSRW  EXP  3                DEFINE EXTERNAL ENTRY POINT
*
*      SYSRW IS USED TO REWIND A FILE I.E. REPOSITION THE FILE
*      AT THE START BEFORE THE FIRST RECORD. THE FILE SHOULD BE
*      CLOSED AND THE NEXT READ OR WRITE CALL WILL OPEN THE
*      FILE AT THE START.
*
*      (WA)                  PTR TO FCBLK OR ZERO
*      (XR)                  REWIND ARG (SCBLK PTR)
*      JSR  SYSRW            CALL TO REWIND FILE
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF REWIND NOT ALLOWED
*      PPM  LOC              RETURN HERE IF I/O ERROR
       EJC
.IF    .CUST
*
*      SYSST -- SET FILE POINTER
*
SYSST  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSST IS CALLED TO CHANGE THE POSITION OF A FILE
*      POINTER. THIS IS ACCOMPLISHED IN A SYSTEM DEPENDENT
*      MANNER, AND THUS THE 2ND AND 3RD ARGUMENTS ARE PASSED
*      UNCONVERTED.
*
*      (WA)                  FCBLK POINTER
*      (WB)                  2ND ARGUMENT
*      (WC)                  3RD ARGUMENT
*      JSR  SYSST            CALL TO SET FILE POINTER
*      PPM  LOC              RETURN HERE IF INVALID 2ND ARG
*      PPM  LOC              RETURN HERE IF INVALID 3RD ARG
*      PPM  LOC              RETURN HERE IF FILE DOES NOT EXIST
*      PPM  LOC              RETURN HERE IF SET NOT ALLOWED
*      PPM  LOC              RETURN HERE IF I/O ERROR
*
       EJC
.FI
*
*      SYSTM -- GET EXECUTION TIME SO FAR
*
SYSTM  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSTM IS USED TO OBTAIN THE AMOUNT OF EXECUTION TIME
*      USED SO FAR SINCE SPITBOL WAS GIVEN CONTROL. THE UNITS
*      ARE DESCRIBED AS MILLISECONDS IN THE SPITBOL OUTPUT, BUT
*      THE EXACT MEANING IS SYSTEM DEPENDENT. WHERE APPROPRIATE,
*      THIS VALUE SHOULD RELATE TO PROCESSOR RATHER THAN CLOCK
*      TIMING VALUES.
*      IF THE SYMBOL .CTMD IS DEFINED, THE UNITS ARE DESCRIBED
*      AS DECISECONDS (0.1 SECOND).
*
*      JSR  SYSTM            CALL TO GET TIMER VALUE
*      (IA)                  TIME SO FAR IN MILLISECONDS
*                            (DECISECONDS IF .CTMD DEFINED)
       EJC
*
*      SYSTT -- TRACE TOGGLE
*
SYSTT  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      CALLED BY SPITBOL FUNCTION TRACE() WITH NO ARGS TO
*      TOGGLE THE SYSTEM TRACE SWITCH.  THIS PERMITS TRACING OF
*      LABELS IN SPITBOL CODE TO BE TURNED ON OR OFF.
*
*      JSR  SYSTT            CALL TO TOGGLE TRACE SWITCH
       EJC
*
*      SYSUL -- UNLOAD EXTERNAL FUNCTION
*
SYSUL  EXP  0                DEFINE EXTERNAL ENTRY POINT
*
*      SYSUL IS USED TO UNLOAD A FUNCTION PREVIOUSLY
*      LOADED WITH A CALL TO SYSLD.
*
*      (XR)                  PTR TO CONTROL BLOCK (EFBLK)
*      JSR  SYSUL            CALL TO UNLOAD FUNCTION
*
*      THE FUNCTION CANNOT BE CALLED FOLLOWING A SYSUL CALL
*      UNTIL ANOTHER SYSLD CALL IS MADE FOR THE SAME FUNCTION.
*
*      THE EFBLK CONTAINS THE FUNCTION CODE POINTER AND ALSO A
*      POINTER TO THE VRBLK CONTAINING THE FUNCTION NAME (SEE
*      DEFINITIONS AND DATA STRUCTURES SECTION).
.IF    .CNEX
.ELSE
       EJC
*
*      SYSXI -- EXIT TO PRODUCE LOAD MODULE
*
SYSXI  EXP  2                DEFINE EXTERNAL ENTRY POINT
*
*      WHEN SYSXI IS CALLED, XL CONTAINS EITHER A STRING POINTER
*      OR ZERO. IN THE FORMER CASE, THE STRING GIVES THE
*      CHARACTER NAME OF A PROGRAM. THE INTENTION IS THAT
*      SPITBOL EXECUTION SHOULD BE TERMINATED FORTHWITH AND
*      THE NAMED PROGRAM LOADED AND EXECUTED. THIS TYPE OF CHAIN
*      EXECUTION IS VERY SYSTEM DEPENDENT AND IMPLEMENTORS MAY
*      CHOOSE TO OMIT IT OR FIND IT IMPOSSIBLE TO PROVIDE.
*      IF (XL) IS ZERO,IA CONTAINS ONE OF THE FOLLOWING INTEGERS
*
*      -1, -2, -3, -4
*           CREATE IF POSSIBLE A LOAD MODULE CONTAINING ONLY THE
*           IMPURE AREA OF MEMORY WHICH NEEDS TO BE LOADED WITH
*           A COMPATIBLE PURE SEGMENT FOR SUBSEQUENT EXECUTIONS.
*           VERSION NUMBERS TO CHECK COMPATIBILITY SHOULD BE
*           KEPT IN BOTH SEGMENTS AND CHECKED ON LOADING.
*           TO ASSIST WITH THIS CHECK, (XR) ON ENTRY IS A
*           POINTER TO AN SCBLK CONTAINING THE SPITBOL MAJOR
*           VERSION NUMBER V.V (SEE SYSID).  THE FILE THUS
*           CREATED IS CALLED A SAVE FILE.
*
*      0    IF POSSIBLE, RETURN CONTROL TO JOB CONTROL
*           COMMAND LEVEL. THE EFFECT IF AVAILABLE WILL BE
*           SYSTEM DEPENDENT.
*
*      +1, +2, +3, +4
*           CREATE IF POSSIBLE A LOAD MODULE FROM ALL OF
*           MEMORY. IT SHOULD BE POSSIBLE TO LOAD AND EXECUTE
*           THIS MODULE DIRECTLY.
*
*      IN THE CASE OF SAVED LOAD MODULES, THE STATUS OF OPEN
*      FILES IS NOT PRESERVED AND IMPLEMENTORS MAY CHOOSE TO
*      OFFER MEANS OF ATTACHING FILES BEFORE EXECUTION OF LOAD
*      MODULES STARTS OR LEAVE IT TO THE USER TO INCLUDE
*      SUITABLE INPUT(), OUTPUT() CALLS IN HIS PROGRAM.
*      SYSXI SHOULD MAKE A NOTE THAT NO I/O CHANNELS,
*      INCLUDING STANDARD FILES, HAVE FILES ATTACHED SO THAT
*      CALLS OF SYSIN, SYSOU, SYSPR, SYSRD SHOULD FAIL UNLESS
*      NEW ASSOCIATIONS ARE MADE FOR THE LOAD MODULE.
*      AT LEAST IN THE CASE OF THE STANDARD OUTPUT FILE, IT IS
*      RECOMMENDED THAT EITHER THE USER BE REQUIRED TO ATTACH
*      A FILE OR THAT A DEFAULT FILE IS ATTACHED, SINCE THE
*      PROBLEM OF ERROR MESSAGES GENERATED BY THE LOAD MODULE
*      IS OTHERWISE SEVERE. AS A LAST RESORT, IF SPITBOL
*      ATTEMPTS TO WRITE TO THE STANDARD OUTPUT FILE AND GETS A
*      REPLY INDICATING THAT SUCH OUPUT IS UNACCEPTABLE IT STOPS
*      BY USING AN ENTRY TO SYSEJ WITH ENDING CODE 998.
*      AS DESCRIBED BELOW, PASSING OF SOME ARGUMENTS MAKES IT
*      CLEAR THAT LOAD MODULE WILL USE A STANDARD OUTPUT FILE.
*
*      IF USE IS MADE OF FCBLKS FOR I/O ASSOCIATION, SPITBOL
*      BUILDS A CHAIN SO THAT THOSE IN USE MAY BE FOUND IN SYSXI
*      AND SYSEJ. THE NODES ARE 4 WORDS LONG. THIRD WORD
*      CONTAINS LINK TO NEXT NODE OR 0, FOURTH WORD CONTAINS
*      FCBLK POINTER.
       EJC
*
*      SYSXI (CONTINUED)
*
*      (XL)                  ZERO OR SCBLK PTR TO FIRST ARGUMENT
*      (XR)                  PTR TO V.V SCBLK
*      (IA)                  SIGNED INTEGER ARGUMENT
*      (WA)                  SCBLK PTR TO SECOND ARGUMENT
*      (WB)                  0 OR PTR TO HEAD OF FCBLK CHAIN
*      JSR  SYSXI            CALL TO EXIT
*      PPM  LOC              REQUESTED ACTION NOT POSSIBLE
*      PPM  LOC              ACTION CAUSED IRRECOVERABLE ERROR
*      (WB,WC,IA,XR,XL,CP)   SHOULD BE PRESERVED OVER CALL
*      (WA)                  0 IN ALL CASES EXCEPT SUCESSFUL
*                            PERFORMANCE OF EXIT(4) OR EXIT(-4),
*                            IN WHICH CASE 1 SHOULD BE RETURNED.
*
*      LOADING AND RUNNING THE LOAD MODULE OR RETURNING FROM
*      JCL COMMAND LEVEL CAUSES EXECUTION TO RESUME AT THE POINT
*      AFTER THE ERROR RETURNS WHICH FOLLOW THE CALL OF SYSXI.
*      THE VALUE PASSED AS EXIT ARGUMENT IS USED TO INDICATE
*      OPTIONS REQUIRED ON RESUMPTION OF LOAD MODULE.
*      +1 OR -1 REQUIRE THAT ON RESUMPTION, SYSID AND SYSPP BE
*      CALLED AND A HEADING PRINTED ON THE STANDARD OUTPUT FILE.
*      +2 OR -2 INDICATE THAT SYSPP WILL BE CALLED BUT NOT SYSID
*      AND NO HEADING WILL BE PUT ON STANDARD OUTPUT FILE.
*      ABOVE OPTIONS HAVE THE OBVIOUS IMPLICATION THAT A
*      STANDARD O/P FILE MUST BE PROVIDED FOR THE LOAD MODULE.
*      +3, +4, -3 OR -4 INDICATE CALLS OF NEITHER SYSID NOR
*      SYSPP AND NO HEADING WILL BE PLACED ON STANDARD OUTPUT
*      FILE.
*      +4 OR -4 INDICATE THAT EXECUTION IS TO CONTINUE AFTER
*      CREATION OF THE SAVE FILE OR LOAD MODULE, ALTHOUGH ALL
*      FILES WILL BE CLOSED BY THE SYSXI ACTION.  THIS PERMITS
*      THE USER TO CHECKPOINT LONG-RUNNING PROGRAMS WHILE
*      CONTINUING EXECUTION.
*
*      NO RETURN FROM SYSXI IS POSSIBLE IF ANOTHER PROGRAM
*      IS LOADED AND ENTERED.
.FI
       EJC
*
*      INTRODUCE THE INTERNAL PROCEDURES.
*
ACESS  INP  R,1
ACOMP  INP  N,5
ALLOC  INP  E,0
.IF    .CNBF
.ELSE
ALOBF  INP  E,0
.FI
ALOCS  INP  E,0
ALOST  INP  E,0
.IF    .CNBF
.ELSE
APNDB  INP  E,2
.FI
.IF    .CNRA
ARITH  INP  N,2
.ELSE
ARITH  INP  N,3
.FI
ASIGN  INP  R,1
ASINP  INP  R,1
BLKLN  INP  E,0
CDGCG  INP  E,0
CDGEX  INP  R,0
CDGNM  INP  R,0
CDGVL  INP  R,0
CDWRD  INP  E,0
CMGEN  INP  R,0
CMPIL  INP  E,0
CNCRD  INP  E,0
COPYB  INP  N,1
DFFNC  INP  E,0
DTACH  INP  E,0
DTYPE  INP  E,0
DUMPR  INP  E,0
.IF    .CENG
ENEVS  INP  R,0
ENGTS  INP  E,0
.FI
ERMSG  INP  E,0
ERTEX  INP  E,0
EVALI  INP  R,4
EVALP  INP  R,1
EVALS  INP  R,3
EVALX  INP  R,1
EXBLD  INP  E,0
EXPAN  INP  E,0
EXPAP  INP  E,1
EXPDM  INP  N,0
EXPOP  INP  N,0
.IF    .CSFN
FILNM  INP  E,0
.FI
.IF    .CULC
FLSTG  INP  E,0
.FI
GBCOL  INP  E,0
GBCPF  INP  E,0
GTARR  INP  E,2
       EJC
GTCOD  INP  E,1
GTEXP  INP  E,1
GTINT  INP  E,1
GTNUM  INP  E,1
GTNVR  INP  E,1
GTPAT  INP  E,1
.IF    .CNRA
.ELSE
GTREA  INP  E,1
.FI
GTSMI  INP  N,2
.IF    .CNBF
.ELSE
GTSTB  INP  N,1
.FI
GTSTG  INP  N,1
GTVAR  INP  E,1
HASHS  INP  E,0
ICBLD  INP  E,0
IDENT  INP  E,1
INOUT  INP  E,0
.IF    .CNBF
.ELSE
INSBF  INP  E,2
.FI
INSTA  INP  E,0
IOFCB  INP  N,3
IOPPF  INP  N,0
IOPUT  INP  N,7
KTREX  INP  R,0
KWNAM  INP  N,0
LCOMP  INP  N,5
LISTR  INP  E,0
LISTT  INP  E,0
.IF    .CSFN
NEWFN  INP  E,0
.FI
NEXTS  INP  E,0
PATIN  INP  N,2
PATST  INP  N,1
PBILD  INP  E,0
PCONC  INP  E,0
PCOPY  INP  N,0
.IF    .CNPF
.ELSE
PRFLR  INP  E,0
PRFLU  INP  E,0
.FI
PRPAR  INP  E,0
PRTCH  INP  E,0
PRTIC  INP  E,0
PRTIS  INP  E,0
PRTIN  INP  E,0
PRTMI  INP  E,0
PRTMM  INP  E,0
PRTMX  INP  E,0
PRTNL  INP  R,0
PRTNM  INP  R,0
PRTNV  INP  E,0
PRTPG  INP  E,0
PRTPS  INP  E,0
PRTSN  INP  E,0
PRTST  INP  R,0
       EJC
PRTTR  INP  E,0
PRTVL  INP  R,0
PRTVN  INP  E,0
.IF    .CNRA
.ELSE
RCBLD  INP  E,0
.FI
READR  INP  E,0
.IF    .CREL
RELAJ  INP  E,0
RELCR  INP  E,0
RELDN  INP  E,0
RELOC  INP  E,0
RELST  INP  E,0
RELWS  INP  E,0
.FI
RSTRT  INP  E,0
.IF    .C370
SBOOL  INP  N,4
.FI
SBSTR  INP  E,0
SCANE  INP  E,0
SCNGF  INP  E,0
SETVR  INP  E,0
.IF    .CNSR
.ELSE
SORTA  INP  N,1
SORTC  INP  E,1
SORTF  INP  E,0
SORTH  INP  N,0
.FI
START  INP  E,0
STGCC  INP  E,0
TFIND  INP  E,1
TMAKE  INP  E,0
TRACE  INP  N,2
TRBLD  INP  E,0
TRIMR  INP  E,0
TRXEQ  INP  R,0
VMAKE  INP  E,1
XSCAN  INP  E,0
XSCNI  INP  N,2
*
*      INTRODUCE THE INTERNAL ROUTINES
*
ARREF  INR
CFUNC  INR
EXFAL  INR
EXINT  INR
EXITS  INR
EXIXR  INR
EXNAM  INR
EXNUL  INR
.IF    .CNRA
.ELSE
EXREA  INR
.FI
EXSID  INR
EXVNM  INR
FAILP  INR
FLPOP  INR
INDIR  INR
MATCH  INR
RETRN  INR
STCOV  INR
STMGO  INR
STOPR  INR
SUCCP  INR
SYSAB  INR
SYSTU  INR
       TTL  S P I T B O L -- DEFINITIONS AND DATA STRUCTURES
*      THIS SECTION CONTAINS ALL SYMBOL DEFINITIONS AND ALSO
*      PICTURES OF ALL DATA STRUCTURES USED IN THE SYSTEM.
*
       SEC                   START OF DEFINITIONS SECTION
*
*      DEFINITIONS OF MACHINE PARAMETERS
*
*      THE MINIMAL TRANSLATOR SHOULD SUPPLY APPROPRIATE VALUES
*      FOR THE PARTICULAR TARGET MACHINE FOR ALL THE
*      EQU  *
*      DEFINITIONS GIVEN AT THE START OF THIS SECTION.
*      NOTE THAT EVEN IF CONDITIONAL ASSEMBLY IS USED TO OMIT
*      SOME FEATURE (E.G. REAL ARITHMETIC) A FULL SET OF CFP_-
*      VALUES MUST BE SUPPLIED. USE DUMMY VALUES IF GENUINE
*      ONES ARE NOT NEEDED.
*
CFP_A  EQU  *                NUMBER OF CHARACTERS IN ALPHABET
*
CFP_B  EQU  *                BYTES/WORD ADDRESSING FACTOR
*
CFP_C  EQU  *                NUMBER OF CHARACTERS PER WORD
*
CFP_F  EQU  *                OFFSET IN BYTES TO CHARS IN
*                            SCBLK. SEE SCBLK FORMAT.
*
CFP_I  EQU  *                NUMBER OF WORDS IN INTEGER CONSTANT
*
CFP_M  EQU  *                MAX POSITIVE INTEGER IN ONE WORD
*
CFP_N  EQU  *                NUMBER OF BITS IN ONE WORD
*
*      THE FOLLOWING DEFINITIONS REQUIRE THE SUPPLY OF EITHER
*      A SINGLE PARAMETER IF REAL ARITHMETIC IS OMITTED OR
*      THREE PARAMETERS IF REAL ARITHMETIC IS INCLUDED.
*
.IF    .CNRA
NSTMX  EQU  *                NO. OF DECIMAL DIGITS IN CFP_M
.ELSE
*
CFP_R  EQU  *                NUMBER OF WORDS IN REAL CONSTANT
*
CFP_S  EQU  *                NUMBER OF SIG DIGS FOR REAL OUTPUT
*
CFP_X  EQU  *                MAX DIGITS IN REAL EXPONENT
.IF    .CNCR
NSTMX  EQU  *                NO. OF DECIMAL DIGITS IN CFP_M
*
MXDGS  EQU  CFP_S+CFP_X      MAX DIGITS IN REAL NUMBER
*
*      MAX SPACE FOR REAL (FOR +0.E+) NEEDS FIVE MORE PLACES
*
NSTMR  EQU  MXDGS+5          MAX SPACE FOR REAL
.ELSE
*
MXDGS  EQU  CFP_S+CFP_X      MAX DIGITS IN REAL NUMBER
*
*
*      MAX SPACE FOR REAL (FOR +0.E+) NEEDS FIVE MORE PLACES
*
NSTMX  EQU  MXDGS+5          MAX SPACE FOR REAL
.FI
.FI
.IF    .CUCF
*
*      THE FOLLOWING DEFINITION FOR CFP_U SUPPLIES A REALISTIC
*      UPPER BOUND ON THE SIZE OF THE ALPHABET.  CFP_U IS USED
*      TO SAVE SPACE IN THE SCANE BSW-IFF-ESW TABLE AND TO EASE
*      TRANSLATION STORAGE REQUIREMENTS.
*
CFP_U  EQU  *                REALISTIC UPPER BOUND ON ALPHABET
.FI
       EJC
*
*      ENVIRONMENT PARAMETERS
*
*      THE SPITBOL PROGRAM IS ESSENTIALLY INDEPENDENT OF
*      THE DEFINITIONS OF THESE PARAMETERS. HOWEVER, THE
*      EFFICIENCY OF THE SYSTEM MAY BE AFFECTED. CONSEQUENTLY,
*      THESE PARAMETERS MAY REQUIRE TUNING FOR A GIVEN VERSION
*      THE VALUES GIVEN IN COMMENTS HAVE BEEN SUCCESSFULLY USED.
*
*      E_SRS IS THE NUMBER OF WORDS TO RESERVE AT THE END OF
*      STORAGE FOR END OF RUN PROCESSING. IT SHOULD BE
*      SET AS SMALL AS POSSIBLE WITHOUT CAUSING MEMORY OVERFLOW
*      IN CRITICAL SITUATIONS (E.G. MEMORY OVERFLOW TERMINATION)
*      AND SHOULD THUS RESERVE SUFFICIENT SPACE AT LEAST FOR
*      AN SCBLK CONTAINING SAY 30 CHARACTERS.
*
E_SRS  EQU  *                30 WORDS
*
*      E_STS IS THE NUMBER OF WORDS GRABBED IN A CHUNK WHEN
*      STORAGE IS ALLOCATED IN THE STATIC REGION. THE MINIMUM
*      PERMITTED VALUE IS 256/CFP_B. LARGER VALUES WILL LEAD
*      TO INCREASED EFFICIENCY AT THE COST OF WASTING MEMORY.
*
E_STS  EQU  *                500 WORDS
*
*      E_CBS IS THE SIZE OF CODE BLOCK ALLOCATED INITIALLY AND
*      THE EXPANSION INCREMENT IF OVERFLOW OCCURS. IF THIS VALUE
*      IS TOO SMALL OR TOO LARGE, EXCESSIVE GARBAGE COLLECTIONS
*      WILL OCCUR DURING COMPILATION AND MEMORY MAY BE LOST
*      IN THE CASE OF A TOO LARGE VALUE.
*
E_CBS  EQU  *                500 WORDS
*
*      E_HNB IS THE NUMBER OF BUCKET HEADERS IN THE VARIABLE
*      HASH TABLE. IT SHOULD ALWAYS BE ODD. LARGER VALUES WILL
*      SPEED UP COMPILATION AND INDIRECT REFERENCES AT THE
*      EXPENSE OF ADDITIONAL STORAGE FOR THE HASH TABLE ITSELF.
*
E_HNB  EQU  *                127 BUCKET HEADERS
*
*      E_HNW IS THE MAXIMUM NUMBER OF WORDS OF A STRING
*      NAME WHICH PARTICIPATE IN THE STRING HASH ALGORITHM.
*      LARGER VALUES GIVE A BETTER HASH AT THE EXPENSE OF TAKING
*      LONGER TO COMPUTE THE HASH. THERE IS SOME OPTIMAL VALUE.
*
E_HNW  EQU  *                6 WORDS
*
*      E_FSP.  IF THE AMOUNT OF FREE SPACE LEFT AFTER A GARBAGE
*      COLLECTION IS SMALL COMPARED TO THE TOTAL AMOUNT OF SPACE
*      IN USE GARBAGE COLLECTOR THRASHING IS LIKELY TO OCCUR AS
*      THIS SPACE IS USED UP.  E_FSP IS A MEASURE OF THE
*      MINIMUM PERCENTAGE OF DYNAMIC MEMORY LEFT AS FREE SPACE
*      BEFORE THE SYSTEM ROUTINE SYSMM IS CALLED TO TRY TO
*      OBTAIN MORE MEMORY.
*
E_FSP  EQU  *                15 PERCENT
.IF    .CSED
*
*      E_SED.  IF THE AMOUNT OF FREE SPACE LEFT IN THE SEDIMENT
*      AFTER A GARBAGE COLLECTION IS A SIGNIFICANT FRACTION OF
*      THE NEW SEDIMENT SIZE, THE SEDIMENT IS MARKED FOR
*      COLLECTION ON THE NEXT CALL TO THE GARBAGE COLLECTOR.
*
E_SED  EQU  *                25 PERCENT
.FI
       EJC
*
*      DEFINITIONS OF CODES FOR LETTERS
*
CH_LA  EQU  *                LETTER A
CH_LB  EQU  *                LETTER B
CH_LC  EQU  *                LETTER C
CH_LD  EQU  *                LETTER D
CH_LE  EQU  *                LETTER E
CH_LF  EQU  *                LETTER F
CH_LG  EQU  *                LETTER G
CH_LH  EQU  *                LETTER H
CH_LI  EQU  *                LETTER I
CH_LJ  EQU  *                LETTER J
CH_LK  EQU  *                LETTER K
CH_LL  EQU  *                LETTER L
CH_LM  EQU  *                LETTER M
CH_LN  EQU  *                LETTER N
CH_LO  EQU  *                LETTER O
CH_LP  EQU  *                LETTER P
CH_LQ  EQU  *                LETTER Q
CH_LR  EQU  *                LETTER R
CH_LS  EQU  *                LETTER S
CH_LT  EQU  *                LETTER T
CH_LU  EQU  *                LETTER U
CH_LV  EQU  *                LETTER V
CH_LW  EQU  *                LETTER W
CH_LX  EQU  *                LETTER X
CH_LY  EQU  *                LETTER Y
CH_L_  EQU  *                LETTER Z
*
*      DEFINITIONS OF CODES FOR DIGITS
*
CH_D0  EQU  *                DIGIT 0
CH_D1  EQU  *                DIGIT 1
CH_D2  EQU  *                DIGIT 2
CH_D3  EQU  *                DIGIT 3
CH_D4  EQU  *                DIGIT 4
CH_D5  EQU  *                DIGIT 5
CH_D6  EQU  *                DIGIT 6
CH_D7  EQU  *                DIGIT 7
CH_D8  EQU  *                DIGIT 8
CH_D9  EQU  *                DIGIT 9
       EJC
*
*      DEFINITIONS OF CODES FOR SPECIAL CHARACTERS
*
*      THE NAMES OF THESE CHARACTERS ARE RELATED TO THEIR
*      ORIGINAL REPRESENTATION IN THE EBCDIC SET CORRESPONDING
*      TO THE DESCRIPTION IN STANDARD SNOBOL4 MANUALS AND TEXTS.
*
CH_AM  EQU  *                KEYWORD OPERATOR (AMPERSAND)
CH_AS  EQU  *                MULTIPLICATION SYMBOL (ASTERISK)
CH_AT  EQU  *                CURSOR POSITION OPERATOR (AT)
CH_BB  EQU  *                LEFT ARRAY BRACKET (LESS THAN)
CH_BL  EQU  *                BLANK
CH_BR  EQU  *                ALTERNATION OPERATOR (VERTICAL BAR)
CH_CL  EQU  *                GOTO SYMBOL (COLON)
CH_CM  EQU  *                COMMA
CH_DL  EQU  *                INDIRECTION OPERATOR (DOLLAR)
CH_DT  EQU  *                NAME OPERATOR (DOT)
CH_DQ  EQU  *                DOUBLE QUOTE
CH_EQ  EQU  *                EQUAL SIGN
CH_EX  EQU  *                EXPONENTIATION OPERATOR (EXCLM)
CH_MN  EQU  *                MINUS SIGN / HYPHEN
CH_NM  EQU  *                NUMBER SIGN
CH_NT  EQU  *                NEGATION OPERATOR (NOT)
CH_PC  EQU  *                PERCENT
CH_PL  EQU  *                PLUS SIGN
CH_PP  EQU  *                LEFT PARENTHESIS
CH_RB  EQU  *                RIGHT ARRAY BRACKET (GRTR THAN)
CH_RP  EQU  *                RIGHT PARENTHESIS
CH_QU  EQU  *                INTERROGATION OPERATOR (QUESTION)
CH_SL  EQU  *                SLASH
CH_SM  EQU  *                SEMICOLON
CH_SQ  EQU  *                SINGLE QUOTE
CH_UN  EQU  *                SPECIAL IDENTIFIER CHAR (UNDERLINE)
CH_OB  EQU  *                OPENING BRACKET
CH_CB  EQU  *                CLOSING BRACKET
       EJC
*
*      REMAINING CHARS ARE OPTIONAL ADDITIONS TO THE STANDARDS.
.IF    .CAHT
*
*      TAB CHARACTERS - SYNTACTICALLY EQUIVALENT TO BLANK
*
CH_HT  EQU  *                HORIZONTAL TAB
.FI
.IF    .CAVT
CH_VT  EQU  *                VERTICAL TAB
.FI
.IF    .CAEX
*
*      UP ARROW SAME AS EXCLAMATION MARK FOR EXPONENTIATION
*
CH_EY  EQU  *                UP ARROW
.FI
.IF    .CASL
*
*      LOWER CASE OR SHIFTED CASE ALPHABETIC CHARS
*
CH__A  EQU  *                SHIFTED A
CH__B  EQU  *                SHIFTED B
CH__C  EQU  *                SHIFTED C
CH__D  EQU  *                SHIFTED D
CH__E  EQU  *                SHIFTED E
CH__F  EQU  *                SHIFTED F
CH__G  EQU  *                SHIFTED G
CH__H  EQU  *                SHIFTED H
CH__I  EQU  *                SHIFTED I
CH__J  EQU  *                SHIFTED J
CH__K  EQU  *                SHIFTED K
CH__L  EQU  *                SHIFTED L
CH__M  EQU  *                SHIFTED M
CH__N  EQU  *                SHIFTED N
CH__O  EQU  *                SHIFTED O
CH__P  EQU  *                SHIFTED P
CH__Q  EQU  *                SHIFTED Q
CH__R  EQU  *                SHIFTED R
CH__S  EQU  *                SHIFTED S
CH__T  EQU  *                SHIFTED T
CH__U  EQU  *                SHIFTED U
CH__V  EQU  *                SHIFTED V
CH__W  EQU  *                SHIFTED W
CH__X  EQU  *                SHIFTED X
CH__Y  EQU  *                SHIFTED Y
CH___  EQU  *                SHIFTED Z
.FI
*      IF A DELIMITER OTHER THAN CH_CM MUST BE USED IN
*      THE THIRD ARGUMENT OF INPUT(),OUTPUT() THEN .CIOD SHOULD
*      BE DEFINED AND A PARAMETER SUPPLIED FOR IODEL.
*
.IF    .CIOD
IODEL  EQU  *
.ELSE
IODEL  EQU  CH_CM
.FI
       EJC
*
*      DATA BLOCK FORMATS AND DEFINITIONS
*
*      THE FOLLOWING SECTIONS DESCRIBE THE DETAILED FORMAT OF
*      ALL POSSIBLE DATA BLOCKS IN STATIC AND DYNAMIC MEMORY.
*
*      EVERY BLOCK HAS A NAME OF THE FORM XXBLK WHERE XX IS A
*      UNIQUE TWO CHARACTER IDENTIFIER. THE FIRST WORD OF EVERY
*      BLOCK MUST CONTAIN A POINTER TO A PROGRAM LOCATION IN THE
*      INTERPRETOR WHICH IS IMMEDIATELY PRECEDED BY AN ADDRESS
*      CONSTANT CONTAINING THE VALUE BL_XX WHERE XX IS THE BLOCK
*      IDENTIFIER. THIS PROVIDES A UNIFORM MECHANISM FOR
*      DISTINGUISHING BETWEEN THE VARIOUS BLOCK TYPES.
*
*      IN SOME CASES, THE CONTENTS OF THE FIRST WORD IS CONSTANT
*      FOR A GIVEN BLOCK TYPE AND MERELY SERVES AS A POINTER
*      TO THE IDENTIFYING ADDRESS CONSTANT. HOWEVER, IN OTHER
*      CASES THERE ARE SEVERAL POSSIBILITIES FOR THE FIRST
*      WORD IN WHICH CASE EACH OF THE SEVERAL PROGRAM ENTRY
*      POINTS MUST BE PRECEDED BY THE APPROPRIATE CONSTANT.
*
*      IN EACH BLOCK, SOME OF THE FIELDS ARE RELOCATABLE. THIS
*      MEANS THAT THEY MAY CONTAIN A POINTER TO ANOTHER BLOCK
*      IN THE DYNAMIC AREA. (TO BE MORE PRECISE, IF THEY CONTAIN
*      A POINTER WITHIN THE DYNAMIC AREA, THEN IT IS A POINTER
*      TO A BLOCK). SUCH FIELDS MUST BE MODIFIED BY THE GARBAGE
*      COLLECTOR (PROCEDURE GBCOL) WHENEVER BLOCKS ARE COMPACTED
*      IN THE DYNAMIC REGION. THE GARBAGE COLLECTOR (ACTUALLY
*      PROCEDURE GBCPF) REQUIRES THAT ALL SUCH RELOCATABLE
*      FIELDS IN A BLOCK MUST BE CONTIGUOUS.
       EJC
*
*      THE DESCRIPTION FORMAT USES THE FOLLOWING SCHEME.
*
*      1)   BLOCK TITLE AND TWO CHARACTER IDENTIFIER
*
*      2)   DESCRIPTION OF BASIC USE OF BLOCK AND INDICATION
*           OF CIRCUMSTANCES UNDER WHICH IT IS CONSTRUCTED.
*
*      3)   PICTURE OF THE BLOCK FORMAT. IN THESE PICTURES LOW
*           MEMORY ADDRESSES ARE AT THE TOP OF THE PAGE. FIXED
*           LENGTH FIELDS ARE SURROUNDED BY I (LETTER I). FIELDS
*           WHICH ARE FIXED LENGTH BUT WHOSE LENGTH IS DEPENDENT
*           ON A CONFIGURATION PARAMETER ARE SURROUNDED BY *
*           (ASTERISK). VARIABLE LENGTH FIELDS ARE SURROUNDED
*           BY / (SLASH).
*
*      4)   DEFINITION OF SYMBOLIC OFFSETS TO FIELDS IN
*           BLOCK AND OF THE SIZE OF THE BLOCK IF FIXED LENGTH
*           OR OF THE SIZE OF THE FIXED LENGTH FIELDS IF THE
*           BLOCK IS VARIABLE LENGTH.
*           NOTE THAT SOME ROUTINES SUCH AS GBCPF ASSUME
*           CERTAIN OFFSETS ARE EQUAL. THE DEFINITIONS
*           GIVEN HERE ENFORCE THIS.  MAKE CHANGES TO
*           THEM ONLY WITH DUE CARE.
*
*      DEFINITIONS OF COMMON OFFSETS
*
OFFS1  EQU  1
OFFS2  EQU  2
OFFS3  EQU  3
*
*      5)   DETAILED COMMENTS ON THE SIGNIFICANCE AND FORMATS
*           OF THE VARIOUS FIELDS.
*
*      THE ORDER IS ALPHABETICAL BY IDENTIFICATION CODE.
       EJC
*
*      DEFINITIONS OF BLOCK CODES
*
*      THIS TABLE PROVIDES A UNIQUE IDENTIFICATION CODE FOR
*      EACH SEPARATE BLOCK TYPE. THE FIRST WORD OF A BLOCK IN
*      THE DYNAMIC AREA ALWAYS CONTAINS THE ADDRESS OF A PROGRAM
*      ENTRY POINT. THE BLOCK CODE IS USED AS THE ENTRY POINT ID
*      THE ORDER OF THESE CODES DICTATES THE ORDER OF THE TABLE
*      USED BY THE DATATYPE FUNCTION (SCNMT IN THE CONSTANT SEC)
*
*      BLOCK CODES FOR ACCESSIBLE DATATYPES
*
*      NOTE THAT REAL AND BUFFER TYPES ARE ALWAYS INCLUDED, EVEN
*      IF THEY ARE CONDITIONALLY EXCLUDED ELSEWHERE.  THIS MAIN-
*      TAINS BLOCK TYPE CODES ACROSS ALL VERSIONS OF SPITBOL,
*      PROVIDING CONSISTANCY FOR EXTERNAL FUNCTIONS.  BUT NOTE
*      THAT THE BCBLK IS OUT OF ALPHABETIC ORDER, PLACED AT THE
*      END OF THE LIST SO AS NOT TO CHANGE THE BLOCK TYPE
*      ORDERING IN USE IN EXISTING EXTERNAL FUNCTIONS.
*
BL_AR  EQU  0                ARBLK     ARRAY
BL_CD  EQU  BL_AR+1          CDBLK     CODE
BL_EX  EQU  BL_CD+1          EXBLK     EXPRESSION
BL_IC  EQU  BL_EX+1          ICBLK     INTEGER
BL_NM  EQU  BL_IC+1          NMBLK     NAME
BL_P0  EQU  BL_NM+1          P0BLK     PATTERN
BL_P1  EQU  BL_P0+1          P1BLK     PATTERN
BL_P2  EQU  BL_P1+1          P2BLK     PATTERN
BL_RC  EQU  BL_P2+1          RCBLK     REAL
BL_SC  EQU  BL_RC+1          SCBLK     STRING
BL_SE  EQU  BL_SC+1          SEBLK     EXPRESSION
BL_TB  EQU  BL_SE+1          TBBLK     TABLE
BL_VC  EQU  BL_TB+1          VCBLK     ARRAY
BL_XN  EQU  BL_VC+1          XNBLK     EXTERNAL
BL_XR  EQU  BL_XN+1          XRBLK     EXTERNAL
BL_BC  EQU  BL_XR+1          BCBLK     BUFFER
BL_PD  EQU  BL_BC+1          PDBLK     PROGRAM DEFINED DATATYPE
*
BL__D  EQU  BL_PD+1          NUMBER OF BLOCK CODES FOR DATA
*
*      OTHER BLOCK CODES
*
BL_TR  EQU  BL_PD+1          TRBLK
BL_BF  EQU  BL_TR+1          BFBLK
BL_CC  EQU  BL_BF+1          CCBLK
BL_CM  EQU  BL_CC+1          CMBLK
BL_CT  EQU  BL_CM+1          CTBLK
BL_DF  EQU  BL_CT+1          DFBLK
BL_EF  EQU  BL_DF+1          EFBLK
BL_EV  EQU  BL_EF+1          EVBLK
BL_FF  EQU  BL_EV+1          FFBLK
BL_KV  EQU  BL_FF+1          KVBLK
BL_PF  EQU  BL_KV+1          PFBLK
BL_TE  EQU  BL_PF+1          TEBLK
*
BL__I  EQU  0                DEFAULT IDENTIFICATION CODE
BL__T  EQU  BL_TR+1          CODE FOR DATA OR TRACE BLOCK
BL___  EQU  BL_TE+1          NUMBER OF BLOCK CODES
       EJC
*
*      FIELD REFERENCES
*
*      REFERENCES TO THE FIELDS OF DATA BLOCKS ARE SYMBOLIC
*      (I.E. USE THE SYMBOLIC OFFSETS) WITH THE FOLLOWING
*      EXCEPTIONS.
*
*      1)   REFERENCES TO THE FIRST WORD ARE USUALLY NOT
*           SYMBOLIC SINCE THEY USE THE (X) OPERAND FORMAT.
*
*      2)   THE CODE WHICH CONSTRUCTS A BLOCK IS OFTEN NOT
*           SYMBOLIC AND SHOULD BE CHANGED IF THE CORRESPONDING
*           BLOCK FORMAT IS MODIFIED.
*
*      3)   THE PLC AND PSC INSTRUCTIONS IMPLY AN OFFSET
*           CORRESPONDING TO THE DEFINITION OF CFP_F.
*
*      4)   THERE ARE NON-SYMBOLIC REFERENCES (EASILY CHANGED)
*           IN THE GARBAGE COLLECTOR (PROCEDURES GBCPF, BLKLN).
*
*      5)   THE FIELDS IDVAL, FARGS APPEAR IN SEVERAL BLOCKS
*           AND ANY CHANGES MUST BE MADE IN PARALLEL TO ALL
*           BLOCKS CONTAINING THE FIELDS. THE ACTUAL REFERENCES
*           TO THESE FIELDS ARE SYMBOLIC WITH THE ABOVE
*           LISTED EXCEPTIONS.
*
*      6)   SEVERAL SPOTS IN THE CODE ASSUME THAT THE
*           DEFINITIONS OF THE FIELDS VRVAL, TEVAL, TRNXT ARE
*           THE SAME (THESE ARE SECTIONS OF CODE WHICH SEARCH
*           OUT ALONG A TRBLK CHAIN FROM A VARIABLE).
*
*      7)   REFERENCES TO THE FIELDS OF AN ARRAY BLOCK IN THE
*           ARRAY REFERENCE ROUTINE ARREF ARE NON-SYMBOLIC.
*
*      APART FROM THE EXCEPTIONS LISTED, REFERENCES ARE SYMBOLIC
*      AS FAR AS POSSIBLE AND MODIFYING THE ORDER OR NUMBER
*      OF FIELDS WILL NOT REQUIRE CHANGES.
       EJC
*
*      COMMON FIELDS FOR FUNCTION BLOCKS
*
*      BLOCKS WHICH REPRESENT CALLABLE FUNCTIONS HAVE TWO
*      COMMON FIELDS AT THE START OF THE BLOCK AS FOLLOWS.
*
*           +------------------------------------+
*           I                FCODE               I
*           +------------------------------------+
*           I                FARGS               I
*           +------------------------------------+
*           /                                    /
*           /       REST OF FUNCTION BLOCK       /
*           /                                    /
*           +------------------------------------+
*
FCODE  EQU  0                POINTER TO CODE FOR FUNCTION
FARGS  EQU  1                NUMBER OF ARGUMENTS
*
*      FCODE IS A POINTER TO THE LOCATION IN THE INTERPRETOR
*      PROGRAM WHICH PROCESSES THIS TYPE OF FUNCTION CALL.
*
*      FARGS IS THE EXPECTED NUMBER OF ARGUMENTS. THE ACTUAL
*      NUMBER OF ARGUMENTS IS ADJUSTED TO THIS AMOUNT BY
*      DELETING EXTRA ARGUMENTS OR SUPPLYING TRAILING NULLS
*      FOR MISSING ONES BEFORE TRANSFERRING THOUGH FCODE.
*      A VALUE OF 999 MAY BE USED IN THIS FIELD TO INDICATE A
*      VARIABLE NUMBER OF ARGUMENTS (SEE SVBLK FIELD SVNAR).
*
*      THE BLOCK TYPES WHICH FOLLOW THIS SCHEME ARE.
*
*      FFBLK                 FIELD FUNCTION
*      DFBLK                 DATATYPE FUNCTION
*      PFBLK                 PROGRAM DEFINED FUNCTION
*      EFBLK                 EXTERNAL LOADED FUNCTION
       EJC
*
*      IDENTIFICATION FIELD
*
*
*      ID   FIELD
*
*      CERTAIN PROGRAM ACCESSIBLE OBJECTS (THOSE WHICH CONTAIN
*      OTHER DATA VALUES AND CAN BE COPIED) ARE GIVEN A UNIQUE
*      IDENTIFICATION NUMBER (SEE EXSID). THIS ID VALUE IS AN
*      ADDRESS INTEGER VALUE WHICH IS ALWAYS STORED IN WORD TWO.
*
IDVAL  EQU  1                ID VALUE FIELD
*
*      THE BLOCKS CONTAINING AN IDVAL FIELD ARE.
*
*      ARBLK                 ARRAY
.IF    .CNBF
.ELSE
*      BCBLK                 BUFFER CONTROL BLOCK
.FI
*      PDBLK                 PROGRAM DEFINED DATATYPE
*      TBBLK                 TABLE
*      VCBLK                 VECTOR BLOCK (ARRAY)
*
*      NOTE THAT A ZERO IDVAL MEANS THAT THE BLOCK IS ONLY
*      HALF BUILT AND SHOULD NOT BE DUMPED (SEE DUMPR).
       EJC
*
*      ARRAY BLOCK (ARBLK)
*
*      AN ARRAY BLOCK REPRESENTS AN ARRAY VALUE OTHER THAN ONE
*      WITH ONE DIMENSION WHOSE LOWER BOUND IS ONE (SEE VCBLK).
*      AN ARBLK IS BUILT WITH A CALL TO THE FUNCTIONS CONVERT
*      (S_CNV) OR ARRAY (S_ARR).
*
*           +------------------------------------+
*           I                ARTYP               I
*           +------------------------------------+
*           I                IDVAL               I
*           +------------------------------------+
*           I                ARLEN               I
*           +------------------------------------+
*           I                AROFS               I
*           +------------------------------------+
*           I                ARNDM               I
*           +------------------------------------+
*           *                ARLBD               *
*           +------------------------------------+
*           *                ARDIM               *
*           +------------------------------------+
*           *                                    *
*           * ABOVE 2 FLDS REPEATED FOR EACH DIM *
*           *                                    *
*           +------------------------------------+
*           I                ARPRO               I
*           +------------------------------------+
*           /                                    /
*           /                ARVLS               /
*           /                                    /
*           +------------------------------------+
       EJC
*
*      ARRAY BLOCK (CONTINUED)
*
ARTYP  EQU  0                POINTER TO DUMMY ROUTINE B_ART
ARLEN  EQU  IDVAL+1          LENGTH OF ARBLK IN BYTES
AROFS  EQU  ARLEN+1          OFFSET IN ARBLK TO ARPRO FIELD
ARNDM  EQU  AROFS+1          NUMBER OF DIMENSIONS
ARLBD  EQU  ARNDM+1          LOW BOUND (FIRST SUBSCRIPT)
ARDIM  EQU  ARLBD+CFP_I      DIMENSION (FIRST SUBSCRIPT)
ARLB2  EQU  ARDIM+CFP_I      LOW BOUND (SECOND SUBSCRIPT)
ARDM2  EQU  ARLB2+CFP_I      DIMENSION (SECOND SUBSCRIPT)
ARPRO  EQU  ARDIM+CFP_I      ARRAY PROTOTYPE (ONE DIMENSION)
ARVLS  EQU  ARPRO+1          START OF VALUES (ONE DIMENSION)
ARPR2  EQU  ARDM2+CFP_I      ARRAY PROTOTYPE (TWO DIMENSIONS)
ARVL2  EQU  ARPR2+1          START OF VALUES (TWO DIMENSIONS)
ARSI_  EQU  ARLBD            NUMBER OF STANDARD FIELDS IN BLOCK
ARDMS  EQU  ARLB2-ARLBD      SIZE OF INFO FOR ONE SET OF BOUNDS
*
*      THE BOUNDS AND DIMENSION FIELDS ARE SIGNED INTEGER
*      VALUES AND EACH OCCUPY CFP_I WORDS IN THE ARBLK.
*
*      THE LENGTH OF AN ARBLK IN BYTES MAY NOT EXCEED MXLEN.
*      THIS IS REQUIRED TO KEEP NAME OFFSETS GARBAGE COLLECTABLE
*
*      THE ACTUAL VALUES ARE ARRANGED IN ROW-WISE ORDER AND
*      CAN CONTAIN A DATA POINTER OR A POINTER TO A TRBLK.
.IF    .CNBF
.ELSE
*
*      BUFFER CONTROL BLOCK (BCBLK)
*
*      A BCBLK IS BUILT FOR EVERY BFBLK.
*
*           +------------------------------------+
*           I                BCTYP               I
*           +------------------------------------+
*           I                IDVAL               I
*           +------------------------------------+
*           I                BCLEN               I
*           +------------------------------------+
*           I                BCBUF               I
*           +------------------------------------+
*
BCTYP  EQU  0                PTR TO DUMMY ROUTINE B_BCT
BCLEN  EQU  IDVAL+1          DEFINED BUFFER LENGTH
BCBUF  EQU  BCLEN+1          PTR TO BFBLK
BCSI_  EQU  BCBUF+1          SIZE OF BCBLK
*
*      A BCBLK IS AN INDIRECT CONTROL HEADER FOR BFBLK.
*      THE REASON FOR NOT STORING THIS DATA DIRECTLY
*      IN THE RELATED BFBLK IS SO THAT THE BFBLK CAN
*      MAINTAIN THE SAME SKELETAL STRUCTURE AS AN SCBLK
*      THUS FACILITATING TRANSPARENT STRING OPERATIONS
*      (FOR THE MOST PART).  SPECIFICALLY, CFP_F IS THE
*      SAME FOR A BFBLK AS FOR AN SCBLK.  BY CONVENTION,
*      WHEREEVER A BUFFER VALUE IS EMPLOYED, THE BCBLK
*      IS POINTED TO.
*
*      THE CORRESPONDING BFBLK IS POINTED TO BY THE
*      BCBUF POINTER IN THE BCBLK.
*
*      BCLEN IS THE CURRENT DEFINED SIZE OF THE CHARACTER
*      ARRAY IN THE BFBLK.  CHARACTERS FOLLOWING THE OFFSET
*      OF BCLEN ARE UNDEFINED.
*
       EJC
*
*      STRING BUFFER BLOCK (BFBLK)
*
*      A BFBLK IS BUILT BY A CALL TO BUFFER(...)
*
*           +------------------------------------+
*           I                BFTYP               I
*           +------------------------------------+
*           I                BFALC               I
*           +------------------------------------+
*           /                                    /
*           /                BFCHR               /
*           /                                    /
*           +------------------------------------+
*
BFTYP  EQU  0                PTR TO DUMMY ROUTINE B_BFT
BFALC  EQU  BFTYP+1          ALLOCATED SIZE OF BUFFER
BFCHR  EQU  BFALC+1          CHARACTERS OF STRING
BFSI_  EQU  BFCHR            SIZE OF STANDARD FIELDS IN BFBLK
*
*      THE CHARACTERS IN THE BUFFER ARE STORED LEFT JUSTIFIED.
*      THE FINAL WORD OF DEFINED CHARACTERS IS ALWAYS ZERO
*      (CHARACTER) PADDED.  ANY TRAILING ALLOCATION PAST THE
*      WORD CONTAINING THE LAST CHARACTER CONTAINS
*      UNPREDICTABLE CONTENTS AND IS NEVER REFERENCED.
*
*      NOTE THAT THE OFFSET TO THE CHARACTERS OF THE STRING
*      IS GIVEN BY CFP_F, AS WITH AN SCBLK.  HOWEVER, THE
*      OFFSET WHICH IS OCCUPIED BY THE LENGTH FOR AN SCBLK
*      IS THE TOTAL CHAR SPACE FOR BFBLKS, AND ROUTINES WHICH
*      DEAL WITH BOTH MUST ACCOUNT FOR THIS DIFFERENCE.
*
*      THE VALUE OF BFALC MAY NOT EXCEED MXLEN.  THE VALUE OF
*      BCLEN IS ALWAYS LESS THAN OR EQUAL TO BFALC.
*
.FI
       EJC
*
*      CODE CONSTRUCTION BLOCK (CCBLK)
*
*      AT ANY ONE MOMENT THERE IS AT MOST ONE CCBLK INTO
*      WHICH THE COMPILER IS CURRENTLY STORING CODE (CDWRD).
*
*           +------------------------------------+
*           I                CCTYP               I
*           +------------------------------------+
*           I                CCLEN               I
.IF    .CSLN
*           +------------------------------------+
*           I                CCSLN               I
.FI
*           +------------------------------------+
*           I                CCUSE               I
*           +------------------------------------+
*           /                                    /
*           /                CCCOD               /
*           /                                    /
*           +------------------------------------+
*
CCTYP  EQU  0                POINTER TO DUMMY ROUTINE B_CCT
CCLEN  EQU  CCTYP+1          LENGTH OF CCBLK IN BYTES
.IF    .CSLN
CCSLN  EQU  CCLEN+1          SOURCE LINE NUMBER
CCUSE  EQU  CCSLN+1          OFFSET PAST LAST USED WORD (BYTES)
.ELSE
CCUSE  EQU  CCLEN+1          OFFSET PAST LAST USED WORD (BYTES)
.FI
CCCOD  EQU  CCUSE+1          START OF GENERATED CODE IN BLOCK
*
*      THE REASON THAT THE CCBLK IS A SEPARATE BLOCK TYPE FROM
*      THE USUAL CDBLK IS THAT THE GARBAGE COLLECTOR MUST
*      ONLY PROCESS THOSE FIELDS WHICH HAVE BEEN SET (SEE GBCPF)
       EJC
*
*      CODE BLOCK (CDBLK)
*
*      A CODE BLOCK IS BUILT FOR EACH STATEMENT COMPILED DURING
*      THE INITIAL COMPILATION OR BY SUBSEQUENT CALLS TO CODE.
*
*           +------------------------------------+
*           I                CDJMP               I
*           +------------------------------------+
*           I                CDSTM               I
.IF    .CSLN
*           +------------------------------------+
*           I                CDSLN               I
.FI
*           +------------------------------------+
*           I                CDLEN               I
*           +------------------------------------+
*           I                CDFAL               I
*           +------------------------------------+
*           /                                    /
*           /                CDCOD               /
*           /                                    /
*           +------------------------------------+
*
CDJMP  EQU  0                PTR TO ROUTINE TO EXECUTE STATEMENT
CDSTM  EQU  CDJMP+1          STATEMENT NUMBER
.IF    .CSLN
CDSLN  EQU  CDSTM+1          SOURCE LINE NUMBER
CDLEN  EQU  CDSLN+1          LENGTH OF CDBLK IN BYTES
CDFAL  EQU  CDLEN+1          FAILURE EXIT (SEE BELOW)
.ELSE
CDLEN  EQU  OFFS2            LENGTH OF CDBLK IN BYTES
CDFAL  EQU  OFFS3            FAILURE EXIT (SEE BELOW)
.FI
CDCOD  EQU  CDFAL+1          EXECUTABLE PSEUDO-CODE
CDSI_  EQU  CDCOD            NUMBER OF STANDARD FIELDS IN CDBLK
*
*      CDSTM IS THE STATEMENT NUMBER OF THE CURRENT STATEMENT.
*
*      CDJMP, CDFAL ARE SET AS FOLLOWS.
*
*      1)   IF THE FAILURE EXIT IS THE NEXT STATEMENT
*
*           CDJMP = B_CDS
*           CDFAL = PTR TO CDBLK FOR NEXT STATEMENT
*
*      2)   IF THE FAILURE EXIT IS A SIMPLE LABEL NAME
*
*           CDJMP = B_CDS
*           CDFAL IS A PTR TO THE VRTRA FIELD OF THE VRBLK
*
*      3)   IF THERE IS NO FAILURE EXIT (-NOFAIL MODE)
*
*           CDJMP = B_CDS
*           CDFAL = O_UNF
*
*      4)   IF THE FAILURE EXIT IS COMPLEX OR DIRECT
*
*           CDJMP = B_CDC
*           CDFAL IS THE OFFSET TO THE O_GOF WORD
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      CDCOD IS THE START OF THE ACTUAL CODE. FIRST WE DESCRIBE
*      THE CODE GENERATED FOR AN EXPRESSION. IN AN EXPRESSION,
*      ELEMENTS ARE FETCHED BY NAME OR BY VALUE. FOR EXAMPLE,
*      THE BINARY EQUAL OPERATOR FETCHES ITS LEFT ARGUMENT
*      BY NAME AND ITS RIGHT ARGUMENT BY VALUE. THESE TWO
*      CASES GENERATE QUITE DIFFERENT CODE AND ARE DESCRIBED
*      SEPARATELY. FIRST WE CONSIDER THE CODE BY VALUE CASE.
*
*      GENERATION OF CODE BY VALUE FOR EXPRESSIONS ELEMENTS.
*
*      EXPRESSION            POINTER TO EXBLK OR SEBLK
*
*      INTEGER CONSTANT      POINTER TO ICBLK
*
*      NULL CONSTANT         POINTER TO NULLS
*
*      PATTERN               (RESULTING FROM PREEVALUATION)
*                            =O_LPT
*                            POINTER TO P0BLK,P1BLK OR P2BLK
*
*      REAL CONSTANT         POINTER TO RCBLK
*
*      STRING CONSTANT       POINTER TO SCBLK
*
*      VARIABLE              POINTER TO VRGET FIELD OF VRBLK
*
*      ADDITION              VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_ADD
*
*      AFFIRMATION           VALUE CODE FOR OPERAND
*                            =O_AFF
*
*      ALTERNATION           VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_ALT
*
*      ARRAY REFERENCE       (CASE OF ONE SUBSCRIPT)
*                            VALUE CODE FOR ARRAY OPERAND
*                            VALUE CODE FOR SUBSCRIPT OPERAND
*                            =O_AOV
*
*                            (CASE OF MORE THAN ONE SUBSCRIPT)
*                            VALUE CODE FOR ARRAY OPERAND
*                            VALUE CODE FOR FIRST SUBSCRIPT
*                            VALUE CODE FOR SECOND SUBSCRIPT
*                            ...
*                            VALUE CODE FOR LAST SUBSCRIPT
*                            =O_AMV
*                            NUMBER OF SUBSCRIPTS
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      ASSIGNMENT            (TO NATURAL VARIABLE)
*                            VALUE CODE FOR RIGHT OPERAND
*                            POINTER TO VRSTO FIELD OF VRBLK
*
*                            (TO ANY OTHER VARIABLE)
*                            NAME CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_ASS
*
*      COMPILE ERROR         =O_CER
*
*
*      COMPLEMENTATION       VALUE CODE FOR OPERAND
*                            =O_COM
*
*      CONCATENATION         (CASE OF PRED FUNC LEFT OPERAND)
*                            VALUE CODE FOR LEFT OPERAND
*                            =O_POP
*                            VALUE CODE FOR RIGHT OPERAND
*
*                            (ALL OTHER CASES)
*                            VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_CNC
*
*      CURSOR ASSIGNMENT     NAME CODE FOR OPERAND
*                            =O_CAS
*
*      DIVISION              VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_DVD
*
*      EXPONENTIATION        VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_EXP
*
*      FUNCTION CALL         (CASE OF CALL TO SYSTEM FUNCTION)
*                            VALUE CODE FOR FIRST ARGUMENT
*                            VALUE CODE FOR SECOND ARGUMENT
*                            ...
*                            VALUE CODE FOR LAST ARGUMENT
*                            POINTER TO SVFNC FIELD OF SVBLK
*
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      FUNCTION CALL         (CASE OF NON-SYSTEM FUNCTION 1 ARG)
*                            VALUE CODE FOR ARGUMENT
*                            =O_FNS
*                            POINTER TO VRBLK FOR FUNCTION
*
*                            (NON-SYSTEM FUNCTION, GT 1 ARG)
*                            VALUE CODE FOR FIRST ARGUMENT
*                            VALUE CODE FOR SECOND ARGUMENT
*                            ...
*                            VALUE CODE FOR LAST ARGUMENT
*                            =O_FNC
*                            NUMBER OF ARGUMENTS
*                            POINTER TO VRBLK FOR FUNCTION
*
*      IMMEDIATE ASSIGNMENT  VALUE CODE FOR LEFT OPERAND
*                            NAME CODE FOR RIGHT OPERAND
*                            =O_IMA
*
*      INDIRECTION           VALUE CODE FOR OPERAND
*                            =O_INV
*
*      INTERROGATION         VALUE CODE FOR OPERAND
*                            =O_INT
*
*      KEYWORD REFERENCE     NAME CODE FOR OPERAND
*                            =O_KWV
*
*      MULTIPLICATION        VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_MLT
*
*      NAME REFERENCE        (NATURAL VARIABLE CASE)
*                            POINTER TO NMBLK FOR NAME
*
*                            (ALL OTHER CASES)
*                            NAME CODE FOR OPERAND
*                            =O_NAM
*
*      NEGATION              =O_NTA
*                            CDBLK OFFSET OF O_NTC WORD
*                            VALUE CODE FOR OPERAND
*                            =O_NTB
*                            =O_NTC
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      PATTERN ASSIGNMENT    VALUE CODE FOR LEFT OPERAND
*                            NAME CODE FOR RIGHT OPERAND
*                            =O_PAS
*
*      PATTERN MATCH         VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_PMV
*
*      PATTERN REPLACEMENT   NAME CODE FOR SUBJECT
*                            VALUE CODE FOR PATTERN
*                            =O_PMN
*                            VALUE CODE FOR REPLACEMENT
*                            =O_RPL
*
*      SELECTION             (FOR FIRST ALTERNATIVE)
*                            =O_SLA
*                            CDBLK OFFSET TO NEXT O_SLC WORD
*                            VALUE CODE FOR FIRST ALTERNATIVE
*                            =O_SLB
*                            CDBLK OFFSET PAST ALTERNATIVES
*
*                            (FOR SUBSEQUENT ALTERNATIVES)
*                            =O_SLC
*                            CDBLK OFFSET TO NEXT O_SLC,O_SLD
*                            VALUE CODE FOR ALTERNATIVE
*                            =O_SLB
*                            OFFSET IN CDBLK PAST ALTERNATIVES
*
*                            (FOR LAST ALTERNATIVE)
*                            =O_SLD
*                            VALUE CODE FOR LAST ALTERNATIVE
*
*      SUBTRACTION           VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_SUB
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      GENERATION OF CODE BY NAME FOR EXPRESSION ELEMENTS.
*
*      VARIABLE              =O_LVN
*                            POINTER TO VRBLK
*
*      EXPRESSION            (CASE OF *NATURAL VARIABLE)
*                            =O_LVN
*                            POINTER TO VRBLK
*
*                            (ALL OTHER CASES)
*                            =O_LEX
*                            POINTER TO EXBLK
*
*
*      ARRAY REFERENCE       (CASE OF ONE SUBSCRIPT)
*                            VALUE CODE FOR ARRAY OPERAND
*                            VALUE CODE FOR SUBSCRIPT OPERAND
*                            =O_AON
*
*                            (CASE OF MORE THAN ONE SUBSCRIPT)
*                            VALUE CODE FOR ARRAY OPERAND
*                            VALUE CODE FOR FIRST SUBSCRIPT
*                            VALUE CODE FOR SECOND SUBSCRIPT
*                            ...
*                            VALUE CODE FOR LAST SUBSCRIPT
*                            =O_AMN
*                            NUMBER OF SUBSCRIPTS
*
*      COMPILE ERROR         =O_CER
*
*      FUNCTION CALL         (SAME CODE AS FOR VALUE CALL)
*                            =O_FNE
*
*      INDIRECTION           VALUE CODE FOR OPERAND
*                            =O_INN
*
*      KEYWORD REFERENCE     NAME CODE FOR OPERAND
*                            =O_KWN
*
*      ANY OTHER OPERAND IS AN ERROR IN A NAME POSITION
*
*      NOTE THAT IN THIS DESCRIPTION, =O_XXX REFERS TO THE
*      GENERATION OF A WORD CONTAINING THE ADDRESS OF ANOTHER
*      WORD WHICH CONTAINS THE ENTRY POINT ADDRESS O_XXX.
       EJC
*
*      CODE BLOCK (CONTINUED)
*
*      NOW WE CONSIDER THE OVERALL STRUCTURE OF THE CODE BLOCK
*      FOR A STATEMENT WITH POSSIBLE GOTO FIELDS.
*
*      FIRST COMES THE CODE FOR THE STATEMENT BODY.
*      THE STATEMENT BODY IS AN EXPRESSION TO BE EVALUATED
*      BY VALUE ALTHOUGH THE VALUE IS NOT ACTUALLY REQUIRED.
*      NORMAL VALUE CODE IS GENERATED FOR THE BODY OF THE
*      STATEMENT EXCEPT IN THE CASE OF A PATTERN MATCH BY
*      VALUE, IN WHICH CASE THE FOLLOWING IS GENERATED.
*
*                            VALUE CODE FOR LEFT OPERAND
*                            VALUE CODE FOR RIGHT OPERAND
*                            =O_PMS
*
*      NEXT WE HAVE THE CODE FOR THE SUCCESS GOTO. THERE ARE
*      SEVERAL CASES AS FOLLOWS.
*
*      1)   NO SUCCESS GOTO  PTR TO CDBLK FOR NEXT STATEMENT
*
*      2)   SIMPLE LABEL     PTR TO VRTRA FIELD OF VRBLK
*
*      3)   COMPLEX GOTO     (CODE BY NAME FOR GOTO OPERAND)
*                            =O_GOC
*
*      4)   DIRECT GOTO      (CODE BY VALUE FOR GOTO OPERAND)
*                            =O_GOD
*
*      FOLLOWING THIS WE GENERATE CODE FOR THE FAILURE GOTO IF
*      IT IS DIRECT OR IF IT IS COMPLEX, SIMPLE FAILURE GOTOS
*      HAVING BEEN HANDLED BY AN APPROPRIATE SETTING OF THE
*      CDFAL FIELD OF THE CDBLK. THE GENERATED CODE IS ONE
*      OF THE FOLLOWING.
*
*      1)   COMPLEX FGOTO    =O_FIF
*                            =O_GOF
*                            NAME CODE FOR GOTO OPERAND
*                            =O_GOC
*
*      2)   DIRECT FGOTO     =O_FIF
*                            =O_GOF
*                            VALUE CODE FOR GOTO OPERAND
*                            =O_GOD
*
*      AN OPTIMIZATION OCCURS IF THE SUCCESS AND FAILURE GOTOS
*      ARE IDENTICAL AND EITHER COMPLEX OR DIRECT. IN THIS CASE,
*      NO CODE IS GENERATED FOR THE SUCCESS GOTO AND CONTROL
*      IS ALLOWED TO FALL INTO THE FAILURE GOTO ON SUCCESS.
       EJC
*
*      COMPILER BLOCK (CMBLK)
*
*      A COMPILER BLOCK (CMBLK) IS BUILT BY EXPAN TO REPRESENT
*      ONE NODE OF A TREE STRUCTURED EXPRESSION REPRESENTATION.
*
*           +------------------------------------+
*           I                CMIDN               I
*           +------------------------------------+
*           I                CMLEN               I
*           +------------------------------------+
*           I                CMTYP               I
*           +------------------------------------+
*           I                CMOPN               I
*           +------------------------------------+
*           /           CMVLS OR CMROP           /
*           /                                    /
*           /                CMLOP               /
*           /                                    /
*           +------------------------------------+
*
CMIDN  EQU  0                POINTER TO DUMMY ROUTINE B_CMT
CMLEN  EQU  CMIDN+1          LENGTH OF CMBLK IN BYTES
CMTYP  EQU  CMLEN+1          TYPE (C_XXX, SEE LIST BELOW)
CMOPN  EQU  CMTYP+1          OPERAND POINTER (SEE BELOW)
CMVLS  EQU  CMOPN+1          OPERAND VALUE POINTERS (SEE BELOW)
CMROP  EQU  CMVLS            RIGHT (ONLY) OPERATOR OPERAND
CMLOP  EQU  CMVLS+1          LEFT OPERATOR OPERAND
CMSI_  EQU  CMVLS            NUMBER OF STANDARD FIELDS IN CMBLK
CMUS_  EQU  CMSI_+1          SIZE OF UNARY OPERATOR CMBLK
CMBS_  EQU  CMSI_+2          SIZE OF BINARY OPERATOR CMBLK
CMAR1  EQU  CMVLS+1          ARRAY SUBSCRIPT POINTERS
*
*      THE CMOPN AND CMVLS FIELDS ARE SET AS FOLLOWS
*
*      ARRAY REFERENCE       CMOPN = PTR TO ARRAY OPERAND
*                            CMVLS = PTRS TO SUBSCRIPT OPERANDS
*
*      FUNCTION CALL         CMOPN = PTR TO VRBLK FOR FUNCTION
*                            CMVLS = PTRS TO ARGUMENT OPERANDS
*
*      SELECTION             CMOPN = ZERO
*                            CMVLS = PTRS TO ALTERNATE OPERANDS
*
*      UNARY OPERATOR        CMOPN = PTR TO OPERATOR DVBLK
*                            CMROP = PTR TO OPERAND
*
*      BINARY OPERATOR       CMOPN = PTR TO OPERATOR DVBLK
*                            CMROP = PTR TO RIGHT OPERAND
*                            CMLOP = PTR TO LEFT OPERAND
       EJC
*
*      CMTYP IS SET TO INDICATE THE TYPE OF EXPRESSION ELEMENT
*      AS SHOWN BY THE FOLLOWING TABLE OF DEFINITIONS.
*
C_ARR  EQU  0                ARRAY REFERENCE
C_FNC  EQU  C_ARR+1          FUNCTION CALL
C_DEF  EQU  C_FNC+1          DEFERRED EXPRESSION (UNARY *)
C_IND  EQU  C_DEF+1          INDIRECTION (UNARY _)
C_KEY  EQU  C_IND+1          KEYWORD REFERENCE (UNARY AMPERSAND)
C_UBO  EQU  C_KEY+1          UNDEFINED BINARY OPERATOR
C_UUO  EQU  C_UBO+1          UNDEFINED UNARY OPERATOR
C_UO_  EQU  C_UUO+1          TEST VALUE (=C_UUO+1=C_UBO+2)
C__NM  EQU  C_UUO+1          NUMBER OF CODES FOR NAME OPERANDS
*
*      THE REMAINING TYPES INDICATE EXPRESSION ELEMENTS WHICH
*      CAN ONLY BE EVALUATED BY VALUE (NOT BY NAME).
*
C_BVL  EQU  C_UUO+1          BINARY OP WITH VALUE OPERANDS
C_UVL  EQU  C_BVL+1          UNARY OPERATOR WITH VALUE OPERAND
C_ALT  EQU  C_UVL+1          ALTERNATION (BINARY BAR)
C_CNC  EQU  C_ALT+1          CONCATENATION
C_CNP  EQU  C_CNC+1          CONCATENATION, NOT PATTERN MATCH
C_UNM  EQU  C_CNP+1          UNARY OP WITH NAME OPERAND
C_BVN  EQU  C_UNM+1          BINARY OP (OPERANDS BY VALUE, NAME)
C_ASS  EQU  C_BVN+1          ASSIGNMENT
C_INT  EQU  C_ASS+1          INTERROGATION
C_NEG  EQU  C_INT+1          NEGATION (UNARY NOT)
C_SEL  EQU  C_NEG+1          SELECTION
C_PMT  EQU  C_SEL+1          PATTERN MATCH
*
C_PR_  EQU  C_BVN            LAST PREEVALUABLE CODE
C__NV  EQU  C_PMT+1          NUMBER OF DIFFERENT CMBLK TYPES
       EJC
*
*      CHARACTER TABLE BLOCK (CTBLK)
*
*      A CHARACTER TABLE BLOCK IS USED TO HOLD LOGICAL CHARACTER
*      TABLES FOR USE WITH ANY,NOTANY,SPAN,BREAK,BREAKX
*      PATTERNS. EACH CHARACTER TABLE CAN BE USED TO STORE
*      CFP_N DISTINCT TABLES AS BIT COLUMNS. A BIT COLUMN
*      ALLOCATED FOR EACH ARGUMENT OF MORE THAN ONE CHARACTER
*      IN LENGTH TO ONE OF THE ABOVE LISTED PATTERN PRIMITIVES.
*
*           +------------------------------------+
*           I                CTTYP               I
*           +------------------------------------+
*           *                                    *
*           *                                    *
*           *                CTCHS               *
*           *                                    *
*           *                                    *
*           +------------------------------------+
*
CTTYP  EQU  0                POINTER TO DUMMY ROUTINE B_CTT
CTCHS  EQU  CTTYP+1          START OF CHARACTER TABLE WORDS
CTSI_  EQU  CTCHS+CFP_A      NUMBER OF WORDS IN CTBLK
*
*      CTCHS IS CFP_A WORDS LONG AND CONSISTS OF A ONE WORD
*      BIT STRING VALUE FOR EACH POSSIBLE CHARACTER IN THE
*      INTERNAL ALPHABET. EACH OF THE CFP_N POSSIBLE BITS IN
*      A BITSTRING IS USED TO FORM A COLUMN OF BIT INDICATORS.
*      A BIT IS SET ON IF THE CHARACTER IS IN THE TABLE AND OFF
*      IF THE CHARACTER IS NOT PRESENT.
       EJC
*
*      DATATYPE FUNCTION BLOCK (DFBLK)
*
*      A DATATYPE FUNCTION IS USED TO CONTROL THE CONSTRUCTION
*      OF A PROGRAM DEFINED DATATYPE OBJECT. A CALL TO THE
*      SYSTEM FUNCTION DATA BUILDS A DFBLK FOR THE DATATYPE NAME
*
*      NOTE THAT THESE BLOCKS ARE BUILT IN STATIC BECAUSE PDBLK
*      LENGTH IS GOT FROM DFLEN FIELD.  IF DFBLK WAS IN DYNAMIC
*      STORE THIS WOULD CAUSE TROUBLE DURING PASS TWO OF GARBAGE
*      COLLECTION.  SCBLK REFERRED TO BY DFNAM FIELD IS ALSO PUT
*      IN STATIC SO THAT THERE ARE NO RELOC. FIELDS. THIS CUTS
*      GARBAGE COLLECTION TASK APPRECIABLY FOR PDBLKS WHICH ARE
*      LIKELY TO BE PRESENT IN LARGE NUMBERS.
*
*           +------------------------------------+
*           I                FCODE               I
*           +------------------------------------+
*           I                FARGS               I
*           +------------------------------------+
*           I                DFLEN               I
*           +------------------------------------+
*           I                DFPDL               I
*           +------------------------------------+
*           I                DFNAM               I
*           +------------------------------------+
*           /                                    /
*           /                DFFLD               /
*           /                                    /
*           +------------------------------------+
*
DFLEN  EQU  FARGS+1          LENGTH OF DFBLK IN BYTES
DFPDL  EQU  DFLEN+1          LENGTH OF CORRESPONDING PDBLK
DFNAM  EQU  DFPDL+1          POINTER TO SCBLK FOR DATATYPE NAME
DFFLD  EQU  DFNAM+1          START OF VRBLK PTRS FOR FIELD NAMES
DFFLB  EQU  DFFLD-1          OFFSET BEHIND DFFLD FOR FIELD FUNC
DFSI_  EQU  DFFLD            NUMBER OF STANDARD FIELDS IN DFBLK
*
*      THE FCODE FIELD POINTS TO THE ROUTINE B_DFC
*
*      FARGS (THE NUMBER OF ARGUMENTS) IS THE NUMBER OF FIELDS.
       EJC
*
*      DOPE VECTOR BLOCK (DVBLK)
*
*      A DOPE VECTOR IS ASSEMBLED FOR EACH POSSIBLE OPERATOR IN
*      THE SNOBOL4 LANGUAGE AS PART OF THE CONSTANT SECTION.
*
*           +------------------------------------+
*           I                DVOPN               I
*           +------------------------------------+
*           I                DVTYP               I
*           +------------------------------------+
*           I                DVLPR               I
*           +------------------------------------+
*           I                DVRPR               I
*           +------------------------------------+
*
DVOPN  EQU  0                ENTRY ADDRESS (PTR TO O_XXX)
DVTYP  EQU  DVOPN+1          TYPE CODE (C_XXX, SEE CMBLK)
DVLPR  EQU  DVTYP+1          LEFT PRECEDENCE (LLXXX, SEE BELOW)
DVRPR  EQU  DVLPR+1          RIGHT PRECEDENCE (RRXXX, SEE BELOW)
DVUS_  EQU  DVLPR+1          SIZE OF UNARY OPERATOR DV
DVBS_  EQU  DVRPR+1          SIZE OF BINARY OPERATOR DV
DVUBS  EQU  DVUS_+DVBS_      SIZE OF UNOP + BINOP (SEE SCANE)
*
*      THE CONTENTS OF THE DVTYP FIELD IS COPIED INTO THE CMTYP
*      FIELD OF THE CMBLK FOR THE OPERATOR IF IT IS USED.
*
*      THE CMOPN FIELD OF AN OPERATOR CMBLK POINTS TO THE DVBLK
*      ITSELF, PROVIDING THE REQUIRED ENTRY ADDRESS POINTER PTR.
*
*      FOR NORMALLY UNDEFINED OPERATORS, THE DVOPN (AND CMOPN)
*      FIELDS CONTAIN A WORD OFFSET FROM R_UBA OF THE FUNCTION
*      BLOCK POINTER FOR THE OPERATOR (INSTEAD OF O_XXX PTR).
*      FOR CERTAIN SPECIAL OPERATORS, THE DVOPN FIELD IS NOT
*      REQUIRED AT ALL AND IS ASSEMBLED AS ZERO.
*
*      THE LEFT PRECEDENCE IS USED IN COMPARING AN OPERATOR TO
*      THE LEFT OF SOME OTHER OPERATOR. IT THEREFORE GOVERNS THE
*      PRECEDENCE OF THE OPERATOR TOWARDS ITS RIGHT OPERAND.
*
*      THE RIGHT PRECEDENCE IS USED IN COMPARING AN OPERATOR TO
*      THE RIGHT OF SOME OTHER OPERATOR. IT THEREFORE GOVERNS
*      THE PRECEDENCE OF THE OPERATOR TOWARDS ITS LEFT OPERAND.
*
*      HIGHER PRECEDENCE VALUES CORRESPOND TO A TIGHTER BINDING
*      CAPABILITY. THUS WE HAVE THE LEFT PRECEDENCE LOWER
*      (HIGHER) THAN THE RIGHT PRECEDENCE FOR RIGHT (LEFT)
*      ASSOCIATIVE BINARY OPERATORS.
*
*      THE LEFT PRECEDENCE OF UNARY OPERATORS IS SET TO AN
*      ARBITRARY HIGH VALUE. THE RIGHT VALUE IS NOT REQUIRED AND
*      CONSEQUENTLY THE DVRPR FIELD IS OMITTED FOR UNARY OPS.
       EJC
*
*      TABLE OF OPERATOR PRECEDENCE VALUES
*
RRASS  EQU  10               RIGHT     EQUAL
LLASS  EQU  00               LEFT      EQUAL
RRPMT  EQU  20               RIGHT     QUESTION MARK
LLPMT  EQU  30               LEFT      QUESTION MARK
RRAMP  EQU  40               RIGHT     AMPERSAND
LLAMP  EQU  50               LEFT      AMPERSAND
RRALT  EQU  70               RIGHT     VERTICAL BAR
LLALT  EQU  60               LEFT      VERTICAL BAR
RRCNC  EQU  90               RIGHT     BLANK
LLCNC  EQU  80               LEFT      BLANK
RRATS  EQU  110              RIGHT     AT
LLATS  EQU  100              LEFT      AT
RRPLM  EQU  120              RIGHT     PLUS, MINUS
LLPLM  EQU  130              LEFT      PLUS, MINUS
RRNUM  EQU  140              RIGHT     NUMBER
LLNUM  EQU  150              LEFT      NUMBER
RRDVD  EQU  160              RIGHT     SLASH
LLDVD  EQU  170              LEFT      SLASH
RRMLT  EQU  180              RIGHT     ASTERISK
LLMLT  EQU  190              LEFT      ASTERISK
RRPCT  EQU  200              RIGHT     PERCENT
LLPCT  EQU  210              LEFT      PERCENT
RREXP  EQU  230              RIGHT     EXCLAMATION
LLEXP  EQU  220              LEFT      EXCLAMATION
RRDLD  EQU  240              RIGHT     DOLLAR, DOT
LLDLD  EQU  250              LEFT      DOLLAR, DOT
RRNOT  EQU  270              RIGHT     NOT
LLNOT  EQU  260              LEFT      NOT
LLUNO  EQU  999              LEFT      ALL UNARY OPERATORS
*
*      PRECEDENCES ARE THE SAME AS IN BTL SNOBOL4 WITH THE
*      FOLLOWING EXCEPTIONS.
*
*      1)   BINARY QUESTION MARK IS LOWERED AND MADE LEFT ASSOC-
*           IATIVE TO REFLECT ITS NEW USE FOR PATTERN MATCHING.
*
*      2)   ALTERNATION AND CONCATENATION ARE MADE RIGHT
*           ASSOCIATIVE FOR GREATER EFFICIENCY IN PATTERN
*           CONSTRUCTION AND MATCHING RESPECTIVELY. THIS CHANGE
*           IS TRANSPARENT TO THE SNOBOL4 PROGRAMMER.
*
*      3)   THE EQUAL SIGN HAS BEEN ADDED AS A LOW PRECEDENCE
*           OPERATOR WHICH IS RIGHT ASSOCIATIVE TO REFLECT ITS
*           MORE GENERAL USAGE IN THIS VERSION OF SNOBOL4.
       EJC
*
*      EXTERNAL FUNCTION BLOCK (EFBLK)
*
*      AN EXTERNAL FUNCTION BLOCK IS USED TO CONTROL THE CALLING
*      OF AN EXTERNAL FUNCTION. IT IS BUILT BY A CALL TO LOAD.
*
*           +------------------------------------+
*           I                FCODE               I
*           +------------------------------------+
*           I                FARGS               I
*           +------------------------------------+
*           I                EFLEN               I
*           +------------------------------------+
*           I                EFUSE               I
*           +------------------------------------+
*           I                EFCOD               I
*           +------------------------------------+
*           I                EFVAR               I
*           +------------------------------------+
*           I                EFRSL               I
*           +------------------------------------+
*           /                                    /
*           /                EFTAR               /
*           /                                    /
*           +------------------------------------+
*
EFLEN  EQU  FARGS+1          LENGTH OF EFBLK IN BYTES
EFUSE  EQU  EFLEN+1          USE COUNT (FOR OPSYN)
EFCOD  EQU  EFUSE+1          PTR TO CODE (FROM SYSLD)
EFVAR  EQU  EFCOD+1          PTR TO ASSOCIATED VRBLK
EFRSL  EQU  EFVAR+1          RESULT TYPE (SEE BELOW)
EFTAR  EQU  EFRSL+1          ARGUMENT TYPES (SEE BELOW)
EFSI_  EQU  EFTAR            NUMBER OF STANDARD FIELDS IN EFBLK
*
*      THE FCODE FIELD POINTS TO THE ROUTINE B_EFC.
*
*      EFUSE IS USED TO KEEP TRACK OF MULTIPLE USE WHEN OPSYN
*      IS EMPLOYED. THE FUNCTION IS AUTOMATICALLY UNLOADED
*      WHEN THERE ARE NO MORE REFERENCES TO THE FUNCTION.
*
*      EFRSL AND EFTAR ARE TYPE CODES AS FOLLOWS.
*
*           0                TYPE IS UNCONVERTED
*           1                TYPE IS STRING
*           2                TYPE IS INTEGER
.IF    .CNRA
.IF    .CNLF
*           3                TYPE IS FILE
.FI
.ELSE
*           3                TYPE IS REAL
.IF    .CNLF
*           4                TYPE IS FILE
.FI
.FI
       EJC
*
*      EXPRESSION VARIABLE BLOCK (EVBLK)
*
*      IN THIS VERSION OF SPITBOL, AN EXPRESSION CAN BE USED IN
*      ANY POSITION WHICH WOULD NORMALLY EXPECT A NAME (FOR
*      EXAMPLE ON THE LEFT SIDE OF EQUALS OR AS THE RIGHT
*      ARGUMENT OF BINARY DOT). THIS CORRESPONDS TO THE CREATION
*      OF A PSEUDO-VARIABLE WHICH IS REPRESENTED BY A POINTER TO
*      AN EXPRESSION VARIABLE BLOCK AS FOLLOWS.
*
*           +------------------------------------+
*           I                EVTYP               I
*           +------------------------------------+
*           I                EVEXP               I
*           +------------------------------------+
*           I                EVVAR               I
*           +------------------------------------+
*
EVTYP  EQU  0                POINTER TO DUMMY ROUTINE B_EVT
EVEXP  EQU  EVTYP+1          POINTER TO EXBLK FOR EXPRESSION
EVVAR  EQU  EVEXP+1          POINTER TO TRBEV DUMMY TRBLK
EVSI_  EQU  EVVAR+1          SIZE OF EVBLK
*
*      THE NAME OF AN EXPRESSION VARIABLE IS REPRESENTED BY A
*      BASE POINTER TO THE EVBLK AND AN OFFSET OF EVVAR. THIS
*      VALUE APPEARS TO BE TRAPPED BY THE DUMMY TRBEV BLOCK.
*
*      NOTE THAT THERE IS NO NEED TO ALLOW FOR THE CASE OF AN
*      EXPRESSION VARIABLE WHICH REFERENCES AN SEBLK SINCE A
*      VARIABLE WHICH IS OF THE FORM *VAR IS EQUIVALENT TO VAR.
       EJC
*
*      EXPRESSION BLOCK (EXBLK)
*
*      AN EXPRESSION BLOCK IS BUILT FOR EACH EXPRESSION
*      REFERENCED IN A PROGRAM OR CREATED BY EVAL OR CONVERT
*      DURING EXECUTION OF A PROGRAM.
*
*           +------------------------------------+
*           I                EXTYP               I
*           +------------------------------------+
*           I                EXSTM               I
.IF    .CSLN
*           +------------------------------------+
*           I                EXSLN               I
.FI
*           +------------------------------------+
*           I                EXLEN               I
*           +------------------------------------+
*           I                EXFLC               I
*           +------------------------------------+
*           /                                    /
*           /                EXCOD               /
*           /                                    /
*           +------------------------------------+
*
EXTYP  EQU  0                PTR TO ROUTINE B_EXL TO LOAD EXPR
EXSTM  EQU  CDSTM            STORES STMNT NO. DURING EVALUATION
.IF    .CSLN
EXSLN  EQU  EXSTM+1          STORES LINE NO. DURING EVALUATION
EXLEN  EQU  EXSLN+1          LENGTH OF EXBLK IN BYTES
.ELSE
EXLEN  EQU  EXSTM+1          LENGTH OF EXBLK IN BYTES
.FI
EXFLC  EQU  EXLEN+1          FAILURE CODE (=O_FEX)
EXCOD  EQU  EXFLC+1          PSEUDO-CODE FOR EXPRESSION
EXSI_  EQU  EXCOD            NUMBER OF STANDARD FIELDS IN EXBLK
*
*      THERE ARE TWO CASES FOR EXCOD DEPENDING ON WHETHER THE
*      EXPRESSION CAN BE EVALUATED BY NAME (SEE DESCRIPTION
*      OF CDBLK FOR DETAILS OF CODE FOR EXPRESSIONS).
*
*      IF THE EXPRESSION CAN BE EVALUATED BY NAME WE HAVE.
*
*                            (CODE FOR EXPR BY NAME)
*                            =O_RNM
*
*      IF THE EXPRESSION CAN ONLY BE EVALUATED BY VALUE.
*
*                            (CODE FOR EXPR BY VALUE)
*                            =O_RVL
       EJC
*
*      FIELD FUNCTION BLOCK (FFBLK)
*
*      A FIELD FUNCTION BLOCK IS USED TO CONTROL THE SELECTION
*      OF A FIELD FROM A PROGRAM DEFINED DATATYPE BLOCK.
*      A CALL TO DATA CREATES AN FFBLK FOR EACH FIELD.
*
*           +------------------------------------+
*           I                FCODE               I
*           +------------------------------------+
*           I                FARGS               I
*           +------------------------------------+
*           I                FFDFP               I
*           +------------------------------------+
*           I                FFNXT               I
*           +------------------------------------+
*           I                FFOFS               I
*           +------------------------------------+
*
FFDFP  EQU  FARGS+1          POINTER TO ASSOCIATED DFBLK
FFNXT  EQU  FFDFP+1          PTR TO NEXT FFBLK ON CHAIN OR ZERO
FFOFS  EQU  FFNXT+1          OFFSET (BYTES) TO FIELD IN PDBLK
FFSI_  EQU  FFOFS+1          SIZE OF FFBLK IN WORDS
*
*      THE FCODE FIELD POINTS TO THE ROUTINE B_FFC.
*
*      FARGS ALWAYS CONTAINS ONE.
*
*      FFDFP IS USED TO VERIFY THAT THE CORRECT PROGRAM DEFINED
*      DATATYPE IS BEING ACCESSED BY THIS CALL.
*      FFDFP IS NON-RELOC. BECAUSE DFBLK IS IN STATIC
*
*      FFOFS IS USED TO SELECT THE APPROPRIATE FIELD. NOTE THAT
*      IT IS AN ACTUAL OFFSET (NOT A FIELD NUMBER)
*
*      FFNXT IS USED TO POINT TO THE NEXT FFBLK OF THE SAME NAME
*      IN THE CASE WHERE THERE ARE SEVERAL FIELDS OF THE SAME
*      NAME FOR DIFFERENT DATATYPES. ZERO MARKS THE END OF CHAIN
       EJC
*
*      INTEGER CONSTANT BLOCK (ICBLK)
*
*      AN ICBLK IS CREATED FOR EVERY INTEGER REFERENCED OR
*      CREATED BY A PROGRAM. NOTE HOWEVER THAT CERTAIN INTERNAL
*      INTEGER VALUES ARE STORED AS ADDRESSES (E.G. THE LENGTH
*      FIELD IN A STRING CONSTANT BLOCK)
*
*           +------------------------------------+
*           I                ICGET               I
*           +------------------------------------+
*           *                ICVAL               *
*           +------------------------------------+
*
ICGET  EQU  0                PTR TO ROUTINE B_ICL TO LOAD INT
ICVAL  EQU  ICGET+1          INTEGER VALUE
ICSI_  EQU  ICVAL+CFP_I      SIZE OF ICBLK
*
*      THE LENGTH OF THE ICVAL FIELD IS CFP_I.
       EJC
*
*      KEYWORD VARIABLE BLOCK (KVBLK)
*
*      A KVBLK IS USED TO REPRESENT A KEYWORD PSEUDO-VARIABLE.
*      A KVBLK IS BUILT FOR EACH KEYWORD REFERENCE (KWNAM).
*
*           +------------------------------------+
*           I                KVTYP               I
*           +------------------------------------+
*           I                KVVAR               I
*           +------------------------------------+
*           I                KVNUM               I
*           +------------------------------------+
*
KVTYP  EQU  0                POINTER TO DUMMY ROUTINE B_KVT
KVVAR  EQU  KVTYP+1          POINTER TO DUMMY BLOCK TRBKV
KVNUM  EQU  KVVAR+1          KEYWORD NUMBER
KVSI_  EQU  KVNUM+1          SIZE OF KVBLK
*
*      THE NAME OF A KEYWORD VARIABLE IS REPRESENTED BY A
*      BASE POINTER TO THE KVBLK AND AN OFFSET OF KVVAR. THE
*      VALUE APPEARS TO BE TRAPPED BY THE POINTER TO TRBKV.
       EJC
*
*      NAME BLOCK (NMBLK)
*
*      A NAME BLOCK IS USED WHEREVER A NAME MUST BE STORED AS
*      A VALUE FOLLOWING USE OF THE UNARY DOT OPERATOR.
*
*           +------------------------------------+
*           I                NMTYP               I
*           +------------------------------------+
*           I                NMBAS               I
*           +------------------------------------+
*           I                NMOFS               I
*           +------------------------------------+
*
NMTYP  EQU  0                PTR TO ROUTINE B_NML TO LOAD NAME
NMBAS  EQU  NMTYP+1          BASE POINTER FOR VARIABLE
NMOFS  EQU  NMBAS+1          OFFSET FOR VARIABLE
NMSI_  EQU  NMOFS+1          SIZE OF NMBLK
*
*      THE ACTUAL FIELD REPRESENTING THE CONTENTS OF THE NAME
*      IS FOUND NMOFS BYTES PAST THE ADDRESS IN NMBAS.
*
*      THE NAME IS SPLIT INTO BASE AND OFFSET FORM TO AVOID
*      CREATION OF A POINTER INTO THE MIDDLE OF A BLOCK WHICH
*      COULD NOT BE HANDLED PROPERLY BY THE GARBAGE COLLECTOR.
*
*      A NAME MAY BE BUILT FOR ANY VARIABLE (SEE SECTION ON
*      REPRESENTATIONS OF VARIABLES) THIS INCLUDES THE
*      CASES OF PSEUDO-VARIABLES.
       EJC
*
*      PATTERN BLOCK, NO PARAMETERS (P0BLK)
*
*      A P0BLK IS USED TO REPRESENT PATTERN NODES WHICH DO
*      NOT REQUIRE THE USE OF ANY PARAMETER VALUES.
*
*           +------------------------------------+
*           I                PCODE               I
*           +------------------------------------+
*           I                PTHEN               I
*           +------------------------------------+
*
PCODE  EQU  0                PTR TO MATCH ROUTINE (P_XXX)
PTHEN  EQU  PCODE+1          POINTER TO SUBSEQUENT NODE
PASI_  EQU  PTHEN+1          SIZE OF P0BLK
*
*      PTHEN POINTS TO THE PATTERN BLOCK FOR THE SUBSEQUENT
*      NODE TO BE MATCHED. THIS IS A POINTER TO THE PATTERN
*      BLOCK NDNTH IF THERE IS NO SUBSEQUENT (END OF PATTERN)
*
*      PCODE IS A POINTER TO THE MATCH ROUTINE FOR THE NODE.
       EJC
*
*      PATTERN BLOCK (ONE PARAMETER)
*
*      A P1BLK IS USED TO REPRESENT PATTERN NODES WHICH
*      REQUIRE ONE PARAMETER VALUE.
*
*           +------------------------------------+
*           I                PCODE               I
*           +------------------------------------+
*           I                PTHEN               I
*           +------------------------------------+
*           I                PARM1               I
*           +------------------------------------+
*
PARM1  EQU  PTHEN+1          FIRST PARAMETER VALUE
PBSI_  EQU  PARM1+1          SIZE OF P1BLK IN WORDS
*
*      SEE P0BLK FOR DEFINITIONS OF PCODE, PTHEN
*
*      PARM1 CONTAINS A PARAMETER VALUE USED IN MATCHING THE
*      NODE. FOR EXAMPLE, IN A LEN PATTERN, IT IS THE INTEGER
*      ARGUMENT TO LEN. THE DETAILS OF THE USE OF THE PARAMETER
*      FIELD ARE INCLUDED IN THE DESCRIPTION OF THE INDIVIDUAL
*      MATCH ROUTINES. PARM1 IS ALWAYS AN ADDRESS POINTER WHICH
*      IS PROCESSED BY THE GARBAGE COLLECTOR.
       EJC
*
*      PATTERN BLOCK (TWO PARAMETERS)
*
*      A P2BLK IS USED TO REPRESENT PATTERN NODES WHICH
*      REQUIRE TWO PARAMETER VALUES.
*
*           +------------------------------------+
*           I                PCODE               I
*           +------------------------------------+
*           I                PTHEN               I
*           +------------------------------------+
*           I                PARM1               I
*           +------------------------------------+
*           I                PARM2               I
*           +------------------------------------+
*
PARM2  EQU  PARM1+1          SECOND PARAMETER VALUE
PCSI_  EQU  PARM2+1          SIZE OF P2BLK IN WORDS
*
*      SEE P1BLK FOR DEFINITIONS OF PCODE, PTHEN, PARM1
*
*      PARM2 IS A PARAMETER WHICH PERFORMS THE SAME SORT OF
*      FUNCTION AS PARM1 (SEE DESCRIPTION OF P1BLK).
*
*      PARM2 IS A NON-RELOCATABLE FIELD AND IS NOT
*      PROCESSED BY THE GARBAGE COLLECTOR. ACCORDINGLY, IT MAY
*      NOT CONTAIN A POINTER TO A BLOCK IN DYNAMIC MEMORY.
       EJC
*
*      PROGRAM-DEFINED DATATYPE BLOCK
*
*      A PDBLK REPRESENTS THE DATA ITEM FORMED BY A CALL TO A
*      DATATYPE FUNCTION AS DEFINED BY THE SYSTEM FUNCTION DATA.
*
*           +------------------------------------+
*           I                PDTYP               I
*           +------------------------------------+
*           I                IDVAL               I
*           +------------------------------------+
*           I                PDDFP               I
*           +------------------------------------+
*           /                                    /
*           /                PDFLD               /
*           /                                    /
*           +------------------------------------+
*
PDTYP  EQU  0                PTR TO DUMMY ROUTINE B_PDT
PDDFP  EQU  IDVAL+1          PTR TO ASSOCIATED DFBLK
PDFLD  EQU  PDDFP+1          START OF FIELD VALUE POINTERS
PDFOF  EQU  DFFLD-PDFLD      DIFFERENCE IN OFFSET TO FIELD PTRS
PDSI_  EQU  PDFLD            SIZE OF STANDARD FIELDS IN PDBLK
PDDFS  EQU  DFSI_-PDSI_      DIFFERENCE IN DFBLK, PDBLK SIZES
*
*      THE PDDFP POINTER MAY BE USED TO DETERMINE THE DATATYPE
*      AND THE NAMES OF THE FIELDS IF REQUIRED. THE DFBLK ALSO
*      CONTAINS THE LENGTH OF THE PDBLK IN BYTES (FIELD DFPDL).
*      PDDFP IS NON-RELOC. BECAUSE DFBLK IS IN STATIC
*
*      PDFLD VALUES ARE STORED IN ORDER FROM LEFT TO RIGHT.
*      THEY CONTAIN VALUES OR POINTERS TO TRBLK CHAINS.
       EJC
*
*      PROGRAM DEFINED FUNCTION BLOCK (PFBLK)
*
*      A PFBLK IS CREATED FOR EACH CALL TO THE DEFINE FUNCTION
*      AND A POINTER TO THE PFBLK PLACED IN THE PROPER VRBLK.
*
*           +------------------------------------+
*           I                FCODE               I
*           +------------------------------------+
*           I                FARGS               I
*           +------------------------------------+
*           I                PFLEN               I
*           +------------------------------------+
*           I                PFVBL               I
*           +------------------------------------+
*           I                PFNLO               I
*           +------------------------------------+
*           I                PFCOD               I
*           +------------------------------------+
*           I                PFCTR               I
*           +------------------------------------+
*           I                PFRTR               I
*           +------------------------------------+
*           /                                    /
*           /                PFARG               /
*           /                                    /
*           +------------------------------------+
*
PFLEN  EQU  FARGS+1          LENGTH OF PFBLK IN BYTES
PFVBL  EQU  PFLEN+1          POINTER TO VRBLK FOR FUNCTION NAME
PFNLO  EQU  PFVBL+1          NUMBER OF LOCALS
PFCOD  EQU  PFNLO+1          PTR TO VRBLK FOR ENTRY LABEL
PFCTR  EQU  PFCOD+1          TRBLK PTR IF CALL TRACED ELSE 0
PFRTR  EQU  PFCTR+1          TRBLK PTR IF RETURN TRACED ELSE 0
PFARG  EQU  PFRTR+1          VRBLK PTRS FOR ARGUMENTS AND LOCALS
PFAGB  EQU  PFARG-1          OFFSET BEHIND PFARG FOR ARG, LOCAL
PFSI_  EQU  PFARG            NUMBER OF STANDARD FIELDS IN PFBLK
*
*      THE FCODE FIELD POINTS TO THE ROUTINE B_PFC.
*
*      PFARG IS STORED IN THE FOLLOWING ORDER.
*
*           ARGUMENTS (LEFT TO RIGHT)
*           LOCALS (LEFT TO RIGHT)
.IF    .CNRA
.ELSE
       EJC
*
*      REAL CONSTANT BLOCK (RCBLK)
*
*      AN RCBLK IS CREATED FOR EVERY REAL REFERENCED OR
*      CREATED BY A PROGRAM.
*
*           +------------------------------------+
*           I                RCGET               I
*           +------------------------------------+
*           *                RCVAL               *
*           +------------------------------------+
*
RCGET  EQU  0                PTR TO ROUTINE B_RCL TO LOAD REAL
RCVAL  EQU  RCGET+1          REAL VALUE
RCSI_  EQU  RCVAL+CFP_R      SIZE OF RCBLK
*
*      THE LENGTH OF THE RCVAL FIELD IS CFP_R.
.FI
       EJC
*
*      STRING CONSTANT BLOCK (SCBLK)
*
*      AN SCBLK IS BUILT FOR EVERY STRING REFERENCED OR CREATED
*      BY A PROGRAM.
*
*           +------------------------------------+
*           I                SCGET               I
*           +------------------------------------+
*           I                SCLEN               I
*           +------------------------------------+
*           /                                    /
*           /                SCHAR               /
*           /                                    /
*           +------------------------------------+
*
SCGET  EQU  0                PTR TO ROUTINE B_SCL TO LOAD STRING
SCLEN  EQU  SCGET+1          LENGTH OF STRING IN CHARACTERS
SCHAR  EQU  SCLEN+1          CHARACTERS OF STRING
SCSI_  EQU  SCHAR            SIZE OF STANDARD FIELDS IN SCBLK
*
*      THE CHARACTERS OF THE STRING ARE STORED LEFT JUSTIFIED.
*      THE FINAL WORD IS PADDED ON THE RIGHT WITH ZEROS.
*      (I.E. THE CHARACTER WHOSE INTERNAL CODE IS ZERO).
*
*      THE VALUE OF SCLEN MAY NOT EXCEED MXLEN. THIS ENSURES
*      THAT CHARACTER OFFSETS (E.G. THE PATTERN MATCH CURSOR)
*      CAN BE CORRECTLY PROCESSED BY THE GARBAGE COLLECTOR.
*
*      NOTE THAT THE OFFSET TO THE CHARACTERS OF THE STRING
*      IS GIVEN IN BYTES BY CFP_F AND THAT THIS VALUE IS
*      AUTOMATICALLY ALLOWED FOR IN PLC, PSC.
*      NOTE THAT FOR A SPITBOL SCBLK, THE VALUE OF CFP_F
*      IS GIVEN BY CFP_B*SCHAR.
       EJC
*
*      SIMPLE EXPRESSION BLOCK (SEBLK)
*
*      AN SEBLK IS USED TO REPRESENT AN EXPRESSION OF THE FORM
*      *(NATURAL VARIABLE). ALL OTHER EXPRESSIONS ARE EXBLKS.
*
*           +------------------------------------+
*           I                SETYP               I
*           +------------------------------------+
*           I                SEVAR               I
*           +------------------------------------+
*
SETYP  EQU  0                PTR TO ROUTINE B_SEL TO LOAD EXPR
SEVAR  EQU  SETYP+1          PTR TO VRBLK FOR VARIABLE
SESI_  EQU  SEVAR+1          LENGTH OF SEBLK IN WORDS
       EJC
*
*      STANDARD VARIABLE BLOCK (SVBLK)
*
*      AN SVBLK IS ASSEMBLED IN THE CONSTANT SECTION FOR EACH
*      VARIABLE WHICH SATISFIES ONE OF THE FOLLOWING CONDITIONS.
*
*      1)   IT IS THE NAME OF A SYSTEM FUNCTION
*      2)   IT HAS AN INITIAL VALUE
*      3)   IT HAS A KEYWORD ASSOCIATION
*      4)   IT HAS A STANDARD I/O ASSOCIATION
*      6)   IT HAS A STANDARD LABEL ASSOCIATION
*
*      IF VRBLKS ARE CONSTRUCTED FOR ANY OF THESE VARIABLES,
*      THEN THE VRSVP FIELD POINTS TO THE SVBLK (SEE VRBLK)
*
*           +------------------------------------+
*           I                SVBIT               I
*           +------------------------------------+
*           I                SVLEN               I
*           +------------------------------------+
*           /                SVCHS               /
*           +------------------------------------+
*           I                SVKNM               I
*           +------------------------------------+
*           I                SVFNC               I
*           +------------------------------------+
*           I                SVNAR               I
*           +------------------------------------+
*           I                SVLBL               I
*           +------------------------------------+
*           I                SVVAL               I
*           +------------------------------------+
       EJC
*
*      STANDARD VARIABLE BLOCK (CONTINUED)
*
SVBIT  EQU  0                BIT STRING INDICATING ATTRIBUTES
SVLEN  EQU  1                (=SCLEN) LENGTH OF NAME IN CHARS
SVCHS  EQU  2                (=SCHAR) CHARACTERS OF NAME
SVSI_  EQU  2                NUMBER OF STANDARD FIELDS IN SVBLK
SVPRE  EQU  1                SET IF PREEVALUATION PERMITTED
SVFFC  EQU  SVPRE+SVPRE      SET ON IF FAST CALL PERMITTED
SVCKW  EQU  SVFFC+SVFFC      SET ON IF KEYWORD VALUE CONSTANT
SVPRD  EQU  SVCKW+SVCKW      SET ON IF PREDICATE FUNCTION
SVNBT  EQU  4                NUMBER OF BITS TO RIGHT OF SVKNM
SVKNM  EQU  SVPRD+SVPRD      SET ON IF KEYWORD ASSOCIATION
SVFNC  EQU  SVKNM+SVKNM      SET ON IF SYSTEM FUNCTION
SVNAR  EQU  SVFNC+SVFNC      SET ON IF SYSTEM FUNCTION
SVLBL  EQU  SVNAR+SVNAR      SET ON IF SYSTEM LABEL
SVVAL  EQU  SVLBL+SVLBL      SET ON IF PREDEFINED VALUE
*
*      NOTE THAT THE LAST FIVE BITS CORRESPOND IN ORDER
*      TO THE FIELDS WHICH ARE PRESENT (SEE PROCEDURE GTNVR).
*
*      THE FOLLOWING DEFINITIONS ARE USED IN THE SVBLK TABLE
*
SVFNF  EQU  SVFNC+SVNAR      FUNCTION WITH NO FAST CALL
SVFNN  EQU  SVFNF+SVFFC      FUNCTION WITH FAST CALL, NO PREEVAL
SVFNP  EQU  SVFNN+SVPRE      FUNCTION ALLOWING PREEVALUATION
SVFPR  EQU  SVFNN+SVPRD      PREDICATE FUNCTION
SVFNK  EQU  SVFNN+SVKNM      NO PREEVAL FUNC + KEYWORD
SVKWV  EQU  SVKNM+SVVAL      KEYWORD + VALUE
SVKWC  EQU  SVCKW+SVKNM      KEYWORD WITH CONSTANT VALUE
SVKVC  EQU  SVKWV+SVCKW      CONSTANT KEYWORD + VALUE
SVKVL  EQU  SVKVC+SVLBL      CONSTANT KEYWORD + VALUE + LABEL
SVFPK  EQU  SVFNP+SVKVC      PREEVAL FCN + CONST KEYWD + VAL
*
*      THE SVPRE BIT ALLOWS THE COMPILER TO PREEVALUATE A CALL
*      TO THE ASSOCIATED SYSTEM FUNCTION IF ALL THE ARGUMENTS
*      ARE THEMSELVES CONSTANTS. FUNCTIONS IN THIS CATEGORY
*      MUST HAVE NO SIDE EFFECTS AND MUST NEVER CAUSE FAILURE.
*      THE CALL MAY GENERATE AN ERROR CONDITION.
*
*      THE SVFFC BIT ALLOWS THE COMPILER TO GENERATE THE SPECIAL
*      FAST CALL AFTER ADJUSTING THE NUMBER OF ARGUMENTS. ONLY
*      THE ITEM AND APPLY FUNCTIONS FALL OUTSIDE THIS CATEGORY.
*
*      THE SVCKW BIT IS SET IF THE ASSOCIATED KEYWORD VALUE IS
*      A CONSTANT, THUS ALLOWING PREEVALUATION FOR A VALUE CALL.
*
*      THE SVPRD BIT IS SET ON FOR ALL PREDICATE FUNCTIONS TO
*      ENABLE THE SPECIAL CONCATENATION CODE OPTIMIZATION.
       EJC
*
*      SVBLK (CONTINUED)
*
*      SVKNM                 KEYWORD NUMBER
*
*           SVKNM IS PRESENT ONLY FOR A STANDARD KEYWORD ASSOC.
*           IT CONTAINS A KEYWORD NUMBER AS DEFINED BY THE
*           KEYWORD NUMBER TABLE GIVEN LATER ON.
*
*      SVFNC                 SYSTEM FUNCTION POINTER
*
*           SVFNC IS PRESENT ONLY FOR A SYSTEM FUNCTION ASSOC.
*           IT IS A POINTER TO THE ACTUAL CODE FOR THE SYSTEM
*           FUNCTION. THE GENERATED CODE FOR A FAST CALL IS A
*           POINTER TO THE SVFNC FIELD OF THE SVBLK FOR THE
*           FUNCTION. THE VRFNC FIELD OF THE VRBLK POINTS TO
*           THIS SAME FIELD, IN WHICH CASE, IT SERVES AS THE
*           FCODE FIELD FOR THE FUNCTION CALL.
*
*      SVNAR                 NUMBER OF FUNCTION ARGUMENTS
*
*           SVNAR IS PRESENT ONLY FOR A SYSTEM FUNCTION ASSOC.
*           IT IS THE NUMBER OF ARGUMENTS REQUIRED FOR A CALL
*           TO THE SYSTEM FUNCTION. THE COMPILER USES THIS
*           VALUE TO ADJUST THE NUMBER OF ARGUMENTS IN A FAST
*           CALL AND IN THE CASE OF A FUNCTION CALLED THROUGH
*           THE VRFNC FIELD OF THE VRBLK, THE SVNAR FIELD
*           SERVES AS THE FARGS FIELD FOR O_FNC. A SPECIAL
*           CASE OCCURS IF THIS VALUE IS SET TO 999. THIS IS
*           USED TO INDICATE THAT THE FUNCTION HAS A VARIABLE
*           NUMBER OF ARGUMENTS AND CAUSES O_FNC TO PASS CONTROL
*           WITHOUT ADJUSTING THE ARGUMENT COUNT. THE ONLY
*           PREDEFINED FUNCTIONS USING THIS ARE APPLY AND ITEM.
*
*      SVLBL                 SYSTEM LABEL POINTER
*
*           SVLBL IS PRESENT ONLY FOR A STANDARD LABEL ASSOC.
*           IT IS A POINTER TO A SYSTEM LABEL ROUTINE (L_XXX).
*           THE VRLBL FIELD OF THE CORRESPONDING VRBLK POINTS TO
*           THE SVLBL FIELD OF THE SVBLK.
*
*      SVVAL                 SYSTEM VALUE POINTER
*
*           SVVAL IS PRESENT ONLY FOR A STANDARD VALUE.
*           IT IS A POINTER TO THE PATTERN NODE (NDXXX) WHICH
*           IS THE STANDARD INITIAL VALUE OF THE VARIABLE.
*           THIS VALUE IS COPIED TO THE VRVAL FIELD OF THE VRBLK
       EJC
*
*      SVBLK (CONTINUED)
*
*      KEYWORD NUMBER TABLE
*
*      THE FOLLOWING TABLE GIVES SYMBOLIC NAMES FOR KEYWORD
*      NUMBERS. THESE VALUES ARE STORED IN THE SVKNM FIELD OF
*      SVBLKS AND IN THE KVNUM FIELD OF KVBLKS. SEE ALSO
*      PROCEDURES ASIGN, ACESS AND KWNAM.
*
*      UNPROTECTED KEYWORDS WITH ONE WORD INTEGER VALUES
*
K_ABE  EQU  0                ABEND
K_ANC  EQU  K_ABE+CFP_B      ANCHOR
.IF    .CULC
K_CAS  EQU  K_ANC+CFP_B      CASE
K_COD  EQU  K_CAS+CFP_B      CODE
.ELSE
K_COD  EQU  K_ANC+CFP_B      CODE
.FI
.IF    .CCMK
K_COM  EQU  K_COD+CFP_B      COMPARE
K_DMP  EQU  K_COM+CFP_B      DUMP
.ELSE
K_DMP  EQU  K_COD+CFP_B      DUMP
.FI
K_ERL  EQU  K_DMP+CFP_B      ERRLIMIT
K_ERT  EQU  K_ERL+CFP_B      ERRTYPE
K_FTR  EQU  K_ERT+CFP_B      FTRACE
K_FLS  EQU  K_FTR+CFP_B      FULLSCAN
K_INP  EQU  K_FLS+CFP_B      INPUT
K_MXL  EQU  K_INP+CFP_B      MAXLENGTH
K_OUP  EQU  K_MXL+CFP_B      OUTPUT
.IF    .CNPF
K_TRA  EQU  K_OUP+CFP_B      TRACE
.ELSE
K_PFL  EQU  K_OUP+CFP_B      PROFILE
K_TRA  EQU  K_PFL+CFP_B      TRACE
.FI
K_TRM  EQU  K_TRA+CFP_B      TRIM
*
*      PROTECTED KEYWORDS WITH ONE WORD INTEGER VALUES
*
K_FNC  EQU  K_TRM+CFP_B      FNCLEVEL
K_LST  EQU  K_FNC+CFP_B      LASTNO
.IF    .CSLN
K_LLN  EQU  K_LST+CFP_B      LASTLINE
K_LIN  EQU  K_LLN+CFP_B      LINE
K_STN  EQU  K_LIN+CFP_B      STNO
.ELSE
K_STN  EQU  K_LST+CFP_B      STNO
.FI
*
*      KEYWORDS WITH CONSTANT PATTERN VALUES
*
K_ABO  EQU  K_STN+CFP_B      ABORT
K_ARB  EQU  K_ABO+PASI_      ARB
K_BAL  EQU  K_ARB+PASI_      BAL
K_FAL  EQU  K_BAL+PASI_      FAIL
K_FEN  EQU  K_FAL+PASI_      FENCE
K_REM  EQU  K_FEN+PASI_      REM
K_SUC  EQU  K_REM+PASI_      SUCCEED
       EJC
*
*      KEYWORD NUMBER TABLE (CONTINUED)
*
*      SPECIAL KEYWORDS
*
K_ALP  EQU  K_SUC+1          ALPHABET
K_RTN  EQU  K_ALP+1          RTNTYPE
K_STC  EQU  K_RTN+1          STCOUNT
K_ETX  EQU  K_STC+1          ERRTEXT
.IF    .CSFN
K_FIL  EQU  K_ETX+1          FILE
K_LFL  EQU  K_FIL+1          LASTFILE
K_STL  EQU  K_LFL+1          STLIMIT
.ELSE
K_STL  EQU  K_ETX+1          STLIMIT
.FI
.IF    .CULK
K_LCS  EQU  K_STL+1          LCASE
K_UCS  EQU  K_LCS+1          UCASE
.FI
*
*      RELATIVE OFFSETS OF SPECIAL KEYWORDS
*
K__AL  EQU  K_ALP-K_ALP      ALPHABET
K__RT  EQU  K_RTN-K_ALP      RTNTYPE
K__SC  EQU  K_STC-K_ALP      STCOUNT
K__ET  EQU  K_ETX-K_ALP      ERRTEXT
.IF    .CSFN
K__FL  EQU  K_FIL-K_ALP      FILE
K__LF  EQU  K_LFL-K_ALP      LASTFILE
.FI
K__SL  EQU  K_STL-K_ALP      STLIMIT
.IF    .CULK
K__LC  EQU  K_LCS-K_ALP      LCASE
K__UC  EQU  K_UCS-K_ALP      UCASE
K__N_  EQU  K__UC+1          NUMBER OF SPECIAL CASES
.ELSE
K__N_  EQU  K__SL+1          NUMBER OF SPECIAL CASES
.FI
*
*      SYMBOLS USED IN ASIGN AND ACESS PROCEDURES
*
K_P__  EQU  K_FNC            FIRST PROTECTED KEYWORD
K_V__  EQU  K_ABO            FIRST KEYWORD WITH CONSTANT VALUE
K_S__  EQU  K_ALP            FIRST KEYWORD WITH SPECIAL ACESS
       EJC
*
*      FORMAT OF A TABLE BLOCK (TBBLK)
*
*      A TABLE BLOCK IS USED TO REPRESENT A TABLE VALUE.
*      IT IS BUILT BY A CALL TO THE TABLE OR CONVERT FUNCTIONS.
*
*           +------------------------------------+
*           I                TBTYP               I
*           +------------------------------------+
*           I                IDVAL               I
*           +------------------------------------+
*           I                TBLEN               I
*           +------------------------------------+
*           I                TBINV               I
*           +------------------------------------+
*           /                                    /
*           /                TBBUK               /
*           /                                    /
*           +------------------------------------+
*
TBTYP  EQU  0                POINTER TO DUMMY ROUTINE B_TBT
TBLEN  EQU  OFFS2            LENGTH OF TBBLK IN BYTES
TBINV  EQU  OFFS3            DEFAULT INITIAL LOOKUP VALUE
TBBUK  EQU  TBINV+1          START OF HASH BUCKET POINTERS
TBSI_  EQU  TBBUK            SIZE OF STANDARD FIELDS IN TBBLK
TBNBK  EQU  11               DEFAULT NO. OF BUCKETS
*
*      THE TABLE BLOCK IS A HASH TABLE WHICH POINTS TO CHAINS
*      OF TABLE ELEMENT BLOCKS REPRESENTING THE ELEMENTS
*      IN THE TABLE WHICH HASH INTO THE SAME BUCKET.
*
*      TBBUK ENTRIES EITHER POINT TO THE FIRST TEBLK ON THE
*      CHAIN OR THEY POINT TO THE TBBLK ITSELF TO INDICATE THE
*      END OF THE CHAIN.
       EJC
*
*      TABLE ELEMENT BLOCK (TEBLK)
*
*      A TABLE ELEMENT IS USED TO REPRESENT A SINGLE ENTRY IN
*      A TABLE (SEE DESCRIPTION OF TBBLK FORMAT FOR HASH TABLE)
*
*           +------------------------------------+
*           I                TETYP               I
*           +------------------------------------+
*           I                TESUB               I
*           +------------------------------------+
*           I                TEVAL               I
*           +------------------------------------+
*           I                TENXT               I
*           +------------------------------------+
*
TETYP  EQU  0                POINTER TO DUMMY ROUTINE B_TET
TESUB  EQU  TETYP+1          SUBSCRIPT VALUE
TEVAL  EQU  TESUB+1          (=VRVAL) TABLE ELEMENT VALUE
TENXT  EQU  TEVAL+1          LINK TO NEXT TEBLK
*      SEE S_CNV WHERE RELATION IS ASSUMED WITH TENXT AND TBBUK
TESI_  EQU  TENXT+1          SIZE OF TEBLK IN WORDS
*
*      TENXT POINTS TO THE NEXT TEBLK ON THE HASH CHAIN FROM THE
*      TBBUK CHAIN FOR THIS HASH INDEX. AT THE END OF THE CHAIN,
*      TENXT POINTS BACK TO THE START OF THE TBBLK.
*
*      TEVAL CONTAINS A DATA POINTER OR A TRBLK POINTER.
*
*      TESUB CONTAINS A DATA POINTER.
       EJC
*
*      TRAP BLOCK (TRBLK)
*
*      A TRAP BLOCK IS USED TO REPRESENT A TRACE OR INPUT OR
*      OUTPUT ASSOCIATION IN RESPONSE TO A CALL TO THE TRACE
*      INPUT OR OUTPUT SYSTEM FUNCTIONS. SEE BELOW FOR DETAILS
*
*           +------------------------------------+
*           I                TRIDN               I
*           +------------------------------------+
*           I                TRTYP               I
*           +------------------------------------+
*           I  TRVAL OR TRLBL OR TRNXT OR TRKVR  I
*           +------------------------------------+
*           I       TRTAG OR TRTER OR TRTRF      I
*           +------------------------------------+
*           I            TRFNC OR TRFPT          I
*           +------------------------------------+
*
TRIDN  EQU  0                POINTER TO DUMMY ROUTINE B_TRT
TRTYP  EQU  TRIDN+1          TRAP TYPE CODE
TRVAL  EQU  TRTYP+1          VALUE OF TRAPPED VARIABLE (=VRVAL)
TRNXT  EQU  TRVAL            PTR TO NEXT TRBLK ON TRBLK CHAIN
TRLBL  EQU  TRVAL            PTR TO ACTUAL LABEL (TRACED LABEL)
TRKVR  EQU  TRVAL            VRBLK POINTER FOR KEYWORD TRACE
TRTAG  EQU  TRVAL+1          TRACE TAG
TRTER  EQU  TRTAG            PTR TO TERMINAL VRBLK OR NULL
TRTRF  EQU  TRTAG            PTR TO TRBLK HOLDING FCBLK PTR
TRFNC  EQU  TRTAG+1          TRACE FUNCTION VRBLK (ZERO IF NONE)
TRFPT  EQU  TRFNC            FCBLK PTR FOR SYSIO
TRSI_  EQU  TRFNC+1          NUMBER OF WORDS IN TRBLK
*
TRTIN  EQU  0                TRACE TYPE FOR INPUT ASSOCIATION
TRTAC  EQU  TRTIN+1          TRACE TYPE FOR ACCESS TRACE
TRTVL  EQU  TRTAC+1          TRACE TYPE FOR VALUE TRACE
TRTOU  EQU  TRTVL+1          TRACE TYPE FOR OUTPUT ASSOCIATION
TRTFC  EQU  TRTOU+1          TRACE TYPE FOR FCBLK IDENTIFICATION
       EJC
*
*      TRAP BLOCK (CONTINUED)
*
*      VARIABLE INPUT ASSOCIATION
*
*           THE VALUE FIELD OF THE VARIABLE POINTS TO A TRBLK
*           INSTEAD OF CONTAINING THE DATA VALUE. IN THE CASE
*           OF A NATURAL VARIABLE, THE VRGET AND VRSTO FIELDS
*           CONTAIN =B_VRA AND =B_VRV TO ACTIVATE THE CHECK.
*
*           TRTYP IS SET TO TRTIN
*           TRNXT POINTS TO NEXT TRBLK OR TRVAL HAS VARIABLE VAL
*           TRTER IS A POINTER TO SVBLK IF ASSOCIATION IS
*           FOR INPUT, TERMINAL, ELSE IT IS NULL.
*           TRTRF POINTS TO THE TRAP BLOCK WHICH IN TURN POINTS
*           TO AN FCBLK USED FOR I/O ASSOCIATION.
*           TRFPT IS THE FCBLK PTR RETURNED BY SYSIO.
*
*      VARIABLE ACCESS TRACE ASSOCIATION
*
*           THE VALUE FIELD OF THE VARIABLE POINTS TO A TRBLK
*           INSTEAD OF CONTAINING THE DATA VALUE. IN THE CASE
*           OF A NATURAL VARIABLE, THE VRGET AND VRSTO FIELDS
*           CONTAIN =B_VRA AND =B_VRV TO ACTIVATE THE CHECK.
*
*           TRTYP IS SET TO TRTAC
*           TRNXT POINTS TO NEXT TRBLK OR TRVAL HAS VARIABLE VAL
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
*
*      VARIABLE VALUE TRACE ASSOCIATION
*
*           THE VALUE FIELD OF THE VARIABLE POINTS TO A TRBLK
*           INSTEAD OF CONTAINING THE DATA VALUE. IN THE CASE
*           OF A NATURAL VARIABLE, THE VRGET AND VRSTO FIELDS
*           CONTAIN =B_VRA AND =B_VRV TO ACTIVATE THE CHECK.
*
*           TRTYP IS SET TO TRTVL
*           TRNXT POINTS TO NEXT TRBLK OR TRVAL HAS VARIABLE VAL
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
       EJC
*      TRAP BLOCK (CONTINUED)
*
*      VARIABLE OUTPUT ASSOCIATION
*
*           THE VALUE FIELD OF THE VARIABLE POINTS TO A TRBLK
*           INSTEAD OF CONTAINING THE DATA VALUE. IN THE CASE
*           OF A NATURAL VARIABLE, THE VRGET AND VRSTO FIELDS
*           CONTAIN =B_VRA AND =B_VRV TO ACTIVATE THE CHECK.
*
*           TRTYP IS SET TO TRTOU
*           TRNXT POINTS TO NEXT TRBLK OR TRVAL HAS VARIABLE VAL
*           TRTER IS A POINTER TO SVBLK IF ASSOCIATION IS
*           FOR OUTPUT, TERMINAL, ELSE IT IS NULL.
*           TRTRF POINTS TO THE TRAP BLOCK WHICH IN TURN POINTS
*           TO AN FCBLK USED FOR I/O ASSOCIATION.
*           TRFPT IS THE FCBLK PTR RETURNED BY SYSIO.
*
*      FUNCTION CALL TRACE
*
*           THE PFCTR FIELD OF THE CORRESPONDING PFBLK IS SET
*           TO POINT TO A TRBLK.
*
*           TRTYP IS SET TO TRTIN
*           TRNXT IS ZERO
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
*
*      FUNCTION RETURN TRACE
*
*           THE PFRTR FIELD OF THE CORRESPONDING PFBLK IS SET
*           TO POINT TO A TRBLK
*
*           TRTYP IS SET TO TRTIN
*           TRNXT IS ZERO
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
*
*      LABEL TRACE
*
*           THE VRLBL OF THE VRBLK FOR THE LABEL IS
*           CHANGED TO POINT TO A TRBLK AND THE VRTRA FIELD IS
*           SET TO B_VRT TO ACTIVATE THE CHECK.
*
*           TRTYP IS SET TO TRTIN
*           TRLBL POINTS TO THE ACTUAL LABEL (CDBLK) VALUE
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
       EJC
*
*      TRAP BLOCK (CONTINUED)
*
*      KEYWORD TRACE
*
*           KEYWORDS WHICH CAN BE TRACED POSSESS A UNIQUE
*           LOCATION WHICH IS ZERO IF THERE IS NO TRACE AND
*           POINTS TO A TRBLK IF THERE IS A TRACE. THE LOCATIONS
*           ARE AS FOLLOWS.
*
*           R_ERT            ERRTYPE
*           R_FNC            FNCLEVEL
*           R_STC            STCOUNT
*
*           THE FORMAT OF THE TRBLK IS AS FOLLOWS.
*
*           TRTYP IS SET TO TRTIN
*           TRKVR IS A POINTER TO THE VRBLK FOR THE KEYWORD
*           TRTAG IS THE TRACE TAG (0 IF NONE)
*           TRFNC IS THE TRACE FUNCTION VRBLK PTR (0 IF NONE)
*
*      INPUT/OUTPUT FILE ARG1 TRAP BLOCK
*
*           THE VALUE FIELD OF THE VARIABLE POINTS TO A TRBLK
*           INSTEAD OF CONTAINING THE DATA VALUE. IN THE CASE OF
*           A NATURAL VARIABLE, THE VRGET AND VRSTO FIELDS
*           CONTAIN =B_VRA AND =B_VRV. THIS TRAP BLOCK IS USED
*           TO HOLD A POINTER TO THE FCBLK WHICH AN
*           IMPLEMENTATION MAY REQUEST TO HOLD INFORMATION
*           ABOUT A FILE.
*
*           TRTYP IS SET TO TRTFC
*           TRNEXT POINTS TO NEXT TRBLK OR TRVAL IS VARIABLE VAL
*           TRFNM IS 0
*           TRFPT IS THE FCBLK POINTER.
*
*      NOTE THAT WHEN MULTIPLE TRAPS ARE SET ON A VARIABLE
*      THE ORDER IS IN ASCENDING VALUE OF TRTYP FIELD.
*
*      INPUT ASSOCIATION (IF PRESENT)
*      ACCESS TRACE (IF PRESENT)
*      VALUE TRACE (IF PRESENT)
*      OUTPUT ASSOCIATION (IF PRESENT)
*
*      THE ACTUAL VALUE OF THE VARIABLE IS STORED IN THE TRVAL
*      FIELD OF THE LAST TRBLK ON THE CHAIN.
*
*      THIS IMPLEMENTATION DOES NOT PERMIT TRACE OR I/O
*      ASSOCIATIONS TO ANY OF THE PSEUDO-VARIABLES.
       EJC
*
*      VECTOR BLOCK (VCBLK)
*
*      A VCBLK IS USED TO REPRESENT AN ARRAY VALUE WHICH HAS
*      ONE DIMENSION WHOSE LOWER BOUND IS ONE. ALL OTHER ARRAYS
*      ARE REPRESENTED BY ARBLKS. A VCBLK IS CREATED BY THE
*      SYSTEM FUNCTION ARRAY (S_ARR) WHEN PASSED AN INTEGER ARG.
*
*           +------------------------------------+
*           I                VCTYP               I
*           +------------------------------------+
*           I                IDVAL               I
*           +------------------------------------+
*           I                VCLEN               I
*           +------------------------------------+
*           I                VCVLS               I
*           +------------------------------------+
*
VCTYP  EQU  0                POINTER TO DUMMY ROUTINE B_VCT
VCLEN  EQU  OFFS2            LENGTH OF VCBLK IN BYTES
VCVLS  EQU  OFFS3            START OF VECTOR VALUES
VCSI_  EQU  VCVLS            SIZE OF STANDARD FIELDS IN VCBLK
VCVLB  EQU  VCVLS-1          OFFSET ONE WORD BEHIND VCVLS
VCTBD  EQU  TBSI_-VCSI_      DIFFERENCE IN SIZES - SEE PRTVL
*
*      VCVLS ARE EITHER DATA POINTERS OR TRBLK POINTERS
*
*      THE DIMENSION CAN BE DEDUCED FROM VCLEN.
       EJC
*
*      VARIABLE BLOCK (VRBLK)
*
*      A VARIABLE BLOCK IS BUILT IN THE STATIC MEMORY AREA
*      FOR EVERY VARIABLE REFERENCED OR CREATED BY A PROGRAM.
*      THE ORDER OF FIELDS IS ASSUMED IN THE MODEL VRBLK STNVR.
*
*      NOTE THAT SINCE THESE BLOCKS ONLY OCCUR IN THE STATIC
*      REGION, IT IS PERMISSIBLE TO POINT TO ANY WORD IN
*      THE BLOCK AND THIS IS USED TO PROVIDE THREE DISTINCT
*      ACCESS POINTS FROM THE GENERATED CODE AS FOLLOWS.
*
*      1)   POINT TO VRGET (FIRST WORD OF VRBLK) TO LOAD THE
*           VALUE OF THE VARIABLE ONTO THE MAIN STACK.
*
*      2)   POINT TO VRSTO (SECOND WORD OF VRBLK) TO STORE THE
*           TOP STACK ELEMENT AS THE VALUE OF THE VARIABLE.
*
*      3)   POINT TO VRTRA (FOURTH WORD OF VRBLK) TO JUMP TO
*           THE LABEL ASSOCIATED WITH THE VARIABLE NAME.
*
*           +------------------------------------+
*           I                VRGET               I
*           +------------------------------------+
*           I                VRSTO               I
*           +------------------------------------+
*           I                VRVAL               I
*           +------------------------------------+
*           I                VRTRA               I
*           +------------------------------------+
*           I                VRLBL               I
*           +------------------------------------+
*           I                VRFNC               I
*           +------------------------------------+
*           I                VRNXT               I
*           +------------------------------------+
*           I                VRLEN               I
*           +------------------------------------+
*           /                                    /
*           /            VRCHS = VRSVP           /
*           /                                    /
*           +------------------------------------+
       EJC
*
*      VARIABLE BLOCK (CONTINUED)
*
VRGET  EQU  0                POINTER TO ROUTINE TO LOAD VALUE
VRSTO  EQU  VRGET+1          POINTER TO ROUTINE TO STORE VALUE
VRVAL  EQU  VRSTO+1          VARIABLE VALUE
VRVLO  EQU  VRVAL-VRSTO      OFFSET TO VALUE FROM STORE FIELD
VRTRA  EQU  VRVAL+1          POINTER TO ROUTINE TO JUMP TO LABEL
VRLBL  EQU  VRTRA+1          POINTER TO CODE FOR LABEL
VRLBO  EQU  VRLBL-VRTRA      OFFSET TO LABEL FROM TRANSFER FIELD
VRFNC  EQU  VRLBL+1          POINTER TO FUNCTION BLOCK
VRNXT  EQU  VRFNC+1          POINTER TO NEXT VRBLK ON HASH CHAIN
VRLEN  EQU  VRNXT+1          LENGTH OF NAME (OR ZERO)
VRCHS  EQU  VRLEN+1          CHARACTERS OF NAME (VRLEN GT 0)
VRSVP  EQU  VRLEN+1          PTR TO SVBLK (VRLEN EQ 0)
VRSI_  EQU  VRCHS+1          NUMBER OF STANDARD FIELDS IN VRBLK
VRSOF  EQU  VRLEN-SCLEN      OFFSET TO DUMMY SCBLK FOR NAME
VRSVO  EQU  VRSVP-VRSOF      PSEUDO-OFFSET TO VRSVP FIELD
*
*      VRGET = B_VRL IF NOT INPUT ASSOCIATED OR ACCESS TRACED
*      VRGET = B_VRA IF INPUT ASSOCIATED OR ACCESS TRACED
*
*      VRSTO = B_VRS IF NOT OUTPUT ASSOCIATED OR VALUE TRACED
*      VRSTO = B_VRV IF OUTPUT ASSOCIATED OR VALUE TRACED
*      VRSTO = B_VRE IF VALUE IS PROTECTED PATTERN VALUE
*
*      VRVAL POINTS TO THE APPROPRIATE VALUE UNLESS THE
*      VARIABLE IS I/O/TRACE ASSOCIATED IN WHICH CASE, VRVAL
*      POINTS TO AN APPROPRIATE TRBLK (TRAP BLOCK) CHAIN.
*
*      VRTRA = B_VRG IF THE LABEL IS NOT TRACED
*      VRTRA = B_VRT IF THE LABEL IS TRACED
*
*      VRLBL POINTS TO A CDBLK IF THERE IS A LABEL
*      VRLBL POINTS TO THE SVBLK SVLBL FIELD FOR A SYSTEM LABEL
*      VRLBL POINTS TO STNDL FOR AN UNDEFINED LABEL
*      VRLBL POINTS TO A TRBLK IF THE LABEL IS TRACED
*
*      VRFNC POINTS TO A FFBLK FOR A FIELD FUNCTION
*      VRFNC POINTS TO A DFBLK FOR A DATATYPE FUNCTION
*      VRFNC POINTS TO A PFBLK FOR A PROGRAM DEFINED FUNCTION
*      VRFNC POINTS TO A EFBLK FOR AN EXTERNAL LOADED FUNCTION
*      VRFNC POINTS TO SVFNC (SVBLK) FOR A SYSTEM FUNCTION
*      VRFNC POINTS TO STNDF IF THE FUNCTION IS UNDEFINED
*
*      VRNXT POINTS TO THE NEXT VRBLK ON THIS CHAIN UNLESS
*      THIS IS THE END OF THE CHAIN IN WHICH CASE IT IS ZERO.
*
*      VRLEN IS THE NAME LENGTH FOR A NON-SYSTEM VARIABLE.
*      VRLEN IS ZERO FOR A SYSTEM VARIABLE.
*
*      VRCHS IS THE NAME (LJRZ) IF VRLEN IS NON-ZERO.
*      VRSVP IS A PTR TO THE SVBLK IF VRLEN IS ZERO.
       EJC
*
*      FORMAT OF A NON-RELOCATABLE EXTERNAL BLOCK (XNBLK)
*
*      AN XNBLK IS A BLOCK REPRESENTING AN UNKNOWN (EXTERNAL)
*      DATA VALUE. THE BLOCK CONTAINS NO POINTERS TO OTHER
*      RELOCATABLE BLOCKS. AN XNBLK IS USED BY EXTERNAL FUNCTION
*      PROCESSING OR POSSIBLY FOR SYSTEM I/O ROUTINES ETC.
*      THE MACRO-SYSTEM ITSELF DOES NOT USE XNBLKS.
*      THIS TYPE OF BLOCK MAY BE USED AS A FILE CONTROL BLOCK.
*      SEE SYSFC,SYSIN,SYSOU,S_INP,S_OUP FOR DETAILS.
*
*           +------------------------------------+
*           I                XNTYP               I
*           +------------------------------------+
*           I                XNLEN               I
*           +------------------------------------+
*           /                                    /
*           /                XNDTA               /
*           /                                    /
*           +------------------------------------+
*
XNTYP  EQU  0                POINTER TO DUMMY ROUTINE B_XNT
XNLEN  EQU  XNTYP+1          LENGTH OF XNBLK IN BYTES
XNDTA  EQU  XNLEN+1          DATA WORDS
XNSI_  EQU  XNDTA            SIZE OF STANDARD FIELDS IN XNBLK
*
*      NOTE THAT THE TERM NON-RELOCATABLE REFERS TO THE CONTENTS
*      AND NOT THE BLOCK ITSELF. AN XNBLK CAN BE MOVED AROUND IF
*      IT IS BUILT IN THE DYNAMIC MEMORY AREA.
       EJC
*
*      RELOCATABLE EXTERNAL BLOCK (XRBLK)
*
*      AN XRBLK IS A BLOCK REPRESENTING AN UNKNOWN (EXTERNAL)
*      DATA VALUE. THE DATA AREA IN THIS BLOCK CONSISTS ONLY
*      OF ADDRESS VALUES AND ANY ADDRESSES POINTING INTO THE
*      DYNAMIC MEMORY AREA MUST POINT TO THE START OF OTHER
*      DATA BLOCKS. SEE ALSO DESCRIPTION OF XNBLK.
*      THIS TYPE OF BLOCK MAY BE USED AS A FILE CONTROL BLOCK.
*      SEE SYSFC,SYSIN,SYSOU,S_INP,S_OUP FOR DETAILS.
*
*           +------------------------------------+
*           I                XRTYP               I
*           +------------------------------------+
*           I                XRLEN               I
*           +------------------------------------+
*           /                                    /
*           /                XRPTR               /
*           /                                    /
*           +------------------------------------+
*
XRTYP  EQU  0                POINTER TO DUMMY ROUTINE B_XRT
XRLEN  EQU  XRTYP+1          LENGTH OF XRBLK IN BYTES
XRPTR  EQU  XRLEN+1          START OF ADDRESS POINTERS
XRSI_  EQU  XRPTR            SIZE OF STANDARD FIELDS IN XRBLK
       EJC
*
*      S_CNV (CONVERT) FUNCTION SWITCH CONSTANTS.  THE VALUES
*      ARE TIED TO THE ORDER OF THE ENTRIES IN THE SVCTB TABLE
*      AND HENCE TO THE BRANCH TABLE IN S_CNV.
*
CNVST  EQU  8                MAX STANDARD TYPE CODE FOR CONVERT
.IF    .CNRA
CNVRT  EQU  CNVST            NO REALS - SAME AS STANDARD TYPES
.ELSE
CNVRT  EQU  CNVST+1          CONVERT CODE FOR REALS
.FI
.IF    .CNBF
CNVBT  EQU  CNVRT            NO BUFFERS - SAME AS REAL CODE
.ELSE
CNVBT  EQU  CNVRT+1          CONVERT CODE FOR BUFFER
.FI
CNVTT  EQU  CNVBT+1          BSW CODE FOR CONVERT
*
*      INPUT IMAGE LENGTH
*
INILN  EQU  1024             DEFAULT IMAGE LENGTH FOR COMPILER
INILS  EQU  1024             IMAGE LENGTH IF -SEQU IN EFFECT
*
IONMB  EQU  2                NAME BASE USED FOR IOCHN IN SYSIO
IONMO  EQU  4                NAME OFFSET USED FOR IOCHN IN SYSIO
*
*      MINIMUM VALUE FOR KEYWORD MAXLNGTH
*      SHOULD BE LARGER THAN INILN
*
MNLEN  EQU  1024             MIN VALUE ALLOWED KEYWORD MAXLNGTH
MXERN  EQU  329              ERR NUM INADEQUATE STARTUP MEMORY
*
*      IN GENERAL, MEANINGFUL MNEMONICS SHOULD BE USED FOR
*      OFFSETS. HOWEVER FOR SMALL INTEGERS USED OFTEN IN
*      LITERALS THE FOLLOWING GENERAL DEFINITIONS ARE PROVIDED.
*
NUM01  EQU  1
NUM02  EQU  2
NUM03  EQU  3
NUM04  EQU  4
NUM05  EQU  5
NUM06  EQU  6
NUM07  EQU  7
NUM08  EQU  8
NUM09  EQU  9
NUM10  EQU  10
NM320  EQU  320
NM321  EQU  321
NINI8  EQU  998
NINI9  EQU  999
THSND  EQU  1000
       EJC
*
*      NUMBERS OF UNDEFINED SPITBOL OPERATORS
*
OPBUN  EQU  5                NO. OF BINARY UNDEFINED OPS
OPUUN  EQU  6                NO OF UNARY UNDEFINED OPS
*
*      OFFSETS USED IN PRTSN, PRTMI AND ACESS
*
PRSNF  EQU  13               OFFSET USED IN PRTSN
PRTMF  EQU  21               OFFSET TO COL 21 (PRTMI)
RILEN  EQU  1024             BUFFER LENGTH FOR SYSRI
*
*      CODES FOR STAGES OF PROCESSING
*
STGIC  EQU  0                INITIAL COMPILE
STGXC  EQU  STGIC+1          EXECUTION COMPILE (CODE)
STGEV  EQU  STGXC+1          EXPRESSION EVAL DURING EXECUTION
STGXT  EQU  STGEV+1          EXECUTION TIME
STGCE  EQU  STGXT+1          INITIAL COMPILE AFTER END LINE
STGXE  EQU  STGCE+1          EXEC. COMPILE AFTER END LINE
STGND  EQU  STGCE-STGIC      DIFFERENCE IN STAGE AFTER END
STGEE  EQU  STGXE+1          EVAL EVALUATING EXPRESSION
STGNO  EQU  STGEE+1          NUMBER OF CODES
       EJC
*
*
*      STATEMENT NUMBER PAD COUNT FOR LISTR
*
.IF    .CSN6
STNPD  EQU  6                STATEMENT NO. PAD COUNT
.FI
.IF    .CSN8
STNPD  EQU  8                STATEMENT NO. PAD COUNT
.FI
.IF    .CSN5
STNPD  EQU  5                STATEMENT NO. PAD COUNT
.FI
*
*      SYNTAX TYPE CODES
*
*      THESE CODES ARE RETURNED FROM THE SCANE PROCEDURE.
*
*      THEY ARE SPACED 3 APART FOR THE BENEFIT OF EXPAN.
*
T_UOP  EQU  0                UNARY OPERATOR
T_LPR  EQU  T_UOP+3          LEFT PAREN
T_LBR  EQU  T_LPR+3          LEFT BRACKET
T_CMA  EQU  T_LBR+3          COMMA
T_FNC  EQU  T_CMA+3          FUNCTION CALL
T_VAR  EQU  T_FNC+3          VARIABLE
T_CON  EQU  T_VAR+3          CONSTANT
T_BOP  EQU  T_CON+3          BINARY OPERATOR
T_RPR  EQU  T_BOP+3          RIGHT PAREN
T_RBR  EQU  T_RPR+3          RIGHT BRACKET
T_COL  EQU  T_RBR+3          COLON
T_SMC  EQU  T_COL+3          SEMI-COLON
*
*      THE FOLLOWING DEFINITIONS ARE USED ONLY IN THE GOTO FIELD
*
T_FGO  EQU  T_SMC+1          FAILURE GOTO
T_SGO  EQU  T_FGO+1          SUCCESS GOTO
*
*      THE ABOVE CODES ARE GROUPED SO THAT CODES FOR ELEMENTS
*      WHICH CAN LEGITIMATELY IMMEDIATELY PRECEDE A UNARY
*      OPERATOR COME FIRST TO FACILITATE OPERATOR SYNTAX CHECK.
*
T_UOK  EQU  T_FNC            LAST CODE OK BEFORE UNARY OPERATOR
       EJC
*
*      DEFINITIONS OF VALUES FOR EXPAN JUMP TABLE
*
T_UO0  EQU  T_UOP+0          UNARY OPERATOR, STATE ZERO
T_UO1  EQU  T_UOP+1          UNARY OPERATOR, STATE ONE
T_UO2  EQU  T_UOP+2          UNARY OPERATOR, STATE TWO
T_LP0  EQU  T_LPR+0          LEFT PAREN, STATE ZERO
T_LP1  EQU  T_LPR+1          LEFT PAREN, STATE ONE
T_LP2  EQU  T_LPR+2          LEFT PAREN, STATE TWO
T_LB0  EQU  T_LBR+0          LEFT BRACKET, STATE ZERO
T_LB1  EQU  T_LBR+1          LEFT BRACKET, STATE ONE
T_LB2  EQU  T_LBR+2          LEFT BRACKET, STATE TWO
T_CM0  EQU  T_CMA+0          COMMA, STATE ZERO
T_CM1  EQU  T_CMA+1          COMMA, STATE ONE
T_CM2  EQU  T_CMA+2          COMMA, STATE TWO
T_FN0  EQU  T_FNC+0          FUNCTION CALL, STATE ZERO
T_FN1  EQU  T_FNC+1          FUNCTION CALL, STATE ONE
T_FN2  EQU  T_FNC+2          FUNCTION CALL, STATE TWO
T_VA0  EQU  T_VAR+0          VARIABLE, STATE ZERO
T_VA1  EQU  T_VAR+1          VARIABLE, STATE ONE
T_VA2  EQU  T_VAR+2          VARIABLE, STATE TWO
T_CO0  EQU  T_CON+0          CONSTANT, STATE ZERO
T_CO1  EQU  T_CON+1          CONSTANT, STATE ONE
T_CO2  EQU  T_CON+2          CONSTANT, STATE TWO
T_BO0  EQU  T_BOP+0          BINARY OPERATOR, STATE ZERO
T_BO1  EQU  T_BOP+1          BINARY OPERATOR, STATE ONE
T_BO2  EQU  T_BOP+2          BINARY OPERATOR, STATE TWO
T_RP0  EQU  T_RPR+0          RIGHT PAREN, STATE ZERO
T_RP1  EQU  T_RPR+1          RIGHT PAREN, STATE ONE
T_RP2  EQU  T_RPR+2          RIGHT PAREN, STATE TWO
T_RB0  EQU  T_RBR+0          RIGHT BRACKET, STATE ZERO
T_RB1  EQU  T_RBR+1          RIGHT BRACKET, STATE ONE
T_RB2  EQU  T_RBR+2          RIGHT BRACKET, STATE TWO
T_CL0  EQU  T_COL+0          COLON, STATE ZERO
T_CL1  EQU  T_COL+1          COLON, STATE ONE
T_CL2  EQU  T_COL+2          COLON, STATE TWO
T_SM0  EQU  T_SMC+0          SEMICOLON, STATE ZERO
T_SM1  EQU  T_SMC+1          SEMICOLON, STATE ONE
T_SM2  EQU  T_SMC+2          SEMICOLON, STATE TWO
*
T_NES  EQU  T_SM2+1          NUMBER OF ENTRIES IN BRANCH TABLE
       EJC
*
*       DEFINITION OF OFFSETS USED IN CONTROL CARD PROCESSING
*
.IF    .CULC
CC_CA  EQU  0                -CASE
CC_DO  EQU  CC_CA+1          -DOUBLE
.ELSE
CC_DO  EQU  0                -DOUBLE
.FI
.IF    .CCMK
CC_CO  EQU  CC_DO+1          -COMPARE
CC_DU  EQU  CC_CO+1          -DUMP
.ELSE
CC_DU  EQU  CC_DO+1          -DUMP
.FI
.IF    .CINC
CC_CP  EQU  CC_DU+1          -COPY
CC_EJ  EQU  CC_CP+1          -EJECT
.ELSE
CC_EJ  EQU  CC_DU+1          -EJECT
.FI
CC_ER  EQU  CC_EJ+1          -ERRORS
CC_EX  EQU  CC_ER+1          -EXECUTE
CC_FA  EQU  CC_EX+1          -FAIL
.IF    .CINC
CC_IN  EQU  CC_FA+1          -INCLUDE
.IF    .CSLN
CC_LN  EQU  CC_IN+1          -LINE
CC_LI  EQU  CC_LN+1          -LIST
.ELSE
CC_LI  EQU  CC_IN+1          -LIST
.FI
.ELSE
.IF    .CSLN
CC_LN  EQU  CC_FA+1          -LINE
CC_LI  EQU  CC_LN+1          -LIST
.ELSE
CC_LI  EQU  CC_FA+1          -LIST
.FI
.FI
CC_NR  EQU  CC_LI+1          -NOERRORS
CC_NX  EQU  CC_NR+1          -NOEXECUTE
CC_NF  EQU  CC_NX+1          -NOFAIL
CC_NL  EQU  CC_NF+1          -NOLIST
CC_NO  EQU  CC_NL+1          -NOOPT
CC_NP  EQU  CC_NO+1          -NOPRINT
CC_OP  EQU  CC_NP+1          -OPTIMISE
CC_PR  EQU  CC_OP+1          -PRINT
CC_SI  EQU  CC_PR+1          -SINGLE
CC_SP  EQU  CC_SI+1          -SPACE
CC_ST  EQU  CC_SP+1          -STITL
CC_TI  EQU  CC_ST+1          -TITLE
CC_TR  EQU  CC_TI+1          -TRACE
CC_NC  EQU  CC_TR+1          NUMBER OF CONTROL CARDS
CCNOC  EQU  4                NO. OF CHARS INCLUDED IN MATCH
CCOFS  EQU  7                OFFSET TO START OF TITLE/SUBTITLE
.IF    .CINC
CCINM  EQU  9                MAX DEPTH OF INCLUDE FILE NESTING
.FI
       EJC
*
*      DEFINITIONS OF STACK OFFSETS USED IN CMPIL PROCEDURE
*
*      SEE DESCRIPTION AT START OF CMPIL PROCEDURE FOR DETAILS
*      OF USE OF THESE LOCATIONS ON THE STACK.
*
CMSTM  EQU  0                TREE FOR STATEMENT BODY
CMSGO  EQU  CMSTM+1          TREE FOR SUCCESS GOTO
CMFGO  EQU  CMSGO+1          TREE FOR FAIL GOTO
CMCGO  EQU  CMFGO+1          CONDITIONAL GOTO FLAG
CMPCD  EQU  CMCGO+1          PREVIOUS CDBLK POINTER
CMFFP  EQU  CMPCD+1          FAILURE FILL IN FLAG FOR PREVIOUS
CMFFC  EQU  CMFFP+1          FAILURE FILL IN FLAG FOR CURRENT
CMSOP  EQU  CMFFC+1          SUCCESS FILL IN OFFSET FOR PREVIOUS
CMSOC  EQU  CMSOP+1          SUCCESS FILL IN OFFSET FOR CURRENT
CMLBL  EQU  CMSOC+1          PTR TO VRBLK FOR CURRENT LABEL
CMTRA  EQU  CMLBL+1          PTR TO ENTRY CDBLK
*
CMNEN  EQU  CMTRA+1          COUNT OF STACK ENTRIES FOR CMPIL
.IF    .CNPF
.ELSE
*
*      A FEW CONSTANTS USED BY THE PROFILER
PFPD1  EQU  8                PAD POSITIONS ...
PFPD2  EQU  20               ... FOR PROFILE ...
PFPD3  EQU  32               ... PRINTOUT
PF_I2  EQU  CFP_I+CFP_I      SIZE OF TABLE ENTRY (2 INTS)
.FI
.IF    .CREL
       EJC
*
*      DEFINITION OF LIMITS AND ADJUSTMENTS THAT ARE BUILT BY
*      RELCR FOR USE BY THE ROUTINES THAT RELOCATE POINTERS
*      AFTER A SAVE FILE IS RELOADED.  SEE RELOC ETC. FOR USAGE.
*
*      A BLOCK OF INFORMATION IS BUILT THAT IS USED IN
*      RELOCATING POINTERS.  THERE ARE RNSI_ INSTANCES
*      OF A RSSI_ WORD STRUCTURE.  EACH INSTANCE CORRESPONDS
*      TO ONE OF THE REGIONS THAT A POINTER MIGHT POINT INTO.
*
*      EACH STRUCTURE TAKES THE FORM:
*
*           +------------------------------------+
*           I    ADDRESS PAST END OF SECTION     I
*           +------------------------------------+
*           I  ADJUSTMENT FROM OLD TO NEW ADRS   I
*           +------------------------------------+
*           I    ADDRESS OF START OF SECTION     I
*           +------------------------------------+
*
*      THE INSTANCES ARE ORDERED THUSLY:
*
*           +------------------------------------+
*           I           DYNAMIC STORAGE          I
*           +------------------------------------+
*           I           STATIC STORAGE           I
*           +------------------------------------+
*           I       WORKING SECTION GLOBALS      I
*           +------------------------------------+
*           I          CONSTANT SECTION          I
*           +------------------------------------+
*           I            CODE SECTION            I
*           +------------------------------------+
*
*      SYMBOLIC NAMES FOR THESE LOCATIONS AS OFFSETS FROM
*      THE FIRST ENTRY ARE PROVIDED HERE.
*
*      DEFINITIONS WITHIN A SECTION
*
RLEND  EQU  0                END
RLADJ  EQU  RLEND+1          ADJUSTMENT
RLSTR  EQU  RLADJ+1          START
RSSI_  EQU  RLSTR+1          SIZE OF SECTION
RNSI_  EQU  5                NUMBER OF STRUCTURES
*
*      OVERALL DEFINITIONS OF ALL STRUCTURES
*
RLDYE  EQU  0                DYNAMIC REGION END
RLDYA  EQU  RLDYE+1          DYNAMIC REGION ADJUSTMENT
RLDYS  EQU  RLDYA+1          DYNAMIC REGION START
RLSTE  EQU  RLDYS+1          STATIC REGION END
RLSTA  EQU  RLSTE+1          STATIC REGION ADJUSTMENT
RLSTS  EQU  RLSTA+1          STATIC REGION START
RLWKE  EQU  RLSTS+1          WORKING SECTION GLOBALS END
RLWKA  EQU  RLWKE+1          WORKING SECTION GLOBALS ADJUSTMENT
RLWKS  EQU  RLWKA+1          WORKING SECTION GLOBALS START
RLCNE  EQU  RLWKS+1          CONSTANTS SECTION END
RLCNA  EQU  RLCNE+1          CONSTANTS SECTION ADJUSTMENT
RLCNS  EQU  RLCNA+1          CONSTANTS SECTION START
RLCDE  EQU  RLCNS+1          CODE SECTION END
RLCDA  EQU  RLCDE+1          CODE SECTION ADJUSTMENT
RLCDS  EQU  RLCDA+1          CODE SECTION START
RLSI_  EQU  RLCDS+1          NUMBER OF FIELDS IN STRUCTURE
.FI
*
       TTL  S P I T B O L -- CONSTANT SECTION
*
*      THIS SECTION CONSISTS ENTIRELY OF ASSEMBLED CONSTANTS.
*
*      ALL LABEL NAMES ARE FIVE LETTERS. THE ORDER IS
*      APPROXIMATELY ALPHABETICAL, BUT IN SOME CASES (ALWAYS
*      DOCUMENTED), CONSTANTS MUST BE PLACED IN SOME SPECIAL
*      ORDER WHICH MUST NOT BE DISTURBED.
*
*      IT MUST ALSO BE REMEMBERED THAT THERE IS A REQUIREMENT
*      FOR NO FORWARD REFERENCES WHICH ALSO DISTURBS THE
*      ALPHABETICAL ORDER IN SOME CASES.
*
       SEC                   START OF CONSTANT SECTION
*
*      START OF CONSTANT SECTION
*
C_AAA  DAC  0                FIRST LOCATION OF CONSTANT SECTION
*
*      FREE STORE PERCENTAGE (USED BY ALLOC)
*
ALFSP  DAC  E_FSP            FREE STORE PERCENTAGE
*
*      BIT CONSTANTS FOR GENERAL USE
*
BITS0  DBC  0                ALL ZERO BITS
BITS1  DBC  1                ONE BIT IN LOW ORDER POSITION
BITS2  DBC  2                BIT IN POSITION 2
BITS3  DBC  4                BIT IN POSITION 3
BITS4  DBC  8                BIT IN POSITION 4
BITS5  DBC  16               BIT IN POSITION 5
BITS6  DBC  32               BIT IN POSITION 6
BITS7  DBC  64               BIT IN POSITION 7
BITS8  DBC  128              BIT IN POSITION 8
BITS9  DBC  256              BIT IN POSITION 9
BIT10  DBC  512              BIT IN POSITION 10
BIT11  DBC  1024             BIT IN POSITION 11
BIT12  DBC  2048             BIT IN POSITION 12
BITSM  DBC  CFP_M            MASK FOR MAX INTEGER
*
*      BIT CONSTANTS FOR SVBLK (SVBIT FIELD) TESTS
*
BTFNC  DBC  SVFNC            BIT TO TEST FOR FUNCTION
BTKNM  DBC  SVKNM            BIT TO TEST FOR KEYWORD NUMBER
BTLBL  DBC  SVLBL            BIT TO TEST FOR LABEL
BTFFC  DBC  SVFFC            BIT TO TEST FOR FAST CALL
BTCKW  DBC  SVCKW            BIT TO TEST FOR CONSTANT KEYWORD
BTKWV  DBC  SVKWV            BITS TO TEST FOR KEWORD WITH VALUE
BTPRD  DBC  SVPRD            BIT TO TEST FOR PREDICATE FUNCTION
BTPRE  DBC  SVPRE            BIT TO TEST FOR PREEVALUATION
BTVAL  DBC  SVVAL            BIT TO TEST FOR VALUE
       EJC
*
*      LIST OF NAMES USED FOR CONTROL CARD PROCESSING
*
.IF    .CULC
CCNMS  DTC  /CASE/
       DTC  /DOUB/
.ELSE
CCNMS  DTC  /DOUB/
.FI
.IF    .CCMK
       DTC  /COMP/
.FI
       DTC  /DUMP/
.IF    .CINC
       DTC  /COPY/
.FI
       DTC  /EJEC/
       DTC  /ERRO/
       DTC  /EXEC/
       DTC  /FAIL/
.IF    .CINC
       DTC  /INCL/
.FI
.IF    .CSLN
       DTC  /LINE/
.FI
       DTC  /LIST/
       DTC  /NOER/
       DTC  /NOEX/
       DTC  /NOFA/
       DTC  /NOLI/
       DTC  /NOOP/
       DTC  /NOPR/
       DTC  /OPTI/
       DTC  /PRIN/
       DTC  /SING/
       DTC  /SPAC/
       DTC  /STIT/
       DTC  /TITL/
       DTC  /TRAC/
*
*      HEADER MESSAGES FOR DUMPR PROCEDURE (SCBLK FORMAT)
*
DMHDK  DAC  B_SCL            DUMP OF KEYWORD VALUES
       DAC  22
       DTC  /Dump of Keyword Values/
*
DMHDV  DAC  B_SCL            DUMP OF NATURAL VARIABLES
       DAC  25
       DTC  /Dump of Natural Variables/
       EJC
*
*      MESSAGE TEXT FOR COMPILATION STATISTICS
*
ENCM1  DAC  B_SCL
.IF    .CBYT
       DAC  19
       DTC  /Memory used (bytes)/
*
ENCM2  DAC  B_SCL
       DAC  19
       DTC  /Memory left (bytes)/
.ELSE
       DAC  19
       DTC  /Memory used (words)/
*
ENCM2  DAC  B_SCL
       DAC  19
       DTC  /Memory left (words)/
.FI
*
ENCM3  DAC  B_SCL
       DAC  11
       DTC  /Comp errors/
*
ENCM4  DAC  B_SCL
.IF    .CTMD
       DAC  19
       DTC  /Comp time (decisec)/
.ELSE
       DAC  20
       DTC  /Comp time (millisec)/
.FI
*
ENCM5  DAC  B_SCL            EXECUTION SUPPRESSED
       DAC  20
       DTC  /Execution suppressed/
*
*      STRING CONSTANT FOR ABNORMAL END
*
ENDAB  DAC  B_SCL
       DAC  12
       DTC  /Abnormal End/
       EJC
*
*      MEMORY OVERFLOW DURING INITIALISATION
*
ENDMO  DAC  B_SCL
ENDML  DAC  15
       DTC  /Memory Overflow/
*
*      STRING CONSTANT FOR MESSAGE ISSUED BY L_END
*
ENDMS  DAC  B_SCL
       DAC  10
       DTC  /Normal end/
*
*      FAIL MESSAGE FOR STACK FAIL SECTION
*
ENDSO  DAC  B_SCL            STACK OVERFLOW IN GARBAGE COLLECTOR
       DAC  36
       DTC  /Stack overflow in garbage collection/
*
*      STRING CONSTANT FOR TIME UP
*
ENDTU  DAC  B_SCL
       DAC  15
       DTC  /Error - Time Up/
       EJC
*
*      STRING CONSTANT FOR ERROR MESSAGE (ERROR SECTION)
*
ERMMS  DAC  B_SCL            ERROR
       DAC  5
       DTC  /Error/
*
ERMNS  DAC  B_SCL            STRING / -- /
       DAC  4
       DTC  / -- /
*
*      STRING CONSTANT FOR PAGE NUMBERING
*
LSTMS  DAC  B_SCL            PAGE
       DAC  5
       DTC  /Page /
*
*      LISTING HEADER MESSAGE
*
HEADR  DAC  B_SCL
       DAC  27
       DTC  /Macro SPITBOL Version 13.01/
*
HEADV  DAC  B_SCL            FOR EXIT() VERSION NO. CHECK
       DAC  5
       DTC  /13.01/
.IF    .CSED
*      FREE STORE PERCENTAGE (USED BY GBCOL)
*
GBSDP  DAC  E_SED            SEDIMENT PERCENTAGE
.FI
*
*      INTEGER CONSTANTS FOR GENERAL USE
*      ICBLD OPTIMISATION USES THE FIRST THREE.
*
INT_R  DAC  B_ICL
INTV0  DIC  +0               0
INTON  DAC  B_ICL
INTV1  DIC  +1               1
INTTW  DAC  B_ICL
INTV2  DIC  +2               2
INTVT  DIC  +10              10
INTVH  DIC  +100             100
INTTH  DIC  +1000            1000
*
*      TABLE USED IN ICBLD OPTIMISATION
*
INTAB  DAC  INT_R            POINTER TO 0
       DAC  INTON            POINTER TO 1
       DAC  INTTW            POINTER TO 2
       EJC
*
*      SPECIAL PATTERN NODES. THE FOLLOWING PATTERN NODES
*      CONSIST SIMPLY OF A PCODE POINTER, SEE MATCH ROUTINES
*      (P_XXX) FOR FULL DETAILS OF THEIR USE AND FORMAT).
*
NDABB  DAC  P_ABB            ARBNO
NDABD  DAC  P_ABD            ARBNO
NDARC  DAC  P_ARC            ARB
NDEXB  DAC  P_EXB            EXPRESSION
NDFNB  DAC  P_FNB            FENCE()
NDFND  DAC  P_FND            FENCE()
NDEXC  DAC  P_EXC            EXPRESSION
NDIMB  DAC  P_IMB            IMMEDIATE ASSIGNMENT
NDIMD  DAC  P_IMD            IMMEDIATE ASSIGNMENT
NDNTH  DAC  P_NTH            PATTERN END (NULL PATTERN)
NDPAB  DAC  P_PAB            PATTERN ASSIGNMENT
NDPAD  DAC  P_PAD            PATTERN ASSIGNMENT
NDUNA  DAC  P_UNA            ANCHOR POINT MOVEMENT
*
*      KEYWORD CONSTANT PATTERN NODES. THE FOLLOWING NODES ARE
*      USED AS THE VALUES OF PATTERN KEYWORDS AND THE INITIAL
*      VALUES OF THE CORRESPONDING NATURAL VARIABLES. ALL
*      NODES ARE IN P0BLK FORMAT AND THE ORDER IS TIED TO THE
*      DEFINITIONS OF CORRESPONDING K_XXX SYMBOLS.
*
NDABO  DAC  P_ABO            ABORT
       DAC  NDNTH
NDARB  DAC  P_ARB            ARB
       DAC  NDNTH
NDBAL  DAC  P_BAL            BAL
       DAC  NDNTH
NDFAL  DAC  P_FAL            FAIL
       DAC  NDNTH
NDFEN  DAC  P_FEN            FENCE
       DAC  NDNTH
NDREM  DAC  P_REM            REM
       DAC  NDNTH
NDSUC  DAC  P_SUC            SUCCEED
       DAC  NDNTH
*
*      NULL STRING. ALL NULL VALUES POINT TO THIS STRING. THE
*      SVCHS FIELD CONTAINS A BLANK TO PROVIDE FOR EASY DEFAULT
*      PROCESSING IN TRACE, STOPTR, LPAD AND RPAD.
*      NULLW CONTAINS 10 BLANKS WHICH ENSURES AN ALL BLANK WORD
*      BUT FOR VERY EXCEPTIONAL MACHINES.
*
NULLS  DAC  B_SCL            NULL STRING VALUE
       DAC  0                SCLEN = 0
NULLW  DTC  /          /
*
.IF    .CULK
*
*      CONSTANT STRINGS FOR LCASE AND UCASE KEYWORDS
*
LCASE  DAC  B_SCL
       DAC  26
       DTC  /abcdefghijklmnopqrstuvwxyz/
*
UCASE  DAC  B_SCL
       DAC  26
       DTC  /ABCDEFGHIJKLMNOPQRSTUVWXYZ/
.FI
       EJC
*
*      OPERATOR DOPE VECTORS (SEE DVBLK FORMAT)
*
OPDVC  DAC  O_CNC            CONCATENATION
       DAC  C_CNC
       DAC  LLCNC
       DAC  RRCNC
*
*      OPDVS IS USED WHEN SCANNING BELOW THE TOP LEVEL TO
*      INSURE THAT THE CONCATENATION WILL NOT BE LATER
*      MISTAKEN FOR PATTERN MATCHING
*
OPDVP  DAC  O_CNC            CONCATENATION - NOT PATTERN MATCH
       DAC  C_CNP
       DAC  LLCNC
       DAC  RRCNC
*
*      NOTE THAT THE ORDER OF THE REMAINING ENTRIES IS TIED TO
*      THE ORDER OF THE CODING IN THE SCANE PROCEDURE.
*
OPDVS  DAC  O_ASS            ASSIGNMENT
       DAC  C_ASS
       DAC  LLASS
       DAC  RRASS
*
       DAC  6                UNARY EQUAL
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  O_PMV            PATTERN MATCH
       DAC  C_PMT
       DAC  LLPMT
       DAC  RRPMT
*
       DAC  O_INT            INTERROGATION
       DAC  C_UVL
       DAC  LLUNO
*
       DAC  1                BINARY AMPERSAND
       DAC  C_UBO
       DAC  LLAMP
       DAC  RRAMP
*
       DAC  O_KWV            KEYWORD REFERENCE
       DAC  C_KEY
       DAC  LLUNO
*
       DAC  O_ALT            ALTERNATION
       DAC  C_ALT
       DAC  LLALT
       DAC  RRALT
       EJC
*
*      OPERATOR DOPE VECTORS (CONTINUED)
*
       DAC  5                UNARY VERTICAL BAR
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  0                BINARY AT
       DAC  C_UBO
       DAC  LLATS
       DAC  RRATS
*
       DAC  O_CAS            CURSOR ASSIGNMENT
       DAC  C_UNM
       DAC  LLUNO
*
       DAC  2                BINARY NUMBER SIGN
       DAC  C_UBO
       DAC  LLNUM
       DAC  RRNUM
*
       DAC  7                UNARY NUMBER SIGN
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  O_DVD            DIVISION
       DAC  C_BVL
       DAC  LLDVD
       DAC  RRDVD
*
       DAC  9                UNARY SLASH
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  O_MLT            MULTIPLICATION
       DAC  C_BVL
       DAC  LLMLT
       DAC  RRMLT
       EJC
*
*      OPERATOR DOPE VECTORS (CONTINUED)
*
       DAC  0                DEFERRED EXPRESSION
       DAC  C_DEF
       DAC  LLUNO
*
       DAC  3                BINARY PERCENT
       DAC  C_UBO
       DAC  LLPCT
       DAC  RRPCT
*
       DAC  8                UNARY PERCENT
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  O_EXP            EXPONENTIATION
       DAC  C_BVL
       DAC  LLEXP
       DAC  RREXP
*
       DAC  10               UNARY EXCLAMATION
       DAC  C_UUO
       DAC  LLUNO
*
       DAC  O_IMA            IMMEDIATE ASSIGNMENT
       DAC  C_BVN
       DAC  LLDLD
       DAC  RRDLD
*
       DAC  O_INV            INDIRECTION
       DAC  C_IND
       DAC  LLUNO
*
       DAC  4                BINARY NOT
       DAC  C_UBO
       DAC  LLNOT
       DAC  RRNOT
*
       DAC  0                NEGATION
       DAC  C_NEG
       DAC  LLUNO
       EJC
*
*      OPERATOR DOPE VECTORS (CONTINUED)
*
       DAC  O_SUB            SUBTRACTION
       DAC  C_BVL
       DAC  LLPLM
       DAC  RRPLM
*
       DAC  O_COM            COMPLEMENTATION
       DAC  C_UVL
       DAC  LLUNO
*
       DAC  O_ADD            ADDITION
       DAC  C_BVL
       DAC  LLPLM
       DAC  RRPLM
*
       DAC  O_AFF            AFFIRMATION
       DAC  C_UVL
       DAC  LLUNO
*
       DAC  O_PAS            PATTERN ASSIGNMENT
       DAC  C_BVN
       DAC  LLDLD
       DAC  RRDLD
*
       DAC  O_NAM            NAME REFERENCE
       DAC  C_UNM
       DAC  LLUNO
*
*      SPECIAL DVS FOR GOTO OPERATORS (SEE PROCEDURE SCNGF)
*
OPDVD  DAC  O_GOD            DIRECT GOTO
       DAC  C_UVL
       DAC  LLUNO
*
OPDVN  DAC  O_GOC            COMPLEX NORMAL GOTO
       DAC  C_UNM
       DAC  LLUNO
       EJC
*
*      OPERATOR ENTRY ADDRESS POINTERS, USED IN CODE
*
OAMN_  DAC  O_AMN            ARRAY REF (MULTI-SUBS BY VALUE)
OAMV_  DAC  O_AMV            ARRAY REF (MULTI-SUBS BY VALUE)
OAON_  DAC  O_AON            ARRAY REF (ONE SUB BY NAME)
OAOV_  DAC  O_AOV            ARRAY REF (ONE SUB BY VALUE)
OCER_  DAC  O_CER            COMPILATION ERROR
OFEX_  DAC  O_FEX            FAILURE IN EXPRESSION EVALUATION
OFIF_  DAC  O_FIF            FAILURE DURING GOTO EVALUATION
OFNC_  DAC  O_FNC            FUNCTION CALL (MORE THAN ONE ARG)
OFNE_  DAC  O_FNE            FUNCTION NAME ERROR
OFNS_  DAC  O_FNS            FUNCTION CALL (SINGLE ARGUMENT)
OGOF_  DAC  O_GOF            SET GOTO FAILURE TRAP
OINN_  DAC  O_INN            INDIRECTION BY NAME
OKWN_  DAC  O_KWN            KEYWORD REFERENCE BY NAME
OLEX_  DAC  O_LEX            LOAD EXPRESSION BY NAME
OLPT_  DAC  O_LPT            LOAD PATTERN
OLVN_  DAC  O_LVN            LOAD VARIABLE NAME
ONTA_  DAC  O_NTA            NEGATION, FIRST ENTRY
ONTB_  DAC  O_NTB            NEGATION, SECOND ENTRY
ONTC_  DAC  O_NTC            NEGATION, THIRD ENTRY
OPMN_  DAC  O_PMN            PATTERN MATCH BY NAME
OPMS_  DAC  O_PMS            PATTERN MATCH (STATEMENT)
OPOP_  DAC  O_POP            POP TOP STACK ITEM
ORNM_  DAC  O_RNM            RETURN NAME FROM EXPRESSION
ORPL_  DAC  O_RPL            PATTERN REPLACEMENT
ORVL_  DAC  O_RVL            RETURN VALUE FROM EXPRESSION
OSLA_  DAC  O_SLA            SELECTION, FIRST ENTRY
OSLB_  DAC  O_SLB            SELECTION, SECOND ENTRY
OSLC_  DAC  O_SLC            SELECTION, THIRD ENTRY
OSLD_  DAC  O_SLD            SELECTION, FOURTH ENTRY
OSTP_  DAC  O_STP            STOP EXECUTION
OUNF_  DAC  O_UNF            UNEXPECTED FAILURE
       EJC
*
*      TABLE OF NAMES OF UNDEFINED BINARY OPERATORS FOR OPSYN
*
OPSNB  DAC  CH_AT            AT
       DAC  CH_AM            AMPERSAND
       DAC  CH_NM            NUMBER
       DAC  CH_PC            PERCENT
       DAC  CH_NT            NOT
*
*      TABLE OF NAMES OF UNDEFINED UNARY OPERATORS FOR OPSYN
*
OPNSU  DAC  CH_BR            VERTICAL BAR
       DAC  CH_EQ            EQUAL
       DAC  CH_NM            NUMBER
       DAC  CH_PC            PERCENT
       DAC  CH_SL            SLASH
       DAC  CH_EX            EXCLAMATION
.IF    .CNPF
.ELSE
*
*      ADDRESS CONST CONTAINING PROFILE TABLE ENTRY SIZE
*
PFI2A  DAC  PF_I2
*
*      PROFILER MESSAGE STRINGS
*
PFMS1  DAC  B_SCL
       DAC  15
       DTC  /Program Profile/
PFMS2  DAC  B_SCL
       DAC  42
       DTC  /STMT    Number Of     -- Execution Time --/
PFMS3  DAC  B_SCL
       DAC  47
       DTC  /Number  Executions  Total(MSec) per Excn(MCSec)/
.FI
*
.IF    .CNRA
.ELSE
*
*      REAL CONSTANTS FOR GENERAL USE. NOTE THAT THE CONSTANTS
*      STARTING AT REAV1 FORM A POWERS OF TEN TABLE (USED IN
*      GTNUM AND GTSTG)
*
REAV0  DRC  +0.0             0.0
.IF    .CNCR
.ELSE
REAP1  DRC  +0.1             0.1
REAP5  DRC  +0.5             0.5
.FI
REAV1  DRC  +1.0             10**0
REAVT  DRC  +1.0E+1          10**1
       DRC  +1.0E+2          10**2
       DRC  +1.0E+3          10**3
       DRC  +1.0E+4          10**4
       DRC  +1.0E+5          10**5
       DRC  +1.0E+6          10**6
       DRC  +1.0E+7          10**7
       DRC  +1.0E+8          10**8
       DRC  +1.0E+9          10**9
REATT  DRC  +1.0E+10         10**10
.FI
       EJC
*
*      STRING CONSTANTS (SCBLK FORMAT) FOR DTYPE PROCEDURE
*
SCARR  DAC  B_SCL            ARRAY
       DAC  5
       DTC  /ARRAY/
.IF    .CNBF
.ELSE
*
SCBUF  DAC  B_SCL            BUFFER
       DAC  6
       DTC  /BUFFER/
.FI
*
SCCOD  DAC  B_SCL            CODE
       DAC  4
       DTC  /CODE/
*
SCEXP  DAC  B_SCL            EXPRESSION
       DAC  10
       DTC  /EXPRESSION/
*
SCEXT  DAC  B_SCL            EXTERNAL
       DAC  8
       DTC  /EXTERNAL/
*
SCINT  DAC  B_SCL            INTEGER
       DAC  7
       DTC  /INTEGER/
*
SCNAM  DAC  B_SCL            NAME
       DAC  4
       DTC  /NAME/
*
SCNUM  DAC  B_SCL            NUMERIC
       DAC  7
       DTC  /NUMERIC/
*
SCPAT  DAC  B_SCL            PATTERN
       DAC  7
       DTC  /PATTERN/
.IF    .CNRA
.ELSE
*
SCREA  DAC  B_SCL            REAL
       DAC  4
       DTC  /REAL/
.FI
*
SCSTR  DAC  B_SCL            STRING
       DAC  6
       DTC  /STRING/
*
SCTAB  DAC  B_SCL            TABLE
       DAC  5
       DTC  /TABLE/
.IF    .CNLF
SCFIL  DAC  B_SCL            FILE (FOR EXTENDED LOAD ARGUMENTS)
       DAC  4
       DTC  /FILE/
.FI
       EJC
*
*      STRING CONSTANTS (SCBLK FORMAT) FOR KVRTN (SEE RETRN)
*
SCFRT  DAC  B_SCL            FRETURN
       DAC  7
       DTC  /FRETURN/
*
SCNRT  DAC  B_SCL            NRETURN
       DAC  7
       DTC  /NRETURN/
*
SCRTN  DAC  B_SCL            RETURN
       DAC  6
       DTC  /RETURN/
*
*      DATATYPE NAME TABLE FOR DTYPE PROCEDURE. THE ORDER OF
*      THESE ENTRIES IS TIED TO THE B_XXX DEFINITIONS FOR BLOCKS
*
*      NOTE THAT SLOTS FOR BUFFER AND REAL DATA TYPES ARE FILLED
*      EVEN IF THESE DATA TYPES ARE CONDITIONALIZED OUT OF THE
*      IMPLEMENTATION.  THIS IS DONE SO THAT THE BLOCK NUMBERING
*      AT BL_AR ETC. REMAINS CONSTANT IN ALL VERSIONS.
*
SCNMT  DAC  SCARR            ARBLK     ARRAY
       DAC  SCCOD            CDBLK     CODE
       DAC  SCEXP            EXBLK     EXPRESSION
       DAC  SCINT            ICBLK     INTEGER
       DAC  SCNAM            NMBLK     NAME
       DAC  SCPAT            P0BLK     PATTERN
       DAC  SCPAT            P1BLK     PATTERN
       DAC  SCPAT            P2BLK     PATTERN
.IF    .CNRA
       DAC  NULLS            RCBLK     NO REAL IN THIS VERSION
.ELSE

       DAC  SCREA            RCBLK     REAL
.FI
       DAC  SCSTR            SCBLK     STRING
       DAC  SCEXP            SEBLK     EXPRESSION
       DAC  SCTAB            TBBLK     TABLE
       DAC  SCARR            VCBLK     ARRAY
       DAC  SCEXT            XNBLK     EXTERNAL
       DAC  SCEXT            XRBLK     EXTERNAL
.IF    .CNBF
       DAC  NULLS            BFBLK     NO BUFFER IN THIS VERSION
.ELSE
       DAC  SCBUF            BFBLK     BUFFER
.FI
*
.IF    .CNRA
.ELSE
*      STRING CONSTANT FOR REAL ZERO
*
SCRE0  DAC  B_SCL
       DAC  2
       DTC  /0./
.FI
       EJC
*
*      USED TO RE-INITIALISE KVSTL
*
.IF    .CS16
STLIM  DIC  +32767           DEFAULT STATEMENT LIMIT
.ELSE
.IF    .CS32
STLIM  DIC  +2147483647      DEFAULT STATEMENT LIMIT
.ELSE
STLIM  DIC  +50000           DEFAULT STATEMENT LIMIT
.FI
.FI
*
*      DUMMY FUNCTION BLOCK USED FOR UNDEFINED FUNCTIONS
*
STNDF  DAC  O_FUN            PTR TO UNDEFINED FUNCTION ERR CALL
       DAC  0                DUMMY FARGS COUNT FOR CALL CIRCUIT
*
*      DUMMY CODE BLOCK USED FOR UNDEFINED LABELS
*
STNDL  DAC  L_UND            CODE PTR POINTS TO UNDEFINED LBL
*
*      DUMMY OPERATOR BLOCK USED FOR UNDEFINED OPERATORS
*
STNDO  DAC  O_OUN            PTR TO UNDEFINED OPERATOR ERR CALL
       DAC  0                DUMMY FARGS COUNT FOR CALL CIRCUIT
*
*      STANDARD VARIABLE BLOCK. THIS BLOCK IS USED TO INITIALIZE
*      THE FIRST SEVEN FIELDS OF A NEWLY CONSTRUCTED VRBLK.
*      ITS FORMAT IS TIED TO THE VRBLK DEFINITIONS (SEE GTNVR).
*
STNVR  DAC  B_VRL            VRGET
       DAC  B_VRS            VRSTO
       DAC  NULLS            VRVAL
       DAC  B_VRG            VRTRA
       DAC  STNDL            VRLBL
       DAC  STNDF            VRFNC
       DAC  0                VRNXT
       EJC
*
*      MESSAGES USED IN END OF RUN PROCESSING (STOPR)
*
STPM1  DAC  B_SCL            IN STATEMENT
       DAC  12
       DTC  /In statement/
*
STPM2  DAC  B_SCL
       DAC  14
       DTC  /Stmts executed/
*
STPM3  DAC  B_SCL
.IF    .CTMD
       DAC  18
       DTC  /Run time (decisec)/
.ELSE
       DAC  19
       DTC  /Run time (millisec)/
.FI
*
STPM4  DAC  B_SCL
       DAC  12
       DTC  _MCSec / Stmt_
*
STPM5  DAC  B_SCL
       DAC  13
       DTC  /Regenerations/
.IF    .CSLN
*
STPM6  DAC  B_SCL            IN LINE
       DAC  7
       DTC  /In line/
.FI
.IF    .CSFN
*
STPM7  DAC  B_SCL            IN FILE
       DAC  7
       DTC  /In file/
.FI
*
*      CHARS FOR /TU/ ENDING CODE
*
STRTU  DTC  /TU/
*
*      TABLE USED BY CONVERT FUNCTION TO CHECK DATATYPE NAME
*      THE ENTRIES ARE ORDERED TO CORRESPOND TO BRANCH TABLE
*      IN S_CNV
*
SVCTB  DAC  SCSTR            STRING
       DAC  SCINT            INTEGER
       DAC  SCNAM            NAME
       DAC  SCPAT            PATTERN
       DAC  SCARR            ARRAY
       DAC  SCTAB            TABLE
       DAC  SCEXP            EXPRESSION
       DAC  SCCOD            CODE
       DAC  SCNUM            NUMERIC
.IF    .CNRA
.ELSE
       DAC  SCREA            REAL
.FI
.IF    .CNBF
.ELSE
       DAC  SCBUF            BUFFER
.FI
       DAC  0                ZERO MARKS END OF LIST
       EJC
*
*      MESSAGES (SCBLK FORMAT) USED BY TRACE PROCEDURES
*
*
TMASB  DAC  B_SCL            ASTERISKS FOR TRACE STATEMENT NO
       DAC  13
       DTC  /************ /

*
TMBEB  DAC  B_SCL            BLANK-EQUAL-BLANK
       DAC  3
       DTC  / = /
*
*      DUMMY TRBLK FOR EXPRESSION VARIABLE
*
TRBEV  DAC  B_TRT            DUMMY TRBLK
*
*      DUMMY TRBLK FOR KEYWORD VARIABLE
*
TRBKV  DAC  B_TRT            DUMMY TRBLK
*
*      DUMMY CODE BLOCK TO RETURN CONTROL TO TRXEQ PROCEDURE
*
TRXDR  DAC  O_TXR            BLOCK POINTS TO RETURN ROUTINE
TRXDC  DAC  TRXDR            POINTER TO BLOCK
       EJC
*
*      STANDARD VARIABLE BLOCKS
*
*      SEE SVBLK FORMAT FOR FULL DETAILS OF THE FORMAT. THE
*      VRBLKS ARE ORDERED BY LENGTH AND WITHIN EACH LENGTH THE
*      ORDER IS ALPHABETICAL BY NAME OF THE VARIABLE.
*
V_EQF  DBC  SVFPR            EQ
       DAC  2
       DTC  /EQ/
       DAC  S_EQF
       DAC  2
*
V_GEF  DBC  SVFPR            GE
       DAC  2
       DTC  /GE/
       DAC  S_GEF
       DAC  2
*
V_GTF  DBC  SVFPR            GT
       DAC  2
       DTC  /GT/
       DAC  S_GTF
       DAC  2
*
V_LEF  DBC  SVFPR            LE
       DAC  2
       DTC  /LE/
       DAC  S_LEF
       DAC  2
.IF    .CMTH
*
V_LNF  DBC  SVFNP            LN
       DAC  2
       DTC  /LN/
       DAC  S_LNF
       DAC  1
.FI
*
V_LTF  DBC  SVFPR            LT
       DAC  2
       DTC  /LT/
       DAC  S_LTF
       DAC  2
*
V_NEF  DBC  SVFPR            NE
       DAC  2
       DTC  /NE/
       DAC  S_NEF
       DAC  2
.IF    .C370
*
V_ORF  DBC  SVFNP            OR
       DAC  2
       DTC  /OR/
       DAC  S_ORF
       DAC  2
.FI
.IF    .C370
*
V_ABS  DBC  SVFNP            ABS
       DAC  3
       DTC  /ABS/
       DAC  S_ABS
       DAC  1
.FI
.IF    .C370
*
V_AND  DBC  SVFNP            AND
       DAC  3
       DTC  /AND/
       DAC  S_AND
       DAC  2
.FI
*
V_ANY  DBC  SVFNP            ANY
       DAC  3
       DTC  /ANY/
       DAC  S_ANY
       DAC  1
*
V_ARB  DBC  SVKVC            ARB
       DAC  3
       DTC  /ARB/
       DAC  K_ARB
       DAC  NDARB
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_ARG  DBC  SVFNN            ARG
       DAC  3
       DTC  /ARG/
       DAC  S_ARG
       DAC  2
*
V_BAL  DBC  SVKVC            BAL
       DAC  3
       DTC  /BAL/
       DAC  K_BAL
       DAC  NDBAL
.IF    .CMTH
*
V_COS  DBC  SVFNP            COS
       DAC  3
       DTC  /COS/
       DAC  S_COS
       DAC  1
.FI
*
V_END  DBC  SVLBL            END
       DAC  3
       DTC  /END/
       DAC  L_END
.IF    .CMTH
*
V_EXP  DBC  SVFNP            EXP
       DAC  3
       DTC  /EXP/
       DAC  S_EXP
       DAC  1
.FI
*
V_LEN  DBC  SVFNP            LEN
       DAC  3
       DTC  /LEN/
       DAC  S_LEN
       DAC  1
*
V_LEQ  DBC  SVFPR            LEQ
       DAC  3
       DTC  /LEQ/
       DAC  S_LEQ
       DAC  2
*
V_LGE  DBC  SVFPR            LGE
       DAC  3
       DTC  /LGE/
       DAC  S_LGE
       DAC  2
*
V_LGT  DBC  SVFPR            LGT
       DAC  3
       DTC  /LGT/
       DAC  S_LGT
       DAC  2
*
V_LLE  DBC  SVFPR            LLE
       DAC  3
       DTC  /LLE/
       DAC  S_LLE
       DAC  2
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_LLT  DBC  SVFPR            LLT
       DAC  3
       DTC  /LLT/
       DAC  S_LLT
       DAC  2
*
V_LNE  DBC  SVFPR            LNE
       DAC  3
       DTC  /LNE/
       DAC  S_LNE
       DAC  2
*
V_POS  DBC  SVFNP            POS
       DAC  3
       DTC  /POS/
       DAC  S_POS
       DAC  1
*
V_REM  DBC  SVKVC            REM
       DAC  3
       DTC  /REM/
       DAC  K_REM
       DAC  NDREM
.IF    .CUST
*
V_SET  DBC  SVFNN            SET
       DAC  3
       DTC  /SET/
       DAC  S_SET
       DAC  3
.FI
.IF    .CMTH
*
V_SIN  DBC  SVFNP            SIN
       DAC  3
       DTC  /SIN/
       DAC  S_SIN
       DAC  1
.FI
*
V_TAB  DBC  SVFNP            TAB
       DAC  3
       DTC  /TAB/
       DAC  S_TAB
       DAC  1
.IF    .CMTH
*
V_TAN  DBC  SVFNP            TAN
       DAC  3
       DTC  /TAN/
       DAC  S_TAN
       DAC  1
.FI
.IF    .C370
*
V_XOR  DBC  SVFNP            XOR
       DAC  3
       DTC  /XOR/
       DAC  S_XOR
       DAC  2
.FI
.IF    .CMTH
*
V_ATN  DBC  SVFNP            ATAN
       DAC  4
       DTC  /ATAN/
       DAC  S_ATN
       DAC  1
.FI
.IF    .CULC
*
V_CAS  DBC  SVKNM            CASE
       DAC  4
       DTC  /CASE/
       DAC  K_CAS
.FI
*
V_CHR  DBC  SVFNP            CHAR
       DAC  4
       DTC  /CHAR/
       DAC  S_CHR
       DAC  1
*
.IF    .CMTH
*
V_CHP  DBC  SVFNP            CHOP
       DAC  4
       DTC  /CHOP/
       DAC  S_CHP
       DAC  1
.FI
V_COD  DBC  SVFNK            CODE
       DAC  4
       DTC  /CODE/
       DAC  K_COD
       DAC  S_COD
       DAC  1
*
V_COP  DBC  SVFNN            COPY
       DAC  4
       DTC  /COPY/
       DAC  S_COP
       DAC  1
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_DAT  DBC  SVFNN            DATA
       DAC  4
       DTC  /DATA/
       DAC  S_DAT
       DAC  1
*
V_DTE  DBC  SVFNN            DATE
       DAC  4
       DTC  /DATE/
       DAC  S_DTE
       DAC  1
*
V_DMP  DBC  SVFNK            DUMP
       DAC  4
       DTC  /DUMP/
       DAC  K_DMP
       DAC  S_DMP
       DAC  1
*
V_DUP  DBC  SVFNN            DUPL
       DAC  4
       DTC  /DUPL/
       DAC  S_DUP
       DAC  2
*
V_EVL  DBC  SVFNN            EVAL
       DAC  4
       DTC  /EVAL/
       DAC  S_EVL
       DAC  1
.IF    .CNEX
.ELSE
*
V_EXT  DBC  SVFNN            EXIT
       DAC  4
       DTC  /EXIT/
       DAC  S_EXT
       DAC  2
.FI
*
V_FAL  DBC  SVKVC            FAIL
       DAC  4
       DTC  /FAIL/
       DAC  K_FAL
       DAC  NDFAL
*
.IF    .CSFN
V_FIL  DBC  SVKNM            FILE
       DAC  4
       DTC  /FILE/
       DAC  K_FIL
*
.FI
V_HST  DBC  SVFNN            HOST
       DAC  4
       DTC  /HOST/
       DAC  S_HST
       DAC  5
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_ITM  DBC  SVFNF            ITEM
       DAC  4
       DTC  /ITEM/
       DAC  S_ITM
       DAC  999
.IF    .CSLN
*
V_LIN  DBC  SVKNM            LINE
       DAC  4
       DTC  /LINE/
       DAC  K_LIN
.FI
.IF    .CNLD
.ELSE
*
V_LOD  DBC  SVFNN            LOAD
       DAC  4
       DTC  /LOAD/
       DAC  S_LOD
       DAC  2
.FI
*
V_LPD  DBC  SVFNP            LPAD
       DAC  4
       DTC  /LPAD/
       DAC  S_LPD
       DAC  3
*
V_RPD  DBC  SVFNP            RPAD
       DAC  4
       DTC  /RPAD/
       DAC  S_RPD
       DAC  3
*
V_RPS  DBC  SVFNP            RPOS
       DAC  4
       DTC  /RPOS/
       DAC  S_RPS
       DAC  1
*
V_RTB  DBC  SVFNP            RTAB
       DAC  4
       DTC  /RTAB/
       DAC  S_RTB
       DAC  1
*
V_SI_  DBC  SVFNP            SIZE
       DAC  4
       DTC  /SIZE/
       DAC  S_SI_
       DAC  1
*
.IF    .CNSR
.ELSE
*
V_SRT  DBC  SVFNN            SORT
       DAC  4
       DTC  /SORT/
       DAC  S_SRT
       DAC  2
.FI
V_SPN  DBC  SVFNP            SPAN
       DAC  4
       DTC  /SPAN/
       DAC  S_SPN
       DAC  1
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
.IF    .CMTH
*
V_SQR  DBC  SVFNP            SQRT
       DAC  4
       DTC  /SQRT/
       DAC  S_SQR
       DAC  1
.FI
V_STN  DBC  SVKNM            STNO
       DAC  4
       DTC  /STNO/
       DAC  K_STN
*
V_TIM  DBC  SVFNN            TIME
       DAC  4
       DTC  /TIME/
       DAC  S_TIM
       DAC  0
*
V_TRM  DBC  SVFNK            TRIM
       DAC  4
       DTC  /TRIM/
       DAC  K_TRM
       DAC  S_TRM
       DAC  1
*
V_ABE  DBC  SVKNM            ABEND
       DAC  5
       DTC  /ABEND/
       DAC  K_ABE
*
V_ABO  DBC  SVKVL            ABORT
       DAC  5
       DTC  /ABORT/
       DAC  K_ABO
       DAC  L_ABO
       DAC  NDABO
*
V_APP  DBC  SVFNF            APPLY
       DAC  5
       DTC  /APPLY/
       DAC  S_APP
       DAC  999
*
V_ABN  DBC  SVFNP            ARBNO
       DAC  5
       DTC  /ARBNO/
       DAC  S_ABN
       DAC  1
*
V_ARR  DBC  SVFNN            ARRAY
       DAC  5
       DTC  /ARRAY/
       DAC  S_ARR
       DAC  2
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_BRK  DBC  SVFNP            BREAK
       DAC  5
       DTC  /BREAK/
       DAC  S_BRK
       DAC  1
*
V_CLR  DBC  SVFNN            CLEAR
       DAC  5
       DTC  /CLEAR/
       DAC  S_CLR
       DAC  1
.IF    .C370
*
V_CMP  DBC  SVFNP            COMPL
       DAC  5
       DTC  /COMPL/
       DAC  S_CMP
       DAC  1
.FI
*
V_EJC  DBC  SVFNN            EJECT
       DAC  5
       DTC  /EJECT/
       DAC  S_EJC
       DAC  1
*
V_FEN  DBC  SVFPK            FENCE
       DAC  5
       DTC  /FENCE/
       DAC  K_FEN
       DAC  S_FNC
       DAC  1
       DAC  NDFEN
*
V_FLD  DBC  SVFNN            FIELD
       DAC  5
       DTC  /FIELD/
       DAC  S_FLD
       DAC  2
*
V_IDN  DBC  SVFPR            IDENT
       DAC  5
       DTC  /IDENT/
       DAC  S_IDN
       DAC  2
*
V_INP  DBC  SVFNK            INPUT
       DAC  5
       DTC  /INPUT/
       DAC  K_INP
       DAC  S_INP
       DAC  3
.IF    .CULK
*
V_LCS  DBC  SVKWC            LCASE
       DAC  5
       DTC  /LCASE/
       DAC  K_LCS
.FI
*
V_LOC  DBC  SVFNN            LOCAL
       DAC  5
       DTC  /LOCAL/
       DAC  S_LOC
       DAC  2
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_OPS  DBC  SVFNN            OPSYN
       DAC  5
       DTC  /OPSYN/
       DAC  S_OPS
       DAC  3
*
V_RMD  DBC  SVFNP            REMDR
       DAC  5
       DTC  /REMDR/
       DAC  S_RMD
       DAC  2
.IF    .CNSR
.ELSE
*
V_RSR  DBC  SVFNN            RSORT
       DAC  5
       DTC  /RSORT/
       DAC  S_RSR
       DAC  2
.FI
*
V_TBL  DBC  SVFNN            TABLE
       DAC  5
       DTC  /TABLE/
       DAC  S_TBL
       DAC  3
*
V_TRA  DBC  SVFNK            TRACE
       DAC  5
       DTC  /TRACE/
       DAC  K_TRA
       DAC  S_TRA
       DAC  4
.IF    .CULK
*
V_UCS  DBC  SVKWC            UCASE
       DAC  5
       DTC  /UCASE/
       DAC  K_UCS
.FI
*
V_ANC  DBC  SVKNM            ANCHOR
       DAC  6
       DTC  /ANCHOR/
       DAC  K_ANC
.IF    .CNBF
.ELSE
*
V_APN  DBC  SVFNN            APPEND
       DAC  6
       DTC  /APPEND/
       DAC  S_APN
       DAC  2
.FI
*
V_BKX  DBC  SVFNP            BREAKX
       DAC  6
       DTC  /BREAKX/
       DAC  S_BKX
       DAC  1
*
.IF    .CNBF
.ELSE
V_BUF  DBC  SVFNN            BUFFER
       DAC  6
       DTC  /BUFFER/
       DAC  S_BUF
       DAC  2
.FI
*
V_DEF  DBC  SVFNN            DEFINE
       DAC  6
       DTC  /DEFINE/
       DAC  S_DEF
       DAC  2
*
V_DET  DBC  SVFNN            DETACH
       DAC  6
       DTC  /DETACH/
       DAC  S_DET
       DAC  1
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_DIF  DBC  SVFPR            DIFFER
       DAC  6
       DTC  /DIFFER/
       DAC  S_DIF
       DAC  2
*
V_FTR  DBC  SVKNM            FTRACE
       DAC  6
       DTC  /FTRACE/
       DAC  K_FTR
*
.IF    .CNBF
.ELSE
V_INS  DBC  SVFNN            INSERT
       DAC  6
       DTC  /INSERT/
       DAC  S_INS
       DAC  4
*
.FI
V_LST  DBC  SVKNM            LASTNO
       DAC  6
       DTC  /LASTNO/
       DAC  K_LST
*
V_NAY  DBC  SVFNP            NOTANY
       DAC  6
       DTC  /NOTANY/
       DAC  S_NAY
       DAC  1
*
V_OUP  DBC  SVFNK            OUTPUT
       DAC  6
       DTC  /OUTPUT/
       DAC  K_OUP
       DAC  S_OUP
       DAC  3
*
V_RET  DBC  SVLBL            RETURN
       DAC  6
       DTC  /RETURN/
       DAC  L_RTN
*
V_REW  DBC  SVFNN            REWIND
       DAC  6
       DTC  /REWIND/
       DAC  S_REW
       DAC  1
*
V_STT  DBC  SVFNN            STOPTR
       DAC  6
       DTC  /STOPTR/
       DAC  S_STT
       DAC  2
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_SUB  DBC  SVFNN            SUBSTR
       DAC  6
       DTC  /SUBSTR/
       DAC  S_SUB
       DAC  3
*
V_UNL  DBC  SVFNN            UNLOAD
       DAC  6
       DTC  /UNLOAD/
       DAC  S_UNL
       DAC  1
*
V_COL  DBC  SVFNN            COLLECT
       DAC  7
       DTC  /COLLECT/
       DAC  S_COL
       DAC  1
.IF    .CCMK
*
V_COM  DBC  SVKNM            COMPARE
       DAC  7
       DTC  /COMPARE/
       DAC  K_COM
.FI
*
V_CNV  DBC  SVFNN            CONVERT
       DAC  7
       DTC  /CONVERT/
       DAC  S_CNV
       DAC  2
*
V_ENF  DBC  SVFNN            ENDFILE
       DAC  7
       DTC  /ENDFILE/
       DAC  S_ENF
       DAC  1
*
V_ETX  DBC  SVKNM            ERRTEXT
       DAC  7
       DTC  /ERRTEXT/
       DAC  K_ETX
*
V_ERT  DBC  SVKNM            ERRTYPE
       DAC  7
       DTC  /ERRTYPE/
       DAC  K_ERT
*
V_FRT  DBC  SVLBL            FRETURN
       DAC  7
       DTC  /FRETURN/
       DAC  L_FRT
*
V_INT  DBC  SVFPR            INTEGER
       DAC  7
       DTC  /INTEGER/
       DAC  S_INT
       DAC  1
*
V_NRT  DBC  SVLBL            NRETURN
       DAC  7
       DTC  /NRETURN/
       DAC  L_NRT
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
.IF    .CNPF
.ELSE
*
V_PFL  DBC  SVKNM            PROFILE
       DAC  7
       DTC  /PROFILE/
       DAC  K_PFL
.FI
*
V_RPL  DBC  SVFNP            REPLACE
       DAC  7
       DTC  /REPLACE/
       DAC  S_RPL
       DAC  3
*
V_RVS  DBC  SVFNP            REVERSE
       DAC  7
       DTC  /REVERSE/
       DAC  S_RVS
       DAC  1
*
V_RTN  DBC  SVKNM            RTNTYPE
       DAC  7
       DTC  /RTNTYPE/
       DAC  K_RTN
*
V_STX  DBC  SVFNN            SETEXIT
       DAC  7
       DTC  /SETEXIT/
       DAC  S_STX
       DAC  1
*
V_STC  DBC  SVKNM            STCOUNT
       DAC  7
       DTC  /STCOUNT/
       DAC  K_STC
*
V_STL  DBC  SVKNM            STLIMIT
       DAC  7
       DTC  /STLIMIT/
       DAC  K_STL
*
V_SUC  DBC  SVKVC            SUCCEED
       DAC  7
       DTC  /SUCCEED/
       DAC  K_SUC
       DAC  NDSUC
*
V_ALP  DBC  SVKWC            ALPHABET
       DAC  8
       DTC  /ALPHABET/
       DAC  K_ALP
*
V_CNT  DBC  SVLBL            CONTINUE
       DAC  8
       DTC  /CONTINUE/
       DAC  L_CNT
       EJC
*
*      STANDARD VARIABLE BLOCKS (CONTINUED)
*
V_DTP  DBC  SVFNP            DATATYPE
       DAC  8
       DTC  /DATATYPE/
       DAC  S_DTP
       DAC  1
*
V_ERL  DBC  SVKNM            ERRLIMIT
       DAC  8
       DTC  /ERRLIMIT/
       DAC  K_ERL
*
V_FNC  DBC  SVKNM            FNCLEVEL
       DAC  8
       DTC  /FNCLEVEL/
       DAC  K_FNC
*
V_FLS  DBC  SVKNM            FULLSCAN
       DAC  8
       DTC  /FULLSCAN/
       DAC  K_FLS
*
.IF    .CSFN
V_LFL  DBC  SVKNM            LASTFILE
       DAC  8
       DTC  /LASTFILE/
       DAC  K_LFL
*
.FI
.IF    .CSLN
V_LLN  DBC  SVKNM            LASTLINE
       DAC  8
       DTC  /LASTLINE/
       DAC  K_LLN
*
.FI
V_MXL  DBC  SVKNM            MAXLNGTH
       DAC  8
       DTC  /MAXLNGTH/
       DAC  K_MXL
*
V_TER  DBC  0                TERMINAL
       DAC  8
       DTC  /TERMINAL/
       DAC  0
*
.IF    .CBSP
V_BSP  DBC  SVFNN            BACKSPACE
       DAC  9
       DTC  /BACKSPACE/
       DAC  S_BSP
       DAC  1
*
.FI
V_PRO  DBC  SVFNN            PROTOTYPE
       DAC  9
       DTC  /PROTOTYPE/
       DAC  S_PRO
       DAC  1
*
V_SCN  DBC  SVLBL            SCONTINUE
       DAC  9
       DTC  /SCONTINUE/
       DAC  L_SCN
*
       DBC  0                DUMMY ENTRY TO END LIST
       DAC  10               LENGTH GT 9 (SCONTINUE)
       EJC
*
*      LIST OF SVBLK POINTERS FOR KEYWORDS TO BE DUMPED. THE
*      LIST IS IN THE ORDER WHICH APPEARS ON THE DUMP OUTPUT.
*
VDMKW  DAC  V_ANC            ANCHOR
.IF    .CULC
       DAC  V_CAS            CCASE
.FI
       DAC  V_COD            CODE
.IF    .CCMK
.IF    .CCMC
       DAC  V_COM            COMPARE
.ELSE
       DAC  1                COMPARE NOT PRINTED
.FI
.FI
       DAC  V_DMP            DUMP
       DAC  V_ERL            ERRLIMIT
       DAC  V_ETX            ERRTEXT
       DAC  V_ERT            ERRTYPE
.IF    .CSFN
       DAC  V_FIL            FILE
.FI
       DAC  V_FNC            FNCLEVEL
       DAC  V_FTR            FTRACE
       DAC  V_FLS            FULLSCAN
       DAC  V_INP            INPUT
.IF    .CSFN
       DAC  V_LFL            LASTFILE
.FI
.IF    .CSLN
       DAC  V_LLN            LASTLINE
.FI
       DAC  V_LST            LASTNO
.IF    .CSLN
       DAC  V_LIN            LINE
.FI
       DAC  V_MXL            MAXLENGTH
       DAC  V_OUP            OUTPUT
.IF    .CNPF
.ELSE
       DAC  V_PFL            PROFILE
.FI
       DAC  V_RTN            RTNTYPE
       DAC  V_STC            STCOUNT
       DAC  V_STL            STLIMIT
       DAC  V_STN            STNO
       DAC  V_TRA            TRACE
       DAC  V_TRM            TRIM
       DAC  0                END OF LIST
*
*      TABLE USED BY GTNVR TO SEARCH SVBLK LISTS
*
VSRCH  DAC  0                DUMMY ENTRY TO GET PROPER INDEXING
       DAC  V_EQF            START OF 1 CHAR VARIABLES (NONE)
       DAC  V_EQF            START OF 2 CHAR VARIABLES
       DAC  V_ANY            START OF 3 CHAR VARIABLES
.IF    .CMTH
       DAC  V_ATN            START OF 4 CHAR VARIABLES
.ELSE
.IF    .CULC
       DAC  V_CAS            START OF 4 CHAR VARIABLES
.ELSE
       DAC  V_CHR            START OF 4 CHAR VARIABLES
.FI
.FI
       DAC  V_ABE            START OF 5 CHAR VARIABLES
       DAC  V_ANC            START OF 6 CHAR VARIABLES
       DAC  V_COL            START OF 7 CHAR VARIABLES
       DAC  V_ALP            START OF 8 CHAR VARIABLES
.IF    .CBSP
       DAC  V_BSP            START OF 9 CHAR VARIABLES
.ELSE
       DAC  V_PRO            START OF 9 CHAR VARIABLES
.FI
*
*      LAST LOCATION IN CONSTANT SECTION
*
C_YYY  DAC  0                LAST LOCATION IN CONSTANT SECTION
       TTL  S P I T B O L -- WORKING STORAGE SECTION
*
*      THE WORKING STORAGE SECTION CONTAINS AREAS WHICH ARE
*      CHANGED DURING EXECUTION OF THE PROGRAM. THE VALUE
*      ASSEMBLED IS THE INITIAL VALUE BEFORE EXECUTION STARTS.
*
*      ALL THESE AREAS ARE FIXED LENGTH AREAS. VARIABLE LENGTH
*      DATA IS STORED IN THE STATIC OR DYNAMIC REGIONS OF THE
*      ALLOCATED DATA AREAS.
*
*      THE VALUES IN THIS AREA ARE DESCRIBED EITHER AS WORK
*      AREAS OR AS GLOBAL VALUES. A WORK AREA IS USED IN AN
*      EPHEMERAL MANNER AND THE VALUE IS NOT SAVED FROM ONE
*      ENTRY INTO A ROUTINE TO ANOTHER. A GLOBAL VALUE IS A
*      LESS TEMPORARY LOCATION WHOSE VALUE IS SAVED FROM ONE
*      CALL TO ANOTHER.
*
*      W_AAA MARKS THE START OF THE WORKING SECTION WHILST
*      W_YYY MARKS ITS END.  G_AAA MARKS THE DIVISION BETWEEN
*      TEMPORARY AND GLOBAL VALUES.
*
*      GLOBAL VALUES ARE FURTHER SUBDIVIDED TO FACILITATE
*      PROCESSING BY THE GARBAGE COLLECTOR. R_AAA THROUGH
*      R_YYY ARE GLOBAL VALUES THAT MAY POINT INTO DYNAMIC
*      STORAGE AND HENCE MUST BE RELOCATED AFTER EACH GARBAGE
*      COLLECTION.  THEY ALSO SERVE AS ROOT POINTERS TO ALL
*      ALLOCATED DATA THAT MUST BE PRESERVED.  POINTERS BETWEEN
*      A_AAA AND R_AAA MAY POINT INTO CODE, STATIC STORAGE,
*      OR MARK THE LIMITS OF DYNAMIC MEMORY.  THESE POINTERS
*      MUST BE ADJUSTED WHEN THE WORKING SECTION IS SAVED TO A
*      FILE AND SUBSEQUENTLY RELOADED AT A DIFFERENT ADDRESS.
*
*      A GENERAL PART OF THE APPROACH IN THIS PROGRAM IS NOT
*      TO OVERLAP WORK AREAS BETWEEN PROCEDURES EVEN THOUGH A
*      SMALL AMOUNT OF SPACE COULD BE SAVED. SUCH OVERLAP IS
*      CONSIDERED A SOURCE OF PROGRAM ERRORS AND DECREASES THE
*      INFORMATION LEFT BEHIND AFTER A SYSTEM CRASH OF ANY KIND.
*
*      THE NAMES OF THESE LOCATIONS ARE LABELS WITH FIVE LETTER
*      (A-Y,_) NAMES. AS FAR AS POSSIBLE THE ORDER IS KEPT
*      ALPHABETICAL BY THESE NAMES BUT IN SOME CASES THERE
*      ARE SLIGHT DEPARTURES CAUSED BY OTHER ORDER REQUIREMENTS.
*
*      UNLESS OTHERWISE DOCUMENTED, THE ORDER OF WORK AREAS
*      DOES NOT AFFECT THE EXECUTION OF THE SPITBOL PROGRAM.
*
       SEC                   START OF WORKING STORAGE SECTION
       EJC
*
*      THIS AREA IS NOT CLEARED BY INITIAL CODE
*
CMLAB  DAC  B_SCL            STRING USED TO CHECK LABEL LEGALITY
       DAC  2
       DTC  /  /
*
*      LABEL TO MARK START OF WORK AREA
*
W_AAA  DAC  0
*
*      WORK AREAS FOR ACESS PROCEDURE
*
ACTRM  DAC  0                TRIM INDICATOR
*
*      WORK AREAS FOR ALLOC PROCEDURE
*
ALDYN  DAC  0                AMOUNT OF DYNAMIC STORE
ALLIA  DIC  +0               DUMP IA
ALLSV  DAC  0                SAVE WB IN ALLOC
*
*      WORK AREAS FOR ALOST PROCEDURE
*
ALSTA  DAC  0                SAVE WA IN ALOST
*
*      WORK AREAS FOR ARRAY FUNCTION (S_ARR)
*
ARCDM  DAC  0                COUNT DIMENSIONS
ARNEL  DIC  +0               COUNT ELEMENTS
ARPTR  DAC  0                OFFSET PTR INTO ARBLK
ARSVL  DIC  +0               SAVE INTEGER LOW BOUND
       EJC
*
*      WORK AREAS FOR ARREF ROUTINE
*
ARFSI  DIC  +0               SAVE CURRENT EVOLVING SUBSCRIPT
ARFXS  DAC  0                SAVE BASE STACK POINTER
*
*      WORK AREAS FOR B_EFC BLOCK ROUTINE
*
BEFOF  DAC  0                SAVE OFFSET PTR INTO EFBLK
*
*      WORK AREAS FOR B_PFC BLOCK ROUTINE
*
BPFPF  DAC  0                SAVE PFBLK POINTER
BPFSV  DAC  0                SAVE OLD FUNCTION VALUE
BPFXT  DAC  0                POINTER TO STACKED ARGUMENTS
*
*      WORK AREA FOR COLLECT FUNCTION (S_COL)
*
CLSVI  DIC  +0               SAVE INTEGER ARGUMENT
*
*      WORK AREAS VALUE FOR CNCRD
*
CNSCC  DAC  0                POINTER TO CONTROL CARD STRING
CNSWC  DAC  0                WORD COUNT
CNR_T  DAC  0                POINTER TO R_TTL OR R_STL
*
*      WORK AREAS FOR CONVERT FUNCTION (S_CNV)
*
CNVTP  DAC  0                SAVE PTR INTO SCVTB
*
*      WORK AREAS FOR DATA FUNCTION (S_DAT)
*
DATDV  DAC  0                SAVE VRBLK PTR FOR DATATYPE NAME
DATXS  DAC  0                SAVE INITIAL STACK POINTER
*
*      WORK AREAS FOR DEFINE FUNCTION (S_DEF)
*
DEFLB  DAC  0                SAVE VRBLK PTR FOR LABEL
DEFNA  DAC  0                COUNT FUNCTION ARGUMENTS
DEFVR  DAC  0                SAVE VRBLK PTR FOR FUNCTION NAME
DEFXS  DAC  0                SAVE INITIAL STACK POINTER
*
*      WORK AREAS FOR DUMPR PROCEDURE
*
DMARG  DAC  0                DUMP ARGUMENT
DMPSA  DAC  0                PRESERVE WA OVER PRTVL CALL
.IF    .CCMK
DMPSB  DAC  0                PRESERVE WB OVER SYSCM CALL
.FI
DMPSV  DAC  0                GENERAL SCRATCH SAVE
DMVCH  DAC  0                CHAIN POINTER FOR VARIABLE BLOCKS
DMPCH  DAC  0                SAVE SORTED VRBLK CHAIN POINTER
DMPKB  DAC  0                DUMMY KVBLK FOR USE IN DUMPR
DMPKT  DAC  0                KVVAR TRBLK PTR (MUST FOLLOW DMPKB)
DMPKN  DAC  0                KEYWORD NUMBER (MUST FOLLOW DMPKT)
*
*      WORK AREA FOR DTACH
*
DTCNB  DAC  0                NAME BASE
DTCNM  DAC  0                NAME PTR
*
*      WORK AREAS FOR DUPL FUNCTION (S_DUP)
*
DUPSI  DIC  +0               STORE INTEGER STRING LENGTH
*
*      WORK AREA FOR ENDFILE (S_ENF)
*
ENFCH  DAC  0                FOR IOCHN CHAIN HEAD
       EJC
*
*      WORK AREAS FOR ERTEX
*
ERTWA  DAC  0                SAVE WA
ERTWB  DAC  0                SAVE WB
*
*      WORK AREAS FOR EVALI
*
EVLIN  DAC  0                DUMMY PATTERN BLOCK PCODE
EVLIS  DAC  0                THEN NODE (MUST FOLLOW EVLIN)
EVLIV  DAC  0                VALUE OF PARM1 (MUST FOLLOW EVLIS)
EVLIO  DAC  0                PTR TO ORIGINAL NODE
EVLIF  DAC  0                FLAG FOR SIMPLE/COMPLEX ARGUMENT
*
*      WORK AREA FOR EXPAN
*
EXPSV  DAC  0                SAVE OP DOPE VECTOR POINTER
*
*      WORK AREAS FOR GBCOL PROCEDURE
*
GBCFL  DAC  0                GARBAGE COLLECTOR ACTIVE FLAG
GBCLM  DAC  0                POINTER TO LAST MOVE BLOCK (PASS 3)
GBCNM  DAC  0                DUMMY FIRST MOVE BLOCK
GBCNS  DAC  0                REST OF DUMMY BLOCK (FOLLOWS GBCNM)
.IF    .CSED
.IF    .CEPP
.ELSE
GBCMK  DAC  0                BIAS WHEN MARKING ENTRY POINT
.FI
GBCIA  DIC  +0               DUMP IA
GBCSD  DAC  0                FIRST ADDRESS BEYOND SEDIMENT
GBCSF  DAC  0                FREE SPACE WITHIN SEDIMENT
.FI
GBSVA  DAC  0                SAVE WA
GBSVB  DAC  0                SAVE WB
GBSVC  DAC  0                SAVE WC
*
*      WORK AREAS FOR GTNVR PROCEDURE
*
GNVHE  DAC  0                PTR TO END OF HASH CHAIN
GNVNW  DAC  0                NUMBER OF WORDS IN STRING NAME
GNVSA  DAC  0                SAVE WA
GNVSB  DAC  0                SAVE WB
GNVSP  DAC  0                POINTER INTO VSRCH TABLE
GNVST  DAC  0                POINTER TO CHARS OF STRING
*
*      WORK AREAS FOR GTARR
*
GTAWA  DAC  0                SAVE WA
*
*      WORK AREAS FOR GTINT
*
GTINA  DAC  0                SAVE WA
GTINB  DAC  0                SAVE WB
       EJC
*
*      WORK AREAS FOR GTNUM PROCEDURE
*
GTNNF  DAC  0                ZERO/NONZERO FOR RESULT +/-
GTNSI  DIC  +0               GENERAL INTEGER SAVE
.IF    .CNRA
.ELSE
GTNDF  DAC  0                0/1 FOR DEC POINT SO FAR NO/YES
GTNES  DAC  0                ZERO/NONZERO EXPONENT +/-
GTNEX  DIC  +0               REAL EXPONENT
GTNSC  DAC  0                SCALE (PLACES AFTER POINT)
GTNSR  DRC  +0.0             GENERAL REAL SAVE
GTNRD  DAC  0                FLAG FOR OK REAL NUMBER
.FI
*
*      WORK AREAS FOR GTPAT PROCEDURE
*
GTPSB  DAC  0                SAVE WB
*
*      WORK AREAS FOR GTSTG PROCEDURE
*
GTSSF  DAC  0                0/1 FOR RESULT +/-
GTSVC  DAC  0                SAVE WC
GTSVB  DAC  0                SAVE WB
.IF    .CNRA
.ELSE
.IF    .CNCR
.ELSE
GTSES  DAC  0                CHAR + OR - FOR EXPONENT +/-
GTSRS  DRC  +0.0             GENERAL REAL SAVE
.FI
.FI
*
*      WORK AREAS FOR GTVAR PROCEDURE
*
GTVRC  DAC  0                SAVE WC
.IF    .CNBF
.ELSE
*
*      WORK AREAS FOR INSBF
*
INSAB  DAC  0                ENTRY WA + ENTRY WB
INSLN  DAC  0                LENGTH OF INSERTION STRING
INSSA  DAC  0                SAVE ENTRY WA
INSSB  DAC  0                SAVE ENTRY WB
INSSC  DAC  0                SAVE ENTRY WC
.FI
*
*      WORK AREAS FOR IOPUT
*
IOPTT  DAC  0                TYPE OF ASSOCIATION
.IF    .CNLD
.ELSE
*
*      WORK AREAS FOR LOAD FUNCTION
*
LODFN  DAC  0                POINTER TO VRBLK FOR FUNC NAME
LODNA  DAC  0                COUNT NUMBER OF ARGUMENTS
.FI
.IF    .CNPF
.ELSE
*
*      WORK AREA FOR PROFILER
*
PFSVW  DAC  0                TO SAVE A W-REG
.FI
*
*      WORK AREAS FOR PRTNM PROCEDURE
*
PRNSI  DIC  +0               SCRATCH INTEGER LOC
*
*      WORK AREAS FOR PRTSN PROCEDURE
*
PRSNA  DAC  0                SAVE WA
*
*      WORK AREAS FOR PRTST PROCEDURE
*
PRSVA  DAC  0                SAVE WA
PRSVB  DAC  0                SAVE WB
PRSVC  DAC  0                SAVE CHAR COUNTER
*
*      WORK AREA FOR PRTNL
*
PRTSA  DAC  0                SAVE WA
PRTSB  DAC  0                SAVE WB
*
*      WORK AREA FOR PRTVL
*
PRVSI  DAC  0                SAVE IDVAL
*
*      WORK AREAS FOR PATTERN MATCH ROUTINES
*
PSAVE  DAC  0                TEMPORARY SAVE FOR CURRENT NODE PTR
PSAVC  DAC  0                SAVE CURSOR IN P_SPN, P_STR
.IF    .CREL
*
*      WORK AREA FOR RELAJ ROUTINE
*
RLALS  DAC  0                PTR TO LIST OF BOUNDS AND ADJUSTS
*
*      WORK AREA FOR RELDN ROUTINE
*
RLDCD  DAC  0                SAVE CODE ADJUSTMENT
RLDST  DAC  0                SAVE STATIC ADJUSTMENT
RLDLS  DAC  0                SAVE LIST POINTER
.FI
*
*      WORK AREAS FOR RETRN ROUTINE
*
RTNBP  DAC  0                TO SAVE A BLOCK POINTER
RTNFV  DAC  0                NEW FUNCTION VALUE (RESULT)
RTNSV  DAC  0                OLD FUNCTION VALUE (SAVED VALUE)
*
*      WORK AREAS FOR SUBSTR FUNCTION (S_SUB)
*
SBSSV  DAC  0                SAVE THIRD ARGUMENT
*
*      WORK AREAS FOR SCAN PROCEDURE
*
SCNSA  DAC  0                SAVE WA
SCNSB  DAC  0                SAVE WB
SCNSC  DAC  0                SAVE WC
SCNOF  DAC  0                SAVE OFFSET
.IF    .CNSR
.ELSE
       EJC
*
*      WORK AREA USED BY SORTA, SORTC, SORTF, SORTH
*
SRTDF  DAC  0                DATATYPE FIELD NAME
SRTFD  DAC  0                FOUND DFBLK ADDRESS
SRTFF  DAC  0                FOUND FIELD NAME
SRTFO  DAC  0                OFFSET TO FIELD NAME
SRTNR  DAC  0                NUMBER OF ROWS
SRTOF  DAC  0                OFFSET WITHIN ROW TO SORT KEY
SRTRT  DAC  0                ROOT OFFSET
SRTS1  DAC  0                SAVE OFFSET 1
SRTS2  DAC  0                SAVE OFFSET 2
SRTSC  DAC  0                SAVE WC
SRTSF  DAC  0                SORT ARRAY FIRST ROW OFFSET
SRTSN  DAC  0                SAVE N
SRTSO  DAC  0                OFFSET TO A(0)
SRTSR  DAC  0                0, NON-ZERO FOR SORT, RSORT
SRTST  DAC  0                STRIDE FROM ONE ROW TO NEXT
SRTWC  DAC  0                DUMP WC
.FI
*
*      WORK AREAS FOR STOPR ROUTINE
*
STPSI  DIC  +0               SAVE VALUE OF STCOUNT
STPTI  DIC  +0               SAVE TIME ELAPSED
*
*      WORK AREAS FOR TFIND PROCEDURE
*
TFNSI  DIC  +0               NUMBER OF HEADERS
*
*      WORK AREAS FOR XSCAN PROCEDURE
*
XSCRT  DAC  0                SAVE RETURN CODE
XSCWB  DAC  0                SAVE REGISTER WB
*
*      START OF GLOBAL VALUES IN WORKING SECTION
*
G_AAA  DAC  0
*
*      GLOBAL VALUE FOR ALLOC PROCEDURE
*
ALFSF  DIC  +0               FACTOR IN FREE STORE PCNTAGE CHECK
*
*      GLOBAL VALUES FOR CMPIL PROCEDURE
*
CMERC  DAC  0                COUNT OF INITIAL COMPILE ERRORS
CMPLN  DAC  0                LINE NUMBER OF FIRST LINE OF STMT
CMPXS  DAC  0                SAVE STACK PTR IN CASE OF ERRORS
CMPSN  DAC  1                NUMBER OF NEXT STATEMENT TO COMPILE
*
*      GLOBAL VALUES FOR CNCRD
*
.IF    .CINC
CNSIL  DAC  0                SAVE SCNIL DURING INCLUDE PROCESS.
CNIND  DAC  0                CURRENT INCLUDE FILE NEST LEVEL
CNSPT  DAC  0                SAVE SCNPT DURING INCLUDE PROCESS.
.FI
CNTTL  DAC  0                FLAG FOR -TITLE, -STITL
*
*      GLOBAL FLAG FOR SUPPRESSION OF COMPILATION STATISTICS.
*
CPSTS  DAC  0                SUPPRESS COMP. STATS IF NON ZERO
*
*      GLOBAL VALUES FOR CONTROL CARD SWITCHES
*
CSWDB  DAC  0                0/1 FOR -SINGLE/-DOUBLE
CSWER  DAC  0                0/1 FOR -ERRORS/-NOERRORS
CSWEX  DAC  0                0/1 FOR -EXECUTE/-NOEXECUTE
CSWFL  DAC  1                0/1 FOR -NOFAIL/-FAIL
CSWIN  DAC  INILN            XXX FOR -INXXX
CSWLS  DAC  1                0/1 FOR -NOLIST/-LIST
CSWNO  DAC  0                0/1 FOR -OPTIMISE/-NOOPT
CSWPR  DAC  0                0/1 FOR -NOPRINT/-PRINT
*
*      GLOBAL LOCATION USED BY PATST PROCEDURE
*
CTMSK  DBC  0                LAST BIT POSITION USED IN R_CTP
CURID  DAC  0                CURRENT ID VALUE
       EJC
*
*      GLOBAL VALUE FOR CDWRD PROCEDURE
*
CWCOF  DAC  0                NEXT WORD OFFSET IN CURRENT CCBLK
.IF    .CSED
*
*      GLOBAL LOCATIONS FOR DYNAMIC STORAGE POINTERS
*
DNAMS  DAC  0                SIZE OF SEDIMENT IN BAUS
.FI
*
*      GLOBAL AREA FOR ERROR PROCESSING.
*
ERICH  DAC  0                COPY ERROR REPORTS TO INT.CHAN IF 1
ERLST  DAC  0                FOR LISTR WHEN ERRORS GO TO INT.CH.
ERRFT  DAC  0                FATAL ERROR FLAG
ERRSP  DAC  0                ERROR SUPPRESSION FLAG
*
*      GLOBAL FLAG FOR SUPPRESSION OF EXECUTION STATS
*
EXSTS  DAC  0                SUPPRESS EXEC STATS IF SET
*
*      GLOBAL VALUES FOR EXFAL AND RETURN
*
FLPRT  DAC  0                LOCATION OF FAIL OFFSET FOR RETURN
FLPTR  DAC  0                LOCATION OF FAILURE OFFSET ON STACK
*
*      GLOBAL LOCATION TO COUNT GARBAGE COLLECTIONS (GBCOL)
*
.IF    .CSED
GBSED  DIC  +0               FACTOR IN SEDIMENT PCNTAGE CHECK
.FI
GBCNT  DAC  0                COUNT OF GARBAGE COLLECTIONS
*
*      GLOBAL VALUE FOR GTCOD AND GTEXP
*
GTCEF  DAC  0                SAVE FAIL PTR IN CASE OF ERROR
*
*      GLOBAL LOCATIONS FOR GTSTG PROCEDURE
*
.IF    .CNRA
.ELSE
.IF    .CNCR
.ELSE
GTSRN  DRC  +0.0             ROUNDING FACTOR 0.5*10**-CFP_S
GTSSC  DRC  +0.0             SCALING VALUE 10**CFP_S
.FI
.FI
GTSWK  DAC  0                PTR TO WORK AREA FOR GTSTG
*
*      GLOBAL FLAG FOR HEADER PRINTING
*
HEADP  DAC  0                HEADER PRINTED FLAG
*
*      GLOBAL VALUES FOR VARIABLE HASH TABLE
*
HSHNB  DIC  +0               NUMBER OF HASH BUCKETS
*
*      GLOBAL AREAS FOR INIT
*
INITR  DAC  0                SAVE TERMINAL FLAG
       EJC
*
*      GLOBAL VALUES FOR KEYWORD VALUES WHICH ARE STORED AS ONE
*      WORD INTEGERS. THESE VALUES MUST BE ASSEMBLED IN THE
*      FOLLOWING ORDER (AS DICTATED BY K_XXX DEFINITION VALUES).
*
KVABE  DAC  0                ABEND
KVANC  DAC  0                ANCHOR
.IF    .CULC
KVCAS  DAC  0                CASE
.FI
KVCOD  DAC  0                CODE
.IF    .CCMK
KVCOM  DAC  0                COMPARE
.FI
KVDMP  DAC  0                DUMP
KVERL  DAC  0                ERRLIMIT
KVERT  DAC  0                ERRTYPE
KVFTR  DAC  0                FTRACE
KVFLS  DAC  1                FULLSCAN
KVINP  DAC  1                INPUT
KVMXL  DAC  5000             MAXLENGTH
KVOUP  DAC  1                OUTPUT
.IF    .CNPF
.ELSE
KVPFL  DAC  0                PROFILE
.FI
KVTRA  DAC  0                TRACE
KVTRM  DAC  0                TRIM
KVFNC  DAC  0                FNCLEVEL
KVLST  DAC  0                LASTNO
.IF    .CSLN
KVLLN  DAC  0                LASTLINE
KVLIN  DAC  0                LINE
.FI
KVSTN  DAC  0                STNO
*
*      GLOBAL VALUES FOR OTHER KEYWORDS
*
KVALP  DAC  0                ALPHABET
KVRTN  DAC  NULLS            RTNTYPE (SCBLK POINTER)
.IF    .CS16
KVSTL  DIC  +32767           STLIMIT
KVSTC  DIC  +32767           STCOUNT (COUNTS DOWN FROM STLIMIT)
.ELSE
.IF    .CS32
KVSTL  DIC  +2147483647      STLIMIT
KVSTC  DIC  +2147483647      STCOUNT (COUNTS DOWN FROM STLIMIT)
.ELSE
KVSTL  DIC  +50000           STLIMIT
KVSTC  DIC  +50000           STCOUNT (COUNTS DOWN FROM STLIMIT)
.FI
.FI
*
*      GLOBAL VALUES FOR LISTR PROCEDURE
*
.IF    .CINC
LSTID  DAC  0                INCLUDE DEPTH OF CURRENT IMAGE
.FI
LSTLC  DAC  0                COUNT LINES ON SOURCE LIST PAGE
LSTNP  DAC  0                MAX NUMBER OF LINES ON PAGE
LSTPF  DAC  1                SET NONZERO IF CURRENT IMAGE LISTED
LSTPG  DAC  0                CURRENT SOURCE LIST PAGE NUMBER
LSTPO  DAC  0                OFFSET TO   PAGE NNN   MESSAGE
LSTSN  DAC  0                REMEMBER LAST STMNUM LISTED
*
*      GLOBAL MAXIMUM SIZE OF SPITBOL OBJECTS
*
MXLEN  DAC  0                INITIALISED BY SYSMX CALL
*
*      GLOBAL EXECUTION CONTROL VARIABLE
*
NOXEQ  DAC  0                SET NON-ZERO TO INHIBIT EXECUTION
.IF    .CNPF
.ELSE
*
*      GLOBAL PROFILER VALUES LOCATIONS
*
PFDMP  DAC  0                SET NON-0 IF &PROFILE SET NON-0
PFFNC  DAC  0                SET NON-0 IF FUNCT JUST ENTERED
PFSTM  DIC  +0               TO STORE STARTING TIME OF STMT
PFETM  DIC  +0               TO STORE ENDING TIME OF STMT
PFNTE  DAC  0                NR OF TABLE ENTRIES
PFSTE  DIC  +0               GETS INT REP OF TABLE ENTRY SIZE
.FI
*
       EJC
*
*      GLOBAL VALUES USED IN PATTERN MATCH ROUTINES
*
PMDFL  DAC  0                PATTERN ASSIGNMENT FLAG
PMHBS  DAC  0                HISTORY STACK BASE POINTER
PMSSL  DAC  0                LENGTH OF SUBJECT STRING IN CHARS
.IF    .CPOL
*
*      GLOBAL VALUES FOR INTERFACE POLLING (SYSPL)
*
POLCS  DAC  1                POLL INTERVAL START VALUE
POLCT  DAC  1                POLL INTERVAL COUNTER
.FI
*
*      GLOBAL FLAGS USED FOR STANDARD FILE LISTING OPTIONS
*
PRICH  DAC  0                PRINTER ON INTERACTIVE CHANNEL
PRSTD  DAC  0                TESTED BY PRTPG
PRSTO  DAC  0                STANDARD LISTING OPTION FLAG
*
*      GLOBAL VALUES FOR PRINT PROCEDURES
*
PRBUF  DAC  0                PTR TO PRINT BFR IN STATIC
PRECL  DAC  0                EXTENDED/COMPACT LISTING FLAG
PRLEN  DAC  0                LENGTH OF PRINT BUFFER IN CHARS
PRLNW  DAC  0                LENGTH OF PRINT BUFFER IN WORDS
PROFS  DAC  0                OFFSET TO NEXT LOCATION IN PRBUF
PRTEF  DAC  0                ENDFILE FLAG
       EJC
*
*      GLOBAL AREA FOR READR
*
RDCLN  DAC  0                CURRENT STATEMENT LINE NUMBER
RDNLN  DAC  0                NEXT STATEMENT LINE NUMBER
*
*      GLOBAL AMOUNT OF MEMORY RESERVED FOR END OF EXECUTION
*
RSMEM  DAC  0                RESERVE MEMORY
*
*      GLOBAL AREA FOR STMGO COUNTERS
*
STMCS  DAC  1                COUNTER STARTUP VALUE
STMCT  DAC  1                COUNTER ACTIVE VALUE
*
*      ADJUSTABLE GLOBAL VALUES
*
*      ALL THE POINTERS IN THIS SECTION CAN POINT TO THE
*      DYNAMIC OR THE STATIC REGION.
*      WHEN A SAVE FILE IS RELOADED, THESE POINTERS MUST
*      BE ADJUSTED IF STATIC OR DYNAMIC MEMORY IS NOW
*      AT A DIFFERENT ADDRESS.  SEE ROUTINE RELOC FOR
*      ADDITIONAL INFORMATION.
*
*      SOME VALUES CANNOT BE MOVE HERE BECAUSE OF ADJACENCY
*      CONSTRAINTS.  THEY ARE HANDLED SPECIALLY BY RELOC ET AL.
*      THESE VALUES ARE KVRTN,
*
*      VALUES GTSWK, KVALP, AND PRBUF ARE REINITIALIZED BY
*      PROCEDURE INSTA, AND DO NOT NEED TO APPEAR HERE.
*
*      VALUES FLPRT, FLPTR, GTCEF, AND STBAS POINT INTO THE
*      STACK AND ARE EXPLICITLY ADJUSTED BY OSINT'S RESTART
*      PROCEDURE.
*
A_AAA  DAC  0                START OF ADJUSTABLE VALUES
CMPSS  DAC  0                SAVE SUBROUTINE STACK PTR
DNAMB  DAC  0                START OF DYNAMIC AREA
DNAMP  DAC  0                NEXT AVAILABLE LOC IN DYNAMIC AREA
DNAME  DAC  0                END OF AVAILABLE DYNAMIC AREA
HSHTB  DAC  0                POINTER TO START OF VRBLK HASH TABL
HSHTE  DAC  0                POINTER PAST END OF VRBLK HASH TABL
INISS  DAC  0                SAVE SUBROUTINE STACK PTR
PFTBL  DAC  0                GETS ADRS OF (IMAG) TABLE BASE
PRNMV  DAC  0                VRBLK PTR FROM LAST NAME SEARCH
STATB  DAC  0                START OF STATIC AREA
STATE  DAC  0                END OF STATIC AREA
STXVR  DAC  NULLS            VRBLK POINTER OR NULL

*
*      RELOCATABLE GLOBAL VALUES
*
*      ALL THE POINTERS IN THIS SECTION CAN POINT TO BLOCKS IN
*      THE DYNAMIC STORAGE AREA AND MUST BE RELOCATED BY THE
*      GARBAGE COLLECTOR. THEY ARE IDENTIFIED BY R_XXX NAMES.
*
R_AAA  DAC  0                START OF RELOCATABLE VALUES
R_ARF  DAC  0                ARRAY BLOCK POINTER FOR ARREF
R_CCB  DAC  0                PTR TO CCBLK BEING BUILT (CDWRD)
R_CIM  DAC  0                PTR TO CURRENT COMPILER INPUT STR
R_CMP  DAC  0                COPY OF R_CIM USED IN CMPIL
R_CNI  DAC  0                PTR TO NEXT COMPILER INPUT STRING
R_CNT  DAC  0                CDBLK POINTER FOR SETEXIT CONTINUE
R_COD  DAC  0                POINTER TO CURRENT CDBLK OR EXBLK
R_CTP  DAC  0                PTR TO CURRENT CTBLK FOR PATST
R_CTS  DAC  0                PTR TO LAST STRING SCANNED BY PATST
R_ERT  DAC  0                TRBLK POINTER FOR ERRTYPE TRACE
R_ETX  DAC  NULLS            POINTER TO ERRTEXT STRING
R_EXS  DAC  0                = SAVE XL IN EXPDM
R_FCB  DAC  0                FCBLK CHAIN HEAD
R_FNC  DAC  0                TRBLK POINTER FOR FNCLEVEL TRACE
R_GTC  DAC  0                KEEP CODE PTR FOR GTCOD,GTEXP
.IF    .CINC
R_ICI  DAC  0                SAVED R_CIM DURING INCLUDE PROCESS.
.IF    .CSFN
R_IFA  DAC  0                ARRAY OF FILE NAMES BY INCL. DEPTH
R_IFL  DAC  0                ARRAY OF LINE NUMS BY INCLUDE DEPTH
.FI
R_IFN  DAC  0                LAST INCLUDE FILE NAME
R_INC  DAC  0                TABLE OF INCLUDE FILE NAMES SEEN
.FI
R_IO1  DAC  0                FILE ARG1 FOR IOPUT
R_IO2  DAC  0                FILE ARG2 FOR IOPUT
R_IOF  DAC  0                FCBLK PTR OR 0
R_ION  DAC  0                NAME BASE PTR
R_IOP  DAC  0                PREDECESSOR BLOCK PTR FOR IOPUT
R_IOT  DAC  0                TRBLK PTR FOR IOPUT
.IF    .CNBF
.ELSE
R_PMB  DAC  0                BUFFER PTR IN PATTERN MATCH
.FI
R_PMS  DAC  0                SUBJECT STRING PTR IN PATTERN MATCH
R_RA2  DAC  0                REPLACE SECOND ARGUMENT LAST TIME
R_RA3  DAC  0                REPLACE THIRD ARGUMENT LAST TIME
R_RPT  DAC  0                PTR TO CTBLK REPLACE TABLE LAST USD
R_SCP  DAC  0                SAVE POINTER FROM LAST SCANE CALL
.IF    .CSFN
R_SFC  DAC  NULLS            CURRENT SOURCE FILE NAME
R_SFN  DAC  0                PTR TO SOURCE FILE NAME TABLE
.FI
R_SXL  DAC  0                PRESERVE XL IN SORTC
R_SXR  DAC  0                PRESERVE XR IN SORTA/SORTC
R_STC  DAC  0                TRBLK POINTER FOR STCOUNT TRACE
R_STL  DAC  0                SOURCE LISTING SUB-TITLE
R_SXC  DAC  0                CODE (CDBLK) PTR FOR SETEXIT TRAP
R_TTL  DAC  NULLS            SOURCE LISTING TITLE
R_XSC  DAC  0                STRING POINTER FOR XSCAN
       EJC
*
*      THE REMAINING POINTERS IN THIS LIST ARE USED TO POINT
*      TO FUNCTION BLOCKS FOR NORMALLY UNDEFINED OPERATORS.
*
R_UBA  DAC  STNDO            BINARY AT
R_UBM  DAC  STNDO            BINARY AMPERSAND
R_UBN  DAC  STNDO            BINARY NUMBER SIGN
R_UBP  DAC  STNDO            BINARY PERCENT
R_UBT  DAC  STNDO            BINARY NOT
R_UUB  DAC  STNDO            UNARY VERTICAL BAR
R_UUE  DAC  STNDO            UNARY EQUAL
R_UUN  DAC  STNDO            UNARY NUMBER SIGN
R_UUP  DAC  STNDO            UNARY PERCENT
R_UUS  DAC  STNDO            UNARY SLASH
R_UUX  DAC  STNDO            UNARY EXCLAMATION
R_YYY  DAC  0                LAST RELOCATABLE LOCATION
*
*      GLOBAL LOCATIONS USED IN SCAN PROCEDURE
*
SCNBL  DAC  0                SET NON-ZERO IF SCANNED PAST BLANKS
SCNCC  DAC  0                NON-ZERO TO SCAN CONTROL CARD NAME
SCNGO  DAC  0                SET NON-ZERO TO SCAN GOTO FIELD
SCNIL  DAC  0                LENGTH OF CURRENT INPUT IMAGE
SCNPT  DAC  0                POINTER TO NEXT LOCATION IN R_CIM
SCNRS  DAC  0                SET NON-ZERO TO SIGNAL RESCAN
SCNSE  DAC  0                START OF CURRENT ELEMENT
SCNTP  DAC  0                SAVE SYNTAX TYPE FROM LAST CALL
*
*      GLOBAL VALUE FOR INDICATING STAGE (SEE ERROR SECTION)
*
STAGE  DAC  0                INITIAL VALUE = INITIAL COMPILE
       EJC
*
*      GLOBAL STACK POINTER
*
STBAS  DAC  0                POINTER PAST STACK BASE
*
*      GLOBAL VALUES FOR SETEXIT FUNCTION (S_STX)
*
STXOC  DAC  0                CODE POINTER OFFSET
STXOF  DAC  0                FAILURE OFFSET
*
*      GLOBAL VALUE FOR TIME KEEPING
*
TIMSX  DIC  +0               TIME AT START OF EXECUTION
TIMUP  DAC  0                SET WHEN TIME UP OCCURS
*
*      GLOBAL VALUES FOR XSCAN AND XSCNI PROCEDURES
*
XSOFS  DAC  0                OFFSET TO CURRENT LOCATION IN R_XSC
*
*      LABEL TO MARK END OF WORKING SECTION
*
W_YYY  DAC  0
       TTL  S P I T B O L -- MINIMAL CODE
       SEC                   START OF PROGRAM SECTION
S_AAA  ENT  BL__I            MARK START OF CODE
.IF    .CREL
       TTL  S P I T B O L -- RELOCATION
*
*      RELOCATION
*      THE FOLLOWING SECTION PROVIDES SERVICES TO OSINT TO
*      RELOCATE PORTIONS OF THE WORKSPACE.  IT IS USED WHEN
*      A SAVED MEMORY IMAGE MUST BE RESTARTED AT A DIFFERENT
*      LOCATION.
*
*      RELAJ -- RELOCATE A LIST OF POINTERS
*
*      (WA)                  PTR PAST LAST POINTER OF LIST
*      (WB)                  PTR TO FIRST POINTER OF LIST
*      (XL)                  LIST OF BOUNDARIES AND ADJUSTMENTS
*      JSR  RELAJ            CALL TO PROCESS LIST OF POINTERS
*      (WB)                  DESTROYED
*
RELAJ  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE XR
       MOV  WA,-(XS)         SAVE WA
       MOV  XL,RLALS         SAVE PTR TO LIST OF BOUNDS
       MOV  WB,XR            PTR TO FIRST POINTER TO PROCESS
*
*      MERGE HERE TO CHECK IF DONE
*
RLAJ0  MOV  RLALS,XL         RESTORE XL
       BNE  XR,(XS),RLAJ1    PROCEED IF MORE TO DO
       MOV  (XS)+,WA         RESTORE WA
       MOV  (XS)+,XR         RESTORE XR
       EXI                   RETURN TO CALLER
*
*      MERGE HERE TO PROCESS NEXT POINTER ON LIST
*
RLAJ1  MOV  (XR),WA          LOAD NEXT POINTER ON LIST
       LCT  WB,=RNSI_        NUMBER OF SECTIONS OF ADJUSTERS
*
*      MERGE HERE TO PROCESS NEXT SECTION OF STACK LIST
*
RLAJ2  BGT  WA,RLEND(XL),RLAJ3 OK IF PAST END OF SECTION
       BLT  WA,RLSTR(XL),RLAJ3 OR IF BEFORE START OF SECTION
       ADD  RLADJ(XL),WA     WITHIN SECTION, ADD ADJUSTMENT
       MOV  WA,(XR)          RETURN UPDATED PTR TO MEMORY
       BRN  RLAJ4            DONE WITH THIS POINTER
*
*      HERE IF NOT WITHIN SECTION
*
RLAJ3  ADD  *RSSI_,XL        ADVANCE TO NEXT SECTION
       BCT  WB,RLAJ2         JUMP IF MORE TO GO
*
*      HERE WHEN FINISHED PROCESSING ONE POINTER
*
RLAJ4  ICA  XR               INCREMENT TO NEXT PTR ON LIST
       BRN  RLAJ0            JUMP TO CHECK  FOR COMPLETION
       ENP                   END PROCEDURE RELAJ
       EJC
*
*      RELCR -- CREATE RELOCATION INFO AFTER SAVE FILE RELOAD
*
*      (WA)                  ORIGINAL S_AAA CODE SECTION ADR
*      (WB)                  ORIGINAL C_AAA CONSTANT SECTION ADR
*      (WC)                  ORIGINAL G_AAA WORKING SECTION ADR
*      (XR)                  PTR TO START OF STATIC REGION
*      (CP)                  PTR TO START OF DYNAMIC REGION
*      (XL)                  PTR TO AREA TO RECEIVE INFORMATION
*      JSR  RELCR            CREATE RELOCATION INFORMATION
*      (WA,WB,WC,XR)         DESTROYED
*
*      A BLOCK OF INFORMATION IS BUILT AT (XL) THAT IS USED
*      IN RELOCATING POINTERS.  THERE ARE RNSI_ INSTANCES
*      OF A RSSI_ WORD STRUCTURE.  EACH INSTANCE CORRESPONDS
*      TO ONE OF THE REGIONS THAT A POINTER MIGHT POINT INTO.
*      THE LAYOUT OF THIS STRUCTURE IS SHOWN IN THE DEFINITIONS
*      SECTION, TOGETHER WITH SYMBOLIC DEFINITIONS OF THE
*      ENTRIES AS OFFSETS FROM XL.
*
RELCR  PRC  E,0              ENTRY POINT
       ADD  *RLSI_,XL        POINT PAST BUILD AREA
       MOV  WA,-(XL)         SAVE ORIGINAL CODE ADDRESS
       MOV  =S_AAA,WA        COMPUTE ADJUSTMENT
       SUB  (XL),WA          AS NEW S_AAA MINUS ORIGINAL S_AAA
       MOV  WA,-(XL)         SAVE CODE ADJUSTMENT
       MOV  =S_YYY,WA        END OF TARGET CODE SECTION
       SUB  =S_AAA,WA        LENGTH OF CODE SECTION
       ADD  NUM01(XL),WA     PLUS ORIGINAL START ADDRESS
       MOV  WA,-(XL)         END OF ORIGINAL CODE SECTION
       MOV  WB,-(XL)         SAVE CONSTANT SECTION ADDRESS
       MOV  =C_AAA,WB        START OF CONSTANTS SECTION
       MOV  =C_YYY,WA        END OF CONSTANTS SECTION
       SUB  WB,WA            LENGTH OF CONSTANTS SECTION
       SUB  (XL),WB          NEW C_AAA MINUS ORIGINAL C_AAA
       MOV  WB,-(XL)         SAVE CONSTANT ADJUSTMENT
       ADD  NUM01(XL),WA     LENGTH PLUS ORIGINAL START ADR
       MOV  WA,-(XL)         SAVE AS END OF ORIGINAL CONSTANTS
       MOV  WC,-(XL)         SAVE WORKING GLOBALS ADDRESS
       MOV  =G_AAA,WC        START OF WORKING GLOBALS SECTION
       MOV  =W_YYY,WA        END OF WORKING SECTION
       SUB  WC,WA            LENGTH OF WORKING GLOBALS
       SUB  (XL),WC          NEW G_AAA MINUS ORIGINAL G_AAA
       MOV  WC,-(XL)         SAVE WORKING GLOBALS ADJUSTMENT
       ADD  NUM01(XL),WA     LENGTH PLUS ORIGINAL START ADR
       MOV  WA,-(XL)         SAVE AS END OF WORKING GLOBALS
       MOV  STATB,WB         OLD START OF STATIC REGION
       MOV  WB,-(XL)         SAVE
       SUB  WB,XR            COMPUTE ADJUSTMENT
       MOV  XR,-(XL)         SAVE NEW STATB MINUS OLD STATB
       MOV  STATE,-(XL)      OLD END OF STATIC REGION
       MOV  DNAMB,WB         OLD START OF DYNAMIC REGION
       MOV  WB,-(XL)         SAVE
       SCP  WA               NEW START OF DYNAMIC
       SUB  WB,WA            COMPUTE ADJUSTMENT
       MOV  WA,-(XL)         SAVE NEW DNAMB MINUS OLD DNAMB
       MOV  DNAMP,WC         OLD END OF DYNAMIC REGION IN USE
       MOV  WC,-(XL)         SAVE AS END OF OLD DYNAMIC REGION
       EXI
       ENP
       EJC
*
*      RELDN -- RELOCATE POINTERS IN THE DYNAMIC REGION
*
*      (XL)                  LIST OF BOUNDARIES AND ADJUSTMENTS
*      (XR)                  PTR TO FIRST LOCATION TO PROCESS
*      (WC)                  PTR PAST LAST LOCATION TO PROCESS
*      JSR  RELDN            CALL TO PROCESS BLOCKS IN DYNAMIC
*      (WA,WB,WC,XR)         DESTROYED
*
*      PROCESSES ALL BLOCKS IN THE DYNAMIC REGION.  WITHIN A
*      BLOCK, POINTERS TO THE CODE SECTION, CONSTANT SECTION,
*      WORKING GLOBALS SECTION, STATIC REGION, AND DYNAMIC
*      REGION ARE RELOCATED AS NEEDED.
*
RELDN  PRC  E,0              ENTRY POINT
       MOV  RLCDA(XL),RLDCD  SAVE CODE ADJUSTMENT
       MOV  RLSTA(XL),RLDST  SAVE STATIC ADJUSTMENT
       MOV  XL,RLDLS         SAVE LIST POINTER
*
*      MERGE HERE TO PROCESS THE NEXT BLOCK IN DYNAMIC
*
RLD01  ADD  RLDCD,(XR)       ADJUST BLOCK TYPE WORD
       MOV  (XR),XL          LOAD BLOCK TYPE WORD
       LEI  XL               LOAD ENTRY POINT ID (BL_XX)
*
*      BLOCK TYPE SWITCH. NOTE THAT BLOCKS WITH NO RELOCATABLE
*      FIELDS JUST RETURN TO RLD05 TO CONTINUE TO NEXT BLOCK.
*
*      NOTE THAT DFBLKS DO NOT APPEAR IN DYNAMIC, ONLY IN STATIC.
*      CCBLKS AND CMBLKS ARE NOT LIVE WHEN A SAVE FILE IS
*      CREATED, AND CAN BE SKIPPED.
*
*      FURTHER NOTE:  STATIC BLOCKS OTHER THAN VRBLKS DISCOVERED
*      WHILE SCANNING DYNAMIC MUST BE ADJUSTED AT THIS TIME.
*      SEE PROCESSING OF FFBLK FOR EXAMPLE.
*
       EJC
*
*      RELDN (CONTINUED)
*
       BSW  XL,BL___         SWITCH ON BLOCK TYPE
       IFF  BL_AR,RLD03      ARBLK
.IF    .CNBF
       IFF  BL_BC,RLD05      BCBLK - DUMMY TO FILL OUT IFFS
.ELSE
       IFF  BL_BC,RLD06      BCBLK
.FI
       IFF  BL_BF,RLD05      BFBLK
       IFF  BL_CC,RLD05      CCBLK
       IFF  BL_CD,RLD07      CDBLK
       IFF  BL_CM,RLD05      CMBLK
       IFF  BL_CT,RLD05      CTBLK
       IFF  BL_DF,RLD05      DFBLK
       IFF  BL_EF,RLD08      EFBLK
       IFF  BL_EV,RLD09      EVBLK
       IFF  BL_EX,RLD10      EXBLK
       IFF  BL_FF,RLD11      FFBLK
       IFF  BL_IC,RLD05      ICBLK
       IFF  BL_KV,RLD13      KVBLK
       IFF  BL_NM,RLD13      NMBLK
       IFF  BL_P0,RLD13      P0BLK
       IFF  BL_P1,RLD14      P1BLK
       IFF  BL_P2,RLD14      P2BLK
       IFF  BL_PD,RLD15      PDBLK
       IFF  BL_PF,RLD16      PFBLK
.IF    .CNRA
.ELSE
       IFF  BL_RC,RLD05      RCBLK
.FI
       IFF  BL_SC,RLD05      SCBLK
       IFF  BL_SE,RLD13      SEBLK
       IFF  BL_TB,RLD17      TBBLK
       IFF  BL_TE,RLD18      TEBLK
       IFF  BL_TR,RLD19      TRBLK
       IFF  BL_VC,RLD17      VCBLK
       IFF  BL_XN,RLD05      XNBLK
       IFF  BL_XR,RLD20      XRBLK
       ESW                   END OF JUMP TABLE
*
*      ARBLK
*
RLD03  MOV  ARLEN(XR),WA     LOAD LENGTH
       MOV  AROFS(XR),WB     SET OFFSET TO 1ST RELOC FLD (ARPRO)
*
*      MERGE HERE TO PROCESS POINTERS IN A BLOCK
*
*      (XR)                  PTR TO CURRENT BLOCK
*      (WC)                  PTR PAST LAST LOCATION TO PROCESS
*      (WA)                  LENGTH (RELOC FLDS + FLDS AT START)
*      (WB)                  OFFSET TO FIRST RELOC FIELD
*
RLD04  ADD  XR,WA            POINT PAST LAST RELOC FIELD
       ADD  XR,WB            POINT TO FIRST RELOC FIELD
       MOV  RLDLS,XL         POINT TO LIST OF BOUNDS
       JSR  RELAJ            ADJUST POINTERS
       EJC
*
*      RELDN (CONTINUED)
*
*
*      MERGE HERE TO ADVANCE TO NEXT BLOCK
*
*      (XR)                  PTR TO CURRENT BLOCK
*      (WC)                  PTR PAST LAST LOCATION TO PROCESS
*
RLD05  MOV  (XR),WA          BLOCK TYPE WORD
       JSR  BLKLN            GET LENGTH OF BLOCK
       ADD  WA,XR            POINT TO NEXT BLOCK
       BLT  XR,WC,RLD01      CONTINUE IF MORE TO PROCESS
       MOV  RLDLS,XL         RESTORE XL
       EXI                   RETURN TO CALLER IF DONE
.IF    .CNBF
.ELSE
*
*      BCBLK
*
RLD06  MOV  *BCSI_,WA        SET LENGTH
       MOV  *BCBUF,WB        AND OFFSET
       BRN  RLD04            ALL SET
.FI
*
*      CDBLK
*
RLD07  MOV  CDLEN(XR),WA     LOAD LENGTH
       MOV  *CDFAL,WB        SET OFFSET
       BNE  (XR),=B_CDC,RLD04 JUMP BACK IF NOT COMPLEX GOTO
       MOV  *CDCOD,WB        DO NOT PROCESS CDFAL WORD
       BRN  RLD04            JUMP BACK
*
*      EFBLK
*
*      IF THE EFCOD WORD POINTS TO AN XNBLK, THE XNBLK TYPE
*      WORD WILL NOT BE ADJUSTED.  SINCE THIS IS IMPLEMENTATION
*      DEPENDENT, WE WILL NOT WORRY ABOUT IT.
*
RLD08  MOV  *EFRSL,WA        SET LENGTH
       MOV  *EFCOD,WB        AND OFFSET
       BRN  RLD04            ALL SET
*
*      EVBLK
*
RLD09  MOV  *OFFS3,WA        POINT PAST THIRD FIELD
       MOV  *EVEXP,WB        SET OFFSET
       BRN  RLD04            ALL SET
*
*      EXBLK
*
RLD10  MOV  EXLEN(XR),WA     LOAD LENGTH
       MOV  *EXFLC,WB        SET OFFSET
       BRN  RLD04            JUMP BACK
       EJC
*
*      RELDN (CONTINUED)
*
*
*      FFBLK
*
*      THIS BLOCK CONTAINS A PTR TO A DFBLK IN THE STATIC RGN.
*      BECAUSE THERE ARE MULTIPLE FFBLKS POINTING TO THE SAME
*      DFBLK (ONE FOR EACH FIELD NAME), WE ONLY PROCESS THE
*      DFBLK WHEN WE ENCOUNTER THE FFBLK FOR THE FIRST FIELD.
*      THE DFBLK IN TURN CONTAINS A POINTER TO AN SCBLK WITHIN
*      STATIC.
*
RLD11  BNE  FFOFS(XR),*PDFLD,RLD12 SKIP DFBLK IF NOT FIRST FIELD
       MOV  XR,-(XS)         SAVE XR
       MOV  FFDFP(XR),XR     LOAD OLD PTR TO DFBLK
       ADD  RLDST,XR         CURRENT LOCATION OF DFBLK
       ADD  RLDCD,(XR)       ADJUST DFBLK TYPE WORD
       MOV  DFLEN(XR),WA     LENGTH OF DFBLK
       MOV  *DFNAM,WB        OFFSET TO DFNAM FIELD
       ADD  XR,WA            POINT PAST LAST RELOC FIELD
       ADD  XR,WB            POINT TO FIRST RELOC FIELD
       MOV  RLDLS,XL         POINT TO LIST OF BOUNDS
       JSR  RELAJ            ADJUST POINTERS
       MOV  DFNAM(XR),XR     POINTER TO STATIC SCBLK
       ADD  RLDCD,(XR)       ADJUST SCBLK TYPE WORD
       MOV  (XS)+,XR         RESTORE FFBLK POINTER
*
*      FFBLK (CONTINUED)
*
*      MERGE HERE TO SET UP FOR ADJUSTMENT OF PTRS IN FFBLK
*
RLD12  MOV  *FFOFS,WA        SET LENGTH
       MOV  *FFDFP,WB        SET OFFSET
       BRN  RLD04            ALL SET
*
*      KVBLK, NMBLK, P0BLK, SEBLK
*
RLD13  MOV  *OFFS2,WA        POINT PAST SECOND FIELD
       MOV  *OFFS1,WB        OFFSET IS ONE (ONLY RELOC FLD IS 2)
       BRN  RLD04            ALL SET
*
*      P1BLK, P2BLK
*
*      IN P2BLKS, PARM2 CONTAINS EITHER A BIT MASK OR THE
*      NAME OFFSET OF A VARIABLE.  IT NEVER REQUIRES RELOCATION.
*
RLD14  MOV  *PARM2,WA        LENGTH (PARM2 IS NON-RELOCATABLE)
       MOV  *PTHEN,WB        SET OFFSET
       BRN  RLD04            ALL SET
*
*      PDBLK
*
*      NOTE THAT THE DFBLK POINTED TO BY THIS PDBLK WAS
*      PROCESSED WHEN THE FFBLK WAS ENCOUNTERED.  BECAUSE
*      THE DATA FUNCTION WILL BE CALLED BEFORE ANY RECORDS ARE
*      DEFINED, THE FFBLK IS ENCOUNTERED BEFORE ANY
*      CORRESPONDING PDBLK.
*
RLD15  MOV  PDDFP(XR),XL     LOAD PTR TO DFBLK
       ADD  RLDST,XL         ADJUST FOR STATIC RELOCATION
       MOV  DFPDL(XL),WA     GET PDBLK LENGTH
       MOV  *PDDFP,WB        SET OFFSET
       BRN  RLD04            ALL SET
       EJC
*
*      RELDN (CONTINUED)
*
*
*      PFBLK
*
RLD16  ADD  RLDST,PFVBL(XR)  ADJUST NON-CONTIGUOUS FIELD
       MOV  PFLEN(XR),WA     GET PFBLK LENGTH
       MOV  *PFCOD,WB        OFFSET TO FIRST RELOC
       BRN  RLD04            ALL SET
*
*      TBBLK, VCBLK
*
RLD17  MOV  OFFS2(XR),WA     LOAD LENGTH
       MOV  *OFFS3,WB        SET OFFSET
       BRN  RLD04            JUMP BACK
*
*      TEBLK
*
RLD18  MOV  *TESI_,WA        SET LENGTH
       MOV  *TESUB,WB        AND OFFSET
       BRN  RLD04            ALL SET
*
*      TRBLK
*
RLD19  MOV  *TRSI_,WA        SET LENGTH
       MOV  *TRVAL,WB        AND OFFSET
       BRN  RLD04            ALL SET
*
*      XRBLK
*
RLD20  MOV  XRLEN(XR),WA     LOAD LENGTH
       MOV  *XRPTR,WB        SET OFFSET
       BRN  RLD04            JUMP BACK
       ENP                   END PROCEDURE RELDN
       EJC
*
*      RELOC -- RELOCATE STORAGE AFTER SAVE FILE RELOAD
*
*      (XL)                  LIST OF BOUNDARIES AND ADJUSTMENTS
*      JSR  RELOC            RELOCATE ALL POINTERS
*      (WA,WB,WC,XR)         DESTROYED
*
*      THE LIST OF BOUNDARIES AND ADJUSTMENTS POINTED TO BY
*      REGISTER XL IS CREATED BY A CALL TO RELCR, WHICH SHOULD
*      BE CONSULTED FOR INFORMATION ON ITS STRUCTURE.
*
RELOC  PRC  E,0              ENTRY POINT
       MOV  RLDYS(XL),XR     OLD START OF DYNAMIC
       MOV  RLDYE(XL),WC     OLD END OF DYNAMIC
       ADD  RLDYA(XL),XR     CREATE NEW START OF DYNAMIC
       ADD  RLDYA(XL),WC     CREATE NEW END OF DYNAMIC
       JSR  RELDN            RELOCATE POINTERS IN DYNAMIC
       JSR  RELWS            RELOCATE POINTERS IN WORKING SECT
       JSR  RELST            RELOCATE POINTERS IN STATIC
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE RELOC
       EJC
*
*      RELST -- RELOCATE POINTERS IN THE STATIC REGION
*
*      (XL)                  LIST OF BOUNDARIES AND ADJUSTMENTS
*      JSR  RELST            CALL TO PROCESS BLOCKS IN STATIC
*      (WA,WB,WC,XR)         DESTROYED
*
*      ONLY VRBLKS ON THE HASH CHAIN AND ANY PROFILE BLOCK ARE
*      PROCESSED.  OTHER STATIC BLOCKS (DFBLKS) ARE PROCESSED
*      DURING PROCESSING OF DYNAMIC BLOCKS.
*
*      GLOBAL WORK LOCATIONS WILL BE PROCESSED AT THIS POINT,
*      SO POINTERS THERE CAN BE RELIED UPON.
*
RELST  PRC  E,0              ENTRY POINT
       MOV  PFTBL,XR         PROFILE TABLE
       BZE  XR,RLS01         BRANCH IF NO TABLE ALLOCATED
       ADD  RLCDA(XL),(XR)   ADJUST BLOCK TYPE WORD
*
*      HERE AFTER DEALING WITH PROFILER
*
RLS01  MOV  HSHTB,WC         POINT TO START OF HASH TABLE
       MOV  WC,WB            POINT TO FIRST HASH BUCKET
       MOV  HSHTE,WA         POINT BEYOND HASH TABLE
       JSR  RELAJ            ADJUST BUCKET POINTERS
*
*      LOOP THROUGH SLOTS IN HASH TABLE
*
RLS02  BEQ  WC,HSHTE,RLS05   DONE IF NONE LEFT
       MOV  WC,XR            ELSE COPY SLOT POINTER
       ICA  WC               BUMP SLOT POINTER
       SUB  *VRNXT,XR        SET OFFSET TO MERGE INTO LOOP
*
*      LOOP THROUGH VRBLKS ON ONE HASH CHAIN
*
RLS03  MOV  VRNXT(XR),XR     POINT TO NEXT VRBLK ON CHAIN
       BZE  XR,RLS02         JUMP FOR NEXT BUCKET IF CHAIN END
       MOV  *VRLEN,WA        OFFSET OF FIRST LOC PAST PTR FIELDS
       MOV  *VRGET,WB        OFFSET OF FIRST LOCATION IN VRBLK
       BNZ  VRLEN(XR),RLS04  JUMP IF NOT SYSTEM VARIABLE
       MOV  *VRSI_,WA        OFFSET TO INCLUDE VRSVP FIELD
*
*      MERGE HERE TO PROCESS FIELDS OF VRBLK
*
RLS04  ADD  XR,WA            CREATE END PTR
       ADD  XR,WB            CREATE START PTR
       JSR  RELAJ            ADJUST POINTERS IN VRBLK
       BRN  RLS03            CHECK FOR ANOTHER VRBLK ON CHAIN
*
*      HERE WHEN ALL VRBLKS PROCESSED
*
RLS05  EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE RELST
       EJC
*
*      RELWS -- RELOCATE POINTERS IN THE WORKING SECTION
*
*      (XL)                  LIST OF BOUNDARIES AND ADJUSTMENTS
*      JSR  RELWS            CALL TO PROCESS WORKING SECTION
*      (WA,WB,WC,XR)         DESTROYED
*
*      POINTERS BETWEEN A_AAA AND R_YYY ARE EXAMINED AND
*      ADJUSTED IF NECESSARY.  THE POINTER KVRTN IS ALSO
*      ADJUSTED ALTHOUGH IT LIES OUTSIDE THIS RANGE.
*      DNAME IS EXPLICITLY ADJUSTED BECAUSE THE LIMITS
*      ON DYNAMIC REGION IN STACK ARE TO THE AREA ACTIVELY
*      IN USE (BETWEEN DNAMB AND DNAMP), AND DNAME IS OUTSIDE
*      THIS RANGE.
*
RELWS  PRC  E,0              ENTRY POINT
       MOV  =A_AAA,WB        POINT TO START OF ADJUSTABLES
       MOV  =R_YYY,WA        POINT TO END OF ADJUSTABLES
       JSR  RELAJ            RELOCATE ADJUSTABLE POINTERS
       ADD  RLDYA(XL),DNAME  ADJUST PTR MISSED BY RELAJ
       MOV  =KVRTN,WB        CASE OF KVRTN
       MOV  WB,WA            HANDLED SPECIALLY
       ICA  WA               ONE VALUE TO ADJUST
       JSR  RELAJ            ADJUST KVRTN
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE RELWS
.FI
       TTL  S P I T B O L -- INITIALIZATION
*
*      INITIALISATION
*      THE FOLLOWING SECTION RECEIVES CONTROL FROM THE SYSTEM
*      AT THE START OF A RUN WITH THE REGISTERS SET AS FOLLOWS.
*
*      (WA)                  INITIAL STACK POINTER
*      (XR)                  POINTS TO FIRST WORD OF DATA AREA
*      (XL)                  POINTS TO LAST WORD OF DATA AREA
*
START  PRC  E,0              ENTRY POINT
*Z-
       MOV  WA,XS            DISCARD RETURN
       JSR  SYSTM            INITIALISE TIMER
.IF    .CNBT
       STI  TIMSX            STORE TIME
       MOV  XR,STATB         START ADDRESS OF STATIC
.ELSE
*
*      INITIALISE WORK AREA (ESSENTIAL FOR BATCHED RUNS)
*
       MOV  XR,WB            PRESERVE XR
       MOV  =W_YYY,WA        POINT TO END OF WORK AREA
       SUB  =W_AAA,WA        GET LENGTH OF WORK AREA
       BTW  WA               CONVERT TO WORDS
       LCT  WA,WA            COUNT FOR LOOP
       MOV  =W_AAA,XR        SET UP INDEX REGISTER
*
*      CLEAR WORK SPACE
*
INI01  ZER  (XR)+            CLEAR A WORD
       BCT  WA,INI01         LOOP TILL DONE
       MOV  =STNDO,WA        UNDEFINED OPERATORS POINTER
       MOV  =R_YYY,WC        POINT TO TABLE END
       SUB  =R_UBA,WC        LENGTH OF UNDEF. OPERATORS TABLE
       BTW  WC               CONVERT TO WORDS
       LCT  WC,WC            LOOP COUNTER
       MOV  =R_UBA,XR        SET UP XR
*
*      SET CORRECT VALUE INTO UNDEFINED OPERATORS TABLE
*
INI02  MOV  WA,(XR)+         STORE VALUE
       BCT  WC,INI02         LOOP TILL ALL DONE
       MOV  =NUM01,WA        GET A 1
.IF    .CPOL
       MOV  WA,POLCS         INTERFACE POLLING INTERVAL
       MOV  WA,POLCT         INTERFACE POLLING INTERVAL
.FI
       MOV  WA,CMPSN         STATEMENT NO
       MOV  WA,CSWFL         NOFAIL
       MOV  WA,CSWLS         LIST
       MOV  WA,KVINP         INPUT
       MOV  WA,KVOUP         OUTPUT
       MOV  WA,LSTPF         NOTHING FOR LISTR YET
       MOV  =INILN,WA        INPUT IMAGE LENGTH
       MOV  WA,CSWIN         -IN72
       EJC
       MOV  =NULLS,WA        GET NULL STRING POINTER
       MOV  WA,KVRTN         RETURN
       MOV  WA,R_ETX         ERRTEXT
       MOV  WA,R_TTL         TITLE FOR LISTING
       MOV  WA,STXVR         SETEXIT
       STI  TIMSX            STORE TIME IN CORRECT PLACE
       LDI  STLIM            GET DEFAULT STLIMIT
       STI  KVSTL            STATEMENT LIMIT
       STI  KVSTC            STATEMENT COUNT
       MOV  WB,STATB         STORE START ADRS OF STATIC
.FI
       MOV  *E_SRS,RSMEM     RESERVE MEMORY
       MOV  XS,STBAS         STORE STACK BASE
       SSS  INISS            SAVE S-R STACK PTR
*
*      NOW CONVERT FREE STORE PERCENTAGE TO A SUITABLE FACTOR
*      FOR EASY TESTING IN ALLOC ROUTINE.
*
       LDI  INTVH            GET 100
       DVI  ALFSP            FORM 100 / ALFSP
       STI  ALFSF            STORE THE FACTOR
.IF    .CSED
*
*      NOW CONVERT FREE SEDIMENT PERCENTAGE TO A SUITABLE FACTOR
*      FOR EASY TESTING IN GBCOL ROUTINE.
*
       LDI  INTVH            GET 100
       DVI  GBSDP            FORM 100 / GBSDP
       STI  GBSED            STORE THE FACTOR
.FI
.IF    .CNRA
.ELSE
.IF    .CNCR
.ELSE
*
*      INITIALIZE VALUES FOR REAL CONVERSION ROUTINE
*
       LCT  WB,=CFP_S        LOAD COUNTER FOR SIGNIFICANT DIGITS
       LDR  REAV1            LOAD 1.0
*
*      LOOP TO COMPUTE 10**(MAX NUMBER SIGNIFICANT DIGITS)
*
INI03  MLR  REAVT            * 10.0
       BCT  WB,INI03         LOOP TILL DONE
       STR  GTSSC            STORE 10**(MAX SIG DIGITS)
       LDR  REAP5            LOAD 0.5
       DVR  GTSSC            COMPUTE 0.5*10**(MAX SIG DIGITS)
       STR  GTSRN            STORE AS ROUNDING BIAS
.FI
.FI
       ZER  WC               SET TO READ PARAMETERS
       JSR  PRPAR            READ THEM
       EJC
*
*      NOW COMPUTE STARTING ADDRESS FOR DYNAMIC STORE AND IF
*      NECESSARY REQUEST MORE MEMORY.
*
       SUB  *E_SRS,XL        ALLOW FOR RESERVE MEMORY
       MOV  PRLEN,WA         GET PRINT BUFFER LENGTH
       ADD  =CFP_A,WA        ADD NO. OF CHARS IN ALPHABET
       ADD  =NSTMX,WA        ADD CHARS FOR GTSTG BFR
       CTB  WA,8             CONVERT TO BYTES, ALLOWING A MARGIN
       MOV  STATB,XR         POINT TO STATIC BASE
       ADD  WA,XR            INCREMENT FOR ABOVE BUFFERS
       ADD  *E_HNB,XR        INCREMENT FOR HASH TABLE
       ADD  *E_STS,XR        BUMP FOR INITIAL STATIC BLOCK
       JSR  SYSMX            GET MXLEN
       MOV  WA,KVMXL         PROVISIONALLY STORE AS MAXLNGTH
       MOV  WA,MXLEN         AND AS MXLEN
       BGT  XR,WA,INI06      SKIP IF STATIC HI EXCEEDS MXLEN
       CTB  WA,1             ROUND UP AND MAKE BIGGER THAN MXLEN
       MOV  WA,XR            USE IT INSTEAD
*
*      HERE TO STORE VALUES WHICH MARK INITIAL DIVISION
*      OF DATA AREA INTO STATIC AND DYNAMIC
*
INI06  MOV  XR,DNAMB         DYNAMIC BASE ADRS
       MOV  XR,DNAMP         DYNAMIC PTR
       BNZ  WA,INI07         SKIP IF NON-ZERO MXLEN
       DCA  XR               POINT A WORD IN FRONT
       MOV  XR,KVMXL         USE AS MAXLNGTH
       MOV  XR,MXLEN         AND AS MXLEN
       EJC
*
*      LOOP HERE IF NECESSARY TILL ENOUGH MEMORY OBTAINED
*      SO THAT DNAME IS ABOVE DNAMB
*
INI07  MOV  XL,DNAME         STORE DYNAMIC END ADDRESS
       BLT  DNAMB,XL,INI09   SKIP IF HIGH ENOUGH
       JSR  SYSMM            REQUEST MORE MEMORY
       WTB  XR               GET AS BAUS (SGD05)
       ADD  XR,XL            BUMP BY AMOUNT OBTAINED
       BNZ  XR,INI07         TRY AGAIN
.IF    .CERA
       MOV  =MXERN,WA        INSUFFICIENT MEMORY FOR MAXLENGTH
       ZER  WB               NO COLUMN NUMBER INFO
       ZER  WC               NO LINE NUMBER INFO
       MOV  =STGIC,XR        INITIAL COMPILE STAGE
.IF    .CSFN
       MOV  =NULLS,XL        NO FILE NAME
.FI
       JSR  SYSEA            ADVISE OF ERROR
       PPM  INI08            CANT USE ERROR LOGIC YET
       BRN  INI08            FORCE TERMINATION
*
*      INSERT TEXT FOR ERROR 329 IN ERROR MESSAGE TABLE
*
       ERB  329,Requested MAXLNGTH too large
.FI
INI08  MOV  =ENDMO,XR        POINT TO FAILURE MESSAGE
       MOV  ENDML,WA         MESSAGE LENGTH
       JSR  SYSPR            PRINT IT (PRTST NOT YET USABLE)
       PPM                   SHOULD NOT FAIL
       ZER  XL               NO FCB CHAIN YET
       MOV  =NUM10,WB        SET SPECIAL CODE VALUE
       JSR  SYSEJ            PACK UP (STOPR NOT YET USABLE)
*
*      INITIALISE STRUCTURES AT START OF STATIC REGION
*
INI09  MOV  STATB,XR         POINT TO STATIC AGAIN
       JSR  INSTA            INITIALIZE STATIC
*
*      INITIALIZE NUMBER OF HASH HEADERS
*
       MOV  =E_HNB,WA        GET NUMBER OF HASH HEADERS
       MTI  WA               CONVERT TO INTEGER
       STI  HSHNB            STORE FOR USE BY GTNVR PROCEDURE
       LCT  WA,WA            COUNTER FOR CLEARING HASH TABLE
       MOV  XR,HSHTB         POINTER TO HASH TABLE
*
*      LOOP TO CLEAR HASH TABLE
*
INI11  ZER  (XR)+            BLANK A WORD
       BCT  WA,INI11         LOOP
       MOV  XR,HSHTE         END OF HASH TABLE ADRS IS KEPT
       MOV  XR,STATE         STORE STATIC END ADDRESS
.IF    .CSFN
*
*      INIT TABLE TO MAP STATEMENT NUMBERS TO SOURCE FILE NAMES
*
       MOV  =NUM01,WC        TABLE WILL HAVE ONLY ONE BUCKET
       MOV  =NULLS,XL        DEFAULT TABLE VALUE
       MOV  XL,R_SFC         CURRENT SOURCE FILE NAME
       JSR  TMAKE            CREATE TABLE
       MOV  XR,R_SFN         SAVE PTR TO TABLE
.FI
.IF    .CINC
*
*      INITIALIZE TABLE TO DETECT DUPLICATE INCLUDE FILE NAMES
*
       MOV  =NUM01,WC        TABLE WILL HAVE ONLY ONE BUCKET
       MOV  =NULLS,XL        DEFAULT TABLE VALUE
       JSR  TMAKE            CREATE TABLE
       MOV  XR,R_INC         SAVE PTR TO TABLE
.IF    .CSFN
*
*      INITIALIZE ARRAY TO HOLD NAMES OF NESTED INCLUDE FILES
*
       MOV  =CCINM,WA        MAXIMUM NESTING LEVEL
       MOV  =NULLS,XL        NULL STRING DEFAULT VALUE
       JSR  VMAKE            CREATE ARRAY
       PPM
       MOV  XR,R_IFA         SAVE PTR TO ARRAY
*
*      INIT ARRAY TO HOLD LINE NUMBERS OF NESTED INCLUDE FILES
*
       MOV  =CCINM,WA        MAXIMUM NESTING LEVEL
       MOV  =INTON,XL        INTEGER ONE DEFAULT VALUE
       JSR  VMAKE            CREATE ARRAY
       PPM
       MOV  XR,R_IFL         SAVE PTR TO ARRAY
.FI
.FI
*Z+
*
*      INITIALIZE VARIABLE BLOCKS FOR INPUT AND OUTPUT
*
       MOV  =V_INP,XL        POINT TO STRING /INPUT/
       MOV  =TRTIN,WB        TRBLK TYPE FOR INPUT
       JSR  INOUT            PERFORM INPUT ASSOCIATION
       MOV  =V_OUP,XL        POINT TO STRING /OUTPUT/
       MOV  =TRTOU,WB        TRBLK TYPE FOR OUTPUT
       JSR  INOUT            PERFORM OUTPUT ASSOCIATION
       MOV  INITR,WC         TERMINAL FLAG
       BZE  WC,INI13         SKIP IF NO TERMINAL
       JSR  PRPAR            ASSOCIATE TERMINAL
       EJC
*
*      CHECK FOR EXPIRY DATE
*
INI13  JSR  SYSDC            CALL DATE CHECK
       MOV  XS,FLPTR         IN CASE STACK OVERFLOWS IN COMPILER
*
*      NOW COMPILE SOURCE INPUT CODE
*
       JSR  CMPIL            CALL COMPILER
       MOV  XR,R_COD         SET PTR TO FIRST CODE BLOCK
       MOV  =NULLS,R_TTL     FORGET TITLE
       MOV  =NULLS,R_STL     FORGET SUB-TITLE
       ZER  R_CIM            FORGET COMPILER INPUT IMAGE
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
.IF    .CINC
       ZER  CNIND            IN CASE END OCCURRED WITH INCLUDE
       ZER  LSTID            LISTING INCLUDE DEPTH
.FI
       ZER  XL               CLEAR DUD VALUE
       ZER  WB               DONT SHIFT DYNAMIC STORE UP
.IF    .CSED
       ZER  DNAMS            COLLECT SEDIMENT TOO
       JSR  GBCOL            CLEAR GARBAGE LEFT FROM COMPILE
       MOV  XR,DNAMS         RECORD NEW SEDIMENT SIZE
.ELSE
       JSR  GBCOL            CLEAR GARBAGE LEFT FROM COMPILE
.FI
       BNZ  CPSTS,INIX0      SKIP IF NO LISTING OF COMP STATS
       JSR  PRTPG            EJECT PAGE
*
*      PRINT COMPILE STATISTICS
*
       JSR  PRTMM            PRINT MEMORY USAGE
       MTI  CMERC            GET COUNT OF ERRORS AS INTEGER
       MOV  =ENCM3,XR        POINT TO /COMPILE ERRORS/
       JSR  PRTMI            PRINT IT
       MTI  GBCNT            GARBAGE COLLECTION COUNT
       SBI  INTV1            ADJUST FOR UNAVOIDABLE COLLECT
       MOV  =STPM5,XR        POINT TO /STORAGE REGENERATIONS/
       JSR  PRTMI            PRINT GBCOL COUNT
       JSR  SYSTM            GET TIME
       SBI  TIMSX            GET COMPILATION TIME
       MOV  =ENCM4,XR        POINT TO COMPILATION TIME (MSEC)/
       JSR  PRTMI            PRINT MESSAGE
       ADD  =NUM05,LSTLC     BUMP LINE COUNT
.IF    .CUEJ
       BZE  HEADP,INIX0      NO EJECT IF NOTHING PRINTED
       JSR  PRTPG            EJECT PRINTER
.FI
       EJC
*
*      PREPARE NOW TO START EXECUTION
*
*      SET DEFAULT INPUT RECORD LENGTH
*
INIX0  BGT  CSWIN,=INILN,INIX1 SKIP IF NOT DEFAULT -IN72 USED
       MOV  =INILS,CSWIN     ELSE USE DEFAULT RECORD LENGTH
*
*      RESET TIMER
*
INIX1  JSR  SYSTM            GET TIME AGAIN
       STI  TIMSX            STORE FOR END RUN PROCESSING
       ZER  GBCNT            INITIALISE COLLECT COUNT
       JSR  SYSBX            CALL BEFORE STARTING EXECUTION
       ADD  CSWEX,NOXEQ      ADD -NOEXECUTE FLAG
       BNZ  NOXEQ,INIX2      JUMP IF EXECUTION SUPPRESSED
.IF    .CUEJ
.ELSE
       BZE  HEADP,INIY0      NO EJECT IF NOTHING PRINTED (SGD11)
       JSR  PRTPG            EJECT PRINTER
.FI
*
*      MERGE WHEN LISTING FILE SET FOR EXECUTION.  ALSO
*      MERGE HERE WHEN RESTARTING A SAVE FILE OR LOAD MODULE.
*
INIY0  MNZ  HEADP            MARK HEADERS OUT REGARDLESS
       ZER  -(XS)            SET FAILURE LOCATION ON STACK
       MOV  XS,FLPTR         SAVE PTR TO FAILURE OFFSET WORD
       MOV  R_COD,XR         LOAD PTR TO ENTRY CODE BLOCK
       MOV  =STGXT,STAGE     SET STAGE FOR EXECUTE TIME
.IF    .CPOL
       MOV  =NUM01,POLCS     RESET INTERFACE POLLING INTERVAL
       MOV  =NUM01,POLCT     RESET INTERFACE POLLING INTERVAL
.FI
.IF    .CNPF
.ELSE
       MOV  CMPSN,PFNTE      COPY STMTS COMPILED COUNT IN CASE
       MOV  KVPFL,PFDMP      START PROFILING IF &PROFILE SET
       JSR  SYSTM            TIME YET AGAIN
       STI  PFSTM
.FI
       JSR  STGCC            COMPUTE STMGO COUNTDOWN COUNTERS
       BRI  (XR)             START XEQ WITH FIRST STATEMENT
*
*      HERE IF EXECUTION IS SUPPRESSED
*
.IF    .CERA
INIX2  ZER  WA               SET ABEND VALUE TO ZERO
.ELSE
INIX2  JSR  PRTNL            PRINT A BLANK LINE
       MOV  =ENCM5,XR        POINT TO /EXECUTION SUPPRESSED/
       JSR  PRTST            PRINT STRING
       JSR  PRTNL            OUTPUT LINE
       ZER  WA               SET ABEND VALUE TO ZERO
.FI
       MOV  =NINI9,WB        SET SPECIAL CODE VALUE
       ZER  XL               NO FCB CHAIN
       JSR  SYSEJ            END OF JOB, EXIT TO SYSTEM
       ENP                   END PROCEDURE START
*
*      HERE FROM OSINT TO RESTART A SAVE FILE OR LOAD MODULE.
*
RSTRT  PRC  E,0              ENTRY POINT
       MOV  STBAS,XS         DISCARD RETURN
       ZER  XL               CLEAR XL
       BRN  INIY0            RESUME EXECUTION
       ENP                   END PROCEDURE RSTRT

       TTL  S P I T B O L -- SNOBOL4 OPERATOR ROUTINES
*
*      THIS SECTION INCLUDES ALL ROUTINES WHICH CAN BE ACCESSED
*      DIRECTLY FROM THE GENERATED CODE EXCEPT SYSTEM FUNCTIONS.
*
*      ALL ROUTINES IN THIS SECTION START WITH A LABEL OF THE
*      FORM O_XXX WHERE XXX IS THREE LETTERS. THE GENERATED CODE
*      CONTAINS A POINTER TO THE APPROPRIATE ENTRY LABEL.
*
*      SINCE THE GENERAL FORM OF THE GENERATED CODE CONSISTS OF
*      POINTERS TO BLOCKS WHOSE FIRST WORD IS THE ADDRESS OF THE
*      ACTUAL ENTRY POINT LABEL (O_XXX).
*
*      THESE ROUTINES ARE IN ALPHABETICAL ORDER BY THEIR
*      ENTRY LABEL NAMES (I.E. BY THE XXX OF THE O_XXX NAME)
*
*      THESE ROUTINES RECEIVE CONTROL AS FOLLOWS
*
*      (CP)                  POINTER TO NEXT CODE WORD
*      (XS)                  CURRENT STACK POINTER
       EJC
*
*      BINARY PLUS (ADDITION)
*
O_ADD  ENT                   ENTRY POINT
       JSR  ARITH            FETCH ARITHMETIC OPERANDS
       ERR  001,Addition left operand is not numeric
       ERR  002,Addition right operand is not numeric
.IF    .CNRA
.ELSE
       PPM  OADD1            JUMP IF REAL OPERANDS
.FI
*
*      HERE TO ADD TWO INTEGERS
*
       ADI  ICVAL(XL)        ADD RIGHT OPERAND TO LEFT
       INO  EXINT            RETURN INTEGER IF NO OVERFLOW
       ERB  003,Addition caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO ADD TWO REALS
*
OADD1  ADR  RCVAL(XL)        ADD RIGHT OPERAND TO LEFT
       RNO  EXREA            RETURN REAL IF NO OVERFLOW
       ERB  261,Addition caused real overflow
.FI
       EJC
*
*      UNARY PLUS (AFFIRMATION)
*
O_AFF  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD OPERAND
       JSR  GTNUM            CONVERT TO NUMERIC
       ERR  004,Affirmation operand is not numeric
       MOV  XR,-(XS)         RESULT IF CONVERTED TO NUMERIC
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      BINARY BAR (ALTERNATION)
*
O_ALT  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD RIGHT OPERAND
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  005,Alternation right operand is not pattern
*
*      MERGE HERE FROM SPECIAL (LEFT ALTERNATION) CASE
*
OALT1  MOV  =P_ALT,WB        SET PCODE FOR ALTERNATIVE NODE
       JSR  PBILD            BUILD ALTERNATIVE NODE
       MOV  XR,XL            SAVE ADDRESS OF ALTERNATIVE NODE
       MOV  (XS)+,XR         LOAD LEFT OPERAND
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  006,Alternation left operand is not pattern
       BEQ  XR,=P_ALT,OALT2  JUMP IF LEFT ARG IS ALTERNATION
       MOV  XR,PTHEN(XL)     SET LEFT OPERAND AS SUCCESSOR
       MOV  XL,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      COME HERE IF LEFT ARGUMENT IS ITSELF AN ALTERNATION
*
*      THE RESULT IS MORE EFFICIENT IF WE MAKE THE REPLACEMENT
*
*      (A / B) / C = A / (B / C)
*
OALT2  MOV  PARM1(XR),PTHEN(XL) BUILD THE (B / C) NODE
       MOV  PTHEN(XR),-(XS)  SET A AS NEW LEFT ARG
       MOV  XL,XR            SET (B / C) AS NEW RIGHT ARG
       BRN  OALT1            MERGE BACK TO BUILD A / (B / C)
       EJC
*
*      ARRAY REFERENCE (MULTIPLE SUBSCRIPTS, BY NAME)
*
O_AMN  ENT                   ENTRY POINT
       LCW  XR               LOAD NUMBER OF SUBSCRIPTS
       MOV  XR,WB            SET FLAG FOR BY NAME
       BRN  ARREF            JUMP TO ARRAY REFERENCE ROUTINE
       EJC
*
*      ARRAY REFERENCE (MULTIPLE SUBSCRIPTS, BY VALUE)
*
O_AMV  ENT                   ENTRY POINT
       LCW  XR               LOAD NUMBER OF SUBSCRIPTS
       ZER  WB               SET FLAG FOR BY VALUE
       BRN  ARREF            JUMP TO ARRAY REFERENCE ROUTINE
       EJC
*
*      ARRAY REFERENCE (ONE SUBSCRIPT, BY NAME)
*
O_AON  ENT                   ENTRY POINT
       MOV  (XS),XR          LOAD SUBSCRIPT VALUE
       MOV  NUM01(XS),XL     LOAD ARRAY VALUE
       MOV  (XL),WA          LOAD FIRST WORD OF ARRAY OPERAND
       BEQ  WA,=B_VCT,OAON2  JUMP IF VECTOR REFERENCE
       BEQ  WA,=B_TBT,OAON3  JUMP IF TABLE REFERENCE
*
*      HERE TO USE CENTRAL ARRAY REFERENCE ROUTINE
*
OAON1  MOV  =NUM01,XR        SET NUMBER OF SUBSCRIPTS TO ONE
       MOV  XR,WB            SET FLAG FOR BY NAME
       BRN  ARREF            JUMP TO ARRAY REFERENCE ROUTINE
*
*      HERE IF WE HAVE A VECTOR REFERENCE
*
OAON2  BNE  (XR),=B_ICL,OAON1 USE LONG ROUTINE IF NOT INTEGER
       LDI  ICVAL(XR)        LOAD INTEGER SUBSCRIPT VALUE
       MFI  WA,EXFAL         COPY AS ADDRESS INT, FAIL IF OVFLO
       BZE  WA,EXFAL         FAIL IF ZERO
       ADD  =VCVLB,WA        COMPUTE OFFSET IN WORDS
       WTB  WA               CONVERT TO BYTES
       MOV  WA,(XS)          COMPLETE NAME ON STACK
       BLT  WA,VCLEN(XL),OAON4 EXIT IF SUBSCRIPT NOT TOO LARGE
       BRN  EXFAL            ELSE FAIL
*
*      HERE FOR TABLE REFERENCE
*
OAON3  MNZ  WB               SET FLAG FOR NAME REFERENCE
       JSR  TFIND            LOCATE/CREATE TABLE ELEMENT
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  XL,NUM01(XS)     STORE NAME BASE ON STACK
       MOV  WA,(XS)          STORE NAME OFFSET ON STACK
*
*      HERE TO EXIT WITH RESULT ON STACK
*
OAON4  LCW  XR               RESULT ON STACK, GET CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      ARRAY REFERENCE (ONE SUBSCRIPT, BY VALUE)
*
O_AOV  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD SUBSCRIPT VALUE
       MOV  (XS)+,XL         LOAD ARRAY VALUE
       MOV  (XL),WA          LOAD FIRST WORD OF ARRAY OPERAND
       BEQ  WA,=B_VCT,OAOV2  JUMP IF VECTOR REFERENCE
       BEQ  WA,=B_TBT,OAOV3  JUMP IF TABLE REFERENCE
*
*      HERE TO USE CENTRAL ARRAY REFERENCE ROUTINE
*
OAOV1  MOV  XL,-(XS)         RESTACK ARRAY VALUE
       MOV  XR,-(XS)         RESTACK SUBSCRIPT
       MOV  =NUM01,XR        SET NUMBER OF SUBSCRIPTS TO ONE
       ZER  WB               SET FLAG FOR VALUE CALL
       BRN  ARREF            JUMP TO ARRAY REFERENCE ROUTINE
*
*      HERE IF WE HAVE A VECTOR REFERENCE
*
OAOV2  BNE  (XR),=B_ICL,OAOV1 USE LONG ROUTINE IF NOT INTEGER
       LDI  ICVAL(XR)        LOAD INTEGER SUBSCRIPT VALUE
       MFI  WA,EXFAL         MOVE AS ONE WORD INT, FAIL IF OVFLO
       BZE  WA,EXFAL         FAIL IF ZERO
       ADD  =VCVLB,WA        COMPUTE OFFSET IN WORDS
       WTB  WA               CONVERT TO BYTES
       BGE  WA,VCLEN(XL),EXFAL FAIL IF SUBSCRIPT TOO LARGE
       JSR  ACESS            ACCESS VALUE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      HERE FOR TABLE REFERENCE BY VALUE
*
OAOV3  ZER  WB               SET FLAG FOR VALUE REFERENCE
       JSR  TFIND            CALL TABLE SEARCH ROUTINE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      ASSIGNMENT
*
O_ASS  ENT                   ENTRY POINT
*
*      O_RPL (PATTERN REPLACEMENT) MERGES HERE
*
OASS0  MOV  (XS)+,WB         LOAD VALUE TO BE ASSIGNED
       MOV  (XS)+,WA         LOAD NAME OFFSET
       MOV  (XS),XL          LOAD NAME BASE
       MOV  WB,(XS)          STORE ASSIGNED VALUE AS RESULT
       JSR  ASIGN            PERFORM ASSIGNMENT
       PPM  EXFAL            FAIL IF ASSIGNMENT FAILS
       LCW  XR               RESULT ON STACK, GET CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      COMPILATION ERROR
*
O_CER  ENT                   ENTRY POINT
       ERB  007,Compilation error encountered during execution
       EJC
*
*      UNARY AT (CURSOR ASSIGNMENT)
*
O_CAS  ENT                   ENTRY POINT
       MOV  (XS)+,WC         LOAD NAME OFFSET (PARM2)
       MOV  (XS)+,XR         LOAD NAME BASE (PARM1)
       MOV  =P_CAS,WB        SET PCODE FOR CURSOR ASSIGNMENT
       JSR  PBILD            BUILD NODE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      CONCATENATION
*
O_CNC  ENT                   ENTRY POINT
       MOV  (XS),XR          LOAD RIGHT ARGUMENT
       BEQ  XR,=NULLS,OCNC3  JUMP IF RIGHT ARG IS NULL
       MOV  1(XS),XL         LOAD LEFT ARGUMENT
       BEQ  XL,=NULLS,OCNC4  JUMP IF LEFT ARGUMENT IS NULL
       MOV  =B_SCL,WA        GET CONSTANT TO TEST FOR STRING
       BNE  WA,(XL),OCNC2    JUMP IF LEFT ARG NOT A STRING
       BNE  WA,(XR),OCNC2    JUMP IF RIGHT ARG NOT A STRING
*
*      MERGE HERE TO CONCATENATE TWO STRINGS
*
OCNC1  MOV  SCLEN(XL),WA     LOAD LEFT ARGUMENT LENGTH
       ADD  SCLEN(XR),WA     COMPUTE RESULT LENGTH
       JSR  ALOCS            ALLOCATE SCBLK FOR RESULT
       MOV  XR,1(XS)         STORE RESULT PTR OVER LEFT ARGUMENT
       PSC  XR               PREPARE TO STORE CHARS OF RESULT
       MOV  SCLEN(XL),WA     GET NUMBER OF CHARS IN LEFT ARG
       PLC  XL               PREPARE TO LOAD LEFT ARG CHARS
       MVC                   MOVE CHARACTERS OF LEFT ARGUMENT
       MOV  (XS)+,XL         LOAD RIGHT ARG POINTER, POP STACK
       MOV  SCLEN(XL),WA     LOAD NUMBER OF CHARS IN RIGHT ARG
       PLC  XL               PREPARE TO LOAD RIGHT ARG CHARS
       MVC                   MOVE CHARACTERS OF RIGHT ARGUMENT
       ZER  XL               CLEAR GARBAGE VALUE IN XL
       LCW  XR               RESULT ON STACK, GET CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      COME HERE IF ARGUMENTS ARE NOT BOTH STRINGS
*
OCNC2  JSR  GTSTG            CONVERT RIGHT ARG TO STRING
       PPM  OCNC5            JUMP IF RIGHT ARG IS NOT STRING
       MOV  XR,XL            SAVE RIGHT ARG PTR
       JSR  GTSTG            CONVERT LEFT ARG TO STRING
       PPM  OCNC6            JUMP IF LEFT ARG IS NOT A STRING
       MOV  XR,-(XS)         STACK LEFT ARGUMENT
       MOV  XL,-(XS)         STACK RIGHT ARGUMENT
       MOV  XR,XL            MOVE LEFT ARG TO PROPER REG
       MOV  (XS),XR          MOVE RIGHT ARG TO PROPER REG
       BRN  OCNC1            MERGE BACK TO CONCATENATE STRINGS
       EJC
*
*      CONCATENATION (CONTINUED)
*
*      COME HERE FOR NULL RIGHT ARGUMENT
*
OCNC3  ICA  XS               REMOVE RIGHT ARG FROM STACK
       LCW  XR               LEFT ARGUMENT ON STACK
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      HERE FOR NULL LEFT ARGUMENT
*
OCNC4  ICA  XS               UNSTACK ONE ARGUMENT
       MOV  XR,(XS)          STORE RIGHT ARGUMENT
       LCW  XR               RESULT ON STACK, GET CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      HERE IF RIGHT ARGUMENT IS NOT A STRING
*
OCNC5  MOV  XR,XL            MOVE RIGHT ARGUMENT PTR
       MOV  (XS)+,XR         LOAD LEFT ARG POINTER
*
*      MERGE HERE WHEN LEFT ARGUMENT IS NOT A STRING
*
OCNC6  JSR  GTPAT            CONVERT LEFT ARG TO PATTERN
       ERR  008,Concatenation left operand is not a string or pattern
       MOV  XR,-(XS)         SAVE RESULT ON STACK
       MOV  XL,XR            POINT TO RIGHT OPERAND
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  009,Concatenation right operand is not a string or pattern
       MOV  XR,XL            MOVE FOR PCONC
       MOV  (XS)+,XR         RELOAD LEFT OPERAND PTR
       JSR  PCONC            CONCATENATE PATTERNS
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      COMPLEMENTATION
*
O_COM  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD OPERAND
       MOV  (XR),WA          LOAD TYPE WORD
*
*      MERGE BACK HERE AFTER CONVERSION
*
OCOM1  BEQ  WA,=B_ICL,OCOM2  JUMP IF INTEGER
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,OCOM3  JUMP IF REAL
.FI
       JSR  GTNUM            ELSE CONVERT TO NUMERIC
       ERR  010,Negation operand is not numeric
       BRN  OCOM1            BACK TO CHECK CASES
*
*      HERE TO COMPLEMENT INTEGER
*
OCOM2  LDI  ICVAL(XR)        LOAD INTEGER VALUE
       NGI                   NEGATE
       INO  EXINT            RETURN INTEGER IF NO OVERFLOW
       ERB  011,Negation caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO COMPLEMENT REAL
*
OCOM3  LDR  RCVAL(XR)        LOAD REAL VALUE
       NGR                   NEGATE
       BRN  EXREA            RETURN REAL RESULT
.FI
       EJC
*
*      BINARY SLASH (DIVISION)
*
O_DVD  ENT                   ENTRY POINT
       JSR  ARITH            FETCH ARITHMETIC OPERANDS
       ERR  012,Division left operand is not numeric
       ERR  013,Division right operand is not numeric
.IF    .CNRA
.ELSE
       PPM  ODVD2            JUMP IF REAL OPERANDS
.FI
*
*      HERE TO DIVIDE TWO INTEGERS
*
       DVI  ICVAL(XL)        DIVIDE LEFT OPERAND BY RIGHT
       INO  EXINT            RESULT OK IF NO OVERFLOW
       ERB  014,Division caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO DIVIDE TWO REALS
*
ODVD2  DVR  RCVAL(XL)        DIVIDE LEFT OPERAND BY RIGHT
       RNO  EXREA            RETURN REAL IF NO OVERFLOW
       ERB  262,Division caused real overflow
.FI
       EJC
*
*      EXPONENTIATION
*
O_EXP  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD EXPONENT
       JSR  GTNUM            CONVERT TO NUMBER
       ERR  015,Exponentiation right operand is not numeric
       MOV  XR,XL            MOVE EXPONENT TO XL
       MOV  (XS)+,XR         LOAD BASE
       JSR  GTNUM            CONVERT TO NUMERIC
       ERR  016,Exponentiation left operand is not numeric
.IF    .CNRA
.ELSE
       BEQ  (XL),=B_RCL,OEXP7 JUMP IF REAL EXPONENT
.FI
       LDI  ICVAL(XL)        LOAD EXPONENT
       ILT  OEX12            JUMP IF NEGATIVE EXPONENT
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,OEXP3  JUMP IF BASE IS REAL
.FI
*
*      HERE TO EXPONENTIATE AN INTEGER BASE AND INTEGER EXPONENT
*
       MFI  WA,OEXP2         CONVERT EXPONENT TO 1 WORD INTEGER
       LCT  WA,WA            SET LOOP COUNTER
       LDI  ICVAL(XR)        LOAD BASE AS INITIAL VALUE
       BNZ  WA,OEXP1         JUMP INTO LOOP IF NON-ZERO EXPONENT
       IEQ  OEXP4            ERROR IF 0**0
       LDI  INTV1            NONZERO**0
       BRN  EXINT            GIVE ONE AS RESULT FOR NONZERO**0
*
*      LOOP TO PERFORM EXPONENTIATION
*
OEX13  MLI  ICVAL(XR)        MULTIPLY BY BASE
       IOV  OEXP2            JUMP IF OVERFLOW
OEXP1  BCT  WA,OEX13         LOOP IF MORE TO GO
       BRN  EXINT            ELSE RETURN INTEGER RESULT
*
*      HERE IF INTEGER OVERFLOW
*
OEXP2  ERB  017,Exponentiation caused integer overflow
       EJC
*
*      EXPONENTIATION (CONTINUED)
.IF    .CNRA
.ELSE
*
*      HERE TO EXPONENTIATE A REAL TO AN INTEGER POWER
*
OEXP3  MFI  WA,OEXP6         CONVERT EXPONENT TO ONE WORD
       LCT  WA,WA            SET LOOP COUNTER
       LDR  RCVAL(XR)        LOAD BASE AS INITIAL VALUE
       BNZ  WA,OEXP5         JUMP INTO LOOP IF NON-ZERO EXPONENT
       REQ  OEXP4            ERROR IF 0.0**0
       LDR  REAV1            NONZERO**0
       BRN  EXREA            RETURN 1.0 IF NONZERO**ZERO
.FI
*
*      HERE FOR ERROR OF 0**0 OR 0.0**0
*
OEXP4  ERB  018,Exponentiation result is undefined
.IF    .CNRA
.ELSE
*
*      LOOP TO PERFORM EXPONENTIATION
*
OEX14  MLR  RCVAL(XR)        MULTIPLY BY BASE
       ROV  OEXP6            JUMP IF OVERFLOW
OEXP5  BCT  WA,OEX14         LOOP TILL COMPUTATION COMPLETE
       BRN  EXREA            THEN RETURN REAL RESULT
*
*      HERE IF REAL OVERFLOW
*
OEXP6  ERB  266,Exponentiation caused real overflow
*
*      HERE WITH REAL EXPONENT IN (XL), NUMERIC BASE IN (XR)
*
.IF    .CMTH
OEXP7  BEQ  (XR),=B_RCL,OEXP8 JUMP IF BASE REAL
       LDI  ICVAL(XR)        LOAD INTEGER BASE
       ITR                   CONVERT TO REAL
       JSR  RCBLD            CREATE REAL IN (XR)
*
*      HERE WITH REAL EXPONENT IN (XL)
*      NUMERIC BASE IN (XR) AND RA
*
OEXP8  ZER  WB               SET POSITIVE RESULT FLAG
       LDR  RCVAL(XR)        LOAD BASE TO RA
       RNE  OEXP9            JUMP IF BASE NON-ZERO
       LDR  RCVAL(XL)        BASE IS ZERO.  CHECK EXPONENT
       REQ  OEXP4            JUMP IF 0.0 ** 0.0
       LDR  REAV0            0.0 TO NON-ZERO EXPONENT YIELDS 0.0
       BRN  EXREA            RETURN ZERO RESULT
*
*      HERE WITH NON-ZERO BASE IN (XR) AND RA, EXPONENT IN (XL)
*
*      A NEGATIVE BASE IS ALLOWED IF THE EXPONENT IS INTEGRAL.
*
OEXP9  RGT  OEX10            JUMP IF BASE GT 0.0
       NGR                   MAKE BASE POSITIVE
       JSR  RCBLD            CREATE POSITIVE BASE IN (XR)
       LDR  RCVAL(XL)        EXAMINE EXPONENT
       CHP                   CHOP TO INTEGRAL VALUE
       RTI  OEXP6            CONVERT TO INTEGER, BR IF TOO LARGE
       SBR  RCVAL(XL)        CHOP(EXPONENT) - EXPONENT
       RNE  OEX11            NON-INTEGRAL POWER WITH NEG BASE
       MFI  WB               RECORD EVEN/ODD EXPONENT
       ANB  BITS1,WB         ODD EXPONENT YIELDS NEGATIVE RESULT
       LDR  RCVAL(XR)        RESTORE BASE TO RA
*
*      HERE WITH POSITIVE BASE IN RA AND (XR), EXPONENT IN (XL)
*
OEX10  LNF                   LOG OF BASE
       ROV  OEXP6            TOO LARGE
       MLR  RCVAL(XL)        TIMES EXPONENT
       ROV  OEXP6            TOO LARGE
       ETX                   E ** (EXPONENT * LN(BASE))
       ROV  OEXP6            TOO LARGE
       BZE  WB,EXREA         IF NO SIGN FIXUP REQUIRED
       NGR                   NEGATIVE RESULT NEEDED
       BRN  EXREA
*
*      HERE FOR NON-INTEGRAL EXPONENT WITH NEGATIVE BASE
*
OEX11  ERB  311,Exponentiation of negative base to non-integral power
.ELSE
OEXP7  ERB  267,Exponentiation right operand is real not integer
.FI
.FI
*
*      HERE WITH NEGATIVE INTEGER EXPONENT IN IA
*
.IF    .CMTH
OEX12  MOV  XR,-(XS)         STACK BASE
       ITR                   CONVERT TO REAL EXPONENT
       JSR  RCBLD            REAL NEGATIVE EXPONENT IN (XR)
       MOV  XR,XL            PUT EXPONENT IN XL
       MOV  (XS)+,XR         RESTORE BASE VALUE
       BRN  OEXP7            PROCESS REAL EXPONENT
.ELSE
OEX12  ERB  019,Exponentiation right operand is negative
.FI
       EJC
*
*      FAILURE IN EXPRESSION EVALUATION
*
*      THIS ENTRY POINT IS USED IF THE EVALUATION OF AN
*      EXPRESSION, INITIATED BY THE EVALX PROCEDURE, FAILS.
*      CONTROL IS RETURNED TO AN APPROPRIATE POINT IN EVALX.
*
O_FEX  ENT                   ENTRY POINT
       BRN  EVLX6            JUMP TO FAILURE LOC IN EVALX
       EJC
*
*      FAILURE DURING EVALUATION OF A COMPLEX OR DIRECT GOTO
*
O_FIF  ENT                   ENTRY POINT
       ERB  020,Goto evaluation failure
       EJC
*
*      FUNCTION CALL (MORE THAN ONE ARGUMENT)
*
O_FNC  ENT                   ENTRY POINT
       LCW  WA               LOAD NUMBER OF ARGUMENTS
       LCW  XR               LOAD FUNCTION VRBLK POINTER
       MOV  VRFNC(XR),XL     LOAD FUNCTION POINTER
       BNE  WA,FARGS(XL),CFUNC USE CENTRAL ROUTINE IF WRONG NUM
       BRI  (XL)             JUMP TO FUNCTION IF ARG COUNT OK
       EJC
*
*      FUNCTION NAME ERROR
*
O_FNE  ENT                   ENTRY POINT
       LCW  WA               GET NEXT CODE WORD
       BNE  WA,=ORNM_,OFNE1  FAIL IF NOT EVALUATING EXPRESSION
       BZE  NUM02(XS),EVLX3  OK IF EXPR. WAS WANTED BY VALUE
*
*      HERE FOR ERROR
*
OFNE1  ERB  021,Function called by name returned a value
       EJC
*
*      FUNCTION CALL (SINGLE ARGUMENT)
*
O_FNS  ENT                   ENTRY POINT
       LCW  XR               LOAD FUNCTION VRBLK POINTER
       MOV  =NUM01,WA        SET NUMBER OF ARGUMENTS TO ONE
       MOV  VRFNC(XR),XL     LOAD FUNCTION POINTER
       BNE  WA,FARGS(XL),CFUNC USE CENTRAL ROUTINE IF WRONG NUM
       BRI  (XL)             JUMP TO FUNCTION IF ARG COUNT OK
       EJC
*      CALL TO UNDEFINED FUNCTION
*
O_FUN  ENT                   ENTRY POINT
       ERB  022,Undefined function called
       EJC
*
*      EXECUTE COMPLEX GOTO
*
O_GOC  ENT                   ENTRY POINT
       MOV  NUM01(XS),XR     LOAD NAME BASE POINTER
       BHI  XR,STATE,OGOC1   JUMP IF NOT NATURAL VARIABLE
       ADD  *VRTRA,XR        ELSE POINT TO VRTRA FIELD
       BRI  (XR)             AND JUMP THROUGH IT
*
*      HERE IF GOTO OPERAND IS NOT NATURAL VARIABLE
*
OGOC1  ERB  023,Goto operand is not a natural variable
       EJC
*
*      EXECUTE DIRECT GOTO
*
O_GOD  ENT                   ENTRY POINT
       MOV  (XS),XR          LOAD OPERAND
       MOV  (XR),WA          LOAD FIRST WORD
       BEQ  WA,=B_CDS,BCDS0  JUMP IF CODE BLOCK TO CODE ROUTINE
       BEQ  WA,=B_CDC,BCDC0  JUMP IF CODE BLOCK TO CODE ROUTINE
       ERB  024,Goto operand in direct goto is not code
       EJC
*
*      SET GOTO FAILURE TRAP
*
*      THIS ROUTINE IS EXECUTED AT THE START OF A COMPLEX OR
*      DIRECT FAILURE GOTO TO TRAP A SUBSEQUENT FAIL (SEE EXFAL)
*
O_GOF  ENT                   ENTRY POINT
       MOV  FLPTR,XR         POINT TO FAIL OFFSET ON STACK
       ICA  (XR)             POINT FAILURE TO O_FIF WORD
       ICP                   POINT TO NEXT CODE WORD
       LCW  XR               FETCH NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      BINARY DOLLAR (IMMEDIATE ASSIGNMENT)
*
*      THE PATTERN BUILT BY BINARY DOLLAR IS A COMPOUND PATTERN.
*      SEE DESCRIPTION AT START OF PATTERN MATCH SECTION FOR
*      DETAILS OF THE STRUCTURE WHICH IS CONSTRUCTED.
*
O_IMA  ENT                   ENTRY POINT
       MOV  =P_IMC,WB        SET PCODE FOR LAST NODE
       MOV  (XS)+,WC         POP NAME OFFSET (PARM2)
       MOV  (XS)+,XR         POP NAME BASE (PARM1)
       JSR  PBILD            BUILD P_IMC NODE
       MOV  XR,XL            SAVE PTR TO NODE
       MOV  (XS),XR          LOAD LEFT ARGUMENT
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  025,Immediate assignment left operand is not pattern
       MOV  XR,(XS)          SAVE PTR TO LEFT OPERAND PATTERN
       MOV  =P_IMA,WB        SET PCODE FOR FIRST NODE
       JSR  PBILD            BUILD P_IMA NODE
       MOV  (XS)+,PTHEN(XR)  SET LEFT OPERAND AS P_IMA SUCCESSOR
       JSR  PCONC            CONCATENATE TO FORM FINAL PATTERN
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      INDIRECTION (BY NAME)
*
O_INN  ENT                   ENTRY POINT
       MNZ  WB               SET FLAG FOR RESULT BY NAME
       BRN  INDIR            JUMP TO COMMON ROUTINE
       EJC
*
*      INTERROGATION
*
O_INT  ENT                   ENTRY POINT
       MOV  =NULLS,(XS)      REPLACE OPERAND WITH NULL
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      INDIRECTION (BY VALUE)
*
O_INV  ENT                   ENTRY POINT
       ZER  WB               SET FLAG FOR BY VALUE
       BRN  INDIR            JUMP TO COMMON ROUTINE
       EJC
*
*      KEYWORD REFERENCE (BY NAME)
*
O_KWN  ENT                   ENTRY POINT
       JSR  KWNAM            GET KEYWORD NAME
       BRN  EXNAM            EXIT WITH RESULT NAME
       EJC
*
*      KEYWORD REFERENCE (BY VALUE)
*
O_KWV  ENT                   ENTRY POINT
       JSR  KWNAM            GET KEYWORD NAME
       MOV  XR,DNAMP         DELETE KVBLK
       JSR  ACESS            ACCESS VALUE
       PPM  EXNUL            DUMMY (UNUSED) FAILURE RETURN
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      LOAD EXPRESSION BY NAME
*
O_LEX  ENT                   ENTRY POINT
       MOV  *EVSI_,WA        SET SIZE OF EVBLK
       JSR  ALLOC            ALLOCATE SPACE FOR EVBLK
       MOV  =B_EVT,(XR)      SET TYPE WORD
       MOV  =TRBEV,EVVAR(XR) SET DUMMY TRBLK POINTER
       LCW  WA               LOAD EXBLK POINTER
       MOV  WA,EVEXP(XR)     SET EXBLK POINTER
       MOV  XR,XL            MOVE NAME BASE TO PROPER REG
       MOV  *EVVAR,WA        SET NAME OFFSET = ZERO
       BRN  EXNAM            EXIT WITH NAME IN (XL,WA)
       EJC
*
*      LOAD PATTERN VALUE
*
O_LPT  ENT                   ENTRY POINT
       LCW  XR               LOAD PATTERN POINTER
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      LOAD VARIABLE NAME
*
O_LVN  ENT                   ENTRY POINT
       LCW  WA               LOAD VRBLK POINTER
       MOV  WA,-(XS)         STACK VRBLK PTR (NAME BASE)
       MOV  *VRVAL,-(XS)     STACK NAME OFFSET
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      BINARY ASTERISK (MULTIPLICATION)
*
O_MLT  ENT                   ENTRY POINT
       JSR  ARITH            FETCH ARITHMETIC OPERANDS
       ERR  026,Multiplication left operand is not numeric
       ERR  027,Multiplication right operand is not numeric
.IF    .CNRA
.ELSE
       PPM  OMLT1            JUMP IF REAL OPERANDS
.FI
*
*      HERE TO MULTIPLY TWO INTEGERS
*
       MLI  ICVAL(XL)        MULTIPLY LEFT OPERAND BY RIGHT
       INO  EXINT            RETURN INTEGER IF NO OVERFLOW
       ERB  028,Multiplication caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO MULTIPLY TWO REALS
*
OMLT1  MLR  RCVAL(XL)        MULTIPLY LEFT OPERAND BY RIGHT
       RNO  EXREA            RETURN REAL IF NO OVERFLOW
       ERB  263,Multiplication caused real overflow
.FI
       EJC
*
*      NAME REFERENCE
*
O_NAM  ENT                   ENTRY POINT
       MOV  *NMSI_,WA        SET LENGTH OF NMBLK
       JSR  ALLOC            ALLOCATE NMBLK
       MOV  =B_NML,(XR)      SET NAME BLOCK CODE
       MOV  (XS)+,NMOFS(XR)  SET NAME OFFSET FROM OPERAND
       MOV  (XS)+,NMBAS(XR)  SET NAME BASE FROM OPERAND
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      NEGATION
*
*      INITIAL ENTRY
*
O_NTA  ENT                   ENTRY POINT
       LCW  WA               LOAD NEW FAILURE OFFSET
       MOV  FLPTR,-(XS)      STACK OLD FAILURE POINTER
       MOV  WA,-(XS)         STACK NEW FAILURE OFFSET
       MOV  XS,FLPTR         SET NEW FAILURE POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      ENTRY AFTER SUCCESSFUL EVALUATION OF OPERAND
*
O_NTB  ENT                   ENTRY POINT
       MOV  NUM02(XS),FLPTR  RESTORE OLD FAILURE POINTER
       BRN  EXFAL            AND FAIL
*
*      ENTRY FOR FAILURE DURING OPERAND EVALUATION
*
O_NTC  ENT                   ENTRY POINT
       ICA  XS               POP FAILURE OFFSET
       MOV  (XS)+,FLPTR      RESTORE OLD FAILURE POINTER
       BRN  EXNUL            EXIT GIVING NULL RESULT
       EJC
*
*      USE OF UNDEFINED OPERATOR
*
O_OUN  ENT                   ENTRY POINT
       ERB  029,Undefined operator referenced
       EJC
*
*      BINARY DOT (PATTERN ASSIGNMENT)
*
*      THE PATTERN BUILT BY BINARY DOT IS A COMPOUND PATTERN.
*      SEE DESCRIPTION AT START OF PATTERN MATCH SECTION FOR
*      DETAILS OF THE STRUCTURE WHICH IS CONSTRUCTED.
*
O_PAS  ENT                   ENTRY POINT
       MOV  =P_PAC,WB        LOAD PCODE FOR P_PAC NODE
       MOV  (XS)+,WC         LOAD NAME OFFSET (PARM2)
       MOV  (XS)+,XR         LOAD NAME BASE (PARM1)
       JSR  PBILD            BUILD P_PAC NODE
       MOV  XR,XL            SAVE PTR TO NODE
       MOV  (XS),XR          LOAD LEFT OPERAND
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  030,Pattern assignment left operand is not pattern
       MOV  XR,(XS)          SAVE PTR TO LEFT OPERAND PATTERN
       MOV  =P_PAA,WB        SET PCODE FOR P_PAA NODE
       JSR  PBILD            BUILD P_PAA NODE
       MOV  (XS)+,PTHEN(XR)  SET LEFT OPERAND AS P_PAA SUCCESSOR
       JSR  PCONC            CONCATENATE TO FORM FINAL PATTERN
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      PATTERN MATCH (BY NAME, FOR REPLACEMENT)
*
O_PMN  ENT                   ENTRY POINT
       ZER  WB               SET TYPE CODE FOR MATCH BY NAME
       BRN  MATCH            JUMP TO ROUTINE TO START MATCH
       EJC
*
*      PATTERN MATCH (STATEMENT)
*
*      O_PMS IS USED IN PLACE OF O_PMV WHEN THE PATTERN MATCH
*      OCCURS AT THE OUTER (STATEMENT) LEVEL SINCE IN THIS
*      CASE THE SUBSTRING VALUE NEED NOT BE CONSTRUCTED.
*
O_PMS  ENT                   ENTRY POINT
       MOV  =NUM02,WB        SET FLAG FOR STATEMENT TO MATCH
       BRN  MATCH            JUMP TO ROUTINE TO START MATCH
       EJC
*
*      PATTERN MATCH (BY VALUE)
*
O_PMV  ENT                   ENTRY POINT
       MOV  =NUM01,WB        SET TYPE CODE FOR VALUE MATCH
       BRN  MATCH            JUMP TO ROUTINE TO START MATCH
       EJC
*
*      POP TOP ITEM ON STACK
*
O_POP  ENT                   ENTRY POINT
       ICA  XS               POP TOP STACK ENTRY
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      TERMINATE EXECUTION (CODE COMPILED FOR END STATEMENT)
*
O_STP  ENT                   ENTRY POINT
       BRN  LEND0            JUMP TO END CIRCUIT
       EJC
*
*      RETURN NAME FROM EXPRESSION
*      THIS ENTRY POINTS IS USED IF THE EVALUATION OF AN
*      EXPRESSION, INITIATED BY THE EVALX PROCEDURE, RETURNS
*      A NAME. CONTROL IS RETURNED TO THE PROPER POINT IN EVALX.
*
O_RNM  ENT                   ENTRY POINT
       BRN  EVLX4            RETURN TO EVALX PROCEDURE
       EJC
*
*      PATTERN REPLACEMENT
*
*      WHEN THIS ROUTINE GETS CONTROL, THE FOLLOWING STACK
*      ENTRIES HAVE BEEN MADE (SEE END OF MATCH ROUTINE P_NTH)
*
*                            SUBJECT NAME BASE
*                            SUBJECT NAME OFFSET
*                            INITIAL CURSOR VALUE
*                            FINAL CURSOR VALUE
*                            SUBJECT STRING POINTER
*      (XS) ---------------- REPLACEMENT VALUE
*
O_RPL  ENT                   ENTRY POINT
       JSR  GTSTG            CONVERT REPLACEMENT VAL TO STRING
       ERR  031,Pattern replacement right operand is not a string
*
*      GET RESULT LENGTH AND ALLOCATE RESULT SCBLK
*
       MOV  (XS),XL          LOAD SUBJECT STRING POINTER
.IF    .CNBF
.ELSE
       BEQ  (XL),=B_BCT,ORPL4 BRANCH IF BUFFER ASSIGNMENT
.FI
       ADD  SCLEN(XL),WA     ADD SUBJECT STRING LENGTH
       ADD  NUM02(XS),WA     ADD STARTING CURSOR
       SUB  NUM01(XS),WA     MINUS FINAL CURSOR = TOTAL LENGTH
       BZE  WA,ORPL3         JUMP IF RESULT IS NULL
       MOV  XR,-(XS)         RESTACK REPLACEMENT STRING
       JSR  ALOCS            ALLOCATE SCBLK FOR RESULT
       MOV  NUM03(XS),WA     GET INITIAL CURSOR (PART 1 LEN)
       MOV  XR,NUM03(XS)     STACK RESULT POINTER
       PSC  XR               POINT TO CHARACTERS OF RESULT
*
*      MOVE PART 1 (START OF SUBJECT) TO RESULT
*
       BZE  WA,ORPL1         JUMP IF FIRST PART IS NULL
       MOV  NUM01(XS),XL     ELSE POINT TO SUBJECT STRING
       PLC  XL               POINT TO SUBJECT STRING CHARS
       MVC                   MOVE FIRST PART TO RESULT
       EJC
*      PATTERN REPLACEMENT (CONTINUED)
*
*      NOW MOVE IN REPLACEMENT VALUE
*
ORPL1  MOV  (XS)+,XL         LOAD REPLACEMENT STRING, POP
       MOV  SCLEN(XL),WA     LOAD LENGTH
       BZE  WA,ORPL2         JUMP IF NULL REPLACEMENT
       PLC  XL               ELSE POINT TO CHARS OF REPLACEMENT
       MVC                   MOVE IN CHARS (PART 2)
*
*      NOW MOVE IN REMAINDER OF STRING (PART 3)
*
ORPL2  MOV  (XS)+,XL         LOAD SUBJECT STRING POINTER, POP
       MOV  (XS)+,WC         LOAD FINAL CURSOR, POP
       MOV  SCLEN(XL),WA     LOAD SUBJECT STRING LENGTH
       SUB  WC,WA            MINUS FINAL CURSOR = PART 3 LENGTH
       BZE  WA,OASS0         JUMP TO ASSIGN IF PART 3 IS NULL
       PLC  XL,WC            ELSE POINT TO LAST PART OF STRING
       MVC                   MOVE PART 3 TO RESULT
       BRN  OASS0            JUMP TO PERFORM ASSIGNMENT
*
*      HERE IF RESULT IS NULL
*
ORPL3  ADD  *NUM02,XS        POP SUBJECT STR PTR, FINAL CURSOR
       MOV  =NULLS,(XS)      SET NULL RESULT
       BRN  OASS0            JUMP TO ASSIGN NULL VALUE
.IF    .CNBF
.ELSE
*
*      HERE FOR BUFFER SUBSTRING ASSIGNMENT
*
ORPL4  MOV  XR,XL            COPY SCBLK REPLACEMENT PTR
       MOV  (XS)+,XR         UNSTACK BCBLK PTR
       MOV  (XS)+,WB         GET FINAL CURSOR VALUE
       MOV  (XS)+,WA         GET INITIAL CURSOR
       SUB  WA,WB            GET LENGTH IN WB
       ADD  *NUM01,XS        GET RID OF NAME OFFSET
       MOV  XR,(XS)          STORE BUFFER RESULT OVER NAME BASE
       JSR  INSBF            INSERT SUBSTRING
       PPM                   CONVERT FAIL IMPOSSIBLE
       PPM  EXFAL            FAIL IF INSERT FAILS
       LCW  XR               RESULT ON STACK, GET CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
.FI
       EJC
*
*      RETURN VALUE FROM EXPRESSION
*
*      THIS ENTRY POINTS IS USED IF THE EVALUATION OF AN
*      EXPRESSION, INITIATED BY THE EVALX PROCEDURE, RETURNS
*      A VALUE. CONTROL IS RETURNED TO THE PROPER POINT IN EVALX
*
O_RVL  ENT                   ENTRY POINT
       BRN  EVLX3            RETURN TO EVALX PROCEDURE
       EJC
*
*      SELECTION
*
*      INITIAL ENTRY
*
O_SLA  ENT                   ENTRY POINT
       LCW  WA               LOAD NEW FAILURE OFFSET
       MOV  FLPTR,-(XS)      STACK OLD FAILURE POINTER
       MOV  WA,-(XS)         STACK NEW FAILURE OFFSET
       MOV  XS,FLPTR         SET NEW FAILURE POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      ENTRY AFTER SUCCESSFUL EVALUATION OF ALTERNATIVE
*
O_SLB  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD RESULT
       ICA  XS               POP FAIL OFFSET
       MOV  (XS),FLPTR       RESTORE OLD FAILURE POINTER
       MOV  XR,(XS)          RESTACK RESULT
       LCW  WA               LOAD NEW CODE OFFSET
       ADD  R_COD,WA         POINT TO ABSOLUTE CODE LOCATION
       LCP  WA               SET NEW CODE POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      ENTRY AT START OF SUBSEQUENT ALTERNATIVES
*
O_SLC  ENT                   ENTRY POINT
       LCW  WA               LOAD NEW FAIL OFFSET
       MOV  WA,(XS)          STORE NEW FAIL OFFSET
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      ENTRY AT START OF LAST ALTERNATIVE
*
O_SLD  ENT                   ENTRY POINT
       ICA  XS               POP FAILURE OFFSET
       MOV  (XS)+,FLPTR      RESTORE OLD FAILURE POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      BINARY MINUS (SUBTRACTION)
*
O_SUB  ENT                   ENTRY POINT
       JSR  ARITH            FETCH ARITHMETIC OPERANDS
       ERR  032,Subtraction left operand is not numeric
       ERR  033,Subtraction right operand is not numeric
.IF    .CNRA
.ELSE
       PPM  OSUB1            JUMP IF REAL OPERANDS
.FI
*
*      HERE TO SUBTRACT TWO INTEGERS
*
       SBI  ICVAL(XL)        SUBTRACT RIGHT OPERAND FROM LEFT
       INO  EXINT            RETURN INTEGER IF NO OVERFLOW
       ERB  034,Subtraction caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO SUBTRACT TWO REALS
*
OSUB1  SBR  RCVAL(XL)        SUBTRACT RIGHT OPERAND FROM LEFT
       RNO  EXREA            RETURN REAL IF NO OVERFLOW
       ERB  264,Subtraction caused real overflow
.FI
       EJC
*
*      DUMMY OPERATOR TO RETURN CONTROL TO TRXEQ PROCEDURE
*
O_TXR  ENT                   ENTRY POINT
       BRN  TRXQ1            JUMP INTO TRXEQ PROCEDURE
       EJC
*
*      UNEXPECTED FAILURE
*
*      NOTE THAT IF A SETEXIT TRAP IS OPERATING THEN
*      TRANSFER TO SYSTEM LABEL CONTINUE
*      WILL RESULT IN LOOPING HERE.  DIFFICULT TO AVOID EXCEPT
*      WITH A CONSIDERABLE OVERHEAD WHICH IS NOT WORTHWHILE OR
*      ELSE BY A TECHNIQUE SUCH AS SETTING KVERL TO ZERO.
*
O_UNF  ENT                   ENTRY POINT
       ERB  035,Unexpected failure in -NOFAIL mode
       TTL  S P I T B O L -- BLOCK ACTION ROUTINES
*
*      THE FIRST WORD OF EVERY BLOCK IN DYNAMIC STORAGE AND THE
*      VRGET, VRSTO AND VRTRA FIELDS OF A VRBLK CONTAIN A
*      POINTER TO AN ENTRY POINT IN THE PROGRAM. ALL SUCH ENTRY
*      POINTS ARE IN THE FOLLOWING SECTION EXCEPT THOSE FOR
*      PATTERN BLOCKS WHICH ARE IN THE PATTERN MATCHING SEGMENT
*      LATER ON (LABELS OF THE FORM P_XXX), AND DOPE VECTORS
*      (D_XXX) WHICH ARE IN THE DOPE VECTOR SECTION FOLLOWING
*      THE PATTERN ROUTINES (DOPE VECTORS ARE USED FOR CMBLKS).
*
*      THE ENTRY POINTS IN THIS SECTION HAVE LABELS OF THE
*      FORM B_XXY WHERE XX IS THE TWO CHARACTER BLOCK TYPE FOR
*      THE CORRESPONDING BLOCK AND Y IS ANY LETTER.
*
*      IN SOME CASES, THE POINTERS SERVE NO OTHER PURPOSE THAN
*      TO IDENTIFY THE BLOCK TYPE. IN THIS CASE THE ROUTINE
*      IS NEVER EXECUTED AND THUS NO CODE IS ASSEMBLED.
*
*      FOR EACH OF THESE ENTRY POINTS CORRESPONDING TO A BLOCK
*      AN ENTRY POINT IDENTIFICATION IS ASSEMBLED (BL_XX).
*
*      THE EXACT ENTRY CONDITIONS DEPEND ON THE MANNER IN
*      WHICH THE ROUTINE IS ACCESSED AND ARE DOCUMENTED WITH
*      THE INDIVIDUAL ROUTINES AS REQUIRED.
*
*      THE ORDER OF THESE ROUTINES IS ALPHABETICAL WITH THE
*      FOLLOWING EXCEPTIONS.
*
*      THE ROUTINES FOR SEBLK AND EXBLK ENTRIES OCCUR FIRST SO
*      THAT EXPRESSIONS CAN BE QUICKLY IDENTIFIED FROM THE FACT
*      THAT THEIR ROUTINES LIE BEFORE THE SYMBOL B_E__.
*
*      THESE ARE IMMEDIATELY FOLLOWED BY THE ROUTINE FOR A TRBLK
*      SO THAT THE TEST AGAINST THE SYMBOL B_T__ CHECKS FOR
*      TRAPPED VALUES OR EXPRESSION VALUES (SEE PROCEDURE EVALP)
*
*      THE PATTERN ROUTINES LIE AFTER THIS SECTION SO THAT
*      PATTERNS ARE IDENTIFIED WITH ROUTINES STARTING AT OR
*      AFTER THE INITIAL INSTRUCTION IN THESE ROUTINES (P_AAA).
*
*      THE SYMBOL B_AAA DEFINES THE FIRST LOCATION FOR BLOCK
*      ROUTINES AND THE SYMBOL P_YYY (AT THE END OF THE PATTERN
*      MATCH ROUTINES SECTION) DEFINES THE LAST SUCH ENTRY POINT
*
B_AAA  ENT  BL__I            ENTRY POINT OF FIRST BLOCK ROUTINE
       EJC
*
*      EXBLK
*
*      THE ROUTINE FOR AN EXBLK LOADS THE EXPRESSION ONTO
*      THE STACK AS A VALUE.
*
*      (XR)                  POINTER TO EXBLK
*
B_EXL  ENT  BL_EX            ENTRY POINT (EXBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      SEBLK
*
*      THE ROUTINE FOR SEBLK IS ACCESSED FROM THE GENERATED
*      CODE TO LOAD THE EXPRESSION VALUE ONTO THE STACK.
*
B_SEL  ENT  BL_SE            ENTRY POINT (SEBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      DEFINE SYMBOL WHICH MARKS END OF ENTRIES FOR EXPRESSIONS
*
B_E__  ENT  BL__I            ENTRY POINT
       EJC
*
*      TRBLK
*
*      THE ROUTINE FOR A TRBLK IS NEVER EXECUTED
*
B_TRT  ENT  BL_TR            ENTRY POINT (TRBLK)
*
*      DEFINE SYMBOL MARKING END OF TRAP AND EXPRESSION BLOCKS
*
B_T__  ENT  BL__I            END OF TRBLK,SEBLK,EXBLK ENTRIES
       EJC
*
*      ARBLK
*
*      THE ROUTINE FOR ARBLK IS NEVER EXECUTED
*
B_ART  ENT  BL_AR            ENTRY POINT (ARBLK)
       EJC
*
*      BCBLK
*
*      THE ROUTINE FOR A BCBLK IS NEVER EXECUTED
*
*      (XR)                  POINTER TO BCBLK
*
B_BCT  ENT  BL_BC            ENTRY POINT (BCBLK)
       EJC
*
*      BFBLK
*
*      THE ROUTINE FOR A BFBLK IS NEVER EXECUTED
*
*      (XR)                  POINTER TO BFBLK
*
B_BFT  ENT  BL_BF            ENTRY POINT (BFBLK)
       EJC
*
*      CCBLK
*
*      THE ROUTINE FOR CCBLK IS NEVER ENTERED
*
B_CCT  ENT  BL_CC            ENTRY POINT (CCBLK)
       EJC
*
*      CDBLK
*
*      THE CDBLK ROUTINES ARE EXECUTED FROM THE GENERATED CODE.
*      THERE ARE TWO CASES DEPENDING ON THE FORM OF CDFAL.
*
*      ENTRY FOR COMPLEX FAILURE CODE AT CDFAL
*
*      (XR)                  POINTER TO CDBLK
*
B_CDC  ENT  BL_CD            ENTRY POINT (CDBLK)
BCDC0  MOV  FLPTR,XS         POP GARBAGE OFF STACK
       MOV  CDFAL(XR),(XS)   SET FAILURE OFFSET
       BRN  STMGO            ENTER STMT
       EJC
*
*      CDBLK (CONTINUED)
*
*      ENTRY FOR SIMPLE FAILURE CODE AT CDFAL
*
*      (XR)                  POINTER TO CDBLK
*
B_CDS  ENT  BL_CD            ENTRY POINT (CDBLK)
BCDS0  MOV  FLPTR,XS         POP GARBAGE OFF STACK
       MOV  *CDFAL,(XS)      SET FAILURE OFFSET
       BRN  STMGO            ENTER STMT
       EJC
*
*      CMBLK
*
*      THE ROUTINE FOR A CMBLK IS NEVER EXECUTED
*
B_CMT  ENT  BL_CM            ENTRY POINT (CMBLK)
       EJC
*
*      CTBLK
*
*      THE ROUTINE FOR A CTBLK IS NEVER EXECUTED
*
B_CTT  ENT  BL_CT            ENTRY POINT (CTBLK)
       EJC
*
*      DFBLK
*
*      THE ROUTINE FOR A DFBLK IS ACCESSED FROM THE O_FNC ENTRY
*      TO CALL A DATATYPE FUNCTION AND BUILD A PDBLK.
*
*      (XL)                  POINTER TO DFBLK
*
B_DFC  ENT  BL_DF            ENTRY POINT
       MOV  DFPDL(XL),WA     LOAD LENGTH OF PDBLK
       JSR  ALLOC            ALLOCATE PDBLK
       MOV  =B_PDT,(XR)      STORE TYPE WORD
       MOV  XL,PDDFP(XR)     STORE DFBLK POINTER
       MOV  XR,WC            SAVE POINTER TO PDBLK
       ADD  WA,XR            POINT PAST PDBLK
       LCT  WA,FARGS(XL)     SET TO COUNT FIELDS
*
*      LOOP TO ACQUIRE FIELD VALUES FROM STACK
*
BDFC1  MOV  (XS)+,-(XR)      MOVE A FIELD VALUE
       BCT  WA,BDFC1         LOOP TILL ALL MOVED
       MOV  WC,XR            RECALL POINTER TO PDBLK
       BRN  EXSID            EXIT SETTING ID FIELD
       EJC
*
*      EFBLK
*
*      THE ROUTINE FOR AN EFBLK IS PASSED CONTROL FORM THE O_FNC
*      ENTRY TO CALL AN EXTERNAL FUNCTION.
*
*      (XL)                  POINTER TO EFBLK
*
B_EFC  ENT  BL_EF            ENTRY POINT (EFBLK)
.IF    .CNLD
.ELSE
       MOV  FARGS(XL),WC     LOAD NUMBER OF ARGUMENTS
       WTB  WC               CONVERT TO OFFSET
       MOV  XL,-(XS)         SAVE POINTER TO EFBLK
       MOV  XS,XT            COPY POINTER TO ARGUMENTS
*
*      LOOP TO CONVERT ARGUMENTS
*
BEFC1  ICA  XT               POINT TO NEXT ENTRY
       MOV  (XS),XR          LOAD POINTER TO EFBLK
       DCA  WC               DECREMENT EFTAR OFFSET
       ADD  WC,XR            POINT TO NEXT EFTAR ENTRY
       MOV  EFTAR(XR),XR     LOAD EFTAR ENTRY
.IF    .CNRA
.IF    .CNLF
       BSW  XR,4             SWITCH ON TYPE
.ELSE
       BSW  XR,3             SWITCH ON TYPE
.FI
.ELSE
.IF    .CNLF
       BSW  XR,5             SWITCH ON TYPE
.ELSE
       BSW  XR,4             SWITCH ON TYPE
.FI
.FI
       IFF  0,BEFC7          NO CONVERSION NEEDED
       IFF  1,BEFC2          STRING
       IFF  2,BEFC3          INTEGER
.IF    .CNRA
.IF    .CNLF
       IFF  3,BEFF1          FILE
.FI
.ELSE
       IFF  3,BEFC4          REAL
.IF    .CNLF
       IFF  4,BEFF1          FILE
.FI
.FI
       ESW                   END OF SWITCH ON TYPE
.IF    .CNLF
*
*      HERE TO CONVERT TO FILE
*
BEFF1  MOV  XT,-(XS)         SAVE ENTRY POINTER
       MOV  WC,BEFOF         SAVE OFFSET
       MOV  (XT),-(XS)       STACK ARG POINTER
       JSR  IOFCB            CONVERT TO FCB
       ERR  298,External function argument is not file
       ERR  298,External function argument is not file
       ERR  298,External function argument is not file
       MOV  WA,XR            POINT TO FCB
       MOV  (XS)+,XT         RELOAD ENTRY POINTER
       BRN  BEFC5            JUMP TO MERGE
.FI
*
*      HERE TO CONVERT TO STRING
*
BEFC2  MOV  (XT),-(XS)       STACK ARG PTR
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
       ERR  039,External function argument is not a string
       BRN  BEFC6            JUMP TO MERGE
       EJC
*
*      EFBLK (CONTINUED)
*
*      HERE TO CONVERT AN INTEGER
*
BEFC3  MOV  (XT),XR          LOAD NEXT ARGUMENT
       MOV  WC,BEFOF         SAVE OFFSET
       JSR  GTINT            CONVERT TO INTEGER
       ERR  040,External function argument is not integer
.IF    .CNRA
.ELSE
       BRN  BEFC5            MERGE WITH REAL CASE
*
*      HERE TO CONVERT A REAL
*
BEFC4  MOV  (XT),XR          LOAD NEXT ARGUMENT
       MOV  WC,BEFOF         SAVE OFFSET
       JSR  GTREA            CONVERT TO REAL
       ERR  265,External function argument is not real
.FI
*
*      INTEGER CASE MERGES HERE
*
BEFC5  MOV  BEFOF,WC         RESTORE OFFSET
*
*      STRING MERGES HERE
*
BEFC6  MOV  XR,(XT)          STORE CONVERTED RESULT
*
*      NO CONVERSION MERGES HERE
*
BEFC7  BNZ  WC,BEFC1         LOOP BACK IF MORE TO GO
*
*      HERE AFTER CONVERTING ALL THE ARGUMENTS
*
       MOV  (XS)+,XL         RESTORE EFBLK POINTER
       MOV  FARGS(XL),WA     GET NUMBER OF ARGS
       JSR  SYSEX            CALL ROUTINE TO CALL EXTERNAL FNC
       PPM  EXFAL            FAIL IF FAILURE
       ERR  327,Calling external function - not found
       ERR  326,Calling external function - bad argument type
.IF    .CEXP
       WTB  WA               CONVERT NUMBER OF ARGS TO BYTES
       ADD  WA,XS            REMOVE ARGUMENTS FROM STACK
.FI
       EJC
*
*      EFBLK (CONTINUED)
*
*      RETURN HERE WITH RESULT IN XR
*
*      FIRST DEFEND AGAINST NON-STANDARD NULL STRING RETURNED
*
       MOV  EFRSL(XL),WB     GET RESULT TYPE ID
       BNZ  WB,BEFA8         BRANCH IF NOT UNCONVERTED
       BNE  (XR),=B_SCL,BEFC8 JUMP IF NOT A STRING
       BZE  SCLEN(XR),EXNUL  RETURN NULL IF NULL
*
*      HERE IF CONVERTED RESULT TO CHECK FOR NULL STRING
*
BEFA8  BNE  WB,=NUM01,BEFC8  JUMP IF NOT A STRING
       BZE  SCLEN(XR),EXNUL  RETURN NULL IF NULL
*
*      RETURN IF RESULT IS IN DYNAMIC STORAGE
*
BEFC8  BLT  XR,DNAMB,BEFC9   JUMP IF NOT IN DYNAMIC STORAGE
       BLE  XR,DNAMP,EXIXR   RETURN RESULT IF ALREADY DYNAMIC
*
*      HERE WE COPY A RESULT INTO THE DYNAMIC REGION
*
BEFC9  MOV  (XR),WA          GET POSSIBLE TYPE WORD
       BZE  WB,BEF11         JUMP IF UNCONVERTED RESULT
       MOV  =B_SCL,WA        STRING
       BEQ  WB,=NUM01,BEF10  YES JUMP
       MOV  =B_ICL,WA        INTEGER
       BEQ  WB,=NUM02,BEF10  YES JUMP
.IF    .CNRA
.ELSE
       MOV  =B_RCL,WA        REAL
.FI
*
*      STORE TYPE WORD IN RESULT
*
BEF10  MOV  WA,(XR)          STORED BEFORE COPYING TO DYNAMIC
*
*      MERGE FOR UNCONVERTED RESULT
*
BEF11  BEQ  (XR),=B_SCL,BEF12 BRANCH IF STRING RESULT
       JSR  BLKLN            GET LENGTH OF BLOCK
       MOV  XR,XL            COPY ADDRESS OF OLD BLOCK
       JSR  ALLOC            ALLOCATE DYNAMIC BLOCK SAME SIZE
       MOV  XR,-(XS)         SET POINTER TO NEW BLOCK AS RESULT
       MVW                   COPY OLD BLOCK TO DYNAMIC BLOCK
       ZER  XL               CLEAR GARBAGE VALUE
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      HERE TO RETURN A STRING RESULT THAT WAS NOT IN DYNAMIC.
*      CANNOT USE THE SIMPLE WORD COPY ABOVE BECAUSE IT WILL NOT
*      GUARANTEE ZERO PADDING IN THE LAST WORD.
*
BEF12  MOV  XR,XL            SAVE SOURCE STRING POINTER
       MOV  SCLEN(XR),WA     FETCH STRING LENGTH
       BZE  WA,EXNUL         RETURN NULL STRING IF LENGTH ZERO
       JSR  ALOCS            ALLOCATE SPACE FOR STRING
       MOV  XR,-(XS)         SAVE AS RESULT POINTER
       PSC  XR               PREPARE TO STORE CHARS OF RESULT
       PLC  XL               POINT TO CHARS IN SOURCE STRING
       MOV  WC,WA            NUMBER OF CHARACTERS TO COPY
       MVC                   MOVE CHARACTERS TO RESULT STRING
       ZER  XL               CLEAR GARBAGE VALUE
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
.FI
       EJC
*
*      EVBLK
*
*      THE ROUTINE FOR AN EVBLK IS NEVER EXECUTED
*
B_EVT  ENT  BL_EV            ENTRY POINT (EVBLK)
       EJC
*
*      FFBLK
*
*      THE ROUTINE FOR AN FFBLK IS EXECUTED FROM THE O_FNC ENTRY
*      TO CALL A FIELD FUNCTION AND EXTRACT A FIELD VALUE/NAME.
*
*      (XL)                  POINTER TO FFBLK
*
B_FFC  ENT  BL_FF            ENTRY POINT (FFBLK)
       MOV  XL,XR            COPY FFBLK POINTER
       LCW  WC               LOAD NEXT CODE WORD
       MOV  (XS),XL          LOAD PDBLK POINTER
       BNE  (XL),=B_PDT,BFFC2 JUMP IF NOT PDBLK AT ALL
       MOV  PDDFP(XL),WA     LOAD DFBLK POINTER FROM PDBLK
*
*      LOOP TO FIND CORRECT FFBLK FOR THIS PDBLK
*
BFFC1  BEQ  WA,FFDFP(XR),BFFC3 JUMP IF THIS IS THE CORRECT FFBLK
       MOV  FFNXT(XR),XR     ELSE LINK TO NEXT FFBLK ON CHAIN
       BNZ  XR,BFFC1         LOOP BACK IF ANOTHER ENTRY TO CHECK
*
*      HERE FOR BAD ARGUMENT
*
BFFC2  ERB  041,FIELD function argument is wrong datatype
       EJC
*
*      FFBLK (CONTINUED)
*
*      HERE AFTER LOCATING CORRECT FFBLK
*
BFFC3  MOV  FFOFS(XR),WA     LOAD FIELD OFFSET
       BEQ  WC,=OFNE_,BFFC5  JUMP IF CALLED BY NAME
       ADD  WA,XL            ELSE POINT TO VALUE FIELD
       MOV  (XL),XR          LOAD VALUE
       BNE  (XR),=B_TRT,BFFC4 JUMP IF NOT TRAPPED
       SUB  WA,XL            ELSE RESTORE NAME BASE,OFFSET
       MOV  WC,(XS)          SAVE NEXT CODE WORD OVER PDBLK PTR
       JSR  ACESS            ACCESS VALUE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  (XS),WC          RESTORE NEXT CODE WORD
*
*      HERE AFTER GETTING VALUE IN (XR), XL IS GARBAGE
*
BFFC4  MOV  XR,(XS)          STORE VALUE ON STACK (OVER PDBLK)
       MOV  WC,XR            COPY NEXT CODE WORD
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO ROUTINE FOR NEXT CODE WORD
*
*      HERE IF CALLED BY NAME
*
BFFC5  MOV  WA,-(XS)         STORE NAME OFFSET (BASE IS SET)
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      ICBLK
*
*      THE ROUTINE FOR ICBLK IS EXECUTED FROM THE GENERATED
*      CODE TO LOAD AN INTEGER VALUE ONTO THE STACK.
*
*      (XR)                  POINTER TO ICBLK
*
B_ICL  ENT  BL_IC            ENTRY POINT (ICBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      KVBLK
*
*      THE ROUTINE FOR A KVBLK IS NEVER EXECUTED.
*
B_KVT  ENT  BL_KV            ENTRY POINT (KVBLK)
       EJC
*
*      NMBLK
*
*      THE ROUTINE FOR A NMBLK IS EXECUTED FROM THE GENERATED
*      CODE FOR THE CASE OF LOADING A NAME ONTO THE STACK
*      WHERE THE NAME IS THAT OF A NATURAL VARIABLE WHICH CAN
*      BE PREEVALUATED AT COMPILE TIME.
*
*      (XR)                  POINTER TO NMBLK
*
B_NML  ENT  BL_NM            ENTRY POINT (NMBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      PDBLK
*
*      THE ROUTINE FOR A PDBLK IS NEVER EXECUTED
*
B_PDT  ENT  BL_PD            ENTRY POINT (PDBLK)
       EJC
*
*      PFBLK
*
*      THE ROUTINE FOR A PFBLK IS EXECUTED FROM THE ENTRY O_FNC
*      TO CALL A PROGRAM DEFINED FUNCTION.
*
*      (XL)                  POINTER TO PFBLK
*
*      THE FOLLOWING STACK ENTRIES ARE MADE BEFORE PASSING
*      CONTROL TO THE PROGRAM DEFINED FUNCTION.
*
*                            SAVED VALUE OF FIRST ARGUMENT
*                            .
*                            SAVED VALUE OF LAST ARGUMENT
*                            SAVED VALUE OF FIRST LOCAL
*                            .
*                            SAVED VALUE OF LAST LOCAL
*                            SAVED VALUE OF FUNCTION NAME
*                            SAVED CODE BLOCK PTR (R_COD)
*                            SAVED CODE POINTER (-R_COD)
*                            SAVED VALUE OF FLPRT
*                            SAVED VALUE OF FLPTR
*                            POINTER TO PFBLK
*      FLPTR --------------- ZERO (TO BE OVERWRITTEN WITH OFFS)
*
B_PFC  ENT  BL_PF            ENTRY POINT (PFBLK)
       MOV  XL,BPFPF         SAVE PFBLK PTR (NEED NOT BE RELOC)
       MOV  XL,XR            COPY FOR THE MOMENT
       MOV  PFVBL(XR),XL     POINT TO VRBLK FOR FUNCTION
*
*      LOOP TO FIND OLD VALUE OF FUNCTION
*
BPF01  MOV  XL,WB            SAVE POINTER
       MOV  VRVAL(XL),XL     LOAD VALUE
       BEQ  (XL),=B_TRT,BPF01 LOOP IF TRBLK
*
*      SET VALUE TO NULL AND SAVE OLD FUNCTION VALUE
*
       MOV  XL,BPFSV         SAVE OLD VALUE
       MOV  WB,XL            POINT BACK TO BLOCK WITH VALUE
       MOV  =NULLS,VRVAL(XL) SET VALUE TO NULL
       MOV  FARGS(XR),WA     LOAD NUMBER OF ARGUMENTS
       ADD  *PFARG,XR        POINT TO PFARG ENTRIES
       BZE  WA,BPF04         JUMP IF NO ARGUMENTS
       MOV  XS,XT            PTR TO LAST ARG
       WTB  WA               CONVERT NO. OF ARGS TO BYTES OFFSET
       ADD  WA,XT            POINT BEFORE FIRST ARG
       MOV  XT,BPFXT         REMEMBER ARG POINTER
       EJC
*
*      PFBLK (CONTINUED)
*
*      LOOP TO SAVE OLD ARGUMENT VALUES AND SET NEW ONES
*
BPF02  MOV  (XR)+,XL         LOAD VRBLK PTR FOR NEXT ARGUMENT
*
*      LOOP THROUGH POSSIBLE TRBLK CHAIN TO FIND VALUE
*
BPF03  MOV  XL,WC            SAVE POINTER
       MOV  VRVAL(XL),XL     LOAD NEXT VALUE
       BEQ  (XL),=B_TRT,BPF03 LOOP BACK IF TRBLK
*
*      SAVE OLD VALUE AND GET NEW VALUE
*
       MOV  XL,WA            KEEP OLD VALUE
       MOV  BPFXT,XT         POINT BEFORE NEXT STACKED ARG
       MOV  -(XT),WB         LOAD ARGUMENT (NEW VALUE)
       MOV  WA,(XT)          SAVE OLD VALUE
       MOV  XT,BPFXT         KEEP ARG PTR FOR NEXT TIME
       MOV  WC,XL            POINT BACK TO BLOCK WITH VALUE
       MOV  WB,VRVAL(XL)     SET NEW VALUE
       BNE  XS,BPFXT,BPF02   LOOP IF NOT ALL DONE
*
*      NOW PROCESS LOCALS
*
BPF04  MOV  BPFPF,XL         RESTORE PFBLK POINTER
       MOV  PFNLO(XL),WA     LOAD NUMBER OF LOCALS
       BZE  WA,BPF07         JUMP IF NO LOCALS
       MOV  =NULLS,WB        GET NULL CONSTANT
       LCT  WA,WA            SET LOCAL COUNTER
*
*      LOOP TO PROCESS LOCALS
*
BPF05  MOV  (XR)+,XL         LOAD VRBLK PTR FOR NEXT LOCAL
*
*      LOOP THROUGH POSSIBLE TRBLK CHAIN TO FIND VALUE
*
BPF06  MOV  XL,WC            SAVE POINTER
       MOV  VRVAL(XL),XL     LOAD NEXT VALUE
       BEQ  (XL),=B_TRT,BPF06 LOOP BACK IF TRBLK
*
*      SAVE OLD VALUE AND SET NULL AS NEW VALUE
*
       MOV  XL,-(XS)         STACK OLD VALUE
       MOV  WC,XL            POINT BACK TO BLOCK WITH VALUE
       MOV  WB,VRVAL(XL)     SET NULL AS NEW VALUE
       BCT  WA,BPF05         LOOP TILL ALL LOCALS PROCESSED
       EJC
*
*      PFBLK (CONTINUED)
*
*      HERE AFTER PROCESSING ARGUMENTS AND LOCALS
*
.IF    .CNPF
BPF07  MOV  R_COD,WA         LOAD OLD CODE BLOCK POINTER
.ELSE
BPF07  ZER  XR               ZERO REG XR IN CASE
       BZE  KVPFL,BPF7C      SKIP IF PROFILING IS OFF
       BEQ  KVPFL,=NUM02,BPF7A BRANCH ON TYPE OF PROFILE
*
*      HERE IF &PROFILE = 1
*
       JSR  SYSTM            GET CURRENT TIME
       STI  PFETM            SAVE FOR A SEC
       SBI  PFSTM            FIND TIME USED BY CALLER
       JSR  ICBLD            BUILD INTO AN ICBLK
       LDI  PFETM            RELOAD CURRENT TIME
       BRN  BPF7B            MERGE
*
*       HERE IF &PROFILE = 2
*
BPF7A  LDI  PFSTM            GET START TIME OF CALLING STMT
       JSR  ICBLD            ASSEMBLE AN ICBLK ROUND IT
       JSR  SYSTM            GET NOW TIME
*
*      BOTH TYPES OF PROFILE MERGE HERE
*
BPF7B  STI  PFSTM            SET START TIME OF 1ST FUNC STMT
       MNZ  PFFNC            FLAG FUNCTION ENTRY
*
*      NO PROFILING MERGES HERE
*
BPF7C  MOV  XR,-(XS)         STACK ICBLK PTR (OR ZERO)
       MOV  R_COD,WA         LOAD OLD CODE BLOCK POINTER
.FI
       SCP  WB               GET CODE POINTER
       SUB  WA,WB            MAKE CODE POINTER INTO OFFSET
       MOV  BPFPF,XL         RECALL PFBLK POINTER
       MOV  BPFSV,-(XS)      STACK OLD VALUE OF FUNCTION NAME
       MOV  WA,-(XS)         STACK CODE BLOCK POINTER
       MOV  WB,-(XS)         STACK CODE OFFSET
       MOV  FLPRT,-(XS)      STACK OLD FLPRT
       MOV  FLPTR,-(XS)      STACK OLD FAILURE POINTER
       MOV  XL,-(XS)         STACK POINTER TO PFBLK
       ZER  -(XS)            DUMMY ZERO ENTRY FOR FAIL RETURN
       CHK                   CHECK FOR STACK OVERFLOW
       MOV  XS,FLPTR         SET NEW FAIL RETURN VALUE
       MOV  XS,FLPRT         SET NEW FLPRT
       MOV  KVTRA,WA         LOAD TRACE VALUE
       ADD  KVFTR,WA         ADD FTRACE VALUE
       BNZ  WA,BPF09         JUMP IF TRACING POSSIBLE
       ICV  KVFNC            ELSE BUMP FNCLEVEL
*
*      HERE TO ACTUALLY JUMP TO FUNCTION
*
BPF08  MOV  PFCOD(XL),XR     POINT TO VRBLK OF ENTRY LABEL
       MOV  VRLBL(XR),XR     POINT TO TARGET CODE
       BEQ  XR,=STNDL,BPF17  TEST FOR UNDEFINED LABEL
       BNE  (XR),=B_TRT,BPF8A JUMP IF NOT TRAPPED
       MOV  TRLBL(XR),XR     ELSE LOAD PTR TO REAL LABEL CODE
BPF8A  BRI  (XR)             OFF TO EXECUTE FUNCTION
*
*      HERE IF TRACING IS POSSIBLE
*
BPF09  MOV  PFCTR(XL),XR     LOAD POSSIBLE CALL TRACE TRBLK
       MOV  PFVBL(XL),XL     LOAD VRBLK POINTER FOR FUNCTION
       MOV  *VRVAL,WA        SET NAME OFFSET FOR VARIABLE
       BZE  KVTRA,BPF10      JUMP IF TRACE MODE IS OFF
       BZE  XR,BPF10         OR IF THERE IS NO CALL TRACE
*
*      HERE IF CALL TRACED
*
       DCV  KVTRA            DECREMENT TRACE COUNT
       BZE  TRFNC(XR),BPF11  JUMP IF PRINT TRACE
       JSR  TRXEQ            EXECUTE FUNCTION TYPE TRACE
       EJC
*
*      PFBLK (CONTINUED)
*
*      HERE TO TEST FOR FTRACE TRACE
*
BPF10  BZE  KVFTR,BPF16      JUMP IF FTRACE IS OFF
       DCV  KVFTR            ELSE DECREMENT FTRACE
*
*      HERE FOR PRINT TRACE
*
BPF11  JSR  PRTSN            PRINT STATEMENT NUMBER
       JSR  PRTNM            PRINT FUNCTION NAME
       MOV  =CH_PP,WA        LOAD LEFT PAREN
       JSR  PRTCH            PRINT LEFT PAREN
       MOV  NUM01(XS),XL     RECOVER PFBLK POINTER
       BZE  FARGS(XL),BPF15  SKIP IF NO ARGUMENTS
       ZER  WB               ELSE SET ARGUMENT COUNTER
       BRN  BPF13            JUMP INTO LOOP
*
*      LOOP TO PRINT ARGUMENT VALUES
*
BPF12  MOV  =CH_CM,WA        LOAD COMMA
       JSR  PRTCH            PRINT TO SEPARATE FROM LAST ARG
*
*      MERGE HERE FIRST TIME (NO COMMA REQUIRED)
*
BPF13  MOV  WB,(XS)          SAVE ARG CTR (OVER FAILOFFS IS OK)
       WTB  WB               CONVERT TO BYTE OFFSET
       ADD  WB,XL            POINT TO NEXT ARGUMENT POINTER
       MOV  PFARG(XL),XR     LOAD NEXT ARGUMENT VRBLK PTR
       SUB  WB,XL            RESTORE PFBLK POINTER
       MOV  VRVAL(XR),XR     LOAD NEXT VALUE
       JSR  PRTVL            PRINT ARGUMENT VALUE
       EJC
*
*      HERE AFTER DEALING WITH ONE ARGUMENT
*
       MOV  (XS),WB          RESTORE ARGUMENT COUNTER
       ICV  WB               INCREMENT ARGUMENT COUNTER
       BLT  WB,FARGS(XL),BPF12 LOOP IF MORE TO PRINT
*
*      MERGE HERE IN NO ARGS CASE TO PRINT PAREN
*
BPF15  MOV  =CH_RP,WA        LOAD RIGHT PAREN
       JSR  PRTCH            PRINT TO TERMINATE OUTPUT
       JSR  PRTNL            TERMINATE PRINT LINE
*
*      MERGE HERE TO EXIT WITH TEST FOR FNCLEVEL TRACE
*
BPF16  ICV  KVFNC            INCREMENT FNCLEVEL
       MOV  R_FNC,XL         LOAD PTR TO POSSIBLE TRBLK
       JSR  KTREX            CALL KEYWORD TRACE ROUTINE
*
*      CALL FUNCTION AFTER TRACE TESTS COMPLETE
*
       MOV  NUM01(XS),XL     RESTORE PFBLK POINTER
       BRN  BPF08            JUMP BACK TO EXECUTE FUNCTION
*
*      HERE IF CALLING A FUNCTION WHOSE ENTRY LABEL IS UNDEFINED
*
BPF17  MOV  NUM02(XS),FLPTR  RESET SO EXFAL CAN RETURN TO EVALX
       ERB  286,Function call to undefined entry label
.IF    .CNRA
.ELSE
       EJC
*
*      RCBLK
*
*      THE ROUTINE FOR AN RCBLK IS EXECUTED FROM THE GENERATED
*      CODE TO LOAD A REAL VALUE ONTO THE STACK.
*
*      (XR)                  POINTER TO RCBLK
*
B_RCL  ENT  BL_RC            ENTRY POINT (RCBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.FI
       EJC
*
*      SCBLK
*
*      THE ROUTINE FOR AN SCBLK IS EXECUTED FROM THE GENERATED
*      CODE TO LOAD A STRING VALUE ONTO THE STACK.
*
*      (XR)                  POINTER TO SCBLK
*
B_SCL  ENT  BL_SC            ENTRY POINT (SCBLK)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      TBBLK
*
*      THE ROUTINE FOR A TBBLK IS NEVER EXECUTED
*
B_TBT  ENT  BL_TB            ENTRY POINT (TBBLK)
       EJC
*
*      TEBLK
*
*      THE ROUTINE FOR A TEBLK IS NEVER EXECUTED
*
B_TET  ENT  BL_TE            ENTRY POINT (TEBLK)
       EJC
*
*      VCBLK
*
*      THE ROUTINE FOR A VCBLK IS NEVER EXECUTED
*
B_VCT  ENT  BL_VC            ENTRY POINT (VCBLK)
       EJC
*
*      VRBLK
*
*      THE VRBLK ROUTINES ARE EXECUTED FROM THE GENERATED CODE.
*      THERE ARE SIX ENTRIES FOR VRBLK COVERING VARIOUS CASES
*
B_VR_  ENT  BL__I            MARK START OF VRBLK ENTRY POINTS
*
*      ENTRY FOR VRGET (TRAPPED CASE). THIS ROUTINE IS CALLED
*      FROM THE GENERATED CODE TO LOAD THE VALUE OF A VARIABLE.
*      THIS ENTRY POINT IS USED IF AN ACCESS TRACE OR INPUT
*      ASSOCIATION IS CURRENTLY ACTIVE.
*
*      (XR)                  POINTER TO VRGET FIELD OF VRBLK
*
B_VRA  ENT  BL__I            ENTRY POINT
       MOV  XR,XL            COPY NAME BASE (VRGET = 0)
       MOV  *VRVAL,WA        SET NAME OFFSET
       JSR  ACESS            ACCESS VALUE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      VRBLK (CONTINUED)
*
*      ENTRY FOR VRSTO (ERROR CASE. THIS ROUTINE IS CALLED FROM
*      THE EXECUTED CODE FOR AN ATTEMPT TO MODIFY THE VALUE
*      OF A PROTECTED (PATTERN VALUED) NATURAL VARIABLE.
*
B_VRE  ENT                   ENTRY POINT
       ERB  042,Attempt to change value of protected variable
       EJC
*
*      VRBLK (CONTINUED)
*
*      ENTRY FOR VRTRA (UNTRAPPED CASE). THIS ROUTINE IS CALLED
*      FROM THE EXECUTED CODE TO TRANSFER TO A LABEL.
*
*      (XR)                  POINTER TO VRTRA FIELD OF VRBLK
*
B_VRG  ENT                   ENTRY POINT
       MOV  VRLBO(XR),XR     LOAD CODE POINTER
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO ROUTINE FOR NEXT CODE WORD
       EJC
*
*      VRBLK (CONTINUED)
*
*      ENTRY FOR VRGET (UNTRAPPED CASE). THIS ROUTINE IS CALLED
*      FROM THE GENERATED CODE TO LOAD THE VALUE OF A VARIABLE.
*
*      (XR)                  POINTS TO VRGET FIELD OF VRBLK
*
B_VRL  ENT                   ENTRY POINT
       MOV  VRVAL(XR),-(XS)  LOAD VALUE ONTO STACK (VRGET = 0)
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      VRBLK (CONTINUED)
*
*      ENTRY FOR VRSTO (UNTRAPPED CASE). THIS ROUTINE IS CALLED
*      FROM THE GENERATED CODE TO STORE THE VALUE OF A VARIABLE.
*
*      (XR)                  POINTER TO VRSTO FIELD OF VRBLK
*
B_VRS  ENT                   ENTRY POINT
       MOV  (XS),VRVLO(XR)   STORE VALUE, LEAVE ON STACK
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      VRBLK (CONTINUED)
*
*      VRTRA (TRAPPED CASE). THIS ROUTINE IS CALLED FROM THE
*      GENERATED CODE TO TRANSFER TO A LABEL WHEN A LABEL
*      TRACE IS CURRENTLY ACTIVE.
*
B_VRT  ENT                   ENTRY POINT
       SUB  *VRTRA,XR        POINT BACK TO START OF VRBLK
       MOV  XR,XL            COPY VRBLK POINTER
       MOV  *VRVAL,WA        SET NAME OFFSET
       MOV  VRLBL(XL),XR     LOAD POINTER TO TRBLK
       BZE  KVTRA,BVRT2      JUMP IF TRACE IS OFF
       DCV  KVTRA            ELSE DECREMENT TRACE COUNT
       BZE  TRFNC(XR),BVRT1  JUMP IF PRINT TRACE CASE
       JSR  TRXEQ            ELSE EXECUTE FULL TRACE
       BRN  BVRT2            MERGE TO JUMP TO LABEL
*
*      HERE FOR PRINT TRACE -- PRINT COLON ( LABEL NAME )
*
BVRT1  JSR  PRTSN            PRINT STATEMENT NUMBER
       MOV  XL,XR            COPY VRBLK POINTER
       MOV  =CH_CL,WA        COLON
       JSR  PRTCH            PRINT IT
       MOV  =CH_PP,WA        LEFT PAREN
       JSR  PRTCH            PRINT IT
       JSR  PRTVN            PRINT LABEL NAME
       MOV  =CH_RP,WA        RIGHT PAREN
       JSR  PRTCH            PRINT IT
       JSR  PRTNL            TERMINATE LINE
       MOV  VRLBL(XL),XR     POINT BACK TO TRBLK
*
*      MERGE HERE TO JUMP TO LABEL
*
BVRT2  MOV  TRLBL(XR),XR     LOAD POINTER TO ACTUAL CODE
       BRI  (XR)             EXECUTE STATEMENT AT LABEL
       EJC
*
*      VRBLK (CONTINUED)
*
*      ENTRY FOR VRSTO (TRAPPED CASE). THIS ROUTINE IS CALLED
*      FROM THE GENERATED CODE TO STORE THE VALUE OF A VARIABLE.
*      THIS ENTRY IS USED WHEN A VALUE TRACE OR OUTPUT
*      ASSOCIATION IS CURRENTLY ACTIVE.
*
*      (XR)                  POINTER TO VRSTO FIELD OF VRBLK
*
B_VRV  ENT                   ENTRY POINT
       MOV  (XS),WB          LOAD VALUE (LEAVE COPY ON STACK)
       SUB  *VRSTO,XR        POINT TO VRBLK
       MOV  XR,XL            COPY VRBLK POINTER
       MOV  *VRVAL,WA        SET OFFSET
       JSR  ASIGN            CALL ASSIGNMENT ROUTINE
       PPM  EXFAL            FAIL IF ASSIGNMENT FAILS
       LCW  XR               ELSE GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      XNBLK
*
*      THE ROUTINE FOR AN XNBLK IS NEVER EXECUTED
*
B_XNT  ENT  BL_XN            ENTRY POINT (XNBLK)
       EJC
*
*      XRBLK
*
*      THE ROUTINE FOR AN XRBLK IS NEVER EXECUTED
*
B_XRT  ENT  BL_XR            ENTRY POINT (XRBLK)
*
*      MARK ENTRY ADDRESS PAST LAST BLOCK ACTION ROUTINE
*
B_YYY  ENT  BL__I            LAST BLOCK ROUTINE ENTRY POINT
       TTL  S P I T B O L -- PATTERN MATCHING ROUTINES
*
*      THE FOLLOWING SECTION CONSISTS OF THE PATTERN MATCHING
*      ROUTINES. ALL PATTERN NODES CONTAIN A POINTER (PCODE)
*      TO ONE OF THE ROUTINES IN THIS SECTION (P_XXX).
*
*      NOTE THAT THIS SECTION FOLLOWS THE B_XXX ROUTINES TO
*      ENABLE A FAST TEST FOR THE PATTERN DATATYPE.
*
P_AAA  ENT  BL__I            ENTRY TO MARK FIRST PATTERN
*
*
*      THE ENTRY CONDITIONS TO THE MATCH ROUTINE ARE AS FOLLOWS
*      (SEE O_PMN, O_PMV, O_PMS AND PROCEDURE MATCH).
*
*      STACK CONTENTS.
*
*                            NAME BASE (O_PMN ONLY)
*                            NAME OFFSET (O_PMN ONLY)
*                            TYPE (0-O_PMN, 1-O_PMV, 2-O_PMS)
*      PMHBS --------------- INITIAL CURSOR (ZERO)
*                            INITIAL NODE POINTER
*      XS ------------------ =NDABO (ANCHORED), =NDUNA (UNANCH)
*
*      REGISTER VALUES.
*
*           (XS)             SET AS SHOWN IN STACK DIAGRAM
*           (XR)             POINTER TO INITIAL PATTERN NODE
*           (WB)             INITIAL CURSOR (ZERO)
*
*      GLOBAL PATTERN VALUES
*
*           R_PMS            POINTER TO SUBJECT STRING SCBLK
*           PMSSL            LENGTH OF SUBJECT STRING IN CHARS
*           PMDFL            DOT FLAG, INITIALLY ZERO
*           PMHBS            SET AS SHOWN IN STACK DIAGRAM
*
*      CONTROL IS PASSED BY BRANCHING THROUGH THE PCODE
*      FIELD OF THE INITIAL PATTERN NODE (BRI (XR)).
       EJC
*
*      DESCRIPTION OF ALGORITHM
*
*      A PATTERN STRUCTURE IS REPRESENTED AS A LINKED GRAPH
*      OF NODES WITH THE FOLLOWING STRUCTURE.
*
*           +------------------------------------+
*           I                PCODE               I
*           +------------------------------------+
*           I                PTHEN               I
*           +------------------------------------+
*           I                PARM1               I
*           +------------------------------------+
*           I                PARM2               I
*           +------------------------------------+
*
*      PCODE IS A POINTER TO THE ROUTINE WHICH WILL PERFORM
*      THE MATCH OF THIS PARTICULAR NODE TYPE.
*
*      PTHEN IS A POINTER TO THE SUCCESSOR NODE. I.E. THE NODE
*      TO BE MATCHED IF THE ATTEMPT TO MATCH THIS NODE SUCCEEDS.
*      IF THIS IS THE LAST NODE OF THE PATTERN PTHEN POINTS
*      TO THE DUMMY NODE NDNTH WHICH INITIATES PATTERN EXIT.
*
*      PARM1, PARM2 ARE PARAMETERS WHOSE USE VARIES WITH THE
*      PARTICULAR NODE. THEY ARE ONLY PRESENT IF REQUIRED.
*
*      ALTERNATIVES ARE HANDLED WITH THE SPECIAL ALTERNATIVE
*      NODE WHOSE PARAMETER POINTS TO THE NODE TO BE MATCHED
*      IF THERE IS A FAILURE ON THE SUCCESSOR PATH.
*
*      THE FOLLOWING EXAMPLE ILLUSTRATES THE MANNER IN WHICH
*      THE STRUCTURE IS BUILT UP. THE PATTERN IS
*
*      (A / B / C) (D / E)   WHERE / IS ALTERNATION
*
*      IN THE DIAGRAM, THE NODE MARKED + REPRESENTS AN
*      ALTERNATIVE NODE AND THE DOTTED LINE FROM A + NODE
*      REPRESENTS THE PARAMETER POINTER TO THE ALTERNATIVE.
*
*      +---+     +---+     +---+     +---+
*      I + I-----I A I-----I + I-----I D I-----
*      +---+     +---+  I  +---+     +---+
*        .              I    .
*        .              I    .
*      +---+     +---+  I  +---+
*      I + I-----I B I--I  I E I-----
*      +---+     +---+  I  +---+
*        .              I
*        .              I
*      +---+            I
*      I C I------------I
*      +---+
       EJC
*
*      DURING THE MATCH, THE REGISTERS ARE USED AS FOLLOWS.
*
*      (XR)                  POINTS TO THE CURRENT NODE
*      (XL)                  SCRATCH
*      (XS)                  MAIN STACK POINTER
*      (WB)                  CURSOR (NUMBER OF CHARS MATCHED)
*      (WA,WC)               SCRATCH
*
*      TO KEEP TRACK OF ALTERNATIVES, THE MAIN STACK IS USED AS
*      A HISTORY STACK AND CONTAINS TWO WORD ENTRIES.
*
*      WORD 1                SAVED CURSOR VALUE
*      WORD 2                NODE TO MATCH ON FAILURE
*
*      WHEN A FAILURE OCCURS, THE MOST RECENT ENTRY ON THIS
*      STACK IS POPPED OFF TO RESTORE THE CURSOR AND POINT
*      TO THE NODE TO BE MATCHED AS AN ALTERNATIVE. THE ENTRY
*      AT THE BOTTOM OF THE STACK POINTS TO THE FOLLOWING
*      SPECIAL NODES DEPENDING ON THE SCAN MODE.
*
*      ANCHORED MODE         THE BOTTOM ENTRY POINTS TO THE
*                            SPECIAL NODE NDABO WHICH CAUSES AN
*                            ABORT. THE CURSOR VALUE STORED
*                            WITH THIS ENTRY IS ALWAYS ZERO.
*
*      UNANCHORED MODE       THE BOTTOM ENTRY POINTS TO THE
*                            SPECIAL NODE NDUNA WHICH MOVES THE
*                            ANCHOR POINT AND RESTARTS THE MATCH
*                            THE CURSOR SAVED WITH THIS ENTRY
*                            IS THE NUMBER OF CHARACTERS WHICH
*                            LIE BEFORE THE INITIAL ANCHOR POINT
*                            (I.E. THE NUMBER OF ANCHOR MOVES).
*                            THIS ENTRY IS THREE WORDS LONG AND
*                            ALSO CONTAINS THE INITIAL PATTERN.
*
*      ENTRIES ARE MADE ON THIS HISTORY STACK BY ALTERNATIVE
*      NODES AND BY SOME SPECIAL COMPOUND PATTERNS AS DESCRIBED
*      LATER ON. THE FOLLOWING GLOBAL LOCATIONS ARE USED DURING
*      PATTERN MATCHING.
*
*      R_PMS                 POINTER TO SUBJECT STRING
*      PMSSL                 LENGTH OF SUBJECT STRING
*      PMDFL                 FLAG SET NON-ZERO FOR DOT PATTERNS
*      PMHBS                 BASE PTR FOR CURRENT HISTORY STACK
*
*      THE FOLLOWING EXIT POINTS ARE AVAILABLE TO MATCH ROUTINES
*
*      SUCCP                 SUCCESS IN MATCHING CURRENT NODE
*      FAILP                 FAILURE IN MATCHING CURRENT NODE
       EJC
*
*      COMPOUND PATTERNS
*
*      SOME PATTERNS HAVE IMPLICIT ALTERNATIVES AND THEIR
*      REPRESENTATION IN THE PATTERN STRUCTURE CONSISTS OF A
*      LINKED SET OF NODES AS INDICATED BY THESE DIAGRAMS.
*
*      AS BEFORE, THE + REPRESENTS AN ALTERNATIVE NODE AND
*      THE DOTTED LINE FROM A + NODE IS THE PARAMETER POINTER
*      TO THE ALTERNATIVE PATTERN.
*
*      ARB
*      ---
*
*           +---+            THIS NODE (P_ARB) MATCHES NULL
*           I B I-----       AND STACKS CURSOR, SUCCESSOR PTR,
*           +---+            CURSOR (COPY) AND A PTR TO NDARC.
*
*
*
*
*      BAL
*      ---
*
*           +---+            THE P_BAL NODE SCANS A BALANCED
*           I B I-----       STRING AND THEN STACKS A POINTER
*           +---+            TO ITSELF ON THE HISTORY STACK.
       EJC
*
*      COMPOUND PATTERN STRUCTURES (CONTINUED)
*
*
*      ARBNO
*      -----
*
*           +---+            THIS ALTERNATIVE NODE MATCHES NULL
*      +----I + I-----       THE FIRST TIME AND STACKS A POINTER
*      I    +---+            TO THE ARGUMENT PATTERN X.
*      I      .
*      I      .
*      I    +---+            NODE (P_ABA) TO STACK CURSOR
*      I    I A I            AND HISTORY STACK BASE PTR.
*      I    +---+
*      I      I
*      I      I
*      I    +---+            THIS IS THE ARGUMENT PATTERN. AS
*      I    I X I            INDICATED, THE SUCCESSOR OF THE
*      I    +---+            PATTERN IS THE P_ABC NODE
*      I      I
*      I      I
*      I    +---+            THIS NODE (P_ABC) POPS PMHBS,
*      +----I C I            STACKS OLD PMHBS AND PTR TO NDABD
*           +---+            (UNLESS OPTIMIZATION HAS OCCURRED)
*
*      STRUCTURE AND EXECUTION OF THIS PATTERN RESEMBLE THOSE OF
*      RECURSIVE PATTERN MATCHING AND IMMEDIATE ASSIGNMENT.
*      THE ALTERNATIVE NODE AT THE HEAD OF THE STRUCTURE MATCHES
*      NULL INITIALLY BUT ON SUBSEQUENT FAILURE ENSURES ATTEMPT
*      TO MATCH THE ARGUMENT.  BEFORE THE ARGUMENT IS MATCHED
*      P_ABA STACKS THE CURSOR, PMHBS AND A PTR TO P_ABB.  IF
*      THE ARGUMENT CANT BE MATCHED , P_ABB REMOVES THIS SPECIAL
*      STACK ENTRY AND FAILS.
*      IF ARGUMENT IS MATCHED , P_ABC RESTORES THE OUTER PMHBS
*      VALUE (SAVED BY P_ABA) .  THEN IF THE ARGUMENT HAS LEFT
*      ALTERNATIVES ON STACK IT STACKS THE INNER VALUE OF PMHBS
*      AND A PTR TO NDABD. IF ARGUMENT LEFT NOTHING ON THE STACK
*      IT OPTIMISES BY REMOVING ITEMS STACKED BY P_ABA.  FINALLY
*      A CHECK IS MADE THAT ARGUMENT MATCHED MORE THAN THE NULL
*      STRING (CHECK IS INTENDED TO PREVENT USELESS LOOPING).
*      IF SO THE SUCCESSOR IS AGAIN THE ALTERNATIVE NODE AT THE
*      HEAD OF THE STRUCTURE , ENSURING A POSSIBLE EXTRA ATTEMPT
*      TO MATCH THE ARG IF NECESSARY.  IF NOT , THE SUCCESSOR TO
*      ALTERNATIVE IS TAKEN SO AS TO TERMINATE THE LOOP.  P_ABD
*      RESTORES INNER PMHBS PTR AND FAILS , THUS TRYING TO MATCH
*      ALTERNATIVES LEFT BY THE ARBNO ARGUMENT.
       EJC
*
*      COMPOUND PATTERN STRUCTURES (CONTINUED)
*
*      BREAKX
*      ------
*
*           +---+            THIS NODE IS A BREAK NODE FOR
*      +----I B I            THE ARGUMENT TO BREAKX, IDENTICAL
*      I    +---+            TO AN ORDINARY BREAK NODE.
*      I      I
*      I      I
*      I    +---+            THIS ALTERNATIVE NODE STACKS A
*      I    I + I-----       POINTER TO THE BREAKX NODE TO
*      I    +---+            ALLOW FOR SUBSEQUENT FAILURE
*      I      .
*      I      .
*      I    +---+            THIS IS THE BREAKX NODE ITSELF. IT
*      +----I X I            MATCHES ONE CHARACTER AND THEN
*           +---+            PROCEEDS BACK TO THE BREAK NODE.
*
*
*
*
*      FENCE
*      -----
*
*           +---+            THE FENCE NODE MATCHES NULL AND
*           I F I-----       STACKS A POINTER TO NODE NDABO TO
*           +---+            ABORT ON A SUBSEQUENT REMATCH
*
*
*
*
*      SUCCEED
*      -------
*
*           +---+            THE NODE FOR SUCCEED MATCHES NULL
*           I S I-----       AND STACKS A POINTER TO ITSELF
*           +---+            TO REPEAT THE MATCH ON A FAILURE.
       EJC
*
*      COMPOUND PATTERNS (CONTINUED)
*
*      BINARY DOT (PATTERN ASSIGNMENT)
*      -------------------------------
*
*           +---+            THIS NODE (P_PAA) SAVES THE CURRENT
*           I A I            CURSOR AND A POINTER TO THE
*           +---+            SPECIAL NODE NDPAB ON THE STACK.
*             I
*             I
*           +---+            THIS IS THE STRUCTURE FOR THE
*           I X I            PATTERN LEFT ARGUMENT OF THE
*           +---+            PATTERN ASSIGNMENT CALL.
*             I
*             I
*           +---+            THIS NODE (P_PAC) SAVES THE CURSOR,
*           I C I-----       A PTR TO ITSELF, THE CURSOR (COPY)
*           +---+            AND A PTR TO NDPAD ON THE STACK.
*
*
*      THE FUNCTION OF THE MATCH ROUTINE FOR NDPAB (P_PAB)
*      IS SIMPLY TO UNSTACK ITSELF AND FAIL BACK ONTO THE STACK.
*
*      THE MATCH ROUTINE FOR P_PAC ALSO SETS THE GLOBAL PATTERN
*      FLAG PMDFL NON-ZERO TO INDICATE THAT PATTERN ASSIGNMENTS
*      MAY HAVE OCCURED IN THE PATTERN MATCH
*
*      IF PMDFL IS SET AT THE END OF THE MATCH (SEE P_NTH), THE
*      HISTORY STACK IS SCANNED FOR MATCHING NDPAB-NDPAD PAIRS
*      AND THE CORRESPONDING PATTERN ASSIGNMENTS ARE EXECUTED.
*
*      THE FUNCTION OF THE MATCH ROUTINE FOR NDPAD (P_PAD)
*      IS SIMPLY TO REMOVE ITS ENTRY FROM THE STACK AND FAIL.
*      THIS INCLUDES REMOVING THE SPECIAL NODE POINTER STORED
*      IN ADDITION TO THE STANDARD TWO ENTRIES ON THE STACK.
       EJC
*
*      COMPOUNT PATTERN STRUCTURES (CONTINUED)
*
*      FENCE (FUNCTION)
*      ----------------
*
*           +---+            THIS NODE (P_FNA) SAVES THE
*           I A I            CURRENT HISTORY STACK AND A
*           +---+            POINTER TO NDFNB ON THE STACK.
*             I
*             I
*           +---+            THIS IS THE PATTERN STRUCTURE
*           I X I            GIVEN AS THE ARGUMENT TO THE
*           +---+            FENCE FUNCTION.
*             I
*             I
*           +---+            THIS NODE P_FNC RESTORES THE OUTER
*           I C I            HISTORY STACK PTR SAVED IN P_FNA,
*           +---+            AND STACKS THE INNER STACK BASE
*                            PTR AND A POINTER TO NDFND ON THE
*                            STACK.
*
*      NDFNB (F_FNB) SIMPLY IS THE FAILURE EXIT FOR PATTERN
*      ARGUMENT FAILURE, AND IT POPS ITSELF AND FAILS ONTO THE
*      STACK.
*
*      THE MATCH ROUTINE P_FNC ALLOWS FOR AN OPTIMIZATION WHEN
*      THE FENCE PATTERN LEAVES NO ALTERNATIVES.  IN THIS CASE,
*      THE NDFNB ENTRY IS POPPED, AND THE MATCH CONTINUES.
*
*      NDFND (P_FND) IS ENTERED WHEN THE PATTERN FAILS AFTER
*      GOING THROUGH A NON-OPTIMIZED P_FNC, AND IT POPS THE
*      STACK BACK PAST THE INNTER STACK BASE CREATED BY P_FNA
       EJC
*
*      COMPOUND PATTERNS (CONTINUED)
*
*      EXPRESSION PATTERNS (RECURSIVE PATTERN MATCHES)
*      -----------------------------------------------
*
*      INITIAL ENTRY FOR A PATTERN NODE IS TO THE ROUTINE P_EXA.
*      IF THE EVALUATED RESULT OF THE EXPRESSION IS ITSELF A
*      PATTERN, THEN THE FOLLOWING STEPS ARE TAKEN TO ARRANGE
*      FOR PROPER RECURSIVE PROCESSING.
*
*      1)   A POINTER TO THE CURRENT NODE (THE P_EXA NODE) IS
*           STORED ON THE HISTORY STACK WITH A DUMMY CURSOR.
*
*      2)   A SPECIAL HISTORY STACK ENTRY IS MADE IN WHICH THE
*           NODE POINTER POINTS TO NDEXB, AND THE CURSOR VALUE
*           IS THE SAVED VALUE OF PMHBS ON ENTRY TO THIS NODE.
*           THE MATCH ROUTINE FOR NDEXB (P_EXB) RESTORES PMHBS
*           FROM THIS CURSOR ENTRY, POPS OFF THE P_EXA NODE
*           POINTER AND FAILS.
*
*      3)   THE RESULTING HISTORY STACK POINTER IS SAVED IN
*           PMHBS TO ESTABLISH A NEW LEVEL OF HISTORY STACK.
*
*      AFTER MATCHING A PATTERN, THE END OF MATCH ROUTINE GETS
*      CONTROL (P_NTH). THIS ROUTINE PROCEEDS AS FOLLOWS.
*
*      1)   LOAD THE CURRENT VALUE OF PMHBS AND RECOGNIZE THE
*           OUTER LEVEL CASE BY THE FACT THAT THE ASSOCIATED
*           CURSOR IN THIS CASE IS THE PATTERN MATCH TYPE CODE
*           WHICH IS LESS THAN 3. TERMINATE THE MATCH IN THIS
*           CASE AND CONTINUE EXECUTION OF THE PROGRAM.
*
*      2)   OTHERWISE MAKE A SPECIAL HISTORY STACK ENTRY IN
*           WHICH THE NODE POINTER POINTS TO THE SPECIAL NODE
*           NDEXC AND THE CURSOR IS THE CURRENT VALUE OF PMHBS.
*           THE MATCH ROUTINE FOR NDEXC (P_EXC) RESETS PMHBS TO
*           THIS (INNER) VALUE AND AND THEN FAILS.
*
*      3)   USING THE HISTORY STACK ENTRY MADE ON STARTING THE
*           EXPRESSION (ACCESSIBLE WITH THE CURRENT VALUE OF
*           PMHBS), RESTORE THE P_EXA NODE POINTER AND THE OLD
*           PMHBS SETTING. TAKE THE SUCCESSOR AND CONTINUE.
*
*      AN OPTIMIZATION IS POSSIBLE IF THE EXPRESSION PATTERN
*      MAKES NO ENTRIES ON THE HISTORY STACK. IN THIS CASE,
*      INSTEAD OF BUILDING THE P_EXC NODE IN STEP 2, IT IS MORE
*      EFFICIENT TO SIMPLY POP OFF THE P_EXB ENTRY AND ITS
*      ASSOCIATED NODE POINTER. THE EFFECT IS THE SAME.
       EJC
*
*      COMPOUND PATTERNS (CONTINUED)
*
*      BINARY DOLLAR (IMMEDIATE ASSIGNMENT)
*      ------------------------------------
*
*           +---+            THIS NODE (P_IMA) STACKS THE CURSOR
*           I A I            PMHBS AND A PTR TO NDIMB AND RESETS
*           +---+            THE STACK PTR PMHBS.
*             I
*             I
*           +---+            THIS IS THE LEFT STRUCTURE FOR THE
*           I X I            PATTERN LEFT ARGUMENT OF THE
*           +---+            IMMEDIATE ASSIGNMENT CALL.
*             I
*             I
*           +---+            THIS NODE (P_IMC) PERFORMS THE
*           I C I-----       ASSIGNMENT, POPS PMHBS AND STACKS
*           +---+            THE OLD PMHBS AND A PTR TO NDIMD.
*
*
*      THE STRUCTURE AND EXECUTION OF THIS PATTERN ARE SIMILAR
*      TO THOSE OF THE RECURSIVE EXPRESSION PATTERN MATCHING.
*
*      THE MATCH ROUTINE FOR NDIMB (P_IMB) RESTORES THE OUTER
*      LEVEL VALUE OF PMHBS, UNSTACKS THE SAVED CURSOR AND FAILS
*
*      THE MATCH ROUTINE P_IMC USES THE CURRENT VALUE OF PMHBS
*      TO LOCATE THE P_IMB ENTRY. THIS ENTRY IS USED TO MAKE
*      THE ASSIGNMENT AND RESTORE THE OUTER LEVEL VALUE OF
*      PMHBS. FINALLY, THE INNER LEVEL VALUE OF PMHBS AND A
*      POINTER TO THE SPECIAL NODE NDIMD ARE STACKED.
*
*      THE MATCH ROUTINE FOR NDIMD (P_IMD) RESTORES THE INNER
*      LEVEL VALUE OF PMHBS AND FAILS BACK INTO THE STACK.
*
*      AN OPTIMIZATION OCCURS IF THE INNER PATTERN MAKES NO
*      ENTRIES ON THE HISTORY STACK. IN THIS CASE, P_IMC POPS
*      THE P_IMB ENTRY INSTEAD OF MAKING A P_IMD ENTRY.
       EJC
*
*      ARBNO
*
*      SEE COMPOUND PATTERNS SECTION FOR STUCTURE AND
*      ALGORITHM FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS
*
P_ABA  ENT  BL_P0            P0BLK
       MOV  WB,-(XS)         STACK CURSOR
       MOV  XR,-(XS)         STACK DUMMY NODE PTR
       MOV  PMHBS,-(XS)      STACK OLD STACK BASE PTR
       MOV  =NDABB,-(XS)     STACK PTR TO NODE NDABB
       MOV  XS,PMHBS         STORE NEW STACK BASE PTR
       BRN  SUCCP            SUCCEED
       EJC
*
*      ARBNO (REMOVE P_ABA SPECIAL STACK ENTRY)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_ABB  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE HISTORY STACK BASE PTR
       BRN  FLPOP            FAIL AND POP DUMMY NODE PTR
       EJC
*
*      ARBNO (CHECK IF ARG MATCHED NULL STRING)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_ABC  ENT  BL_P0            P0BLK
       MOV  PMHBS,XT         KEEP P_ABB STACK BASE
       MOV  NUM03(XT),WA     LOAD INITIAL CURSOR
       MOV  NUM01(XT),PMHBS  RESTORE OUTER STACK BASE PTR
       BEQ  XT,XS,PABC1      JUMP IF NO HISTORY STACK ENTRIES
       MOV  XT,-(XS)         ELSE SAVE INNER PMHBS ENTRY
       MOV  =NDABD,-(XS)     STACK PTR TO SPECIAL NODE NDABD
       BRN  PABC2            MERGE
*
*      OPTIMISE CASE OF NO EXTRA ENTRIES ON STACK FROM ARBNO ARG
*
PABC1  ADD  *NUM04,XS        REMOVE NDABB ENTRY AND CURSOR
*
*      MERGE TO CHECK FOR MATCHING OF NULL STRING
*
PABC2  BNE  WA,WB,SUCCP      ALLOW FURTHER ATTEMPT IF NON-NULL
       MOV  PTHEN(XR),XR     BYPASS ALTERNATIVE NODE SO AS TO ...
       BRN  SUCCP            ... REFUSE FURTHER MATCH ATTEMPTS
       EJC
*
*      ARBNO (TRY FOR ALTERNATIVES IN ARBNO ARGUMENT)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_ABD  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE INNER STACK BASE PTR
       BRN  FAILP            AND FAIL
       EJC
*
*      ABORT
*
*      NO PARAMETERS
*
P_ABO  ENT  BL_P0            P0BLK
       BRN  EXFAL            SIGNAL STATEMENT FAILURE
       EJC
*
*      ALTERNATION
*
*      PARM1                 ALTERNATIVE NODE
*
P_ALT  ENT  BL_P1            P1BLK
       MOV  WB,-(XS)         STACK CURSOR
       MOV  PARM1(XR),-(XS)  STACK POINTER TO ALTERNATIVE
       CHK                   CHECK FOR STACK OVERFLOW
       BRN  SUCCP            IF ALL OK, THEN SUCCEED
       EJC
*
*      ANY (ONE CHARACTER ARGUMENT) (1-CHAR STRING ALSO)
*
*      PARM1                 CHARACTER ARGUMENT
*
P_ANS  ENT  BL_P1            P1BLK
       BEQ  WB,PMSSL,FAILP   FAIL IF NO CHARS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       LCH  WA,(XL)          LOAD CURRENT CHARACTER
       BNE  WA,PARM1(XR),FAILP FAIL IF NO MATCH
       ICV  WB               ELSE BUMP CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      ANY (MULTI-CHARACTER ARGUMENT CASE)
*
*      PARM1                 POINTER TO CTBLK
*      PARM2                 BIT MASK TO SELECT BIT IN CTBLK
*
P_ANY  ENT  BL_P2            P2BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PANY1  BEQ  WB,PMSSL,FAILP   FAIL IF NO CHARACTERS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            GET CHAR PTR TO CURRENT CHARACTER
       LCH  WA,(XL)          LOAD CURRENT CHARACTER
       MOV  PARM1(XR),XL     POINT TO CTBLK
       WTB  WA               CHANGE TO BYTE OFFSET
       ADD  WA,XL            POINT TO ENTRY IN CTBLK
       MOV  CTCHS(XL),WA     LOAD WORD FROM CTBLK
       ANB  PARM2(XR),WA     AND WITH SELECTED BIT
       ZRB  WA,FAILP         FAIL IF NO MATCH
       ICV  WB               ELSE BUMP CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      ANY (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_AYD  ENT  BL_P1            P1BLK
       JSR  EVALS            EVALUATE STRING ARGUMENT
       ERR  043,ANY evaluated argument is not a string
       PPM  FAILP            FAIL IF EVALUATION FAILURE
       PPM  PANY1            MERGE MULTI-CHAR CASE IF OK
       EJC
*
*      P_ARB                 INITIAL ARB MATCH
*
*      NO PARAMETERS
*
*      THE P_ARB NODE IS PART OF A COMPOUND PATTERN STRUCTURE
*      FOR AN ARB PATTERN (SEE DESCRIPTION OF COMPOUND PATTERNS)
*
P_ARB  ENT  BL_P0            P0BLK
       MOV  PTHEN(XR),XR     LOAD SUCCESSOR POINTER
       MOV  WB,-(XS)         STACK DUMMY CURSOR
       MOV  XR,-(XS)         STACK SUCCESSOR POINTER
       MOV  WB,-(XS)         STACK CURSOR
       MOV  =NDARC,-(XS)     STACK PTR TO SPECIAL NODE NDARC
       BRI  (XR)             EXECUTE NEXT NODE MATCHING NULL
       EJC
*
*      P_ARC                 EXTEND ARB MATCH
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_ARC  ENT                   ENTRY POINT
       BEQ  WB,PMSSL,FLPOP   FAIL AND POP STACK TO SUCCESSOR
       ICV  WB               ELSE BUMP CURSOR
       MOV  WB,-(XS)         STACK UPDATED CURSOR
       MOV  XR,-(XS)         RESTACK POINTER TO NDARC NODE
       MOV  NUM02(XS),XR     LOAD SUCCESSOR POINTER
       BRI  (XR)             OFF TO REEXECUTE SUCCESSOR NODE
       EJC
*
*      BAL
*
*      NO PARAMETERS
*
*      THE P_BAL NODE IS PART OF THE COMPOUND STRUCTURE BUILT
*      FOR BAL (SEE SECTION ON COMPOUND PATTERNS).
*
P_BAL  ENT  BL_P0            P0BLK
       ZER  WC               ZERO PARENTHESES LEVEL COUNTER
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       BRN  PBAL2            JUMP INTO SCAN LOOP
*
*      LOOP TO SCAN OUT CHARACTERS
*
PBAL1  LCH  WA,(XL)+         LOAD NEXT CHARACTER, BUMP POINTER
       ICV  WB               PUSH CURSOR FOR CHARACTER
       BEQ  WA,=CH_PP,PBAL3  JUMP IF LEFT PAREN
       BEQ  WA,=CH_RP,PBAL4  JUMP IF RIGHT PAREN
       BZE  WC,PBAL5         ELSE SUCCEED IF AT OUTER LEVEL
*
*      HERE AFTER PROCESSING ONE CHARACTER
*
PBAL2  BNE  WB,PMSSL,PBAL1   LOOP BACK UNLESS END OF STRING
       BRN  FAILP            IN WHICH CASE, FAIL
*
*      HERE ON LEFT PAREN
*
PBAL3  ICV  WC               BUMP PAREN LEVEL
       BRN  PBAL2            LOOP BACK TO CHECK END OF STRING
*
*      HERE FOR RIGHT PAREN
*
PBAL4  BZE  WC,FAILP         FAIL IF NO MATCHING LEFT PAREN
       DCV  WC               ELSE DECREMENT LEVEL COUNTER
       BNZ  WC,PBAL2         LOOP BACK IF NOT AT OUTER LEVEL
*
*      HERE AFTER SUCCESSFULLY SCANNING A BALANCED STRING
*
PBAL5  MOV  WB,-(XS)         STACK CURSOR
       MOV  XR,-(XS)         STACK PTR TO BAL NODE FOR EXTEND
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      BREAK (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_BKD  ENT  BL_P1            P1BLK
       JSR  EVALS            EVALUATE STRING EXPRESSION
       ERR  044,BREAK evaluated argument is not a string
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PBRK1            MERGE WITH MULTI-CHAR CASE IF OK
       EJC
*
*      BREAK (ONE CHARACTER ARGUMENT)
*
*      PARM1                 CHARACTER ARGUMENT
*
P_BKS  ENT  BL_P1            P1BLK
       MOV  PMSSL,WC         GET SUBJECT STRING LENGTH
       SUB  WB,WC            GET NUMBER OF CHARACTERS LEFT
       BZE  WC,FAILP         FAIL IF NO CHARACTERS LEFT
       LCT  WC,WC            SET COUNTER FOR CHARS LEFT
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
*
*      LOOP TO SCAN TILL BREAK CHARACTER FOUND
*
PBKS1  LCH  WA,(XL)+         LOAD NEXT CHAR, BUMP POINTER
       BEQ  WA,PARM1(XR),SUCCP SUCCEED IF BREAK CHARACTER FOUND
       ICV  WB               ELSE PUSH CURSOR
       BCT  WC,PBKS1         LOOP BACK IF MORE TO GO
       BRN  FAILP            FAIL IF END OF STRING, NO BREAK CHR
       EJC
*
*      BREAK (MULTI-CHARACTER ARGUMENT)
*
*      PARM1                 POINTER TO CTBLK
*      PARM2                 BIT MASK TO SELECT BIT COLUMN
*
P_BRK  ENT  BL_P2            P2BLK
*
*      EXPRESSION ARGUMENT MERGES HERE
*
PBRK1  MOV  PMSSL,WC         LOAD SUBJECT STRING LENGTH
       SUB  WB,WC            GET NUMBER OF CHARACTERS LEFT
       BZE  WC,FAILP         FAIL IF NO CHARACTERS LEFT
       LCT  WC,WC            SET COUNTER FOR CHARACTERS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       MOV  XR,PSAVE         SAVE NODE POINTER
*
*      LOOP TO SEARCH FOR BREAK CHARACTER
*
PBRK2  LCH  WA,(XL)+         LOAD NEXT CHAR, BUMP POINTER
       MOV  PARM1(XR),XR     LOAD POINTER TO CTBLK
       WTB  WA               CONVERT TO BYTE OFFSET
       ADD  WA,XR            POINT TO CTBLK ENTRY
       MOV  CTCHS(XR),WA     LOAD CTBLK WORD
       MOV  PSAVE,XR         RESTORE NODE POINTER
       ANB  PARM2(XR),WA     AND WITH SELECTED BIT
       NZB  WA,SUCCP         SUCCEED IF BREAK CHARACTER FOUND
       ICV  WB               ELSE PUSH CURSOR
       BCT  WC,PBRK2         LOOP BACK UNLESS END OF STRING
       BRN  FAILP            FAIL IF END OF STRING, NO BREAK CHR
       EJC
*
*      BREAKX (EXTENSION)
*
*      THIS IS THE ENTRY WHICH CAUSES AN EXTENSION OF A BREAKX
*      MATCH WHEN FAILURE OCCURS. SEE SECTION ON COMPOUND
*      PATTERNS FOR FULL DETAILS OF BREAKX MATCHING.
*
*      NO PARAMETERS
*
P_BKX  ENT  BL_P0            P0BLK
       ICV  WB               STEP CURSOR PAST PREVIOUS BREAK CHR
       BRN  SUCCP            SUCCEED TO REMATCH BREAK
       EJC
*
*      BREAKX (EXPRESSION ARGUMENT)
*
*      SEE SECTION ON COMPOUND PATTERNS FOR FULL STRUCTURE OF
*      BREAKX PATTERN. THE ACTUAL CHARACTER MATCHING USES A
*      BREAK NODE. HOWEVER, THE ENTRY FOR THE EXPRESSION
*      ARGUMENT CASE IS SEPARATED TO GET PROPER ERROR MESSAGES.
*
*      PARM1                 EXPRESSION POINTER
*
P_BXD  ENT  BL_P1            P1BLK
       JSR  EVALS            EVALUATE STRING ARGUMENT
       ERR  045,BREAKX evaluated argument is not a string
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PBRK1            MERGE WITH BREAK IF ALL OK
       EJC
*
*      CURSOR ASSIGNMENT
*
*      PARM1                 NAME BASE
*      PARM2                 NAME OFFSET
*
P_CAS  ENT  BL_P2            P2BLK
       MOV  XR,-(XS)         SAVE NODE POINTER
       MOV  WB,-(XS)         SAVE CURSOR
       MOV  PARM1(XR),XL     LOAD NAME BASE
       MTI  WB               LOAD CURSOR AS INTEGER
       MOV  PARM2(XR),WB     LOAD NAME OFFSET
       JSR  ICBLD            GET ICBLK FOR CURSOR VALUE
       MOV  WB,WA            MOVE NAME OFFSET
       MOV  XR,WB            MOVE VALUE TO ASSIGN
       JSR  ASINP            PERFORM ASSIGNMENT
       PPM  FLPOP            FAIL ON ASSIGNMENT FAILURE
       MOV  (XS)+,WB         ELSE RESTORE CURSOR
       MOV  (XS)+,XR         RESTORE NODE POINTER
       BRN  SUCCP            AND SUCCEED MATCHING NULL
       EJC
*
*      EXPRESSION NODE (P_EXA, INITIAL ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR HANDLING EXPRESSION NODES.
*
*      PARM1                 EXPRESSION POINTER
*
P_EXA  ENT  BL_P1            P1BLK
       JSR  EVALP            EVALUATE EXPRESSION
       PPM  FAILP            FAIL IF EVALUATION FAILS
       BLO  WA,=P_AAA,PEXA1  JUMP IF RESULT IS NOT A PATTERN
*
*      HERE IF RESULT OF EXPRESSION IS A PATTERN
*
       MOV  WB,-(XS)         STACK DUMMY CURSOR
       MOV  XR,-(XS)         STACK PTR TO P_EXA NODE
       MOV  PMHBS,-(XS)      STACK HISTORY STACK BASE PTR
       MOV  =NDEXB,-(XS)     STACK PTR TO SPECIAL NODE NDEXB
       MOV  XS,PMHBS         STORE NEW STACK BASE POINTER
       MOV  XL,XR            COPY NODE POINTER
       BRI  (XR)             MATCH FIRST NODE IN EXPRESSION PAT
*
*      HERE IF RESULT OF EXPRESSION IS NOT A PATTERN
*
PEXA1  BEQ  WA,=B_SCL,PEXA2  JUMP IF IT IS ALREADY A STRING
       MOV  XL,-(XS)         ELSE STACK RESULT
       MOV  XR,XL            SAVE NODE POINTER
       JSR  GTSTG            CONVERT RESULT TO STRING
       ERR  046,Expression does not evaluate to pattern
       MOV  XR,WC            COPY STRING POINTER
       MOV  XL,XR            RESTORE NODE POINTER
       MOV  WC,XL            COPY STRING POINTER AGAIN
*
*      MERGE HERE WITH STRING POINTER IN XL
*
PEXA2  BZE  SCLEN(XL),SUCCP  JUST SUCCEED IF NULL STRING
       BRN  PSTR1            ELSE MERGE WITH STRING CIRCUIT
       EJC
*
*      EXPRESSION NODE (P_EXB, REMOVE NDEXB ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR HANDLING EXPRESSION NODES.
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_EXB  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE OUTER LEVEL STACK POINTER
       BRN  FLPOP            FAIL AND POP P_EXA NODE PTR
       EJC
*
*      EXPRESSION NODE (P_EXC, REMOVE NDEXC ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR HANDLING EXPRESSION NODES.
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_EXC  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE INNER STACK BASE POINTER
       BRN  FAILP            AND FAIL INTO EXPR PATTERN ALTERNVS
       EJC
*
*      FAIL
*
*      NO PARAMETERS
*
P_FAL  ENT  BL_P0            P0BLK
       BRN  FAILP            JUST SIGNAL FAILURE
       EJC
*
*      FENCE
*
*      SEE COMPOUND PATTERNS SECTION FOR THE STRUCTURE AND
*      ALGORITHM FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS
*
P_FEN  ENT  BL_P0            P0BLK
       MOV  WB,-(XS)         STACK DUMMY CURSOR
       MOV  =NDABO,-(XS)     STACK PTR TO ABORT NODE
       BRN  SUCCP            AND SUCCEED MATCHING NULL
       EJC
*
*      FENCE (FUNCTION)
*
*      SEE COMPOUND PATTERNS COMMENTS AT START OF THIS SECTION
*      FOR DETAILS OF SCHEME
*
*      NO PARAMETERS
*
P_FNA  ENT  BL_P0            P0BLK
       MOV  PMHBS,-(XS)      STACK CURRENT HISTORY STACK BASE
       MOV  =NDFNB,-(XS)     STACK INDIR PTR TO P_FNB (FAILURE)
       MOV  XS,PMHBS         BEGIN NEW HISTORY STACK
       BRN  SUCCP            SUCCEED
       EJC
*
*      FENCE (FUNCTION) (RESET HISTORY STACK AND FAIL)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_FNB  ENT  BL_P0            P0BLK
       MOV  WB,PMHBS         RESTORE OUTER PMHBS STACK BASE
       BRN  FAILP            ...AND FAIL
       EJC
*
*      FENCE (FUNCTION) (MAKE FENCE TRAP ENTRY ON STACK)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_FNC  ENT  BL_P0            P0BLK
       MOV  PMHBS,XT         GET INNER STACK BASE PTR
       MOV  NUM01(XT),PMHBS  RESTORE OUTER STACK BASE
       BEQ  XT,XS,PFNC1      OPTIMIZE IF NO ALTERNATIVES
       MOV  XT,-(XS)         ELSE STACK INNER STACK BASE
       MOV  =NDFND,-(XS)     STACK PTR TO NDFND
       BRN  SUCCP            SUCCEED
*
*      HERE WHEN FENCE FUNCTION LEFT NOTHING ON THE STACK
*
PFNC1  ADD  *NUM02,XS        POP OFF P_FNB ENTRY
       BRN  SUCCP            SUCCEED
       EJC
*
*      FENCE (FUNCTION) (SKIP PAST ALTERNATIVES ON FAILURE)
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_FND  ENT  BL_P0            P0BLK
       MOV  WB,XS            POP STACK TO FENCE() HISTORY BASE
       BRN  FLPOP            POP BASE ENTRY AND FAIL
       EJC
*
*      IMMEDIATE ASSIGNMENT (INITIAL ENTRY, SAVE CURRENT CURSOR)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR DETAILS OF THE
*      STRUCTURE AND ALGORITHM FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS
*
P_IMA  ENT  BL_P0            P0BLK
       MOV  WB,-(XS)         STACK CURSOR
       MOV  XR,-(XS)         STACK DUMMY NODE POINTER
       MOV  PMHBS,-(XS)      STACK OLD STACK BASE POINTER
       MOV  =NDIMB,-(XS)     STACK PTR TO SPECIAL NODE NDIMB
       MOV  XS,PMHBS         STORE NEW STACK BASE POINTER
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      IMMEDIATE ASSIGNMENT (REMOVE CURSOR MARK ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR DETAILS OF THE
*      STRUCTURE AND ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_IMB  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE HISTORY STACK BASE PTR
       BRN  FLPOP            FAIL AND POP DUMMY NODE PTR
       EJC
*
*      IMMEDIATE ASSIGNMENT (PERFORM ACTUAL ASSIGNMENT)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR DETAILS OF THE
*      STRUCTURE AND ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      PARM1                 NAME BASE OF VARIABLE
*      PARM2                 NAME OFFSET OF VARIABLE
*
P_IMC  ENT  BL_P2            P2BLK
       MOV  PMHBS,XT         LOAD POINTER TO P_IMB ENTRY
       MOV  WB,WA            COPY FINAL CURSOR
       MOV  NUM03(XT),WB     LOAD INITIAL CURSOR
       MOV  NUM01(XT),PMHBS  RESTORE OUTER STACK BASE POINTER
       BEQ  XT,XS,PIMC1      JUMP IF NO HISTORY STACK ENTRIES
       MOV  XT,-(XS)         ELSE SAVE INNER PMHBS POINTER
       MOV  =NDIMD,-(XS)     AND A PTR TO SPECIAL NODE NDIMD
       BRN  PIMC2            MERGE
*
*      HERE IF NO ENTRIES MADE ON HISTORY STACK
*
PIMC1  ADD  *NUM04,XS        REMOVE NDIMB ENTRY AND CURSOR
*
*      MERGE HERE TO PERFORM ASSIGNMENT
*
PIMC2  MOV  WA,-(XS)         SAVE CURRENT (FINAL) CURSOR
       MOV  XR,-(XS)         SAVE CURRENT NODE POINTER
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       SUB  WB,WA            COMPUTE SUBSTRING LENGTH
       JSR  SBSTR            BUILD SUBSTRING
       MOV  XR,WB            MOVE RESULT
       MOV  (XS),XR          RELOAD NODE POINTER
       MOV  PARM1(XR),XL     LOAD NAME BASE
       MOV  PARM2(XR),WA     LOAD NAME OFFSET
       JSR  ASINP            PERFORM ASSIGNMENT
       PPM  FLPOP            FAIL IF ASSIGNMENT FAILS
       MOV  (XS)+,XR         ELSE RESTORE NODE POINTER
       MOV  (XS)+,WB         RESTORE CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      IMMEDIATE ASSIGNMENT (REMOVE NDIMD ENTRY ON FAILURE)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR DETAILS OF THE
*      STRUCTURE AND ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_IMD  ENT                   ENTRY POINT
       MOV  WB,PMHBS         RESTORE INNER STACK BASE POINTER
       BRN  FAILP            AND FAIL
       EJC
*
*      LEN (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
P_LEN  ENT  BL_P1            P1BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PLEN1  ADD  PARM1(XR),WB     PUSH CURSOR INDICATED AMOUNT
       BLE  WB,PMSSL,SUCCP   SUCCEED IF NOT OFF END
       BRN  FAILP            ELSE FAIL
       EJC
*
*      LEN (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_LND  ENT  BL_P1            P1BLK
       JSR  EVALI            EVALUATE INTEGER ARGUMENT
       ERR  047,LEN evaluated argument is not integer
       ERR  048,LEN evaluated argument is negative or too large
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PLEN1            MERGE WITH NORMAL CIRCUIT IF OK
       EJC
*
*      NOTANY (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_NAD  ENT  BL_P1            P1BLK
       JSR  EVALS            EVALUATE STRING ARGUMENT
       ERR  049,NOTANY evaluated argument is not a string
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PNAY1            MERGE WITH MULTI-CHAR CASE IF OK
       EJC
*
*      NOTANY (ONE CHARACTER ARGUMENT)
*
*      PARM1                 CHARACTER ARGUMENT
*
P_NAS  ENT  BL_P1            ENTRY POINT
       BEQ  WB,PMSSL,FAILP   FAIL IF NO CHARS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER IN STRIN
       LCH  WA,(XL)          LOAD CURRENT CHARACTER
       BEQ  WA,PARM1(XR),FAILP FAIL IF MATCH
       ICV  WB               ELSE BUMP CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      NOTANY (MULTI-CHARACTER STRING ARGUMENT)
*
*      PARM1                 POINTER TO CTBLK
*      PARM2                 BIT MASK TO SELECT BIT COLUMN
*
P_NAY  ENT  BL_P2            P2BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PNAY1  BEQ  WB,PMSSL,FAILP   FAIL IF NO CHARACTERS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       LCH  WA,(XL)          LOAD CURRENT CHARACTER
       WTB  WA               CONVERT TO BYTE OFFSET
       MOV  PARM1(XR),XL     LOAD POINTER TO CTBLK
       ADD  WA,XL            POINT TO ENTRY IN CTBLK
       MOV  CTCHS(XL),WA     LOAD ENTRY FROM CTBLK
       ANB  PARM2(XR),WA     AND WITH SELECTED BIT
       NZB  WA,FAILP         FAIL IF CHARACTER IS MATCHED
       ICV  WB               ELSE BUMP CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      END OF PATTERN MATCH
*
*      THIS ROUTINE IS ENTERED ON SUCCESSFUL COMPLETION.
*      SEE DESCRIPTION OF EXPRESSION PATTERNS IN COMPOUND
*      PATTERN SECTION FOR HANDLING OF RECURSION IN MATCHING.
*
*      THIS PATTERN ALSO RESULTS FROM AN ATTEMPT TO CONVERT THE
*      NULL STRING TO A PATTERN VIA CONVERT()
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_NTH  ENT  BL_P0            P0BLK (DUMMY)
       MOV  PMHBS,XT         LOAD POINTER TO BASE OF STACK
       MOV  NUM01(XT),WA     LOAD SAVED PMHBS (OR PATTERN TYPE)
       BLE  WA,=NUM02,PNTH2  JUMP IF OUTER LEVEL (PATTERN TYPE)
*
*      HERE WE ARE AT THE END OF MATCHING AN EXPRESSION PATTERN
*
       MOV  WA,PMHBS         RESTORE OUTER STACK BASE POINTER
       MOV  NUM02(XT),XR     RESTORE POINTER TO P_EXA NODE
       BEQ  XT,XS,PNTH1      JUMP IF NO HISTORY STACK ENTRIES
       MOV  XT,-(XS)         ELSE STACK INNER STACK BASE PTR
       MOV  =NDEXC,-(XS)     STACK PTR TO SPECIAL NODE NDEXC
       BRN  SUCCP            AND SUCCEED
*
*      HERE IF NO HISTORY STACK ENTRIES DURING PATTERN
*
PNTH1  ADD  *NUM04,XS        REMOVE P_EXB ENTRY AND NODE PTR
       BRN  SUCCP            AND SUCCEED
*
*      HERE IF END OF MATCH AT OUTER LEVEL
*
PNTH2  MOV  WB,PMSSL         SAVE FINAL CURSOR IN SAFE PLACE
       BZE  PMDFL,PNTH6      JUMP IF NO PATTERN ASSIGNMENTS
       EJC
*
*      END OF PATTERN MATCH (CONTINUED)
*
*      NOW WE MUST PERFORM PATTERN ASSIGNMENTS. THIS IS DONE BY
*      SCANNING THE HISTORY STACK FOR MATCHING NDPAB-NDPAD PAIRS
*
PNTH3  DCA  XT               POINT PAST CURSOR ENTRY
       MOV  -(XT),WA         LOAD NODE POINTER
       BEQ  WA,=NDPAD,PNTH4  JUMP IF NDPAD ENTRY
       BNE  WA,=NDPAB,PNTH5  JUMP IF NOT NDPAB ENTRY
*
*      HERE FOR NDPAB ENTRY, STACK INITIAL CURSOR
*      NOTE THAT THERE MUST BE MORE ENTRIES ON THE STACK.
*
       MOV  NUM01(XT),-(XS)  STACK INITIAL CURSOR
       CHK                   CHECK FOR STACK OVERFLOW
       BRN  PNTH3            LOOP BACK IF OK
*
*      HERE FOR NDPAD ENTRY. THE STARTING CURSOR FROM THE
*      MATCHING NDPAD ENTRY IS NOW THE TOP STACK ENTRY.
*
PNTH4  MOV  NUM01(XT),WA     LOAD FINAL CURSOR
       MOV  (XS),WB          LOAD INITIAL CURSOR FROM STACK
       MOV  XT,(XS)          SAVE HISTORY STACK SCAN PTR
       SUB  WB,WA            COMPUTE LENGTH OF STRING
*
*      BUILD SUBSTRING AND PERFORM ASSIGNMENT
*
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       JSR  SBSTR            CONSTRUCT SUBSTRING
       MOV  XR,WB            COPY SUBSTRING POINTER
       MOV  (XS),XT          RELOAD HISTORY STACK SCAN PTR
       MOV  NUM02(XT),XL     LOAD POINTER TO P_PAC NODE WITH NAM
       MOV  PARM2(XL),WA     LOAD NAME OFFSET
       MOV  PARM1(XL),XL     LOAD NAME BASE
       JSR  ASINP            PERFORM ASSIGNMENT
       PPM  EXFAL            MATCH FAILS IF NAME EVAL FAILS
       MOV  (XS)+,XT         ELSE RESTORE HISTORY STACK PTR
       EJC
*
*      END OF PATTERN MATCH (CONTINUED)
*
*      HERE CHECK FOR END OF ENTRIES
*
PNTH5  BNE  XT,XS,PNTH3      LOOP IF MORE ENTRIES TO SCAN
*
*      HERE AFTER DEALING WITH PATTERN ASSIGNMENTS
*
PNTH6  MOV  PMHBS,XS         WIPE OUT HISTORY STACK
       MOV  (XS)+,WB         LOAD INITIAL CURSOR
       MOV  (XS)+,WC         LOAD MATCH TYPE CODE
       MOV  PMSSL,WA         LOAD FINAL CURSOR VALUE
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       ZER  R_PMS            CLEAR SUBJECT STRING PTR FOR GBCOL
       BZE  WC,PNTH7         JUMP IF CALL BY NAME
       BEQ  WC,=NUM02,PNTH9  EXIT IF STATEMENT LEVEL CALL
*
*      HERE WE HAVE A CALL BY VALUE, BUILD SUBSTRING
*
       SUB  WB,WA            COMPUTE LENGTH OF STRING
       JSR  SBSTR            BUILD SUBSTRING
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      HERE FOR CALL BY NAME, MAKE STACK ENTRIES FOR O_RPL
*
PNTH7  MOV  WB,-(XS)         STACK INITIAL CURSOR
       MOV  WA,-(XS)         STACK FINAL CURSOR
.IF    .CNBF
.ELSE
       BZE  R_PMB,PNTH8      SKIP IF SUBJECT NOT BUFFER
       MOV  R_PMB,XL         ELSE GET PTR TO BCBLK INSTEAD
.FI
*
*      HERE WITH XL POINTING TO SCBLK OR BCBLK
*
PNTH8  MOV  XL,-(XS)         STACK SUBJECT POINTER
*
*      HERE TO OBEY NEXT CODE WORD
*
PNTH9  LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      POS (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
P_POS  ENT  BL_P1            P1BLK
*
*      OPTIMIZE POS IF IT IS THE FIRST PATTERN ELEMENT,
*      UNANCHORED MODE, CURSOR IS ZERO AND POS ARGUMENT
*      IS NOT BEYOND END OF STRING.  FORCE CURSOR POSITION
*      AND NUMBER OF UNANCHORED MOVES.
*
*      THIS OPTIMIZATION IS PERFORMED INVISIBLE PROVIDED
*      THE ARGUMENT IS EITHER A SIMPLE INTEGER OR AN
*      EXPRESSION THAT IS AN UNTRACED VARIABLE (THAT IS,
*      IT HAS NO SIDE EFFECTS THAT WOULD BE LOST BY SHORT-
*      CIRCUITING THE NORMAL LOGIC OF FAILING AND MOVING THE
*      UNANCHORED STARTING POINT.)
*
*      POS (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
       BEQ  WB,PARM1(XR),SUCCP SUCCEED IF AT RIGHT LOCATION
       BNZ  WB,FAILP         DON'T LOOK FURTHER IF CURSOR NOT 0
       MOV  PMHBS,XT         GET HISTORY STACK BASE PTR
       BNE  XR,-(XT),FAILP   FAIL IF POS IS NOT FIRST NODE
*
*      EXPRESSION ARGUMENT CIRCUIT MERGES HERE
*
PPOS2  BNE  -(XT),=NDUNA,FAILP  FAIL IF NOT UNANCHORED MODE
       MOV  PARM1(XR),WB     GET DESIRED CURSOR POSITION
       BGT  WB,PMSSL,EXFAL   ABORT IF OFF END
       MOV  WB,NUM02(XT)     FAKE NUMBER OF UNANCHORED MOVES
       BRN  SUCCP            CONTINUE MATCH WITH ADJUSTED CURSOR
       EJC
*
*      POS (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_PSD  ENT  BL_P1            P1BLK
       JSR  EVALI            EVALUATE INTEGER ARGUMENT
       ERR  050,POS evaluated argument is not integer
       ERR  051,POS evaluated argument is negative or too large
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PPOS1            PROCESS EXPRESSION CASE
*
PPOS1  BEQ  WB,PARM1(XR),SUCCP SUCCEED IF AT RIGHT LOCATION
       BNZ  WB,FAILP         DON'T LOOK FURTHER IF CURSOR NOT 0
       BNZ  EVLIF,FAILP      FAIL IF COMPLEX ARGUMENT
       MOV  PMHBS,XT         GET HISTORY STACK BASE PTR
       MOV  EVLIO,WA         GET ORIGINAL NODE PTR
       BNE  WA,-(XT),FAILP   FAIL IF POS IS NOT FIRST NODE
       BRN  PPOS2            MERGE WITH INTEGER ARGUMENT CODE
       EJC
*
*      PATTERN ASSIGNMENT (INITIAL ENTRY, SAVE CURSOR)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS
*
P_PAA  ENT  BL_P0            P0BLK
       MOV  WB,-(XS)         STACK INITIAL CURSOR
       MOV  =NDPAB,-(XS)     STACK PTR TO NDPAB SPECIAL NODE
       BRN  SUCCP            AND SUCCEED MATCHING NULL
       EJC
*
*      PATTERN ASSIGNMENT (REMOVE SAVED CURSOR)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS (DUMMY PATTERN)
*
P_PAB  ENT                   ENTRY POINT
       BRN  FAILP            JUST FAIL (ENTRY IS ALREADY POPPED)
       EJC
*
*      PATTERN ASSIGNMENT (END OF MATCH, MAKE ASSIGN ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      PARM1                 NAME BASE OF VARIABLE
*      PARM2                 NAME OFFSET OF VARIABLE
*
P_PAC  ENT  BL_P2            P2BLK
       MOV  WB,-(XS)         STACK DUMMY CURSOR VALUE
       MOV  XR,-(XS)         STACK POINTER TO P_PAC NODE
       MOV  WB,-(XS)         STACK FINAL CURSOR
       MOV  =NDPAD,-(XS)     STACK PTR TO SPECIAL NDPAD NODE
       MNZ  PMDFL            SET DOT FLAG NON-ZERO
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      PATTERN ASSIGNMENT (REMOVE ASSIGN ENTRY)
*
*      SEE COMPOUND PATTERNS DESCRIPTION FOR THE STRUCTURE AND
*      ALGORITHMS FOR MATCHING THIS NODE TYPE.
*
*      NO PARAMETERS (DUMMY NODE)
*
P_PAD  ENT                   ENTRY POINT
       BRN  FLPOP            FAIL AND REMOVE P_PAC NODE
       EJC
*
*      REM
*
*      NO PARAMETERS
*
P_REM  ENT  BL_P0            P0BLK
       MOV  PMSSL,WB         POINT CURSOR TO END OF STRING
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      RPOS (EXPRESSION ARGUMENT)
*
*      OPTIMIZE RPOS IF IT IS THE FIRST PATTERN ELEMENT,
*      UNANCHORED MODE, CURSOR IS ZERO AND RPOS ARGUMENT
*      IS NOT BEYOND END OF STRING.  FORCE CURSOR POSITION
*      AND NUMBER OF UNANCHORED MOVES.
*
*      THIS OPTIMIZATION IS PERFORMED INVISIBLY PROVIDED
*      THE ARGUMENT IS EITHER A SIMPLE INTEGER OR AN
*      EXPRESSION THAT IS AN UNTRACED VARIABLE (THAT IS,
*      IT HAS NO SIDE EFFECTS THAT WOULD BE LOST BY SHORT-
*      CIRCUITING THE NORMAL LOGIC OF FAILING AND MOVING THE
*      UNANCHORED STARTING POINT).
*
*      PARM1                 EXPRESSION POINTER
*
P_RPD  ENT  BL_P1            P1BLK
       JSR  EVALI            EVALUATE INTEGER ARGUMENT
       ERR  052,RPOS evaluated argument is not integer
       ERR  053,RPOS evaluated argument is negative or too large
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PRPS1            MERGE WITH NORMAL CASE IF OK
*
PRPS1  MOV  PMSSL,WC         GET LENGTH OF STRING
       SUB  WB,WC            GET NUMBER OF CHARACTERS REMAINING
       BEQ  WC,PARM1(XR),SUCCP SUCCEED IF AT RIGHT LOCATION
       BNZ  WB,FAILP         DON'T LOOK FURTHER IF CURSOR NOT 0
       BNZ  EVLIF,FAILP      FAIL IF COMPLEX ARGUMENT
       MOV  PMHBS,XT         GET HISTORY STACK BASE PTR
       MOV  EVLIO,WA         GET ORIGINAL NODE PTR
       BNE  WA,-(XT),FAILP   FAIL IF POS IS NOT FIRST NODE
       BRN  PRPS2            MERGE WITH INTEGER ARG CODE
       EJC
*
*      RPOS (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
P_RPS  ENT  BL_P1            P1BLK
*
*      RPOS (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
       MOV  PMSSL,WC         GET LENGTH OF STRING
       SUB  WB,WC            GET NUMBER OF CHARACTERS REMAINING
       BEQ  WC,PARM1(XR),SUCCP SUCCEED IF AT RIGHT LOCATION
       BNZ  WB,FAILP         DON'T LOOK FURTHER IF CURSOR NOT 0
       MOV  PMHBS,XT         GET HISTORY STACK BASE PTR
       BNE  XR,-(XT),FAILP   FAIL IF RPOS IS NOT FIRST NODE
*
*      EXPRESSION ARGUMENT MERGES HERE
*
PRPS2  BNE  -(XT),=NDUNA,FAILP  FAIL IF NOT UNANCHORED MODE
       MOV  PMSSL,WB         POINT TO END OF STRING
       BLT  WB,PARM1(XR),FAILP FAIL IF STRING NOT LONG ENOUGH
       SUB  PARM1(XR),WB     ELSE SET NEW CURSOR
       MOV  WB,NUM02(XT)     FAKE NUMBER OF UNANCHORED MOVES
       BRN  SUCCP            CONTINUE MATCH WITH ADJUSTED CURSOR
       EJC
*
*      RTAB (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
P_RTB  ENT  BL_P1            P1BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PRTB1  MOV  WB,WC            SAVE INITIAL CURSOR
       MOV  PMSSL,WB         POINT TO END OF STRING
       BLT  WB,PARM1(XR),FAILP FAIL IF STRING NOT LONG ENOUGH
       SUB  PARM1(XR),WB     ELSE SET NEW CURSOR
       BGE  WB,WC,SUCCP      AND SUCCEED IF NOT TOO FAR ALREADY
       BRN  FAILP            IN WHICH CASE, FAIL
       EJC
*
*      RTAB (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_RTD  ENT  BL_P1            P1BLK
       JSR  EVALI            EVALUATE INTEGER ARGUMENT
       ERR  054,RTAB evaluated argument is not integer
       ERR  055,RTAB evaluated argument is negative or too large
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PRTB1            MERGE WITH NORMAL CASE IF SUCCESS
       EJC
*
*      SPAN (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_SPD  ENT  BL_P1            P1BLK
       JSR  EVALS            EVALUATE STRING ARGUMENT
       ERR  056,SPAN evaluated argument is not a string
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PSPN1            MERGE WITH MULTI-CHAR CASE IF OK
       EJC
*
*      SPAN (MULTI-CHARACTER ARGUMENT CASE)
*
*      PARM1                 POINTER TO CTBLK
*      PARM2                 BIT MASK TO SELECT BIT COLUMN
*
P_SPN  ENT  BL_P2            P2BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PSPN1  MOV  PMSSL,WC         COPY SUBJECT STRING LENGTH
       SUB  WB,WC            CALCULATE NUMBER OF CHARACTERS LEFT
       BZE  WC,FAILP         FAIL IF NO CHARACTERS LEFT
       MOV  R_PMS,XL         POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       MOV  WB,PSAVC         SAVE INITIAL CURSOR
       MOV  XR,PSAVE         SAVE NODE POINTER
       LCT  WC,WC            SET COUNTER FOR CHARS LEFT
*
*      LOOP TO SCAN MATCHING CHARACTERS
*
PSPN2  LCH  WA,(XL)+         LOAD NEXT CHARACTER, BUMP POINTER
       WTB  WA               CONVERT TO BYTE OFFSET
       MOV  PARM1(XR),XR     POINT TO CTBLK
       ADD  WA,XR            POINT TO CTBLK ENTRY
       MOV  CTCHS(XR),WA     LOAD CTBLK ENTRY
       MOV  PSAVE,XR         RESTORE NODE POINTER
       ANB  PARM2(XR),WA     AND WITH SELECTED BIT
       ZRB  WA,PSPN3         JUMP IF NO MATCH
       ICV  WB               ELSE PUSH CURSOR
       BCT  WC,PSPN2         LOOP BACK UNLESS END OF STRING
*
*      HERE AFTER SCANNING MATCHING CHARACTERS
*
PSPN3  BNE  WB,PSAVC,SUCCP   SUCCEED IF CHARS MATCHED
       BRN  FAILP            ELSE FAIL IF NULL STRING MATCHED
       EJC
*
*      SPAN (ONE CHARACTER ARGUMENT)
*
*      PARM1                 CHARACTER ARGUMENT
*
P_SPS  ENT  BL_P1            P1BLK
       MOV  PMSSL,WC         GET SUBJECT STRING LENGTH
       SUB  WB,WC            CALCULATE NUMBER OF CHARACTERS LEFT
       BZE  WC,FAILP         FAIL IF NO CHARACTERS LEFT
       MOV  R_PMS,XL         ELSE POINT TO SUBJECT STRING
       PLC  XL,WB            POINT TO CURRENT CHARACTER
       MOV  WB,PSAVC         SAVE INITIAL CURSOR
       LCT  WC,WC            SET COUNTER FOR CHARACTERS LEFT
*
*      LOOP TO SCAN MATCHING CHARACTERS
*
PSPS1  LCH  WA,(XL)+         LOAD NEXT CHARACTER, BUMP POINTER
       BNE  WA,PARM1(XR),PSPS2 JUMP IF NO MATCH
       ICV  WB               ELSE PUSH CURSOR
       BCT  WC,PSPS1         AND LOOP UNLESS END OF STRING
*
*      HERE AFTER SCANNING MATCHING CHARACTERS
*
PSPS2  BNE  WB,PSAVC,SUCCP   SUCCEED IF CHARS MATCHED
       BRN  FAILP            FAIL IF NULL STRING MATCHED
       EJC
*
*      MULTI-CHARACTER STRING
*
*      NOTE THAT ONE CHARACTER STRINGS USE THE CIRCUIT FOR
*      ONE CHARACTER ANY ARGUMENTS (P_AN1).
*
*      PARM1                 POINTER TO SCBLK FOR STRING ARG
*
P_STR  ENT  BL_P1            P1BLK
       MOV  PARM1(XR),XL     GET POINTER TO STRING
*
*      MERGE HERE AFTER EVALUATING EXPRESSION WITH STRING VALUE
*
PSTR1  MOV  XR,PSAVE         SAVE NODE POINTER
       MOV  R_PMS,XR         LOAD SUBJECT STRING POINTER
       PLC  XR,WB            POINT TO CURRENT CHARACTER
       ADD  SCLEN(XL),WB     COMPUTE NEW CURSOR POSITION
       BGT  WB,PMSSL,FAILP   FAIL IF PAST END OF STRING
       MOV  WB,PSAVC         SAVE UPDATED CURSOR
       MOV  SCLEN(XL),WA     GET NUMBER OF CHARS TO COMPARE
       PLC  XL               POINT TO CHARS OF TEST STRING
       CMC  FAILP,FAILP      COMPARE, FAIL IF NOT EQUAL
       MOV  PSAVE,XR         IF ALL MATCHED, RESTORE NODE PTR
       MOV  PSAVC,WB         RESTORE UPDATED CURSOR
       BRN  SUCCP            AND SUCCEED
       EJC
*
*      SUCCEED
*
*      SEE SECTION ON COMPOUND PATTERNS FOR DETAILS OF THE
*      STRUCTURE AND ALGORITHMS FOR MATCHING THIS NODE TYPE
*
*      NO PARAMETERS
*
P_SUC  ENT  BL_P0            P0BLK
       MOV  WB,-(XS)         STACK CURSOR
       MOV  XR,-(XS)         STACK POINTER TO THIS NODE
       BRN  SUCCP            SUCCEED MATCHING NULL
       EJC
*
*      TAB (INTEGER ARGUMENT)
*
*      PARM1                 INTEGER ARGUMENT
*
P_TAB  ENT  BL_P1            P1BLK
*
*      EXPRESSION ARGUMENT CASE MERGES HERE
*
PTAB1  BGT  WB,PARM1(XR),FAILP FAIL IF TOO FAR ALREADY
       MOV  PARM1(XR),WB     ELSE SET NEW CURSOR POSITION
       BLE  WB,PMSSL,SUCCP   SUCCEED IF NOT OFF END
       BRN  FAILP            ELSE FAIL
       EJC
*
*      TAB (EXPRESSION ARGUMENT)
*
*      PARM1                 EXPRESSION POINTER
*
P_TBD  ENT  BL_P1            P1BLK
       JSR  EVALI            EVALUATE INTEGER ARGUMENT
       ERR  057,TAB evaluated argument is not integer
       ERR  058,TAB evaluated argument is negative or too large
       PPM  FAILP            FAIL IF EVALUATION FAILS
       PPM  PTAB1            MERGE WITH NORMAL CASE IF OK
       EJC
*
*      ANCHOR MOVEMENT
*
*      NO PARAMETERS (DUMMY NODE)
*
P_UNA  ENT                   ENTRY POINT
       MOV  WB,XR            COPY INITIAL PATTERN NODE POINTER
       MOV  (XS),WB          GET INITIAL CURSOR
       BEQ  WB,PMSSL,EXFAL   MATCH FAILS IF AT END OF STRING
       ICV  WB               ELSE INCREMENT CURSOR
       MOV  WB,(XS)          STORE INCREMENTED CURSOR
       MOV  XR,-(XS)         RESTACK INITIAL NODE PTR
       MOV  =NDUNA,-(XS)     RESTACK UNANCHORED NODE
       BRI  (XR)             REMATCH FIRST NODE
       EJC
*
*      END OF PATTERN MATCH ROUTINES
*
*      THE FOLLOWING ENTRY POINT MARKS THE END OF THE PATTERN
*      MATCHING ROUTINES AND ALSO THE END OF THE ENTRY POINTS
*      REFERENCED FROM THE FIRST WORD OF BLOCKS IN DYNAMIC STORE
*
P_YYY  ENT  BL__I            MARK LAST ENTRY IN PATTERN SECTION
       TTL  S P I T B O L -- SNOBOL4 BUILT-IN LABEL ROUTINES
*
*      THE FOLLOWING SECTION CONTAINS THE ROUTINES FOR LABELS
*      WHICH HAVE A PREDEFINED MEANING IN SNOBOL4.
*
*      CONTROL IS PASSED DIRECTLY TO THE LABEL NAME ENTRY POINT.
*
*      ENTRY NAMES ARE OF THE FORM L_XXX WHERE XXX IS THE THREE
*      LETTER VARIABLE NAME IDENTIFIER.
*
*      ENTRIES ARE IN ALPHABETICAL ORDER
       EJC
*
*      ABORT
*
L_ABO  ENT                   ENTRY POINT
*
*      MERGE HERE IF EXECUTION TERMINATES IN ERROR
*
LABO1  MOV  KVERT,WA         LOAD ERROR CODE
       BZE  WA,LABO3         JUMP IF NO ERROR HAS OCCURED
.IF    .CSAX
       JSR  SYSAX            CALL AFTER EXECUTION PROC
.FI
.IF    .CERA
.IF    .CSFN
       MOV  KVSTN,WC         CURRENT STATEMENT
       JSR  FILNM            OBTAIN FILE NAME FOR THIS STATEMENT
.FI
.IF    .CSLN
       MOV  R_COD,XR         CURRENT CODE BLOCK
       MOV  CDSLN(XR),WC     LINE NUMBER
.ELSE
       ZER  WC               LINE NUMBER
.FI
       ZER  WB               COLUMN NUMBER
       MOV  STAGE,XR
       JSR  SYSEA            ADVISE SYSTEM OF ERROR
       PPM  STPR4            IF SYSTEM DOES NOT WANT PRINT
.FI
       JSR  PRTPG            ELSE EJECT PRINTER
.IF    .CERA
       BZE  XR,LABO2         DID SYSEA REQUEST PRINT
       JSR  PRTST            PRINT TEXT FROM SYSEA
.FI
LABO2  JSR  ERMSG            PRINT ERROR MESSAGE
       ZER  XR               INDICATE NO MESSAGE TO PRINT
       BRN  STOPR            JUMP TO ROUTINE TO STOP RUN
*
*      HERE IF NO ERROR HAD OCCURED
*
LABO3  ERB  036,Goto ABORT with no preceding error
       EJC
*
*      CONTINUE
*
L_CNT  ENT                   ENTRY POINT
*
*      MERGE HERE AFTER EXECUTION ERROR
*
LCNT1  MOV  R_CNT,XR         LOAD CONTINUATION CODE BLOCK PTR
       BZE  XR,LCNT3         JUMP IF NO PREVIOUS ERROR
       ZER  R_CNT            CLEAR FLAG
       MOV  XR,R_COD         ELSE STORE AS NEW CODE BLOCK PTR
       BNE  (XR),=B_CDC,LCNT2 JUMP IF NOT COMPLEX GO
       MOV  STXOC,WA         GET OFFSET OF ERROR
       BGE  WA,STXOF,LCNT4   JUMP IF ERROR IN GOTO EVALUATION
*
*      HERE IF ERROR DID NOT OCCUR IN COMPLEX FAILURE GOTO
*
LCNT2  ADD  STXOF,XR         ADD FAILURE OFFSET
       LCP  XR               LOAD CODE POINTER
       MOV  FLPTR,XS         RESET STACK POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      HERE IF NO PREVIOUS ERROR
*
LCNT3  ICV  ERRFT            FATAL ERROR
       ERB  037,Goto CONTINUE with no preceding error
*
*      HERE IF ERROR IN EVALUATION OF FAILURE GOTO.
*      CANNOT CONTINUE BACK TO FAILURE GOTO!
*
LCNT4  ICV  ERRFT            FATAL ERROR
       ERB  332,Goto CONTINUE with error in failure goto
       EJC
*
*      END
*
L_END  ENT                   ENTRY POINT
*
*      MERGE HERE FROM END CODE CIRCUIT
*
LEND0  MOV  =ENDMS,XR        POINT TO MESSAGE /NORMAL TERM.../
       BRN  STOPR            JUMP TO ROUTINE TO STOP RUN
       EJC
*
*      FRETURN
*
L_FRT  ENT                   ENTRY POINT
       MOV  =SCFRT,WA        POINT TO STRING /FRETURN/
       BRN  RETRN            JUMP TO COMMON RETURN ROUTINE
       EJC
*
*      NRETURN
*
L_NRT  ENT                   ENTRY POINT
       MOV  =SCNRT,WA        POINT TO STRING /NRETURN/
       BRN  RETRN            JUMP TO COMMON RETURN ROUTINE
       EJC
*
*      RETURN
*
L_RTN  ENT                   ENTRY POINT
       MOV  =SCRTN,WA        POINT TO STRING /RETURN/
       BRN  RETRN            JUMP TO COMMON RETURN ROUTINE
       EJC
*
*      SCONTINUE
*
L_SCN  ENT                   ENTRY POINT
       MOV  R_CNT,XR         LOAD CONTINUATION CODE BLOCK PTR
       BZE  XR,LSCN2         JUMP IF NO PREVIOUS ERROR
       ZER  R_CNT            CLEAR FLAG
       BNE  KVERT,=NM320,LSCN1 ERROR MUST BE USER INTERRUPT
       BEQ  KVERT,=NM321,LSCN2 DETECT SCONTINUE LOOP
       MOV  XR,R_COD         ELSE STORE AS NEW CODE BLOCK PTR
       ADD  STXOC,XR         ADD RESUME OFFSET
       LCP  XR               LOAD CODE POINTER
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      HERE IF NO USER INTERRUPT
*
LSCN1  ICV  ERRFT            FATAL ERROR
       ERB  331,Goto SCONTINUE with no user interrupt
*
*      HERE IF IN SCONTINUE LOOP OR IF NO PREVIOUS ERROR
*
LSCN2  ICV  ERRFT            FATAL ERROR
       ERB  321,Goto SCONTINUE with no preceding error
       EJC
*
*      UNDEFINED LABEL
*
L_UND  ENT                   ENTRY POINT
       ERB  038,Goto undefined label
       TTL  S P I T B O L -- PREDEFINED SNOBOL4 FUNCTIONS
*
*      THE FOLLOWING SECTION CONTAINS CODING FOR FUNCTIONS
*      WHICH ARE PREDEFINED AND AVAILABLE AT THE SNOBOL LEVEL.
*
*      THESE ROUTINES RECEIVE CONTROL DIRECTLY FROM THE CODE OR
*      INDIRECTLY THROUGH THE O_FNC, O_FNS OR CFUNC ROUTINES.
*      IN BOTH CASES THE CONDITIONS ON ENTRY ARE AS FOLLOWS
*
*      THE ARGUMENTS ARE ON THE STACK. THE NUMBER OF ARGUMENTS
*      HAS BEEN ADJUSTED TO CORRESPOND TO THE SVBLK SVNAR FIELD.
*
*      IN CERTAIN FUNCTIONS THE DIRECT CALL IS NOT PERMITTED
*      AND IN THESE INSTANCES WE ALSO HAVE.
*
*      (WA)                  ACTUAL NUMBER OF ARGUMENTS IN CALL
*
*      CONTROL RETURNS BY PLACING THE FUNCTION RESULT VALUE ON
*      ON THE STACK AND CONTINUING EXECUTION WITH THE NEXT
*      WORD FROM THE GENERATED CODE.
*
*      THE NAMES OF THE ENTRY POINTS OF THESE FUNCTIONS ARE OF
*      THE FORM S_XXX WHERE XXX IS THE THREE LETTER CODE FOR
*      THE SYSTEM VARIABLE NAME. THE FUNCTIONS ARE IN ORDER
*      ALPHABETICALLY BY THEIR ENTRY NAMES.
       EJC
.IF    .C370
*
*      ABS
*
S_ABS  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTNUM            MAKE NUMERIC
       ERR  xxx,ABS argument not numeric
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,SABS1  JUMP IF REAL
.FI
       LDI  ICVAL(XR)        LOAD INTEGER VALUE
       IGE  EXIXR            NO CHANGE IF NOT NEGATIVE
       NGI                   PRODUCE ABSOLUTE VALUE
       INO  EXINT            RETURN INTEGER IF NO OVERFLOW
       ERB  xxx,ABS caused integer overflow
.IF    .CNRA
.ELSE
*
*      HERE TO PROCESS REAL ARGUMENT
*
SABS1  LDR  RCVAL(XR)        LOAD REAL VALUE
       RGE  EXIXR            NO CHANGE IF NOT NEGATIVE
       NGR                   PRODUCE ABSOLUTE VALUE
       RNO  EXREA            RETURN REAL IF NO OVERFLOW
       ERB  xxx,ABS caused real overflow
.FI
.FI
.IF    .C370
*
*      AND
*
S_AND  ENT                   ENTRY POINT
       MNZ  WB               SIGNAL TWO ARGUMENTS
       JSR  SBOOL            CALL STRING BOOLEAN ROUTINE
       ERR  xxx,AND first argument is not a string
       ERR  xxx,AND second argument is not a string
       ERR  xxx,AND arguments not same length
       PPM  EXITS            NULL STRING ARGUMENTS
*
*      HERE TO PROCESS (WC) WORDS.  RESULT IS STACKED.
*
SAND1  MOV  (XL)+,WA         GET NEXT CFP_C CHARS FROM ARG 1
       ANB  (XR),WA          AND WITH CHARACTERS FROM ARG 2
       MOV  WA,(XR)+         PUT BACK IN MEMORY
       BCT  WC,SAND1         LOOP OVER ALL WORDS IN STRING BLOCK
       BRN  EXITS            FETCH NEXT CODE WORD
       EJC
.FI
*
*      ANY
*
S_ANY  ENT                   ENTRY POINT
       MOV  =P_ANS,WB        SET PCODE FOR SINGLE CHAR CASE
       MOV  =P_ANY,XL        PCODE FOR MULTI-CHAR CASE
       MOV  =P_AYD,WC        PCODE FOR EXPRESSION CASE
       JSR  PATST            CALL COMMON ROUTINE TO BUILD NODE
       ERR  059,ANY argument is not a string or expression
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
.IF    .CNBF
.ELSE
*
*      APPEND
*
S_APN  ENT                   ENTRY POINT
       MOV  (XS)+,XL         GET APPEND ARGUMENT
       MOV  (XS)+,XR         GET BCBLK
       BEQ  (XR),=B_BCT,SAPN1 OK IF FIRST ARG IS BCBLK
       ERB  275,APPEND first argument is not a buffer
*
*      HERE TO DO THE APPEND
*
SAPN1  JSR  APNDB            DO THE APPEND
       ERR  276,APPEND second argument is not a string
       PPM  EXFAL            NO ROOM - FAIL
       BRN  EXNUL            EXIT WITH NULL RESULT
       EJC
.FI
*
*      APPLY
*
*      APPLY DOES NOT PERMIT THE DIRECT (FAST) CALL SO THAT
*      WA CONTAINS THE ACTUAL NUMBER OF ARGUMENTS PASSED.
*
S_APP  ENT                   ENTRY POINT
       BZE  WA,SAPP3         JUMP IF NO ARGUMENTS
       DCV  WA               ELSE GET APPLIED FUNC ARG COUNT
       MOV  WA,WB            COPY
       WTB  WB               CONVERT TO BYTES
       MOV  XS,XT            COPY STACK POINTER
       ADD  WB,XT            POINT TO FUNCTION ARGUMENT ON STACK
       MOV  (XT),XR          LOAD FUNCTION PTR (APPLY 1ST ARG)
       BZE  WA,SAPP2         JUMP IF NO ARGS FOR APPLIED FUNC
       LCT  WB,WA            ELSE SET COUNTER FOR LOOP
*
*      LOOP TO MOVE ARGUMENTS UP ON STACK
*
SAPP1  DCA  XT               POINT TO NEXT ARGUMENT
       MOV  (XT),NUM01(XT)   MOVE ARGUMENT UP
       BCT  WB,SAPP1         LOOP TILL ALL MOVED
*
*      MERGE HERE TO CALL FUNCTION (WA = NUMBER OF ARGUMENTS)
*
SAPP2  ICA  XS               ADJUST STACK PTR FOR APPLY 1ST ARG
       JSR  GTNVR            GET VARIABLE BLOCK ADDR FOR FUNC
       PPM  SAPP3            JUMP IF NOT NATURAL VARIABLE
       MOV  VRFNC(XR),XL     ELSE POINT TO FUNCTION BLOCK
       BRN  CFUNC            GO CALL APPLIED FUNCTION
*
*      HERE FOR INVALID FIRST ARGUMENT
*
SAPP3  ERB  060,APPLY first arg is not natural variable name
       EJC
*
*      ARBNO
*
*      ARBNO BUILDS A COMPOUND PATTERN. SEE DESCRIPTION AT
*      START OF PATTERN MATCHING SECTION FOR STRUCTURE FORMED.
*
S_ABN  ENT                   ENTRY POINT
       ZER  XR               SET PARM1 = 0 FOR THE MOMENT
       MOV  =P_ALT,WB        SET PCODE FOR ALTERNATIVE NODE
       JSR  PBILD            BUILD ALTERNATIVE NODE
       MOV  XR,XL            SAVE PTR TO ALTERNATIVE PATTERN
       MOV  =P_ABC,WB        PCODE FOR P_ABC
       ZER  XR               P0BLK
       JSR  PBILD            BUILD P_ABC NODE
       MOV  XL,PTHEN(XR)     PUT ALTERNATIVE NODE AS SUCCESSOR
       MOV  XL,WA            REMEMBER ALTERNATIVE NODE POINTER
       MOV  XR,XL            COPY P_ABC NODE PTR
       MOV  (XS),XR          LOAD ARBNO ARGUMENT
       MOV  WA,(XS)          STACK ALTERNATIVE NODE POINTER
       JSR  GTPAT            GET ARBNO ARGUMENT AS PATTERN
       ERR  061,ARBNO argument is not pattern
       JSR  PCONC            CONCAT ARG WITH P_ABC NODE
       MOV  XR,XL            REMEMBER PTR TO CONCD PATTERNS
       MOV  =P_ABA,WB        PCODE FOR P_ABA
       ZER  XR               P0BLK
       JSR  PBILD            BUILD P_ABA NODE
       MOV  XL,PTHEN(XR)     CONCATENATE NODES
       MOV  (XS),XL          RECALL PTR TO ALTERNATIVE NODE
       MOV  XR,PARM1(XL)     POINT ALTERNATIVE BACK TO ARGUMENT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      ARG
*
S_ARG  ENT                   ENTRY POINT
       JSR  GTSMI            GET SECOND ARG AS SMALL INTEGER
       ERR  062,ARG second argument is not integer
       PPM  EXFAL            FAIL IF OUT OF RANGE OR NEGATIVE
       MOV  XR,WA            SAVE ARGUMENT NUMBER
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       JSR  GTNVR            LOCATE VRBLK
       PPM  SARG1            JUMP IF NOT NATURAL VARIABLE
       MOV  VRFNC(XR),XR     ELSE LOAD FUNCTION BLOCK POINTER
       BNE  (XR),=B_PFC,SARG1 JUMP IF NOT PROGRAM DEFINED
       BZE  WA,EXFAL         FAIL IF ARG NUMBER IS ZERO
       BGT  WA,FARGS(XR),EXFAL FAIL IF ARG NUMBER IS TOO LARGE
       WTB  WA               ELSE CONVERT TO BYTE OFFSET
       ADD  WA,XR            POINT TO ARGUMENT SELECTED
       MOV  PFAGB(XR),XR     LOAD ARGUMENT VRBLK POINTER
       BRN  EXVNM            EXIT TO BUILD NMBLK
*
*      HERE IF 1ST ARGUMENT IS BAD
*
SARG1  ERB  063,ARG first argument is not program function name
       EJC
*
*      ARRAY
*
S_ARR  ENT                   ENTRY POINT
       MOV  (XS)+,XL         LOAD INITIAL ELEMENT VALUE
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       JSR  GTINT            CONVERT FIRST ARG TO INTEGER
       PPM  SAR02            JUMP IF NOT INTEGER
*
*      HERE FOR INTEGER FIRST ARGUMENT, BUILD VCBLK
*
       LDI  ICVAL(XR)        LOAD INTEGER VALUE
       ILE  SAR10            JUMP IF ZERO OR NEG (BAD DIMENSION)
       MFI  WA,SAR11         ELSE CONVERT TO ONE WORD, TEST OVFL
       JSR  VMAKE            CREATE VECTOR
       PPM  SAR11            FAIL IF TOO LARGE
       BRN  EXSID            EXIT SETTING IDVAL
       EJC
*
*      ARRAY (CONTINUED)
*
*      HERE IF FIRST ARGUMENT IS NOT AN INTEGER
*
SAR02  MOV  XR,-(XS)         REPLACE ARGUMENT ON STACK
       JSR  XSCNI            INITIALIZE SCAN OF FIRST ARGUMENT
       ERR  064,ARRAY first argument is not integer or string
       PPM  EXNUL            DUMMY (UNUSED) NULL STRING EXIT
       MOV  R_XSC,-(XS)      SAVE PROTOTYPE POINTER
       MOV  XL,-(XS)         SAVE DEFAULT VALUE
       ZER  ARCDM            ZERO COUNT OF DIMENSIONS
       ZER  ARPTR            ZERO OFFSET TO INDICATE PASS ONE
       LDI  INTV1            LOAD INTEGER ONE
       STI  ARNEL            INITIALIZE ELEMENT COUNT
*
*      THE FOLLOWING CODE IS EXECUTED TWICE. THE FIRST TIME
*      (ARPTR EQ 0), IT IS USED TO COUNT THE NUMBER OF ELEMENTS
*      AND NUMBER OF DIMENSIONS. THE SECOND TIME (ARPTR GT 0) IS
*      USED TO ACTUALLY FILL IN THE DIM,LBD FIELDS OF THE ARBLK.
*
SAR03  LDI  INTV1            LOAD ONE AS DEFAULT LOW BOUND
       STI  ARSVL            SAVE AS LOW BOUND
       MOV  =CH_CL,WC        SET DELIMITER ONE = COLON
       MOV  =CH_CM,XL        SET DELIMITER TWO = COMMA
       ZER  WA               RETAIN BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN NEXT BOUND
       BNE  WA,=NUM01,SAR04  JUMP IF NOT COLON
*
*      HERE WE HAVE A COLON ENDING A LOW BOUND
*
       JSR  GTINT            CONVERT LOW BOUND
       ERR  065,ARRAY first argument lower bound is not integer
       LDI  ICVAL(XR)        LOAD VALUE OF LOW BOUND
       STI  ARSVL            STORE LOW BOUND VALUE
       MOV  =CH_CM,WC        SET DELIMITER ONE = COMMA
       MOV  WC,XL            AND DELIMITER TWO = COMMA
       ZER  WA               RETAIN BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN HIGH BOUND
       EJC
*
*      ARRAY (CONTINUED)
*
*      MERGE HERE TO PROCESS UPPER BOUND
*
SAR04  JSR  GTINT            CONVERT HIGH BOUND TO INTEGER
       ERR  066,ARRAY first argument upper bound is not integer
       LDI  ICVAL(XR)        GET HIGH BOUND
       SBI  ARSVL            SUBTRACT LOWER BOUND
       IOV  SAR10            BAD DIMENSION IF OVERFLOW
       ILT  SAR10            BAD DIMENSION IF NEGATIVE
       ADI  INTV1            ADD 1 TO GET DIMENSION
       IOV  SAR10            BAD DIMENSION IF OVERFLOW
       MOV  ARPTR,XL         LOAD OFFSET (ALSO PASS INDICATOR)
       BZE  XL,SAR05         JUMP IF FIRST PASS
*
*      HERE IN SECOND PASS TO STORE LBD AND DIM IN ARBLK
*
       ADD  (XS),XL          POINT TO CURRENT LOCATION IN ARBLK
       STI  CFP_I(XL)        STORE DIMENSION
       LDI  ARSVL            LOAD LOW BOUND
       STI  (XL)             STORE LOW BOUND
       ADD  *ARDMS,ARPTR     BUMP OFFSET TO NEXT BOUNDS
       BRN  SAR06            JUMP TO CHECK FOR END OF BOUNDS
*
*      HERE IN PASS 1
*
SAR05  ICV  ARCDM            BUMP DIMENSION COUNT
       MLI  ARNEL            MULTIPLY DIMENSION BY COUNT SO FAR
       IOV  SAR11            TOO LARGE IF OVERFLOW
       STI  ARNEL            ELSE STORE UPDATED ELEMENT COUNT
*
*      MERGE HERE AFTER PROCESSING ONE SET OF BOUNDS
*
SAR06  BNZ  WA,SAR03         LOOP BACK UNLESS END OF BOUNDS
       BNZ  ARPTR,SAR09      JUMP IF END OF PASS 2
       EJC
*
*      ARRAY (CONTINUED)
*
*      HERE AT END OF PASS ONE, BUILD ARBLK
*
       LDI  ARNEL            GET NUMBER OF ELEMENTS
       MFI  WB,SAR11         GET AS ADDR INTEGER, TEST OVFLO
       WTB  WB               ELSE CONVERT TO LENGTH IN BYTES
       MOV  *ARSI_,WA        SET SIZE OF STANDARD FIELDS
       LCT  WC,ARCDM         SET DIMENSION COUNT TO CONTROL LOOP
*
*      LOOP TO ALLOW SPACE FOR DIMENSIONS
*
SAR07  ADD  *ARDMS,WA        ALLOW SPACE FOR ONE SET OF BOUNDS
       BCT  WC,SAR07         LOOP BACK TILL ALL ACCOUNTED FOR
       MOV  WA,XL            SAVE SIZE (=AROFS)
*
*      NOW ALLOCATE SPACE FOR ARBLK
*
       ADD  WB,WA            ADD SPACE FOR ELEMENTS
       ICA  WA               ALLOW FOR ARPRO PROTOTYPE FIELD
       BGT  WA,MXLEN,SAR11   FAIL IF TOO LARGE
       JSR  ALLOC            ELSE ALLOCATE ARBLK
       MOV  (XS),WB          LOAD DEFAULT VALUE
       MOV  XR,(XS)          SAVE ARBLK POINTER
       MOV  WA,WC            SAVE LENGTH IN BYTES
       BTW  WA               CONVERT LENGTH BACK TO WORDS
       LCT  WA,WA            SET COUNTER TO CONTROL LOOP
*
*      LOOP TO CLEAR ENTIRE ARBLK TO DEFAULT VALUE
*
SAR08  MOV  WB,(XR)+         SET ONE WORD
       BCT  WA,SAR08         LOOP TILL ALL SET
       EJC
*
*      ARRAY (CONTINUED)
*
*      NOW SET INITIAL FIELDS OF ARBLK
*
       MOV  (XS)+,XR         RELOAD ARBLK POINTER
       MOV  (XS),WB          LOAD PROTOTYPE
       MOV  =B_ART,(XR)      SET TYPE WORD
       MOV  WC,ARLEN(XR)     STORE LENGTH IN BYTES
       ZER  IDVAL(XR)        ZERO ID TILL WE GET IT BUILT
       MOV  XL,AROFS(XR)     SET PROTOTYPE FIELD PTR
       MOV  ARCDM,ARNDM(XR)  SET NUMBER OF DIMENSIONS
       MOV  XR,WC            SAVE ARBLK POINTER
       ADD  XL,XR            POINT TO PROTOTYPE FIELD
       MOV  WB,(XR)          STORE PROTOTYPE PTR IN ARBLK
       MOV  *ARLBD,ARPTR     SET OFFSET FOR PASS 2 BOUNDS SCAN
       MOV  WB,R_XSC         RESET STRING POINTER FOR XSCAN
       MOV  WC,(XS)          STORE ARBLK POINTER ON STACK
       ZER  XSOFS            RESET OFFSET PTR TO START OF STRING
       BRN  SAR03            JUMP BACK TO RESCAN BOUNDS
*
*      HERE AFTER FILLING IN BOUNDS INFORMATION (END PASS TWO)
*
SAR09  MOV  (XS)+,XR         RELOAD POINTER TO ARBLK
       BRN  EXSID            EXIT SETTING IDVAL
*
*      HERE FOR BAD DIMENSION
*
SAR10  ERB  067,ARRAY dimension is zero, negative or out of range
*
*      HERE IF ARRAY IS TOO LARGE
*
SAR11  ERB  068,ARRAY size exceeds maximum permitted
       EJC
.IF    .CMTH
*
*      ATAN
*
S_ATN  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  301,ATAN argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       ATN                   TAKE ARCTANGENT
       BRN  EXREA            OVERFLOW, OUT OF RANGE NOT POSSIBLE
       EJC
.FI
.IF    .CBSP
       EJC
*
*      BACKSPACE
*
S_BSP  ENT                   ENTRY POINT
       JSR  IOFCB            CALL FCBLK ROUTINE
       ERR  316,BACKSPACE argument is not a suitable name
       ERR  316,BACKSPACE argument is not a suitable name
       ERR  317,BACKSPACE file does not exist
       JSR  SYSBS            CALL BACKSPACE FILE FUNCTION
       ERR  317,BACKSPACE file does not exist
       ERR  318,BACKSPACE file does not permit backspace
       ERR  319,BACKSPACE caused non-recoverable error
       BRN  EXNUL            RETURN NULL AS RESULT
       EJC
.FI
.IF    .CNBF
.ELSE
*
*      BUFFER
*
S_BUF  ENT                   ENTRY POINT
       MOV  (XS)+,XL         GET INITIAL VALUE
       MOV  (XS)+,XR         GET REQUESTED ALLOCATION
       JSR  GTINT            CONVERT TO INTEGER
       ERR  269,BUFFER first argument is not integer
       LDI  ICVAL(XR)        GET VALUE
       ILE  SBF01            BRANCH IF NEGATIVE OR ZERO
       MFI  WA,SBF02         MOVE WITH OVERFLOW CHECK
       JSR  ALOBF            ALLOCATE THE BUFFER
       JSR  APNDB            COPY IT IN
       ERR  270,BUFFER second argument is not a string or buffer
       ERR  271,BUFFER initial value too big for allocation
       BRN  EXSID            EXIT SETTING IDVAL
*
*      HERE FOR INVALID ALLOCATION SIZE
*
SBF01  ERB  272,BUFFER first argument is not positive
*
*      HERE FOR ALLOCATION SIZE INTEGER OVERFLOW
*
SBF02  ERB  273,BUFFER size exceeds value of MAXLNGTH keyword
       EJC
.FI
*
*      BREAK
*
S_BRK  ENT                   ENTRY POINT
       MOV  =P_BKS,WB        SET PCODE FOR SINGLE CHAR CASE
       MOV  =P_BRK,XL        PCODE FOR MULTI-CHAR CASE
       MOV  =P_BKD,WC        PCODE FOR EXPRESSION CASE
       JSR  PATST            CALL COMMON ROUTINE TO BUILD NODE
       ERR  069,BREAK argument is not a string or expression
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      BREAKX
*
*      BREAKX IS A COMPOUND PATTERN. SEE DESCRIPTION AT START
*      OF PATTERN MATCHING SECTION FOR STRUCTURE FORMED.
*
S_BKX  ENT                   ENTRY POINT
       MOV  =P_BKS,WB        PCODE FOR SINGLE CHAR ARGUMENT
       MOV  =P_BRK,XL        PCODE FOR MULTI-CHAR ARGUMENT
       MOV  =P_BXD,WC        PCODE FOR EXPRESSION CASE
       JSR  PATST            CALL COMMON ROUTINE TO BUILD NODE
       ERR  070,BREAKX argument is not a string or expression
*
*      NOW HOOK BREAKX NODE ON AT FRONT END
*
       MOV  XR,-(XS)         SAVE PTR TO BREAK NODE
       MOV  =P_BKX,WB        SET PCODE FOR BREAKX NODE
       JSR  PBILD            BUILD IT
       MOV  (XS),PTHEN(XR)   SET BREAK NODE AS SUCCESSOR
       MOV  =P_ALT,WB        SET PCODE FOR ALTERNATION NODE
       JSR  PBILD            BUILD (PARM1=ALT=BREAKX NODE)
       MOV  XR,WA            SAVE PTR TO ALTERNATION NODE
       MOV  (XS),XR          POINT TO BREAK NODE
       MOV  WA,PTHEN(XR)     SET ALTERNATE NODE AS SUCCESSOR
       LCW  XR               RESULT ON STACK
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      CHAR
*
S_CHR  ENT                   ENTRY POINT
       JSR  GTSMI            CONVERT ARG TO INTEGER
       ERR  281,CHAR argument not integer
       PPM  SCHR1            TOO BIG ERROR EXIT
       BGE  WC,=CFP_A,SCHR1  SEE IF OUT OF RANGE OF HOST SET
       MOV  =NUM01,WA        IF NOT SET SCBLK ALLOCATION
       MOV  WC,WB            SAVE CHAR CODE
       JSR  ALOCS            ALLOCATE 1 BAU SCBLK
       MOV  XR,XL            COPY SCBLK POINTER
       PSC  XL               GET SET TO STUFF CHAR
       SCH  WB,(XL)          STUFF IT
       CSC  XL               COMPLETE STORE CHARACTER
       ZER  XL               CLEAR SLOP IN XL
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      HERE IF CHAR ARGUMENT IS OUT OF RANGE
*
SCHR1  ERB  282,CHAR argument not in range
       EJC
.IF    .CMTH
*
*      CHOP
*
S_CHP  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  302,CHOP argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       CHP                   TRUNCATE TO INTEGER VALUED REAL
       BRN  EXREA            NO OVERFLOW POSSIBLE
       EJC
.FI
*
*      CLEAR
*
S_CLR  ENT                   ENTRY POINT
       JSR  XSCNI            INITIALIZE TO SCAN ARGUMENT
       ERR  071,CLEAR argument is not a string
       PPM  SCLR2            JUMP IF NULL
*
*      LOOP TO SCAN OUT NAMES IN FIRST ARGUMENT. VARIABLES IN
*      THE LIST ARE FLAGGED BY SETTING VRGET OF VRBLK TO ZERO.
*
SCLR1  MOV  =CH_CM,WC        SET DELIMITER ONE = COMMA
       MOV  WC,XL            DELIMITER TWO = COMMA
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN NEXT VARIABLE NAME
       JSR  GTNVR            LOCATE VRBLK
       ERR  072,CLEAR argument has null variable name
       ZER  VRGET(XR)        ELSE FLAG BY ZEROING VRGET FIELD
       BNZ  WA,SCLR1         LOOP BACK IF STOPPED BY COMMA
*
*      HERE AFTER FLAGGING VARIABLES IN ARGUMENT LIST
*
SCLR2  MOV  HSHTB,WB         POINT TO START OF HASH TABLE
*
*      LOOP THROUGH SLOTS IN HASH TABLE
*
SCLR3  BEQ  WB,HSHTE,EXNUL   EXIT RETURNING NULL IF NONE LEFT
       MOV  WB,XR            ELSE COPY SLOT POINTER
       ICA  WB               BUMP SLOT POINTER
       SUB  *VRNXT,XR        SET OFFSET TO MERGE INTO LOOP
*
*      LOOP THROUGH VRBLKS ON ONE HASH CHAIN
*
SCLR4  MOV  VRNXT(XR),XR     POINT TO NEXT VRBLK ON CHAIN
       BZE  XR,SCLR3         JUMP FOR NEXT BUCKET IF CHAIN END
       BNZ  VRGET(XR),SCLR5  JUMP IF NOT FLAGGED
       EJC
*
*      CLEAR (CONTINUED)
*
*      HERE FOR FLAGGED VARIABLE, DO NOT SET VALUE TO NULL
*
       JSR  SETVR            FOR FLAGGED VAR, RESTORE VRGET
       BRN  SCLR4            AND LOOP BACK FOR NEXT VRBLK
*
*      HERE TO SET VALUE OF A VARIABLE TO NULL
*      PROTECTED VARIABLES (ARB, ETC) ARE EXEMPT
*
SCLR5  BEQ  VRSTO(XR),=B_VRE,SCLR4 CHECK FOR PROTECTED VARIABLE
       MOV  XR,XL            COPY VRBLK POINTER
*
*      LOOP TO LOCATE VALUE AT END OF POSSIBLE TRBLK CHAIN
*
SCLR6  MOV  XL,WA            SAVE BLOCK POINTER
       MOV  VRVAL(XL),XL     LOAD NEXT VALUE FIELD
       BEQ  (XL),=B_TRT,SCLR6 LOOP BACK IF TRAPPED
*
*      NOW STORE THE NULL VALUE
*
       MOV  WA,XL            RESTORE BLOCK POINTER
       MOV  =NULLS,VRVAL(XL) STORE NULL CONSTANT VALUE
       BRN  SCLR4            LOOP BACK FOR NEXT VRBLK
       EJC
*
*      CODE
*
S_COD  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTCOD            CONVERT TO CODE
       PPM  EXFAL            FAIL IF CONVERSION IS IMPOSSIBLE
       MOV  XR,-(XS)         STACK RESULT
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      COLLECT
*
S_COL  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTINT            CONVERT TO INTEGER
       ERR  073,COLLECT argument is not integer
       LDI  ICVAL(XR)        LOAD COLLECT ARGUMENT
       STI  CLSVI            SAVE COLLECT ARGUMENT
       ZER  WB               SET NO MOVE UP
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
.IF    .CSED
       ZER  DNAMS            COLLECT SEDIMENT TOO
       JSR  GBCOL            PERFORM GARBAGE COLLECTION
       MOV  XR,DNAMS         RECORD NEW SEDIMENT SIZE
.ELSE
       JSR  GBCOL            PERFORM GARBAGE COLLECTION
.FI
       MOV  DNAME,WA         POINT TO END OF MEMORY
       SUB  DNAMP,WA         SUBTRACT NEXT LOCATION
       BTW  WA               CONVERT BYTES TO WORDS
       MTI  WA               CONVERT WORDS AVAILABLE AS INTEGER
       SBI  CLSVI            SUBTRACT ARGUMENT
       IOV  EXFAL            FAIL IF OVERFLOW
       ILT  EXFAL            FAIL IF NOT ENOUGH
       ADI  CLSVI            ELSE RECOMPUTE AVAILABLE
       BRN  EXINT            AND EXIT WITH INTEGER RESULT
       EJC
.IF    .C370
*
*      COMPL
*
S_CMP  ENT                   ENTRY POINT
       ZER  WB               SIGNAL ONE ARGUMENT
       JSR  SBOOL            CALL STRING BOOLEAN ROUTINE
       PPM                   ONLY ONE ARGUMENT, CANNOT GET HERE
       ERR  xxx,COMPL argument is not a string
       PPM                   CANNOT HAVE TWO STRINGS UNEQUAL
       PPM  EXITS            NULL STRING ARGUMENT
*
*      HERE TO PROCESS (WA) CHARACTERS.  RESULT IS STACKED.
*
       LCT  WC,WA            PREPARE COUNT
       PLC  XL               PREPARE TO LOAD CHARS FROM (XL)
       PSC  XR               PREPARE TO STORE CHARS INTO (XR)
SCMP1  LCH  WA,(XL)+         GET NEXT CHAR FROM ARG 1
       CMB  WA               COMPLEMENT
       SCH  WA,(XR)+         STORE INTO RESULT
       BCT  WC,SCMP1         LOOP OVER ALL CHARS IN STRING BLOCK
       CSC                   COMPLETE STORE CHARACTER
       BRN  EXITS            FETCH NEXT CODE WORD.
       EJC
.FI
*
*      CONVERT
*
S_CNV  ENT                   ENTRY POINT
       JSR  GTSTG            CONVERT SECOND ARGUMENT TO STRING
       PPM  SCV29            ERROR IF SECOND ARGUMENT NOT STRING
       BZE  WA,SCV29         OR IF NULL STRING
.IF    .CULC
       JSR  FLSTG            FOLD LOWER CASE TO UPPER CASE
.FI
       MOV  (XS),XL          LOAD FIRST ARGUMENT
       BNE  (XL),=B_PDT,SCV01 JUMP IF NOT PROGRAM DEFINED
*
*      HERE FOR PROGRAM DEFINED DATATYPE
*
       MOV  PDDFP(XL),XL     POINT TO DFBLK
       MOV  DFNAM(XL),XL     LOAD DATATYPE NAME
       JSR  IDENT            COMPARE WITH SECOND ARG
       PPM  EXITS            EXIT IF IDENT WITH ARG AS RESULT
       BRN  EXFAL            ELSE FAIL
*
*      HERE IF NOT PROGRAM DEFINED DATATYPE
*
SCV01  MOV  XR,-(XS)         SAVE STRING ARGUMENT
       MOV  =SVCTB,XL        POINT TO TABLE OF NAMES TO COMPARE
       ZER  WB               INITIALIZE COUNTER
       MOV  WA,WC            SAVE LENGTH OF ARGUMENT STRING
*
*      LOOP THROUGH TABLE ENTRIES
*
SCV02  MOV  (XL)+,XR         LOAD NEXT TABLE ENTRY, BUMP POINTER
       BZE  XR,EXFAL         FAIL IF ZERO MARKING END OF LIST
       BNE  WC,SCLEN(XR),SCV05 JUMP IF WRONG LENGTH
       MOV  XL,CNVTP         ELSE STORE TABLE POINTER
       PLC  XR               POINT TO CHARS OF TABLE ENTRY
       MOV  (XS),XL          LOAD POINTER TO STRING ARGUMENT
       PLC  XL               POINT TO CHARS OF STRING ARG
       MOV  WC,WA            SET NUMBER OF CHARS TO COMPARE
       CMC  SCV04,SCV04      COMPARE, JUMP IF NO MATCH
       EJC
*
*      CONVERT (CONTINUED)
*
*      HERE WE HAVE A MATCH
*
SCV03  MOV  WB,XL            COPY ENTRY NUMBER
       ICA  XS               POP STRING ARG OFF STACK
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       BSW  XL,CNVTT         JUMP TO APPROPRIATE ROUTINE
       IFF  0,SCV06          STRING
       IFF  1,SCV07          INTEGER
       IFF  2,SCV09          NAME
       IFF  3,SCV10          PATTERN
       IFF  4,SCV11          ARRAY
       IFF  5,SCV19          TABLE
       IFF  6,SCV25          EXPRESSION
       IFF  7,SCV26          CODE
       IFF  8,SCV27          NUMERIC
.IF    .CNRA
.ELSE
       IFF  CNVRT,SCV08      REAL
.FI
.IF    .CNBF
.ELSE
       IFF  CNVBT,SCV28      BUFFER
.FI
       ESW                   END OF SWITCH TABLE
*
*      HERE IF NO MATCH WITH TABLE ENTRY
*
SCV04  MOV  CNVTP,XL         RESTORE TABLE POINTER, MERGE
*
*      MERGE HERE IF LENGTHS DID NOT MATCH
*
SCV05  ICV  WB               BUMP ENTRY NUMBER
       BRN  SCV02            LOOP BACK TO CHECK NEXT ENTRY
*
*      HERE TO CONVERT TO STRING
*
SCV06  MOV  XR,-(XS)         REPLACE STRING ARGUMENT ON STACK
       JSR  GTSTG            CONVERT TO STRING
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      CONVERT (CONTINUED)
*
*      HERE TO CONVERT TO INTEGER
*
SCV07  JSR  GTINT            CONVERT TO INTEGER
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.IF    .CNRA
.ELSE
*
*      HERE TO CONVERT TO REAL
*
SCV08  JSR  GTREA            CONVERT TO REAL
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.FI
*
*      HERE TO CONVERT TO NAME
*
SCV09  BEQ  (XR),=B_NML,EXIXR RETURN IF ALREADY A NAME
       JSR  GTNVR            ELSE TRY STRING TO NAME CONVERT
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       BRN  EXVNM            ELSE EXIT BUILDING NMBLK FOR VRBLK
*
*      HERE TO CONVERT TO PATTERN
*
SCV10  JSR  GTPAT            CONVERT TO PATTERN
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      CONVERT TO ARRAY
*
*      IF THE FIRST ARGUMENT IS A TABLE, THEN WE GO THROUGH
*      AN INTERMEDIATE ARRAY OF ADDRESSES THAT IS SORTED TO
*      PROVIDE A RESULT ORDERED BY TIME OF ENTRY IN THE
*      ORIGINAL TABLE.  SEE C3.762.
*
SCV11  MOV  XR,-(XS)         SAVE ARGUMENT ON STACK
       ZER  WA               USE TABLE CHAIN BLOCK ADDRESSES
       JSR  GTARR            GET AN ARRAY
       PPM  EXFAL            FAIL IF EMPTY TABLE
       PPM  EXFAL            FAIL IF NOT CONVERTIBLE
       MOV  (XS)+,XL         RELOAD ORIGINAL ARG
       BNE  (XL),=B_TBT,EXSID EXIT IF ORIGINAL NOT A TABLE
       MOV  XR,-(XS)         SORT THE INTERMEDIATE ARRAY
       MOV  =NULLS,-(XS)     ON FIRST COLUMN
       ZER  WA               SORT ASCENDING
       JSR  SORTA            DO SORT
       PPM  EXFAL            IF SORT FAILS, SO SHALL WE
       MOV  XR,WB            SAVE ARRAY RESULT
       LDI  ARDIM(XR)        LOAD DIM 1 (NUMBER OF ELEMENTS)
       MFI  WA               GET AS ONE WORD INTEGER
       LCT  WA,WA            COPY TO CONTROL LOOP
       ADD  *ARVL2,XR        POINT TO FIRST ELEMENT IN ARRAY
*
*      HERE FOR EACH ROW OF THIS 2-COLUMN ARRAY
*
SCV12  MOV  (XR),XL          GET TEBLK ADDRESS
       MOV  TESUB(XL),(XR)+  REPLACE WITH SUBSCRIPT
       MOV  TEVAL(XL),(XR)+  REPLACE WITH VALUE
       BCT  WA,SCV12         LOOP TILL ALL COPIED OVER
       MOV  WB,XR            RETRIEVE ARRAY ADDRESS
       BRN  EXSID            EXIT SETTING ID FIELD
*
*      CONVERT TO TABLE
*
SCV19  MOV  (XR),WA          LOAD FIRST WORD OF BLOCK
       MOV  XR,-(XS)         REPLACE ARBLK POINTER ON STACK
       BEQ  WA,=B_TBT,EXITS  RETURN ARG IF ALREADY A TABLE
       BNE  WA,=B_ART,EXFAL  ELSE FAIL IF NOT AN ARRAY
       EJC
*
*      CONVERT (CONTINUED)
*
*      HERE TO CONVERT AN ARRAY TO TABLE
*
       BNE  ARNDM(XR),=NUM02,EXFAL FAIL IF NOT 2-DIM ARRAY
       LDI  ARDM2(XR)        LOAD DIM 2
       SBI  INTV2            SUBTRACT 2 TO COMPARE
       INE  EXFAL            FAIL IF DIM2 NOT 2
*
*      HERE WE HAVE AN ARBLK OF THE RIGHT SHAPE
*
       LDI  ARDIM(XR)        LOAD DIM 1 (NUMBER OF ELEMENTS)
       MFI  WA               GET AS ONE WORD INTEGER
       LCT  WB,WA            COPY TO CONTROL LOOP
       ADD  =TBSI_,WA        ADD SPACE FOR STANDARD FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       JSR  ALLOC            ALLOCATE SPACE FOR TBBLK
       MOV  XR,WC            COPY TBBLK POINTER
       MOV  XR,-(XS)         SAVE TBBLK POINTER
       MOV  =B_TBT,(XR)+     STORE TYPE WORD
       ZER  (XR)+            STORE ZERO FOR IDVAL FOR NOW
       MOV  WA,(XR)+         STORE LENGTH
       MOV  =NULLS,(XR)+     NULL INITIAL LOOKUP VALUE
*
*      LOOP TO INITIALIZE BUCKET PTRS TO POINT TO TABLE
*
SCV20  MOV  WC,(XR)+         SET BUCKET PTR TO POINT TO TBBLK
       BCT  WB,SCV20         LOOP TILL ALL INITIALIZED
       MOV  *ARVL2,WB        SET OFFSET TO FIRST ARBLK ELEMENT
*
*      LOOP TO COPY ELEMENTS FROM ARRAY TO TABLE
*
SCV21  MOV  NUM01(XS),XL     POINT TO ARBLK
       BEQ  WB,ARLEN(XL),SCV24 JUMP IF ALL MOVED
       ADD  WB,XL            ELSE POINT TO CURRENT LOCATION
       ADD  *NUM02,WB        BUMP OFFSET
       MOV  (XL),XR          LOAD SUBSCRIPT NAME
       DCA  XL               ADJUST PTR TO MERGE (TRVAL=1+1)
       EJC
*
*      CONVERT (CONTINUED)
*
*      LOOP TO CHASE DOWN TRBLK CHAIN FOR VALUE
*
SCV22  MOV  TRVAL(XL),XL     POINT TO NEXT VALUE
       BEQ  (XL),=B_TRT,SCV22 LOOP BACK IF TRAPPED
*
*      HERE WITH NAME IN XR, VALUE IN XL
*
SCV23  MOV  XL,-(XS)         STACK VALUE
       MOV  NUM01(XS),XL     LOAD TBBLK POINTER
       JSR  TFIND            BUILD TEBLK (NOTE WB GT 0 BY NAME)
       PPM  EXFAL            FAIL IF ACESS FAILS
       MOV  (XS)+,TEVAL(XL)  STORE VALUE IN TEBLK
       BRN  SCV21            LOOP BACK FOR NEXT ELEMENT
*
*      HERE AFTER MOVING ALL ELEMENTS TO TBBLK
*
SCV24  MOV  (XS)+,XR         LOAD TBBLK POINTER
       ICA  XS               POP ARBLK POINTER
       BRN  EXSID            EXIT SETTING IDVAL
*
*      CONVERT TO EXPRESSION
*
.IF    .CEVB
SCV25  ZER  WB               BY VALUE
       JSR  GTEXP            CONVERT TO EXPRESSION
.ELSE
SCV25  JSR  GTEXP            CONVERT TO EXPRESSION
.FI
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      CONVERT TO CODE
*
SCV26  JSR  GTCOD            CONVERT TO CODE
       PPM  EXFAL            FAIL IF CONVERSION IS NOT POSSIBLE
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      CONVERT TO NUMERIC
*
SCV27  JSR  GTNUM            CONVERT TO NUMERIC
       PPM  EXFAL            FAIL IF UNCONVERTIBLE
SCV31  MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
.IF    .CNBF
.ELSE
*
*      CONVERT TO BUFFER
*
SCV28  MOV  XR,-(XS)         STACK FIRST ARG FOR PROCEDURE
       JSR  GTSTB            GET STRING OR BUFFER
       PPM  EXFAL            FAIL IF CONVERSION NOT POSSIBLE
       BNZ  WB,SCV30         JUMP IF ALREADY A BUFFER
       MOV  XR,XL            SAVE STRING POINTER
       JSR  ALOBF            ALLOCATE BUFFER OF SAME SIZE
       JSR  APNDB            COPY IN THE STRING
       PPM                   ALREADY STRING - CANT FAIL TO CNV
       PPM                   MUST BE ENOUGH ROOM
       BRN  EXSID            EXIT SETTING IDVAL FIELD
*
*      HERE IF ARGUMENT IS ALREADY A BUFFER
*
SCV30  MOV  WB,XR            RETURN BUFFER WITHOUT CONVERSION
       BRN  SCV31            MERGE TO RETURN RESULT
       EJC
.FI
*
*      SECOND ARGUMENT NOT STRING OR NULL
*
SCV29  ERB  074,CONVERT second argument is not a string
*
*      COPY
*
S_COP  ENT                   ENTRY POINT
       JSR  COPYB            COPY THE BLOCK
       PPM  EXITS            RETURN IF NO IDVAL FIELD
       BRN  EXSID            EXIT SETTING ID VALUE
       EJC
.IF    .CMTH
*
*      COS
*
S_COS  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  303,COS argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       COS                   TAKE COSINE
       RNO  EXREA            IF NO OVERFLOW, RETURN RESULT IN RA
       ERB  322,COS argument is out of range
       EJC
.FI
*
*      DATA
*
S_DAT  ENT                   ENTRY POINT
       JSR  XSCNI            PREPARE TO SCAN ARGUMENT
       ERR  075,DATA argument is not a string
       ERR  076,DATA argument is null
*
*      SCAN OUT DATATYPE NAME
*
       MOV  =CH_PP,WC        DELIMITER ONE = LEFT PAREN
       MOV  WC,XL            DELIMITER TWO = LEFT PAREN
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN DATATYPE NAME
       BNZ  WA,SDAT1         SKIP IF LEFT PAREN FOUND
       ERB  077,DATA argument is missing a left paren
*
*      HERE AFTER SCANNING DATATYPE NAME
*
.IF    .CULC
SDAT1  MOV  SCLEN(XR),WA     GET LENGTH
       BZE  WA,SDT1A         AVOID FOLDING IF NULL STRING
       JSR  FLSTG            FOLD LOWER CASE TO UPPER CASE
SDT1A  MOV  XR,XL            SAVE NAME PTR
.ELSE
SDAT1  MOV  XR,XL            SAVE NAME PTR
.FI
       MOV  SCLEN(XR),WA     GET LENGTH
       CTB  WA,SCSI_         COMPUTE SPACE NEEDED
       JSR  ALOST            REQUEST STATIC STORE FOR NAME
       MOV  XR,-(XS)         SAVE DATATYPE NAME
       MVW                   COPY NAME TO STATIC
       MOV  (XS),XR          GET NAME PTR
       ZER  XL               SCRUB DUD REGISTER
       JSR  GTNVR            LOCATE VRBLK FOR DATATYPE NAME
       ERR  078,DATA argument has null datatype name
       MOV  XR,DATDV         SAVE VRBLK POINTER FOR DATATYPE
       MOV  XS,DATXS         STORE STARTING STACK VALUE
       ZER  WB               ZERO COUNT OF FIELD NAMES
*
*      LOOP TO SCAN FIELD NAMES AND STACK VRBLK POINTERS
*
SDAT2  MOV  =CH_RP,WC        DELIMITER ONE = RIGHT PAREN
       MOV  =CH_CM,XL        DELIMITER TWO = COMMA
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN NEXT FIELD NAME
       BNZ  WA,SDAT3         JUMP IF DELIMITER FOUND
       ERB  079,DATA argument is missing a right paren
*
*      HERE AFTER SCANNING OUT ONE FIELD NAME
*
SDAT3  JSR  GTNVR            LOCATE VRBLK FOR FIELD NAME
       ERR  080,DATA argument has null field name
       MOV  XR,-(XS)         STACK VRBLK POINTER
       ICV  WB               INCREMENT COUNTER
       BEQ  WA,=NUM02,SDAT2  LOOP BACK IF STOPPED BY COMMA
       EJC
*
*      DATA (CONTINUED)
*
*      NOW BUILD THE DFBLK
*
       MOV  =DFSI_,WA        SET SIZE OF DFBLK STANDARD FIELDS
       ADD  WB,WA            ADD NUMBER OF FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       MOV  WB,WC            PRESERVE NO. OF FIELDS
       JSR  ALOST            ALLOCATE SPACE FOR DFBLK
       MOV  WC,WB            GET NO OF FIELDS
       MOV  DATXS,XT         POINT TO START OF STACK
       MOV  (XT),WC          LOAD DATATYPE NAME
       MOV  XR,(XT)          SAVE DFBLK POINTER ON STACK
       MOV  =B_DFC,(XR)+     STORE TYPE WORD
       MOV  WB,(XR)+         STORE NUMBER OF FIELDS (FARGS)
       MOV  WA,(XR)+         STORE LENGTH (DFLEN)
       SUB  *PDDFS,WA        COMPUTE PDBLK LENGTH (FOR DFPDL)
       MOV  WA,(XR)+         STORE PDBLK LENGTH (DFPDL)
       MOV  WC,(XR)+         STORE DATATYPE NAME (DFNAM)
       LCT  WC,WB            COPY NUMBER OF FIELDS
*
*      LOOP TO MOVE FIELD NAME VRBLK POINTERS TO DFBLK
*
SDAT4  MOV  -(XT),(XR)+      MOVE ONE FIELD NAME VRBLK POINTER
       BCT  WC,SDAT4         LOOP TILL ALL MOVED
*
*      NOW DEFINE THE DATATYPE FUNCTION
*
       MOV  WA,WC            COPY LENGTH OF PDBLK FOR LATER LOOP
       MOV  DATDV,XR         POINT TO VRBLK
       MOV  DATXS,XT         POINT BACK ON STACK
       MOV  (XT),XL          LOAD DFBLK POINTER
       JSR  DFFNC            DEFINE FUNCTION
       EJC
*
*      DATA (CONTINUED)
*
*      LOOP TO BUILD FFBLKS
*
*
*      NOTICE THAT THE FFBLKS ARE CONSTRUCTED IN REVERSE ORDER
*      SO THAT THE REQUIRED OFFSETS CAN BE OBTAINED FROM
*      SUCCESSIVE DECREMENTATION OF THE PDBLK LENGTH (IN WC).
*
SDAT5  MOV  *FFSI_,WA        SET LENGTH OF FFBLK
       JSR  ALLOC            ALLOCATE SPACE FOR FFBLK
       MOV  =B_FFC,(XR)      SET TYPE WORD
       MOV  =NUM01,FARGS(XR) STORE FARGS (ALWAYS ONE)
       MOV  DATXS,XT         POINT BACK ON STACK
       MOV  (XT),FFDFP(XR)   COPY DFBLK PTR TO FFBLK
       DCA  WC               DECREMENT OLD DFPDL TO GET NEXT OFS
       MOV  WC,FFOFS(XR)     SET OFFSET TO THIS FIELD
       ZER  FFNXT(XR)        TENTATIVELY SET ZERO FORWARD PTR
       MOV  XR,XL            COPY FFBLK POINTER FOR DFFNC
       MOV  (XS),XR          LOAD VRBLK POINTER FOR FIELD
       MOV  VRFNC(XR),XR     LOAD CURRENT FUNCTION POINTER
       BNE  (XR),=B_FFC,SDAT6 SKIP IF NOT CURRENTLY A FIELD FUNC
*
*      HERE WE MUST CHAIN AN OLD FFBLK PTR TO PRESERVE IT IN THE
*      CASE OF MULTIPLE FIELD FUNCTIONS WITH THE SAME NAME
*
       MOV  XR,FFNXT(XL)     LINK NEW FFBLK TO PREVIOUS CHAIN
*
*      MERGE HERE TO DEFINE FIELD FUNCTION
*
SDAT6  MOV  (XS)+,XR         LOAD VRBLK POINTER
       JSR  DFFNC            DEFINE FIELD FUNCTION
       BNE  XS,DATXS,SDAT5   LOOP BACK TILL ALL DONE
       ICA  XS               POP DFBLK POINTER
       BRN  EXNUL            RETURN WITH NULL RESULT
       EJC
*
*      DATATYPE
*
S_DTP  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  DTYPE            GET DATATYPE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      DATE
*
S_DTE  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTINT            CONVERT TO AN INTEGER
       ERR  330,DATE argument is not integer
       JSR  SYSDT            CALL SYSTEM DATE ROUTINE
       MOV  NUM01(XL),WA     LOAD LENGTH FOR SBSTR
       BZE  WA,EXNUL         RETURN NULL IF LENGTH IS ZERO
       ZER  WB               SET ZERO OFFSET
       JSR  SBSTR            USE SBSTR TO BUILD SCBLK
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      DEFINE
*
S_DEF  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD SECOND ARGUMENT
       ZER  DEFLB            ZERO LABEL POINTER IN CASE NULL
       BEQ  XR,=NULLS,SDF01  JUMP IF NULL SECOND ARGUMENT
       JSR  GTNVR            ELSE FIND VRBLK FOR LABEL
       PPM  SDF12            JUMP IF NOT A VARIABLE NAME
       MOV  XR,DEFLB         ELSE SET SPECIFIED ENTRY
*
*      SCAN FUNCTION NAME
*
SDF01  JSR  XSCNI            PREPARE TO SCAN FIRST ARGUMENT
       ERR  081,DEFINE first argument is not a string
       ERR  082,DEFINE first argument is null
       MOV  =CH_PP,WC        DELIMITER ONE = LEFT PAREN
       MOV  WC,XL            DELIMITER TWO = LEFT PAREN
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN OUT FUNCTION NAME
       BNZ  WA,SDF02         JUMP IF LEFT PAREN FOUND
       ERB  083,DEFINE first argument is missing a left paren
*
*      HERE AFTER SCANNING OUT FUNCTION NAME
*
SDF02  JSR  GTNVR            GET VARIABLE NAME
       ERR  084,DEFINE first argument has null function name
       MOV  XR,DEFVR         SAVE VRBLK POINTER FOR FUNCTION NAM
       ZER  WB               ZERO COUNT OF ARGUMENTS
       MOV  XS,DEFXS         SAVE INITIAL STACK POINTER
       BNZ  DEFLB,SDF03      JUMP IF SECOND ARGUMENT GIVEN
       MOV  XR,DEFLB         ELSE DEFAULT IS FUNCTION NAME
*
*      LOOP TO SCAN ARGUMENT NAMES AND STACK VRBLK POINTERS
*
SDF03  MOV  =CH_RP,WC        DELIMITER ONE = RIGHT PAREN
       MOV  =CH_CM,XL        DELIMITER TWO = COMMA
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN OUT NEXT ARGUMENT NAME
       BNZ  WA,SDF04         SKIP IF DELIMITER FOUND
       ERB  085,Null arg name or missing ) in DEFINE first arg.
       EJC
*
*      DEFINE (CONTINUED)
*
*      HERE AFTER SCANNING AN ARGUMENT NAME
*
SDF04  BNE  XR,=NULLS,SDF05  SKIP IF NON-NULL
       BZE  WB,SDF06         IGNORE NULL IF CASE OF NO ARGUMENTS
*
*      HERE AFTER DEALING WITH THE CASE OF NO ARGUMENTS
*
SDF05  JSR  GTNVR            GET VRBLK POINTER
       PPM  SDF03            LOOP BACK TO IGNORE NULL NAME
       MOV  XR,-(XS)         STACK ARGUMENT VRBLK POINTER
       ICV  WB               INCREMENT COUNTER
       BEQ  WA,=NUM02,SDF03  LOOP BACK IF STOPPED BY A COMMA
*
*      HERE AFTER SCANNING OUT FUNCTION ARGUMENT NAMES
*
SDF06  MOV  WB,DEFNA         SAVE NUMBER OF ARGUMENTS
       ZER  WB               ZERO COUNT OF LOCALS
*
*      LOOP TO SCAN LOCAL NAMES AND STACK VRBLK POINTERS
*
SDF07  MOV  =CH_CM,WC        SET DELIMITER ONE = COMMA
       MOV  WC,XL            SET DELIMITER TWO = COMMA
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN OUT NEXT LOCAL NAME
       BNE  XR,=NULLS,SDF08  SKIP IF NON-NULL
       BZE  WA,SDF09         EXIT SCAN IF END OF STRING
*
*      HERE AFTER SCANNING OUT A LOCAL NAME
*
SDF08  JSR  GTNVR            GET VRBLK POINTER
       PPM  SDF07            LOOP BACK TO IGNORE NULL NAME
       ICV  WB               IF OK, INCREMENT COUNT
       MOV  XR,-(XS)         STACK VRBLK POINTER
       BNZ  WA,SDF07         LOOP BACK IF STOPPED BY A COMMA
       EJC
*
*      DEFINE (CONTINUED)
*
*      HERE AFTER SCANNING LOCALS, BUILD PFBLK
*
SDF09  MOV  WB,WA            COPY COUNT OF LOCALS
       ADD  DEFNA,WA         ADD NUMBER OF ARGUMENTS
       MOV  WA,WC            SET SUM ARGS+LOCALS AS LOOP COUNT
       ADD  =PFSI_,WA        ADD SPACE FOR STANDARD FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       JSR  ALLOC            ALLOCATE SPACE FOR PFBLK
       MOV  XR,XL            SAVE POINTER TO PFBLK
       MOV  =B_PFC,(XR)+     STORE FIRST WORD
       MOV  DEFNA,(XR)+      STORE NUMBER OF ARGUMENTS
       MOV  WA,(XR)+         STORE LENGTH (PFLEN)
       MOV  DEFVR,(XR)+      STORE VRBLK PTR FOR FUNCTION NAME
       MOV  WB,(XR)+         STORE NUMBER OF LOCALS
       ZER  (XR)+            DEAL WITH LABEL LATER
       ZER  (XR)+            ZERO PFCTR
       ZER  (XR)+            ZERO PFRTR
       BZE  WC,SDF11         SKIP IF NO ARGS OR LOCALS
       MOV  XL,WA            KEEP PFBLK POINTER
       MOV  DEFXS,XT         POINT BEFORE ARGUMENTS
       LCT  WC,WC            GET COUNT OF ARGS+LOCALS FOR LOOP
*
*      LOOP TO MOVE LOCALS AND ARGS TO PFBLK
*
SDF10  MOV  -(XT),(XR)+      STORE ONE ENTRY AND BUMP POINTERS
       BCT  WC,SDF10         LOOP TILL ALL STORED
       MOV  WA,XL            RECOVER PFBLK POINTER
       EJC
*
*      DEFINE (CONTINUED)
*
*      NOW DEAL WITH LABEL
*
SDF11  MOV  DEFXS,XS         POP STACK
       MOV  DEFLB,PFCOD(XL)  STORE LABEL VRBLK IN PFBLK
       MOV  DEFVR,XR         POINT BACK TO VRBLK FOR FUNCTION
       JSR  DFFNC            DEFINE FUNCTION
       BRN  EXNUL            AND EXIT RETURNING NULL
*
*      HERE FOR ERRONEOUS LABEL
*
SDF12  ERB  086,DEFINE function entry point is not defined label
       EJC
*
*      DETACH
*
S_DET  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTVAR            LOCATE VARIABLE
       ERR  087,DETACH argument is not appropriate name
       JSR  DTACH            DETACH I/O ASSOCIATION FROM NAME
       BRN  EXNUL            RETURN NULL RESULT
       EJC
*
*      DIFFER
*
S_DIF  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD SECOND ARGUMENT
       MOV  (XS)+,XL         LOAD FIRST ARGUMENT
       JSR  IDENT            CALL IDENT COMPARISON ROUTINE
       PPM  EXFAL            FAIL IF IDENT
       BRN  EXNUL            RETURN NULL IF DIFFER
       EJC
*
*      DUMP
*
S_DMP  ENT                   ENTRY POINT
       JSR  GTSMI            LOAD DUMP ARG AS SMALL INTEGER
       ERR  088,DUMP argument is not integer
       ERR  089,DUMP argument is negative or too large
       JSR  DUMPR            ELSE CALL DUMP ROUTINE
       BRN  EXNUL            AND RETURN NULL AS RESULT
       EJC
*
*      DUPL
*
S_DUP  ENT                   ENTRY POINT
       JSR  GTSMI            GET SECOND ARGUMENT AS SMALL INTEGR
       ERR  090,DUPL second argument is not integer
       PPM  SDUP7            JUMP IF NEGATIVE OR TOO BIG
       MOV  XR,WB            SAVE DUPLICATION FACTOR
       JSR  GTSTG            GET FIRST ARG AS STRING
       PPM  SDUP4            JUMP IF NOT A STRING
*
*      HERE FOR CASE OF DUPLICATION OF A STRING
*
       MTI  WA               ACQUIRE LENGTH AS INTEGER
       STI  DUPSI            SAVE FOR THE MOMENT
       MTI  WB               GET DUPLICATION FACTOR AS INTEGER
       MLI  DUPSI            FORM PRODUCT
       IOV  SDUP3            JUMP IF OVERFLOW
       IEQ  EXNUL            RETURN NULL IF RESULT LENGTH = 0
       MFI  WA,SDUP3         GET AS ADDR INTEGER, CHECK OVFLO
*
*      MERGE HERE WITH RESULT LENGTH IN WA
*
SDUP1  MOV  XR,XL            SAVE STRING POINTER
       JSR  ALOCS            ALLOCATE SPACE FOR STRING
       MOV  XR,-(XS)         SAVE AS RESULT POINTER
       MOV  XL,WC            SAVE POINTER TO ARGUMENT STRING
       PSC  XR               PREPARE TO STORE CHARS OF RESULT
       LCT  WB,WB            SET COUNTER TO CONTROL LOOP
*
*      LOOP THROUGH DUPLICATIONS
*
SDUP2  MOV  WC,XL            POINT BACK TO ARGUMENT STRING
       MOV  SCLEN(XL),WA     GET NUMBER OF CHARACTERS
       PLC  XL               POINT TO CHARS IN ARGUMENT STRING
       MVC                   MOVE CHARACTERS TO RESULT STRING
       BCT  WB,SDUP2         LOOP TILL ALL DUPLICATIONS DONE
       ZER  XL               CLEAR GARBAGE VALUE
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      DUPL (CONTINUED)
*
*      HERE IF TOO LARGE, SET MAX LENGTH AND LET ALOCS CATCH IT
*
SDUP3  MOV  DNAME,WA         SET IMPOSSIBLE LENGTH FOR ALOCS
       BRN  SDUP1            MERGE BACK
*
*      HERE IF NOT A STRING
*
SDUP4  JSR  GTPAT            CONVERT ARGUMENT TO PATTERN
       ERR  091,DUPL first argument is not a string or pattern
*
*      HERE TO DUPLICATE A PATTERN ARGUMENT
*
       MOV  XR,-(XS)         STORE PATTERN ON STACK
       MOV  =NDNTH,XR        START OFF WITH NULL PATTERN
       BZE  WB,SDUP6         NULL PATTERN IS RESULT IF DUPFAC=0
       MOV  WB,-(XS)         PRESERVE LOOP COUNT
*
*      LOOP TO DUPLICATE BY SUCCESSIVE CONCATENATION
*
SDUP5  MOV  XR,XL            COPY CURRENT VALUE AS RIGHT ARGUMNT
       MOV  NUM01(XS),XR     GET A NEW COPY OF LEFT
       JSR  PCONC            CONCATENATE
       DCV  (XS)             COUNT DOWN
       BNZ  (XS),SDUP5       LOOP
       ICA  XS               POP LOOP COUNT
*
*      HERE TO EXIT AFTER CONSTRUCTING PATTERN
*
SDUP6  MOV  XR,(XS)          STORE RESULT ON STACK
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
*
*      FAIL IF SECOND ARG IS OUT OF RANGE
*
SDUP7  ICA  XS               POP FIRST ARGUMENT
       BRN  EXFAL            FAIL
       EJC
*
*      EJECT
*
S_EJC  ENT                   ENTRY POINT
       JSR  IOFCB            CALL FCBLK ROUTINE
       ERR  092,EJECT argument is not a suitable name
       PPM  SEJC1            NULL ARGUMENT
       ERR  093,EJECT file does not exist
       JSR  SYSEF            CALL EJECT FILE FUNCTION
       ERR  093,EJECT file does not exist
       ERR  094,EJECT file does not permit page eject
       ERR  095,EJECT caused non-recoverable output error
       BRN  EXNUL            RETURN NULL AS RESULT
*
*      HERE TO EJECT STANDARD OUTPUT FILE
*
SEJC1  JSR  SYSEP            CALL ROUTINE TO EJECT PRINTER
       BRN  EXNUL            EXIT WITH NULL RESULT
       EJC
*
*      ENDFILE
*
S_ENF  ENT                   ENTRY POINT
       JSR  IOFCB            CALL FCBLK ROUTINE
       ERR  096,ENDFILE argument is not a suitable name
       ERR  097,ENDFILE argument is null
       ERR  098,ENDFILE file does not exist
       JSR  SYSEN            CALL ENDFILE ROUTINE
       ERR  098,ENDFILE file does not exist
       ERR  099,ENDFILE file does not permit endfile
       ERR  100,ENDFILE caused non-recoverable output error
       MOV  XL,WB            REMEMBER VRBLK PTR FROM IOFCB CALL
       MOV  XL,XR            COPY POINTER
*
*      LOOP TO FIND TRTRF BLOCK
*
SENF1  MOV  XR,XL            REMEMBER PREVIOUS ENTRY
       MOV  TRVAL(XR),XR     CHAIN ALONG
       BNE  (XR),=B_TRT,EXNUL SKIP OUT IF CHAIN END
       BNE  TRTYP(XR),=TRTFC,SENF1 LOOP IF NOT FOUND
       MOV  TRVAL(XR),TRVAL(XL) REMOVE TRTRF
       MOV  TRTRF(XR),ENFCH  POINT TO HEAD OF IOCHN
       MOV  TRFPT(XR),WC     POINT TO FCBLK
       MOV  WB,XR            FILEARG1 VRBLK FROM IOFCB
       JSR  SETVR            RESET IT
       MOV  =R_FCB,XL        PTR TO HEAD OF FCBLK CHAIN
       SUB  *NUM02,XL        ADJUST READY TO ENTER LOOP
*
*      FIND FCBLK
*
SENF2  MOV  XL,XR            COPY PTR
       MOV  NUM02(XL),XL     GET NEXT LINK
       BZE  XL,SENF4         STOP IF CHAIN END
       BEQ  NUM03(XL),WC,SENF3 JUMP IF FCBLK FOUND
       BRN  SENF2            LOOP
*
*      REMOVE FCBLK
*
SENF3  MOV  NUM02(XL),NUM02(XR) DELETE FCBLK FROM CHAIN
*
*      LOOP WHICH DETACHES ALL VBLS ON IOCHN CHAIN
*
SENF4  MOV  ENFCH,XL         GET CHAIN HEAD
       BZE  XL,EXNUL         FINISHED IF CHAIN END
       MOV  TRTRF(XL),ENFCH  CHAIN ALONG
       MOV  IONMO(XL),WA     NAME OFFSET
       MOV  IONMB(XL),XL     NAME BASE
       JSR  DTACH            DETACH NAME
       BRN  SENF4            LOOP TILL DONE
       EJC
*
*      EQ
*
S_EQF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  101,EQ first argument is not numeric
       ERR  102,EQ second argument is not numeric
       PPM  EXFAL            FAIL IF LT
       PPM  EXNUL            RETURN NULL IF EQ
       PPM  EXFAL            FAIL IF GT
       EJC
*
*      EVAL
*
S_EVL  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
.IF    .CEVB
.ELSE
       JSR  GTEXP            CONVERT TO EXPRESSION
       ERR  103,EVAL argument is not expression
.FI
       LCW  WC               LOAD NEXT CODE WORD
       BNE  WC,=OFNE_,SEVL1  JUMP IF CALLED BY VALUE
       SCP  XL               COPY CODE POINTER
       MOV  (XL),WA          GET NEXT CODE WORD
       BNE  WA,=ORNM_,SEVL2  BY NAME UNLESS EXPRESSION
       BNZ  NUM01(XS),SEVL2  JUMP IF BY NAME
*
*      HERE IF CALLED BY VALUE
*
SEVL1  ZER  WB               SET FLAG FOR BY VALUE
.IF    .CEVB
       MOV  WC,-(XS)         SAVE CODE WORD
       JSR  GTEXP            CONVERT TO EXPRESSION
       ERR  103,EVAL argument is not expression
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
       ZER  WB               SET FLAG FOR BY VALUE
.ELSE
       MOV  WC,-(XS)         SAVE CODE WORD
.FI
       JSR  EVALX            EVALUATE EXPRESSION BY VALUE
       PPM  EXFAL            FAIL IF EVALUATION FAILS
       MOV  XR,XL            COPY RESULT
       MOV  (XS),XR          RELOAD NEXT CODE WORD
       MOV  XL,(XS)          STACK RESULT
       BRI  (XR)             JUMP TO EXECUTE NEXT CODE WORD
*
*      HERE IF CALLED BY NAME
*
SEVL2  MOV  =NUM01,WB        SET FLAG FOR BY NAME
.IF    .CEVB
       JSR  GTEXP            CONVERT TO EXPRESSION
       ERR  103,EVAL argument is not expression
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
       MOV  =NUM01,WB        SET FLAG FOR BY NAME
.FI
       JSR  EVALX            EVALUATE EXPRESSION BY NAME
       PPM  EXFAL            FAIL IF EVALUATION FAILS
       BRN  EXNAM            EXIT WITH NAME
.IF    .CNEX
.ELSE
       EJC
*
*      EXIT
*
S_EXT  ENT                   ENTRY POINT
       ZER  WB               CLEAR AMOUNT OF STATIC SHIFT
       ZER  R_CCB            FORGET INTERIM CODE BLOCK
.IF    .CSED
       ZER  DNAMS            COLLECT SEDIMENT TOO
       JSR  GBCOL            COMPACT MEMORY BY COLLECTING
       MOV  XR,DNAMS         RECORD NEW SEDIMENT SIZE
.ELSE
       JSR  GBCOL            COMPACT MEMORY BY COLLECTING
.FI
       JSR  GTSTG
       ERR  288,EXIT second argument is not a string
       MOV  XR,XL            COPY SECOND ARG STRING POINTER
       JSR  GTSTG            CONVERT ARG TO STRING
       ERR  104,EXIT first argument is not suitable integer or string
       MOV  XL,-(XS)         SAVE SECOND ARGUMENT
       MOV  XR,XL            COPY FIRST ARG STRING PTR
       JSR  GTINT            CHECK IT IS INTEGER
       PPM  SEXT1            SKIP IF UNCONVERTIBLE
       ZER  XL               NOTE IT IS INTEGER
       LDI  ICVAL(XR)        GET INTEGER ARG
*
*      MERGE TO CALL OSINT EXIT ROUTINE
*
SEXT1  MOV  R_FCB,WB         GET FCBLK CHAIN HEADER
       MOV  =HEADV,XR        POINT TO V.V STRING
       MOV  (XS)+,WA         PROVIDE SECOND ARGUMENT SCBLK
       JSR  SYSXI            CALL EXTERNAL ROUTINE
       ERR  105,EXIT action not available in this implementation
       ERR  106,EXIT action caused irrecoverable error
       IEQ  EXNUL            RETURN IF ARGUMENT 0
       IGT  SEXT2            SKIP IF POSITIVE
       NGI                   MAKE POSITIVE
*
*      CHECK FOR OPTION RESPECIFICATION
*
*      SYSXI RETURNS 0 IN WA WHEN A FILE HAS BEEN RESUMED,
*      1 WHEN THIS IS A CONTINUATION OF AN EXIT(4) OR EXIT(-4)
*      ACTION.
*
SEXT2  MFI  WC               GET VALUE IN WORK REG
       ADD  WC,WA            PREPARE TO TEST FOR CONTINUE
       BEQ  WA,=NUM05,SEXT5  CONTINUED EXECUTION IF 4 PLUS 1
       ZER  GBCNT            RESUMING EXECUTION SO RESET
       BGE  WC,=NUM03,SEXT3  SKIP IF WAS 3 OR 4
       MOV  WC,-(XS)         SAVE VALUE
       ZER  WC               SET TO READ OPTIONS
       JSR  PRPAR            READ SYSPP OPTIONS
       MOV  (XS)+,WC         RESTORE VALUE
*
*      DEAL WITH HEADER OPTION (FIDDLED BY PRPAR)
*
SEXT3  MNZ  HEADP            ASSUME NO HEADERS
       BNE  WC,=NUM01,SEXT4  SKIP IF NOT 1
       ZER  HEADP            REQUEST HEADER PRINTING
*
*      ALMOST READY TO RESUME RUNNING
*
SEXT4  JSR  SYSTM            GET EXECUTION TIME START (SGD11)
       STI  TIMSX            SAVE AS INITIAL TIME
       LDI  KVSTC            RESET TO ENSURE ...
       STI  KVSTL            ... CORRECT EXECUTION STATS
       JSR  STGCC            RECOMPUTE COUNTDOWN COUNTERS
       BRN  EXNUL            RESUME EXECUTION
*
*      HERE AFTER EXIT(4) OR EXIT(-4) -- CREATE SAVE FILE
*      OR LOAD MODULE AND CONTINUE EXECUTION.
*
*      RETURN INTEGER 1 TO SIGNAL THE CONTINUATION OF THE
*      ORIGINAL EXECUTION.
*
SEXT5  MOV  =INTON,XR        INTEGER ONE
       BRN  EXIXR            RETURN AS RESULT
.FI
       EJC
.IF    .CMTH
*
*      EXP
*
S_EXP  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  304,EXP argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       ETX                   TAKE EXPONENTIAL
       RNO  EXREA            IF NO OVERFLOW, RETURN RESULT IN RA
       ERB  305,EXP produced real overflow
       EJC
.FI
*
*      FIELD
*
S_FLD  ENT                   ENTRY POINT
       JSR  GTSMI            GET SECOND ARGUMENT (FIELD NUMBER)
       ERR  107,FIELD second argument is not integer
       PPM  EXFAL            FAIL IF OUT OF RANGE
       MOV  XR,WB            ELSE SAVE INTEGER VALUE
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       JSR  GTNVR            POINT TO VRBLK
       PPM  SFLD1            JUMP (ERROR) IF NOT VARIABLE NAME
       MOV  VRFNC(XR),XR     ELSE POINT TO FUNCTION BLOCK
       BNE  (XR),=B_DFC,SFLD1 ERROR IF NOT DATATYPE FUNCTION
*
*      HERE IF FIRST ARGUMENT IS A DATATYPE FUNCTION NAME
*
       BZE  WB,EXFAL         FAIL IF ARGUMENT NUMBER IS ZERO
       BGT  WB,FARGS(XR),EXFAL FAIL IF TOO LARGE
       WTB  WB               ELSE CONVERT TO BYTE OFFSET
       ADD  WB,XR            POINT TO FIELD NAME
       MOV  DFFLB(XR),XR     LOAD VRBLK POINTER
       BRN  EXVNM            EXIT TO BUILD NMBLK
*
*      HERE FOR BAD FIRST ARGUMENT
*
SFLD1  ERB  108,FIELD first argument is not datatype name
       EJC
*
*      FENCE
*
S_FNC  ENT                   ENTRY POINT
       MOV  =P_FNC,WB        SET PCODE FOR P_FNC
       ZER  XR               P0BLK
       JSR  PBILD            BUILD P_FNC NODE
       MOV  XR,XL            SAVE POINTER TO IT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  259,FENCE argument is not pattern
       JSR  PCONC            CONCATENATE TO P_FNC NODE
       MOV  XR,XL            SAVE PTR TO CONCATENATED PATTERN
       MOV  =P_FNA,WB        SET FOR P_FNA PCODE
       ZER  XR               P0BLK
       JSR  PBILD            CONSTRUCT P_FNA NODE
       MOV  XL,PTHEN(XR)     SET PATTERN AS PTHEN
       MOV  XR,-(XS)         SET AS RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE NEXT CODE WORD
       EJC
*
*      GE
*
S_GEF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  109,GE first argument is not numeric
       ERR  110,GE second argument is not numeric
       PPM  EXFAL            FAIL IF LT
       PPM  EXNUL            RETURN NULL IF EQ
       PPM  EXNUL            RETURN NULL IF GT
       EJC
*
*      GT
*
S_GTF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  111,GT first argument is not numeric
       ERR  112,GT second argument is not numeric
       PPM  EXFAL            FAIL IF LT
       PPM  EXFAL            FAIL IF EQ
       PPM  EXNUL            RETURN NULL IF GT
       EJC
*
*      HOST
*
S_HST  ENT                   ENTRY POINT
       MOV  (XS)+,WC         GET FIFTH ARG
       MOV  (XS)+,WB         GET FOURTH ARG
       MOV  (XS)+,XR         GET THIRD ARG
       MOV  (XS)+,XL         GET SECOND ARG
       MOV  (XS)+,WA         GET FIRST ARG
       JSR  SYSHS            ENTER SYSHS ROUTINE
       ERR  254,Erroneous argument for HOST
       ERR  255,Error during execution of HOST
       PPM  SHST1            STORE HOST STRING
       PPM  EXNUL            RETURN NULL RESULT
       PPM  EXIXR            RETURN XR
       PPM  EXFAL            FAIL RETURN
       PPM  SHST3            STORE ACTUAL STRING
       PPM  SHST4            RETURN COPY OF XR
*
*      RETURN HOST STRING
*
SHST1  BZE  XL,EXNUL         NULL STRING IF SYSHS UNCOOPERATIVE
       MOV  SCLEN(XL),WA     LENGTH
       ZER  WB               ZERO OFFSET
*
*      COPY STRING AND RETURN
*
SHST2  JSR  SBSTR            BUILD COPY OF STRING
       MOV  XR,-(XS)         STACK THE RESULT
       LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      RETURN ACTUAL STRING POINTED TO BY XL
*
SHST3  ZER  WB               TREAT XL LIKE AN SCBLK PTR
       SUB  =CFP_F,WB        BY CREATING A NEGATIVE OFFSET
       BRN  SHST2            JOIN TO COPY STRING
*
*      RETURN COPY OF BLOCK POINTED TO BY XR
*
SHST4  MOV  XR,-(XS)         STACK RESULTS
       JSR  COPYB            MAKE COPY OF BLOCK
       PPM  EXITS            IF NOT AN AGGREGATE STRUCTURE
       BRN  EXSID            SET CURRENT ID VALUE OTHERWISE
       EJC
*
*      IDENT
*
S_IDN  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD SECOND ARGUMENT
       MOV  (XS)+,XL         LOAD FIRST ARGUMENT
       JSR  IDENT            CALL IDENT COMPARISON ROUTINE
       PPM  EXNUL            RETURN NULL IF IDENT
       BRN  EXFAL            FAIL IF DIFFER
       EJC
*
*      INPUT
*
S_INP  ENT                   ENTRY POINT
       ZER  WB               INPUT FLAG
       JSR  IOPUT            CALL INPUT/OUTPUT ASSOC. ROUTINE
       ERR  113,INPUT third argument is not a string
       ERR  114,Inappropriate second argument for INPUT
       ERR  115,Inappropriate first argument for INPUT
       ERR  116,Inappropriate file specification for INPUT
       PPM  EXFAL            FAIL IF FILE DOES NOT EXIST
       ERR  117,INPUT file cannot be read
       ERR  289,INPUT channel currently in use
       BRN  EXNUL            RETURN NULL STRING
       EJC
.IF    .CNBF
.ELSE
*
*      INSERT
*
S_INS  ENT                   ENTRY POINT
       MOV  (XS)+,XL         GET STRING ARG
       JSR  GTSMI            GET REPLACE LENGTH
       ERR  277,INSERT third argument not integer
       PPM  EXFAL            FAIL IF OUT OF RANGE
       MOV  WC,WB            COPY TO PROPER REG
       JSR  GTSMI            GET REPLACE POSITION
       ERR  278,INSERT second argument not integer
       PPM  EXFAL            FAIL IF OUT OF RANGE
       BZE  WC,EXFAL         FAIL IF ZERO
       DCV  WC               DECREMENT TO GET OFFSET
       MOV  WC,WA            PUT IN PROPER REGISTER
       MOV  (XS)+,XR         GET BUFFER
       BEQ  (XR),=B_BCT,SINS1 PRESS ON IF TYPE OK
       ERB  279,INSERT first argument is not a buffer
*
*      HERE WHEN EVERYTHING LOADED UP
*
SINS1  JSR  INSBF            CALL TO INSERT
       ERR  280,INSERT fourth argument is not a string
       PPM  EXFAL            FAIL IF OUT OF RANGE
       BRN  EXNUL            ELSE OK - EXIT WITH NULL
       EJC
.FI
*
*      INTEGER
*
S_INT  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTNUM            CONVERT TO NUMERIC
       PPM  EXFAL            FAIL IF NON-NUMERIC
       BEQ  WA,=B_ICL,EXNUL  RETURN NULL IF INTEGER
       BRN  EXFAL            FAIL IF REAL
       EJC
*
*      ITEM
*
*      ITEM DOES NOT PERMIT THE DIRECT (FAST) CALL SO THAT
*      WA CONTAINS THE ACTUAL NUMBER OF ARGUMENTS PASSED.
*
S_ITM  ENT                   ENTRY POINT
*
*      DEAL WITH CASE OF NO ARGS
*
       BNZ  WA,SITM1         JUMP IF AT LEAST ONE ARG
       MOV  =NULLS,-(XS)     ELSE SUPPLY GARBAGE NULL ARG
       MOV  =NUM01,WA        AND FIX ARGUMENT COUNT
*
*      CHECK FOR NAME/VALUE CASES
*
SITM1  SCP  XR               GET CURRENT CODE POINTER
       MOV  (XR),XL          LOAD NEXT CODE WORD
       DCV  WA               GET NUMBER OF SUBSCRIPTS
       MOV  WA,XR            COPY FOR ARREF
       BEQ  XL,=OFNE_,SITM2  JUMP IF CALLED BY NAME
*
*      HERE IF CALLED BY VALUE
*
       ZER  WB               SET CODE FOR CALL BY VALUE
       BRN  ARREF            OFF TO ARRAY REFERENCE ROUTINE
*
*      HERE FOR CALL BY NAME
*
SITM2  MNZ  WB               SET CODE FOR CALL BY NAME
       LCW  WA               LOAD AND IGNORE OFNE_ CALL
       BRN  ARREF            OFF TO ARRAY REFERENCE ROUTINE
       EJC
*
*      LE
*
S_LEF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  118,LE first argument is not numeric
       ERR  119,LE second argument is not numeric
       PPM  EXNUL            RETURN NULL IF LT
       PPM  EXNUL            RETURN NULL IF EQ
       PPM  EXFAL            FAIL IF GT
       EJC
*
*      LEN
*
S_LEN  ENT                   ENTRY POINT
       MOV  =P_LEN,WB        SET PCODE FOR INTEGER ARG CASE
       MOV  =P_LND,WA        SET PCODE FOR EXPR ARG CASE
       JSR  PATIN            CALL COMMON ROUTINE TO BUILD NODE
       ERR  120,LEN argument is not integer or expression
       ERR  121,LEN argument is negative or too large
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      LEQ
*
S_LEQ  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  122,LEQ first argument is not a string
       ERR  123,LEQ second argument is not a string
       PPM  EXFAL            FAIL IF LLT
       PPM  EXNUL            RETURN NULL IF LEQ
       PPM  EXFAL            FAIL IF LGT
       EJC
*
*      LGE
*
S_LGE  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  124,LGE first argument is not a string
       ERR  125,LGE second argument is not a string
       PPM  EXFAL            FAIL IF LLT
       PPM  EXNUL            RETURN NULL IF LEQ
       PPM  EXNUL            RETURN NULL IF LGT
       EJC
*
*      LGT
*
S_LGT  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  126,LGT first argument is not a string
       ERR  127,LGT second argument is not a string
       PPM  EXFAL            FAIL IF LLT
       PPM  EXFAL            FAIL IF LEQ
       PPM  EXNUL            RETURN NULL IF LGT
       EJC
*
*      LLE
*
S_LLE  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  128,LLE first argument is not a string
       ERR  129,LLE second argument is not a string
       PPM  EXNUL            RETURN NULL IF LLT
       PPM  EXNUL            RETURN NULL IF LEQ
       PPM  EXFAL            FAIL IF LGT
       EJC
*
*      LLT
*
S_LLT  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  130,LLT first argument is not a string
       ERR  131,LLT second argument is not a string
       PPM  EXNUL            RETURN NULL IF LLT
       PPM  EXFAL            FAIL IF LEQ
       PPM  EXFAL            FAIL IF LGT
       EJC
*
*      LNE
*
S_LNE  ENT                   ENTRY POINT
       JSR  LCOMP            CALL STRING COMPARISON ROUTINE
       ERR  132,LNE first argument is not a string
       ERR  133,LNE second argument is not a string
       PPM  EXNUL            RETURN NULL IF LLT
       PPM  EXFAL            FAIL IF LEQ
       PPM  EXNUL            RETURN NULL IF LGT
       EJC
.IF    .CMTH
*
*      LN
*
S_LNF  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  306,LN argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       REQ  SLNF1            OVERFLOW IF ARGUMENT IS 0
       RLT  SLNF2            ERROR IF ARGUMENT LESS THAN 0
       LNF                   TAKE NATURAL LOGARITHM
       RNO  EXREA            IF NO OVERFLOW, RETURN RESULT IN RA
SLNF1  ERB  307,LN produced real overflow
*
*      HERE FOR BAD ARGUMENT
*
SLNF2  ERB  315,LN argument negative
       EJC
.FI
*
*      LOCAL
*
S_LOC  ENT                   ENTRY POINT
       JSR  GTSMI            GET SECOND ARGUMENT (LOCAL NUMBER)
       ERR  134,LOCAL second argument is not integer
       PPM  EXFAL            FAIL IF OUT OF RANGE
       MOV  XR,WB            SAVE LOCAL NUMBER
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       JSR  GTNVR            POINT TO VRBLK
       PPM  SLOC1            JUMP IF NOT VARIABLE NAME
       MOV  VRFNC(XR),XR     ELSE LOAD FUNCTION POINTER
       BNE  (XR),=B_PFC,SLOC1 JUMP IF NOT PROGRAM DEFINED
*
*      HERE IF WE HAVE A PROGRAM DEFINED FUNCTION NAME
*
       BZE  WB,EXFAL         FAIL IF SECOND ARG IS ZERO
       BGT  WB,PFNLO(XR),EXFAL OR TOO LARGE
       ADD  FARGS(XR),WB     ELSE ADJUST OFFSET TO INCLUDE ARGS
       WTB  WB               CONVERT TO BYTES
       ADD  WB,XR            POINT TO LOCAL POINTER
       MOV  PFAGB(XR),XR     LOAD VRBLK POINTER
       BRN  EXVNM            EXIT BUILDING NMBLK
*
*      HERE IF FIRST ARGUMENT IS NO GOOD
*
SLOC1  ERB  135,LOCAL first arg is not a program function name
.IF    .CNLD
.ELSE
       EJC
*
*      LOAD
*
S_LOD  ENT                   ENTRY POINT
       JSR  GTSTG            LOAD LIBRARY NAME
       ERR  136,LOAD second argument is not a string
       MOV  XR,XL            SAVE LIBRARY NAME
       JSR  XSCNI            PREPARE TO SCAN FIRST ARGUMENT
       ERR  137,LOAD first argument is not a string
       ERR  138,LOAD first argument is null
       MOV  XL,-(XS)         STACK LIBRARY NAME
       MOV  =CH_PP,WC        SET DELIMITER ONE = LEFT PAREN
       MOV  WC,XL            SET DELIMITER TWO = LEFT PAREN
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN FUNCTION NAME
       MOV  XR,-(XS)         SAVE PTR TO FUNCTION NAME
       BNZ  WA,SLOD1         JUMP IF LEFT PAREN FOUND
       ERB  139,LOAD first argument is missing a left paren
*
*      HERE AFTER SUCCESSFULLY SCANNING FUNCTION NAME
*
SLOD1  JSR  GTNVR            LOCATE VRBLK
       ERR  140,LOAD first argument has null function name
       MOV  XR,LODFN         SAVE VRBLK POINTER
       ZER  LODNA            ZERO COUNT OF ARGUMENTS
*
*      LOOP TO SCAN ARGUMENT DATATYPE NAMES
*
SLOD2  MOV  =CH_RP,WC        DELIMITER ONE IS RIGHT PAREN
       MOV  =CH_CM,XL        DELIMITER TWO IS COMMA
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN NEXT ARGUMENT NAME
       ICV  LODNA            BUMP ARGUMENT COUNT
       BNZ  WA,SLOD3         JUMP IF OK DELIMITER WAS FOUND
       ERB  141,LOAD first argument is missing a right paren
       EJC
*
*      LOAD (CONTINUED)
*
*      COME HERE TO ANALYZE THE DATATYPE POINTER IN (XR). THIS
*      CODE IS USED BOTH FOR ARGUMENTS (WA=1,2) AND FOR THE
*      RESULT DATATYPE (WITH WA SET TO ZERO).
*
.IF    .CULC
SLOD3  MOV  WA,WB            SAVE SCAN MODE
       MOV  SCLEN(XR),WA     DATATYPE LENGTH
       BZE  WA,SLD3A         BYPASS IF NULL STRING
       JSR  FLSTG            FOLD TO UPPER CASE
SLD3A  MOV  WB,WA            RESTORE SCAN MODE
       MOV  XR,-(XS)         STACK DATATYPE NAME POINTER
.ELSE
SLOD3  MOV  XR,-(XS)         STACK DATATYPE NAME POINTER
.FI
       MOV  =NUM01,WB        SET STRING CODE IN CASE
       MOV  =SCSTR,XL        POINT TO /STRING/
       JSR  IDENT            CHECK FOR MATCH
       PPM  SLOD4            JUMP IF MATCH
       MOV  (XS),XR          ELSE RELOAD NAME
       ADD  WB,WB            SET CODE FOR INTEGER (2)
       MOV  =SCINT,XL        POINT TO /INTEGER/
       JSR  IDENT            CHECK FOR MATCH
       PPM  SLOD4            JUMP IF MATCH
.IF    .CNRA
.ELSE
       MOV  (XS),XR          ELSE RELOAD STRING POINTER
       ICV  WB               SET CODE FOR REAL (3)
       MOV  =SCREA,XL        POINT TO /REAL/
       JSR  IDENT            CHECK FOR MATCH
       PPM  SLOD4            JUMP IF MATCH
.FI
.IF    .CNLF
       MOV  (XS),XR          RELOAD STRING POINTER
       ICV  WB               CODE FOR FILE (4, OR 3 IF NO REALS)
       MOV  =SCFIL,XL        POINT TO /FILE/
       JSR  IDENT            CHECK FOR MATCH
       PPM  SLOD4            JUMP IF MATCH
.FI
       ZER  WB               ELSE GET CODE FOR NO CONVERT
*
*      MERGE HERE WITH PROPER DATATYPE CODE IN WB
*
SLOD4  MOV  WB,(XS)          STORE CODE ON STACK
       BEQ  WA,=NUM02,SLOD2  LOOP BACK IF ARG STOPPED BY COMMA
       BZE  WA,SLOD5         JUMP IF THAT WAS THE RESULT TYPE
*
*      HERE WE SCAN OUT THE RESULT TYPE (ARG STOPPED BY ) )
*
       MOV  MXLEN,WC         SET DUMMY (IMPOSSIBLE) DELIMITER 1
       MOV  WC,XL            AND DELIMITER TWO
       MNZ  WA               SKIP/TRIM BLANKS IN PROTOTYPE
       JSR  XSCAN            SCAN RESULT NAME
       ZER  WA               SET CODE FOR PROCESSING RESULT
       BRN  SLOD3            JUMP BACK TO PROCESS RESULT NAME
       EJC
*
*      LOAD (CONTINUED)
*
*      HERE AFTER PROCESSING ALL ARGS AND RESULT
*
SLOD5  MOV  LODNA,WA         GET NUMBER OF ARGUMENTS
       MOV  WA,WC            COPY FOR LATER
       WTB  WA               CONVERT LENGTH TO BYTES
       ADD  *EFSI_,WA        ADD SPACE FOR STANDARD FIELDS
       JSR  ALLOC            ALLOCATE EFBLK
       MOV  =B_EFC,(XR)      SET TYPE WORD
       MOV  WC,FARGS(XR)     SET NUMBER OF ARGUMENTS
       ZER  EFUSE(XR)        SET USE COUNT (DFFNC WILL SET TO 1)
       ZER  EFCOD(XR)        ZERO CODE POINTER FOR NOW
       MOV  (XS)+,EFRSL(XR)  STORE RESULT TYPE CODE
       MOV  LODFN,EFVAR(XR)  STORE FUNCTION VRBLK POINTER
       MOV  WA,EFLEN(XR)     STORE EFBLK LENGTH
       MOV  XR,WB            SAVE EFBLK POINTER
       ADD  WA,XR            POINT PAST END OF EFBLK
       LCT  WC,WC            SET NUMBER OF ARGUMENTS FOR LOOP
*
*      LOOP TO SET ARGUMENT TYPE CODES FROM STACK
*
SLOD6  MOV  (XS)+,-(XR)      STORE ONE TYPE CODE FROM STACK
       BCT  WC,SLOD6         LOOP TILL ALL STORED
*
*      NOW LOAD THE EXTERNAL FUNCTION AND PERFORM DEFINITION
*
       MOV  (XS)+,XR         LOAD FUNCTION STRING NAME
.IF    .CULC
       MOV  SCLEN(XR),WA     FUNCTION NAME LENGTH
       JSR  FLSTG            FOLD TO UPPER CASE
.FI
       MOV  (XS),XL          LOAD LIBRARY NAME
       MOV  WB,(XS)          STORE EFBLK POINTER
       JSR  SYSLD            CALL FUNCTION TO LOAD EXTERNAL FUNC
       ERR  142,LOAD function does not exist
       ERR  143,LOAD function caused input error during load
       ERR  328,LOAD function - insufficient memory
       MOV  (XS)+,XL         RECALL EFBLK POINTER
       MOV  XR,EFCOD(XL)     STORE CODE POINTER
       MOV  LODFN,XR         POINT TO VRBLK FOR FUNCTION
       JSR  DFFNC            PERFORM FUNCTION DEFINITION
       BRN  EXNUL            RETURN NULL RESULT
.FI
       EJC
*
*      LPAD
*
S_LPD  ENT                   ENTRY POINT
       JSR  GTSTG            GET PAD CHARACTER
       ERR  144,LPAD third argument is not a string
       PLC  XR               POINT TO CHARACTER (NULL IS BLANK)
       LCH  WB,(XR)          LOAD PAD CHARACTER
       JSR  GTSMI            GET PAD LENGTH
       ERR  145,LPAD second argument is not integer
       PPM  SLPD4            SKIP IF NEGATIVE OR LARGE
*
*      MERGE TO CHECK FIRST ARG
*
SLPD1  JSR  GTSTG            GET FIRST ARGUMENT (STRING TO PAD)
       ERR  146,LPAD first argument is not a string
       BGE  WA,WC,EXIXR      RETURN 1ST ARG IF TOO LONG TO PAD
       MOV  XR,XL            ELSE MOVE PTR TO STRING TO PAD
*
*      NOW WE ARE READY FOR THE PAD
*
*      (XL)                  POINTER TO STRING TO PAD
*      (WB)                  PAD CHARACTER
*      (WC)                  LENGTH TO PAD STRING TO
*
       MOV  WC,WA            COPY LENGTH
       JSR  ALOCS            ALLOCATE SCBLK FOR NEW STRING
       MOV  XR,-(XS)         SAVE AS RESULT
       MOV  SCLEN(XL),WA     LOAD LENGTH OF ARGUMENT
       SUB  WA,WC            CALCULATE NUMBER OF PAD CHARACTERS
       PSC  XR               POINT TO CHARS IN RESULT STRING
       LCT  WC,WC            SET COUNTER FOR PAD LOOP
*
*      LOOP TO PERFORM PAD
*
SLPD2  SCH  WB,(XR)+         STORE PAD CHARACTER, BUMP PTR
       BCT  WC,SLPD2         LOOP TILL ALL PAD CHARS STORED
       CSC  XR               COMPLETE STORE CHARACTERS
*
*      NOW COPY STRING
*
       BZE  WA,SLPD3         EXIT IF NULL STRING
       PLC  XL               ELSE POINT TO CHARS IN ARGUMENT
       MVC                   MOVE CHARACTERS TO RESULT STRING
       ZER  XL               CLEAR GARBAGE XL
*
*      HERE TO EXIT WITH RESULT ON STACK
*
SLPD3  LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      HERE IF 2ND ARG IS NEGATIVE OR LARGE
*
SLPD4  ZER  WC               ZERO PAD COUNT
       BRN  SLPD1            MERGE
       EJC
*
*      LT
*
S_LTF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  147,LT first argument is not numeric
       ERR  148,LT second argument is not numeric
       PPM  EXNUL            RETURN NULL IF LT
       PPM  EXFAL            FAIL IF EQ
       PPM  EXFAL            FAIL IF GT
       EJC
*
*      NE
*
S_NEF  ENT                   ENTRY POINT
       JSR  ACOMP            CALL ARITHMETIC COMPARISON ROUTINE
       ERR  149,NE first argument is not numeric
       ERR  150,NE second argument is not numeric
       PPM  EXNUL            RETURN NULL IF LT
       PPM  EXFAL            FAIL IF EQ
       PPM  EXNUL            RETURN NULL IF GT
       EJC
*
*      NOTANY
*
S_NAY  ENT                   ENTRY POINT
       MOV  =P_NAS,WB        SET PCODE FOR SINGLE CHAR ARG
       MOV  =P_NAY,XL        PCODE FOR MULTI-CHAR ARG
       MOV  =P_NAD,WC        SET PCODE FOR EXPR ARG
       JSR  PATST            CALL COMMON ROUTINE TO BUILD NODE
       ERR  151,NOTANY argument is not a string or expression
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      OPSYN
*
S_OPS  ENT                   ENTRY POINT
       JSR  GTSMI            LOAD THIRD ARGUMENT
       ERR  152,OPSYN third argument is not integer
       ERR  153,OPSYN third argument is negative or too large
       MOV  WC,WB            IF OK, SAVE THIRD ARGUMNET
       MOV  (XS)+,XR         LOAD SECOND ARGUMENT
       JSR  GTNVR            LOCATE VARIABLE BLOCK
       ERR  154,OPSYN second arg is not natural variable name
       MOV  VRFNC(XR),XL     IF OK, LOAD FUNCTION BLOCK POINTER
       BNZ  WB,SOPS2         JUMP IF OPERATOR OPSYN CASE
*
*      HERE FOR FUNCTION OPSYN (THIRD ARG ZERO)
*
       MOV  (XS)+,XR         LOAD FIRST ARGUMENT
       JSR  GTNVR            GET VRBLK POINTER
       ERR  155,OPSYN first arg is not natural variable name
*
*      MERGE HERE TO PERFORM FUNCTION DEFINITION
*
SOPS1  JSR  DFFNC            CALL FUNCTION DEFINER
       BRN  EXNUL            EXIT WITH NULL RESULT
*
*      HERE FOR OPERATOR OPSYN (THIRD ARG NON-ZERO)
*
SOPS2  JSR  GTSTG            GET OPERATOR NAME
       PPM  SOPS5            JUMP IF NOT STRING
       BNE  WA,=NUM01,SOPS5  ERROR IF NOT ONE CHAR LONG
       PLC  XR               ELSE POINT TO CHARACTER
       LCH  WC,(XR)          LOAD CHARACTER NAME
       EJC
*
*      OPSYN (CONTINUED)
*
*      NOW SET TO SEARCH FOR MATCHING UNARY OR BINARY OPERATOR
*      NAME AS APPROPRIATE. NOTE THAT THERE ARE =OPBUN UNDEFINED
*      BINARY OPERATORS AND =OPUUN UNDEFINED UNARY OPERATORS.
*
       MOV  =R_UUB,WA        POINT TO UNOP POINTERS IN CASE
       MOV  =OPNSU,XR        POINT TO NAMES OF UNARY OPERATORS
       ADD  =OPBUN,WB        ADD NO. OF UNDEFINED BINARY OPS
       BEQ  WB,=OPUUN,SOPS3  JUMP IF UNOP (THIRD ARG WAS 1)
       MOV  =R_UBA,WA        ELSE POINT TO BINARY OPERATOR PTRS
       MOV  =OPSNB,XR        POINT TO NAMES OF BINARY OPERATORS
       MOV  =OPBUN,WB        SET NUMBER OF UNDEFINED BINOPS
*
*      MERGE HERE TO CHECK LIST (WB = NUMBER TO CHECK)
*
SOPS3  LCT  WB,WB            SET COUNTER TO CONTROL LOOP
*
*      LOOP TO SEARCH FOR NAME MATCH
*
SOPS4  BEQ  WC,(XR),SOPS6    JUMP IF NAMES MATCH
       ICA  WA               ELSE PUSH POINTER TO FUNCTION PTR
       ICA  XR               BUMP POINTER
       BCT  WB,SOPS4         LOOP BACK TILL ALL CHECKED
*
*      HERE IF BAD OPERATOR NAME
*
SOPS5  ERB  156,OPSYN first arg is not correct operator name
*
*      COME HERE ON FINDING A MATCH IN THE OPERATOR NAME TABLE
*
SOPS6  MOV  WA,XR            COPY POINTER TO FUNCTION BLOCK PTR
       SUB  *VRFNC,XR        MAKE IT LOOK LIKE DUMMY VRBLK
       BRN  SOPS1            MERGE BACK TO DEFINE OPERATOR
       EJC
.IF    .C370
*
*      OR
*
S_ORF  ENT                   ENTRY POINT
       MNZ  WB               SIGNAL TWO ARGUMENTS
       JSR  SBOOL            CALL STRING BOOLEAN ROUTINE
       ERR  xxx,OR first argument is not a string
       ERR  xxx,OR second argument is not a string
       ERR  xxx,OR arguments not same length
       PPM  EXITS            NULL STRING ARGUMENTS
*
*      HERE TO PROCESS (WC) WORDS.  RESULT IS STACKED.
*
SORF1  MOV  (XL)+,WA         GET NEXT CFP_C CHARS FROM ARG 1
       ORB  (XR),WA          OR WITH CHARACTERS FROM ARG 2
       MOV  WA,(XR)+         PUT BACK IN MEMORY
       BCT  WC,SORF1         LOOP OVER ALL WORDS IN STRING BLOCK
       BRN  EXITS            FETCH NEXT CODE WORD
       EJC
.FI
*
*      OUTPUT
*
S_OUP  ENT                   ENTRY POINT
       MOV  =NUM03,WB        OUTPUT FLAG
       JSR  IOPUT            CALL INPUT/OUTPUT ASSOC. ROUTINE
       ERR  157,OUTPUT third argument is not a string
       ERR  158,Inappropriate second argument for OUTPUT
       ERR  159,Inappropriate first argument for OUTPUT
       ERR  160,Inappropriate file specification for OUTPUT
       PPM  EXFAL            FAIL IF FILE DOES NOT EXIST
       ERR  161,OUTPUT file cannot be written to
       ERR  290,OUTPUT channel currently in use
       BRN  EXNUL            RETURN NULL STRING
       EJC
*
*      POS
*
S_POS  ENT                   ENTRY POINT
       MOV  =P_POS,WB        SET PCODE FOR INTEGER ARG CASE
       MOV  =P_PSD,WA        SET PCODE FOR EXPRESSION ARG CASE
       JSR  PATIN            CALL COMMON ROUTINE TO BUILD NODE
       ERR  162,POS argument is not integer or expression
       ERR  163,POS argument is negative or too large
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      PROTOTYPE
*
S_PRO  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       MOV  TBLEN(XR),WB     LENGTH IF TABLE, VECTOR (=VCLEN)
       BTW  WB               CONVERT TO WORDS
       MOV  (XR),WA          LOAD TYPE WORD OF ARGUMENT BLOCK
       BEQ  WA,=B_ART,SPRO4  JUMP IF ARRAY
       BEQ  WA,=B_TBT,SPRO1  JUMP IF TABLE
       BEQ  WA,=B_VCT,SPRO3  JUMP IF VECTOR
.IF    .CNBF
.ELSE
       BEQ  WA,=B_BCT,SPR05  JUMP IF BUFFER
.FI
       ERB  164,PROTOTYPE argument is not valid object
*
*      HERE FOR TABLE
*
SPRO1  SUB  =TBSI_,WB        SUBTRACT STANDARD FIELDS
*
*      MERGE FOR VECTOR
*
SPRO2  MTI  WB               CONVERT TO INTEGER
       BRN  EXINT            EXIT WITH INTEGER RESULT
*
*      HERE FOR VECTOR
*
SPRO3  SUB  =VCSI_,WB        SUBTRACT STANDARD FIELDS
       BRN  SPRO2            MERGE
*
*      HERE FOR ARRAY
*
SPRO4  ADD  AROFS(XR),XR     POINT TO PROTOTYPE FIELD
       MOV  (XR),XR          LOAD PROTOTYPE
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.IF    .CNBF
.ELSE
*
*      HERE FOR BUFFER
*
SPR05  MOV  BCBUF(XR),XR     POINT TO BFBLK
       MTI  BFALC(XR)        LOAD ALLOCATED LENGTH
       BRN  EXINT            EXIT WITH INTEGER ALLOCATION
.FI
       EJC
*
*      REMDR
*
S_RMD  ENT                   ENTRY POINT
.IF    .CMTH
       JSR  ARITH            GET TWO INTEGERS OR TWO REALS
       ERR  166,REMDR first argument is not numeric
       ERR  165,REMDR second argument is not numeric
       PPM  SRM06            IF REAL
.ELSE
       MOV  (XS),XR          LOAD SECOND ARGUMENT
       JSR  GTINT            CONVERT TO INTEGER
       ERR  165,REMDR second argument is not integer
       MOV  XR,(XS)          PLACE CONVERTED ARG IN STACK
       JSR  ARITH            CONVERT ARGS
       PPM  SRM04            FIRST ARG NOT INTEGER
       PPM                   SECOND ARG CHECKED ABOVE
.IF    .CNRA
.ELSE
       PPM  SRM01            FIRST ARG REAL
.FI
.FI
*
*      BOTH ARGUMENTS INTEGER
*
       ZER  WB               SET POSITIVE FLAG
       LDI  ICVAL(XR)        LOAD LEFT ARGUMENT VALUE
       IGE  SRM01            JUMP IF POSITIVE
       MNZ  WB               SET NEGATIVE FLAG
SRM01  RMI  ICVAL(XL)        GET REMAINDER
       IOV  SRM05            ERROR IF OVERFLOW
*
*      MAKE SIGN OF RESULT MATCH SIGN OF FIRST ARGUMENT
*
       BZE  WB,SRM03         IF RESULT SHOULD BE POSITIVE
       ILE  EXINT            IF SHOULD BE NEGATIVE, AND IS
SRM02  NGI                   ADJUST SIGN OF RESULT
       BRN  EXINT            RETURN RESULT
SRM03  ILT  SRM02            SHOULD BE POS, AND RESULT NEGATIVE
       BRN  EXINT            SHOULD BE POSITIVE, AND IS
*
*      FAIL FIRST ARGUMENT
*
SRM04  ERB  166,REMDR first argument is not numeric
*
*      FAIL IF OVERFLOW
*
SRM05  ERB  167,REMDR caused integer overflow
.IF    .CMTH
*
*      HERE WITH 1ST ARGUMENT IN (XR), 2ND IN (XL), BOTH REAL
*
*      RESULT = N1 - CHOP(N1/N2)*N2
*
SRM06  ZER  WB               SET POSITIVE FLAG
       LDR  RCVAL(XR)        LOAD LEFT ARGUMENT VALUE
       RGE  SRM07            JUMP IF POSITIVE
       MNZ  WB               SET NEGATIVE FLAG
SRM07  DVR  RCVAL(XL)        COMPUTE N1/N2
       ROV  SRM10            JUMP IF OVERFLOW
       CHP                   CHOP RESULT
       MLR  RCVAL(XL)        TIMES N2
       SBR  RCVAL(XR)        COMPUTE DIFFERENCE
*
*      MAKE SIGN OF RESULT MATCH SIGN OF FIRST ARGUMENT
*      -RESULT IS IN RA AT THIS POINT
*
       BZE  WB,SRM09         IF RESULT SHOULD BE POSITIVE
       RLE  EXREA            IF SHOULD BE NEGATIVE, AND IS
SRM08  NGR                   ADJUST SIGN OF RESULT
       BRN  EXREA            RETURN RESULT
SRM09  RLT  SRM08            SHOULD BE POS, AND RESULT NEGATIVE
       BRN  EXREA            SHOULD BE POSITIVE, AND IS
*
*      FAIL IF OVERFLOW
*
SRM10  ERB  312,REMDR caused real overflow
.FI
       EJC
*
*      REPLACE
*
*      THE ACTUAL REPLACE OPERATION USES AN SCBLK WHOSE CFP_A
*      CHARS CONTAIN THE TRANSLATED VERSIONS OF ALL THE CHARS.
*      THE TABLE POINTER IS REMEMBERED FROM CALL TO CALL AND
*      THE TABLE IS ONLY BUILT WHEN THE ARGUMENTS CHANGE.
*
*      WE ALSO PERFORM AN OPTIMIZATION GLEANED FROM SPITBOL 370.
*      IF THE SECOND ARGUMENT IS &ALPHABET, THERE IS NO NEED TO
*      TO BUILD A REPLACE TABLE.  THE THIRD ARGUMENT CAN BE
*      USED DIRECTLY AS THE REPLACE TABLE.
*
S_RPL  ENT                   ENTRY POINT
       JSR  GTSTG            LOAD THIRD ARGUMENT AS STRING
       ERR  168,REPLACE third argument is not a string
       MOV  XR,XL            SAVE THIRD ARG PTR
       JSR  GTSTG            GET SECOND ARGUMENT
       ERR  169,REPLACE second argument is not a string
*
*      CHECK TO SEE IF THIS IS THE SAME TABLE AS LAST TIME
*
       BNE  XR,R_RA2,SRPL1   JUMP IF 2ND ARGUMENT DIFFERENT
       BEQ  XL,R_RA3,SRPL4   JUMP IF ARGS SAME AS LAST TIME
*
*      HERE WE BUILD A NEW REPLACE TABLE (NOTE WA = 2ND ARG LEN)
*
SRPL1  MOV  SCLEN(XL),WB     LOAD 3RD ARGUMENT LENGTH
       BNE  WA,WB,SRPL6      JUMP IF ARGUMENTS NOT SAME LENGTH
       BEQ  XR,KVALP,SRPL5   JUMP IF 2ND ARG IS ALPHABET STRING
       BZE  WB,SRPL6         JUMP IF NULL 2ND ARGUMENT
       MOV  XL,R_RA3         SAVE THIRD ARG FOR NEXT TIME IN
       MOV  XR,R_RA2         SAVE SECOND ARG FOR NEXT TIME IN
       MOV  KVALP,XL         POINT TO ALPHABET STRING
       MOV  SCLEN(XL),WA     LOAD ALPHABET SCBLK LENGTH
       MOV  R_RPT,XR         POINT TO CURRENT TABLE (IF ANY)
       BNZ  XR,SRPL2         JUMP IF WE ALREADY HAVE A TABLE
*
*      HERE WE ALLOCATE A NEW TABLE
*
       JSR  ALOCS            ALLOCATE NEW TABLE
       MOV  WC,WA            KEEP SCBLK LENGTH
       MOV  XR,R_RPT         SAVE TABLE POINTER FOR NEXT TIME
*
*      MERGE HERE WITH POINTER TO NEW TABLE BLOCK IN (XR)
*
SRPL2  CTB  WA,SCSI_         COMPUTE LENGTH OF SCBLK
       MVW                   COPY TO GET INITIAL TABLE VALUES
       EJC
*
*      REPLACE (CONTINUED)
*
*      NOW WE MUST PLUG SELECTED ENTRIES AS REQUIRED. NOTE THAT
*      WE ARE SHORT OF INDEX REGISTERS FOR THE FOLLOWING LOOP.
*      HENCE THE NEED TO REPEATEDLY RE-INITIALISE CHAR PTR XL
*
       MOV  R_RA2,XL         POINT TO SECOND ARGUMENT
       LCT  WB,WB            NUMBER OF CHARS TO PLUG
       ZER  WC               ZERO CHAR OFFSET
       MOV  R_RA3,XR         POINT TO 3RD ARG
       PLC  XR               GET CHAR PTR FOR 3RD ARG
*
*      LOOP TO PLUG CHARS
*
SRPL3  MOV  R_RA2,XL         POINT TO 2ND ARG
       PLC  XL,WC            POINT TO NEXT CHAR
       ICV  WC               INCREMENT OFFSET
       LCH  WA,(XL)          GET NEXT CHAR
       MOV  R_RPT,XL         POINT TO TRANSLATE TABLE
       PSC  XL,WA            CONVERT CHAR TO OFFSET INTO TABLE
       LCH  WA,(XR)+         GET TRANSLATED CHAR
       SCH  WA,(XL)          STORE IN TABLE
       CSC  XL               COMPLETE STORE CHARACTERS
       BCT  WB,SRPL3         LOOP TILL DONE
       EJC
*
*      REPLACE (CONTINUED)
*
*      HERE TO USE R_RPT AS REPLACE TABLE.
*
SRPL4  MOV  R_RPT,XL         REPLACE TABLE TO USE
*
*      HERE TO PERFORM TRANSLATE USING TABLE IN XL.
*
.IF    .CNBF
SRPL5  JSR  GTSTG            GET FIRST ARGUMENT
       ERR  170,REPLACE first argument is not a string
.ELSE
*
*      IF FIRST ARG IS A BUFFER, PERFORM TRANSLATE IN PLACE.
*
SRPL5  JSR  GTSTB            GET FIRST ARGUMENT
       ERR  170,REPLACE first argument is not a string or buffer
       BNZ  WB,SRPL7         BRANCH IF BUFFER
.FI
       BZE  WA,EXNUL         RETURN NULL IF NULL ARGUMENT
       MOV  XL,-(XS)         STACK REPLACE TABLE TO USE
       MOV  XR,XL            COPY POINTER
       MOV  WA,WC            SAVE LENGTH
       CTB  WA,SCHAR         GET SCBLK LENGTH
       JSR  ALLOC            ALLOCATE SPACE FOR COPY
       MOV  XR,WB            SAVE ADDRESS OF COPY
       MVW                   MOVE SCBLK CONTENTS TO COPY
       MOV  (XS)+,XR         UNSTACK REPLACE TABLE
       PLC  XR               POINT TO CHARS OF TABLE
       MOV  WB,XL            POINT TO STRING TO TRANSLATE
       PLC  XL               POINT TO CHARS OF STRING
       MOV  WC,WA            SET NUMBER OF CHARS TO TRANSLATE
       TRC                   PERFORM TRANSLATION
SRPL8  MOV  WB,-(XS)         STACK RESULT
       LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      ERROR POINT
*
SRPL6  ERB  171,Null or unequally long 2nd, 3rd args to REPLACE
.IF    .CNBF
.ELSE
*
*      HERE TO PERFORM REPLACEMENT WITHIN BUFFER
*
SRPL7  BZE  WA,SRPL8         RETURN BUFFER UNCHANGED IF EMPTY
       MOV  XR,WC            COPY BFBLK POINTER TO WC
       MOV  XL,XR            TRANSLATE TABLE TO XR
       PLC  XR               POINT TO CHARS OF TABLE
       MOV  WC,XL            POINT TO STRING TO TRANSLATE
       PLC  XL               POINT TO CHARS OF STRING
       TRC                   PERFORM TRANSLATION
       BRN  SRPL8            STACK RESULT AND EXIT
.FI
       EJC
*
*      REWIND
*
S_REW  ENT                   ENTRY POINT
       JSR  IOFCB            CALL FCBLK ROUTINE
       ERR  172,REWIND argument is not a suitable name
       ERR  173,REWIND argument is null
       ERR  174,REWIND file does not exist
       JSR  SYSRW            CALL SYSTEM REWIND FUNCTION
       ERR  174,REWIND file does not exist
       ERR  175,REWIND file does not permit rewind
       ERR  176,REWIND caused non-recoverable error
       BRN  EXNUL            EXIT WITH NULL RESULT IF NO ERROR
       EJC
*
*      REVERSE
*
S_RVS  ENT                   ENTRY POINT
.IF    .CNBF
       JSR  GTSTG            LOAD STRING ARGUMENT
       ERR  177,REVERSE argument is not a string
.ELSE
       JSR  GTSTB            LOAD STRING OR BUFFER ARGUMENT
       ERR  177,REVERSE argument is not a string or buffer
       BNZ  WB,SRVS3         BRANCH IF BUFFER
.FI
       BZE  WA,EXIXR         RETURN ARGUMENT IF NULL
       MOV  XR,XL            ELSE SAVE POINTER TO STRING ARG
       JSR  ALOCS            ALLOCATE SPACE FOR NEW SCBLK
       MOV  XR,-(XS)         STORE SCBLK PTR ON STACK AS RESULT
       PSC  XR               PREPARE TO STORE IN NEW SCBLK
       PLC  XL,WC            POINT PAST LAST CHAR IN ARGUMENT
       LCT  WC,WC            SET LOOP COUNTER
*
*      LOOP TO MOVE CHARS IN REVERSE ORDER
*
SRVS1  LCH  WB,-(XL)         LOAD NEXT CHAR FROM ARGUMENT
       SCH  WB,(XR)+         STORE IN RESULT
       BCT  WC,SRVS1         LOOP TILL ALL MOVED
*
*      HERE WHEN COMPLETE TO EXECUTE NEXT CODE WORD
*
SRVS4  CSC  XR               COMPLETE STORE CHARACTERS
       ZER  XL               CLEAR GARBAGE XL
SRVS2  LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.IF    .CNBF
.ELSE
*
*      HERE IF ARGUMENT IS A BUFFER.  PERFORM REVERSE IN PLACE.
*
SRVS3  MOV  WB,-(XS)         STACK BUFFER AS RESULT
       BZE  WA,SRVS2         RETURN BUFFER UNCHANGED IF EMPTY
       MOV  XR,XL            COPY BFBLK POINTER TO XL
       PSC  XR               PREPARE TO STORE AT FIRST CHAR
       PLC  XL,WA            POINT PAST LAST CHAR IN ARGUMENT
       RSH  WA,1             OPERATE ON HALF THE STRING
       LCT  WC,WA            SET LOOP COUNTER
*
*      LOOP TO SWAP CHARS FROM END TO END.  NOTE THAT IN THE
*      CASE OF AN ODD COUNT, THE MIDDLE CHAR IS NOT TOUCHED.
*
SRVS5  LCH  WB,-(XL)         LOAD NEXT CHAR FROM END
       LCH  WA,(XR)          LOAD NEXT CHAR FROM FRONT
       SCH  WB,(XR)+         STORE END CHAR IN FRONT
       SCH  WA,(XL)          STORE FRONT CHAR AT END
       BCT  WC,SRVS5         LOOP TILL ALL MOVED
       BRN  SRVS4            COMPLETE STORE
.FI
       EJC
*
*      RPAD
*
S_RPD  ENT                   ENTRY POINT
       JSR  GTSTG            GET PAD CHARACTER
       ERR  178,RPAD third argument is not a string
       PLC  XR               POINT TO CHARACTER (NULL IS BLANK)
       LCH  WB,(XR)          LOAD PAD CHARACTER
       JSR  GTSMI            GET PAD LENGTH
       ERR  179,RPAD second argument is not integer
       PPM  SRPD3            SKIP IF NEGATIVE OR LARGE
*
*      MERGE TO CHECK FIRST ARG.
*
SRPD1  JSR  GTSTG            GET FIRST ARGUMENT (STRING TO PAD)
       ERR  180,RPAD first argument is not a string
       BGE  WA,WC,EXIXR      RETURN 1ST ARG IF TOO LONG TO PAD
       MOV  XR,XL            ELSE MOVE PTR TO STRING TO PAD
*
*      NOW WE ARE READY FOR THE PAD
*
*      (XL)                  POINTER TO STRING TO PAD
*      (WB)                  PAD CHARACTER
*      (WC)                  LENGTH TO PAD STRING TO
*
       MOV  WC,WA            COPY LENGTH
       JSR  ALOCS            ALLOCATE SCBLK FOR NEW STRING
       MOV  XR,-(XS)         SAVE AS RESULT
       MOV  SCLEN(XL),WA     LOAD LENGTH OF ARGUMENT
       SUB  WA,WC            CALCULATE NUMBER OF PAD CHARACTERS
       PSC  XR               POINT TO CHARS IN RESULT STRING
       LCT  WC,WC            SET COUNTER FOR PAD LOOP
*
*      COPY ARGUMENT STRING
*
       BZE  WA,SRPD2         JUMP IF ARGUMENT IS NULL
       PLC  XL               ELSE POINT TO ARGUMENT CHARS
       MVC                   MOVE CHARACTERS TO RESULT STRING
       ZER  XL               CLEAR GARBAGE XL
*
*      LOOP TO SUPPLY PAD CHARACTERS
*
SRPD2  SCH  WB,(XR)+         STORE PAD CHARACTER, BUMP PTR
       BCT  WC,SRPD2         LOOP TILL ALL PAD CHARS STORED
       CSC  XR               COMPLETE CHARACTER STORING
       LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
*
*      HERE IF 2ND ARG IS NEGATIVE OR LARGE
*
SRPD3  ZER  WC               ZERO PAD COUNT
       BRN  SRPD1            MERGE
       EJC
*
*      RTAB
*
S_RTB  ENT                   ENTRY POINT
       MOV  =P_RTB,WB        SET PCODE FOR INTEGER ARG CASE
       MOV  =P_RTD,WA        SET PCODE FOR EXPRESSION ARG CASE
       JSR  PATIN            CALL COMMON ROUTINE TO BUILD NODE
       ERR  181,RTAB argument is not integer or expression
       ERR  182,RTAB argument is negative or too large
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
.IF    .CUST
*
*      SET
*
S_SET  ENT                   ENTRY POINT
       MOV  (XS)+,R_IO2      SAVE THIRD ARG (WHENCE)
.IF    .CUSR
       MOV  (XS)+,XR         GET SECOND ARG (OFFSET)
       JSR  GTREA            CONVERT TO REAL
       ERR  324,SET second argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
.ELSE
       MOV  (XS)+,R_IO1      SAVE SECOND ARG (OFFSET)
.FI
       JSR  IOFCB            CALL FCBLK ROUTINE
       ERR  291,SET first argument is not a suitable name
       ERR  292,SET first argument is null
       ERR  295,SET file does not exist
.IF    .CUSR
.ELSE
       MOV  R_IO1,WB         LOAD SECOND ARG
.FI
       MOV  R_IO2,WC         LOAD THIRD ARG
       JSR  SYSST            CALL SYSTEM SET ROUTINE
       ERR  293,Inappropriate second argument to SET
       ERR  294,Inappropriate third argument to SET
       ERR  295,SET file does not exist
       ERR  296,SET file does not permit setting file pointer
       ERR  297,SET caused non-recoverable I/O error
.IF    .CUSR
       RTI  EXREA            RETURN REAL POSITION IF NOT ABLE
       BRN  EXINT            TO RETURN INTEGER POSITION
.ELSE
       BRN  EXINT            OTHERWISE RETURN POSITION
.FI
       EJC
.FI
*
*      TAB
*
S_TAB  ENT                   ENTRY POINT
       MOV  =P_TAB,WB        SET PCODE FOR INTEGER ARG CASE
       MOV  =P_TBD,WA        SET PCODE FOR EXPRESSION ARG CASE
       JSR  PATIN            CALL COMMON ROUTINE TO BUILD NODE
       ERR  183,TAB argument is not integer or expression
       ERR  184,TAB argument is negative or too large
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      RPOS
*
S_RPS  ENT                   ENTRY POINT
       MOV  =P_RPS,WB        SET PCODE FOR INTEGER ARG CASE
       MOV  =P_RPD,WA        SET PCODE FOR EXPRESSION ARG CASE
       JSR  PATIN            CALL COMMON ROUTINE TO BUILD NODE
       ERR  185,RPOS argument is not integer or expression
       ERR  186,RPOS argument is negative or too large
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.IF    .CNSR
.ELSE
       EJC
*
*      RSORT
*
S_RSR  ENT                   ENTRY POINT
       MNZ  WA               MARK AS RSORT
       JSR  SORTA            CALL SORT ROUTINE
       PPM  EXFAL            IF CONVERSION FAILS, SO SHALL WE
       BRN  EXSID            RETURN, SETTING IDVAL
.FI
       EJC
*
*      SETEXIT
*
S_STX  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       MOV  STXVR,WA         LOAD OLD VRBLK POINTER
       ZER  XL               LOAD ZERO IN CASE NULL ARG
       BEQ  XR,=NULLS,SSTX1  JUMP IF NULL ARGUMENT (RESET CALL)
       JSR  GTNVR            ELSE GET SPECIFIED VRBLK
       PPM  SSTX2            JUMP IF NOT NATURAL VARIABLE
       MOV  VRLBL(XR),XL     ELSE LOAD LABEL
       BEQ  XL,=STNDL,SSTX2  JUMP IF LABEL IS NOT DEFINED
       BNE  (XL),=B_TRT,SSTX1 JUMP IF NOT TRAPPED
       MOV  TRLBL(XL),XL     ELSE LOAD PTR TO REAL LABEL CODE
*
*      HERE TO SET/RESET SETEXIT TRAP
*
SSTX1  MOV  XR,STXVR         STORE NEW VRBLK POINTER (OR NULL)
       MOV  XL,R_SXC         STORE NEW CODE PTR (OR ZERO)
       BEQ  WA,=NULLS,EXNUL  RETURN NULL IF NULL RESULT
       MOV  WA,XR            ELSE COPY VRBLK POINTER
       BRN  EXVNM            AND RETURN BUILDING NMBLK
*
*      HERE IF BAD ARGUMENT
*
SSTX2  ERB  187,SETEXIT argument is not label name or null
.IF    .CMTH
*
*      SIN
*
S_SIN  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  308,SIN argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       SIN                   TAKE SINE
       RNO  EXREA            IF NO OVERFLOW, RETURN RESULT IN RA
       ERB  323,SIN argument is out of range
       EJC
.FI
.IF    .CMTH
*
*      SQRT
*
S_SQR  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  313,SQRT argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       RLT  SSQR1            NEGATIVE NUMBER
       SQR                   TAKE SQUARE ROOT
       BRN  EXREA            NO OVERFLOW POSSIBLE, RESULT IN RA
*
*      HERE IF BAD ARGUMENT
*
SSQR1  ERB  314,SQRT argument negative
       EJC
.FI
.IF    .CNSR
.ELSE
       EJC
*
*      SORT
*
S_SRT  ENT                   ENTRY POINT
       ZER  WA               MARK AS SORT
       JSR  SORTA            CALL SORT ROUTINE
       PPM  EXFAL            IF CONVERSION FAILS, SO SHALL WE
       BRN  EXSID            RETURN, SETTING IDVAL
.FI
       EJC
*
*      SPAN
*
S_SPN  ENT                   ENTRY POINT
       MOV  =P_SPS,WB        SET PCODE FOR SINGLE CHAR ARG
       MOV  =P_SPN,XL        SET PCODE FOR MULTI-CHAR ARG
       MOV  =P_SPD,WC        SET PCODE FOR EXPRESSION ARG
       JSR  PATST            CALL COMMON ROUTINE TO BUILD NODE
       ERR  188,SPAN argument is not a string or expression
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      SIZE
*
S_SI_  ENT                   ENTRY POINT
.IF    .CNBF
       JSR  GTSTG            LOAD STRING ARGUMENT
       ERR  189,SIZE argument is not a string
.ELSE
       JSR  GTSTB            LOAD STRING ARGUMENT
       ERR  189,SIZE argument is not a string or buffer
.FI
*
*      MERGE WITH BFBLK OR SCBLK PTR IN XR.  WA HAS LENGTH.
*
       MTI  WA               LOAD LENGTH AS INTEGER
       BRN  EXINT            EXIT WITH INTEGER RESULT
       EJC
*
*      STOPTR
*
S_STT  ENT                   ENTRY POINT
       ZER  XL               INDICATE STOPTR CASE
       JSR  TRACE            CALL TRACE PROCEDURE
       ERR  190,STOPTR first argument is not appropriate name
       ERR  191,STOPTR second argument is not trace type
       BRN  EXNUL            RETURN NULL
       EJC
*
*      SUBSTR
*
S_SUB  ENT                   ENTRY POINT
       JSR  GTSMI            LOAD THIRD ARGUMENT
       ERR  192,SUBSTR third argument is not integer
       PPM  EXFAL            JUMP IF NEGATIVE OR TOO LARGE
       MOV  XR,SBSSV         SAVE THIRD ARGUMENT
       JSR  GTSMI            LOAD SECOND ARGUMENT
       ERR  193,SUBSTR second argument is not integer
       PPM  EXFAL            JUMP IF OUT OF RANGE
       MOV  XR,WC            SAVE SECOND ARGUMENT
       BZE  WC,EXFAL         JUMP IF SECOND ARGUMENT ZERO
       DCV  WC               ELSE DECREMENT FOR ONES ORIGIN
.IF    .CNBF
       JSR  GTSTG            LOAD FIRST ARGUMENT
       ERR  194,SUBSTR first argument is not a string
.ELSE
       JSR  GTSTB            LOAD FIRST ARGUMENT
       ERR  194,SUBSTR first argument is not a string or buffer
.FI
*
*      MERGE WITH BFBLK OR SCBLK PTR IN XR.  WA HAS LENGTH
*
       MOV  WC,WB            COPY SECOND ARG TO WB
       MOV  SBSSV,WC         RELOAD THIRD ARGUMENT
       BNZ  WC,SSUB2         SKIP IF THIRD ARG GIVEN
       MOV  WA,WC            ELSE GET STRING LENGTH
       BGT  WB,WC,EXFAL      FAIL IF IMPROPER
       SUB  WB,WC            REDUCE BY OFFSET TO START
*
*      MERGE
*
SSUB2  MOV  WA,XL            SAVE STRING LENGTH
       MOV  WC,WA            SET LENGTH OF SUBSTRING
       ADD  WB,WC            ADD 2ND ARG TO 3RD ARG
       BGT  WC,XL,EXFAL      JUMP IF IMPROPER SUBSTRING
       MOV  XR,XL            COPY POINTER TO FIRST ARG
       JSR  SBSTR            BUILD SUBSTRING
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      TABLE
*
S_TBL  ENT                   ENTRY POINT
       MOV  (XS)+,XL         GET INITIAL LOOKUP VALUE
       ICA  XS               POP SECOND ARGUMENT
       JSR  GTSMI            LOAD ARGUMENT
       ERR  195,TABLE argument is not integer
       ERR  196,TABLE argument is out of range
       BNZ  WC,STBL1         JUMP IF NON-ZERO
       MOV  =TBNBK,WC        ELSE SUPPLY DEFAULT VALUE
*
*      MERGE HERE WITH NUMBER OF HEADERS IN WC
*
STBL1  JSR  TMAKE            MAKE TABLE
       BRN  EXSID            EXIT SETTING IDVAL
       EJC
.IF    .CMTH
*
*      TAN
*
S_TAN  ENT                   ENTRY POINT
       MOV  (XS)+,XR         GET ARGUMENT
       JSR  GTREA            CONVERT TO REAL
       ERR  309,TAN argument not numeric
       LDR  RCVAL(XR)        LOAD ACCUMULATOR WITH ARGUMENT
       TAN                   TAKE TANGENT
       RNO  EXREA            IF NO OVERFLOW, RETURN RESULT IN RA
       ERB  310,TAN produced real overflow or argument is out of range
       EJC
.FI
*
*      TIME
*
S_TIM  ENT                   ENTRY POINT
       JSR  SYSTM            GET TIMER VALUE
       SBI  TIMSX            SUBTRACT STARTING TIME
       BRN  EXINT            EXIT WITH INTEGER VALUE
       EJC
*
*      TRACE
*
S_TRA  ENT                   ENTRY POINT
       BEQ  NUM03(XS),=NULLS,STR02 JUMP IF FIRST ARGUMENT IS NULL
       MOV  (XS)+,XR         LOAD FOURTH ARGUMENT
       ZER  XL               TENTATIVELY SET ZERO POINTER
       BEQ  XR,=NULLS,STR01  JUMP IF 4TH ARGUMENT IS NULL
       JSR  GTNVR            ELSE POINT TO VRBLK
       PPM  STR03            JUMP IF NOT VARIABLE NAME
       MOV  XR,XL            ELSE SAVE VRBLK IN TRFNC
*
*      HERE WITH VRBLK OR ZERO IN XL
*
STR01  MOV  (XS)+,XR         LOAD THIRD ARGUMENT (TAG)
       ZER  WB               SET ZERO AS TRTYP VALUE FOR NOW
       JSR  TRBLD            BUILD TRBLK FOR TRACE CALL
       MOV  XR,XL            MOVE TRBLK POINTER FOR TRACE
       JSR  TRACE            CALL TRACE PROCEDURE
       ERR  198,TRACE first argument is not appropriate name
       ERR  199,TRACE second argument is not trace type
       BRN  EXNUL            RETURN NULL
*
*      HERE TO CALL SYSTEM TRACE TOGGLE ROUTINE
*
STR02  JSR  SYSTT            CALL IT
       ADD  *NUM04,XS        POP TRACE ARGUMENTS
       BRN  EXNUL            RETURN
*
*      HERE FOR BAD FOURTH ARGUMENT
*
STR03  ERB  197,TRACE fourth arg is not function name or null
       EJC
*
*      TRIM
*
S_TRM  ENT                   ENTRY POINT
.IF    .CNBF
       JSR  GTSTG            LOAD ARGUMENT AS STRING
       ERR  200,TRIM argument is not a string
.ELSE
       JSR  GTSTB            LOAD ARGUMENT AS STRING
       ERR  200,TRIM argument is not a string or buffer
       BNZ  WB,STRM0         BRANCH IF BUFFER
.FI
       BZE  WA,EXNUL         RETURN NULL IF ARGUMENT IS NULL
       MOV  XR,XL            COPY STRING POINTER
       CTB  WA,SCHAR         GET BLOCK LENGTH
       JSR  ALLOC            ALLOCATE COPY SAME SIZE
       MOV  XR,WB            SAVE POINTER TO COPY
       MVW                   COPY OLD STRING BLOCK TO NEW
       MOV  WB,XR            RESTORE PTR TO NEW BLOCK
       JSR  TRIMR            TRIM BLANKS (WB IS NON-ZERO)
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.IF    .CNBF
.ELSE
*
*      ARGUMENT IS A BUFFER, PERFORM TRIM IN PLACE.
*
STRM0  MOV  WB,-(XS)         STACK BUFFER AS RESULT
       BZE  WA,STRM6         RETURN BUFFER UNCHANGED IF EMPTY
       MOV  XR,XL            GET BFBLK PTR
       MOV  WB,XR            COPY BCBLK PTR TO XR
       PLC  XL,WA            POINT PAST LAST CHARACTER
       MOV  =CH_BL,WC        LOAD BLANK CHARACTER
*
*      LOOP THROUGH CHARACTERS FROM RIGHT TO LEFT
*
STRM1  LCH  WB,-(XL)         LOAD NEXT CHARACTER
.IF    .CAHT
       BEQ  WB,=CH_HT,STRM2  JUMP IF HORIZONTAL TAB
.FI
       BNE  WB,WC,STRM3      JUMP IF NON-BLANK FOUND
STRM2  DCV  WA               ELSE DECREMENT CHARACTER COUNT
       BNZ  WA,STRM1         LOOP BACK IF MORE TO CHECK
*
*      HERE WHEN BUFFER TRIM COMPLETE
*
STRM3  MOV  WA,BCLEN(XR)     SET NEW LENGTH IN BCBLK
       MOV  BCBUF(XR),XR     GET BFBLK PTR
       MOV  WA,WB            COPY LENGTH
       CTB  WB,0             WORDS NEEDED CONVERTED TO BYTES
       SUB  WA,WB            NUMBER OF ZEROS NEEDED
       PSC  XR,WA            READY FOR STORING ZEROS
       ZER  WC               SET ZERO CHAR
*
*      LOOP TO ZERO PAD LAST WORD OF CHARACTERS
*
STRM4  BZE  WB,STRM5         LOOP WHILE MORE TO BE DONE
       SCH  WC,(XR)+         STORE ZERO CHARACTER
       DCV  WB               DECREMENT COUNT
       BRN  STRM4            CONTINUE LOOP
STRM5  CSC  XR               COMPLETE STORE CHARACTERS
STRM6  LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
.FI
       EJC
*
*      UNLOAD
*
S_UNL  ENT                   ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       JSR  GTNVR            POINT TO VRBLK
       ERR  201,UNLOAD argument is not natural variable name
       MOV  =STNDF,XL        GET PTR TO UNDEFINED FUNCTION
       JSR  DFFNC            UNDEFINE NAMED FUNCTION
       BRN  EXNUL            RETURN NULL AS RESULT
.IF    .C370
       EJC
*
*      XOR
*
S_XOR  ENT                   ENTRY POINT
       MNZ  WB               SIGNAL TWO ARGUMENTS
       JSR  SBOOL            CALL STRING BOOLEAN ROUTINE
       ERR  xxx,XOR first argument is not a string
       ERR  xxx,XOR second argument is not a string
       ERR  xxx,XOR arguments not same length
       PPM  EXITS            NULL STRING ARGUMENTS
*
*      HERE TO PROCESS (WC) WORDS.  RESULT IS STACKED.
*
SXOR1  MOV  (XL)+,WA         GET NEXT CFP_C CHARS FROM ARG 1
       XOB  (XR),WA          XOR WITH CHARACTERS FROM ARG 2
       MOV  WA,(XR)+         PUT BACK IN MEMORY
       BCT  WC,SXOR1         LOOP OVER ALL WORDS IN STRING BLOCK
       BRN  EXITS            FETCH NEXT CODE WORD
.FI
       TTL  S P I T B O L -- UTILITY ROUTINES
*
*      THE FOLLOWING SECTION CONTAINS UTILITY ROUTINES USED FOR
*      VARIOUS PURPOSES THROUGHOUT THE SYSTEM. THESE DIFFER
*      FROM THE PROCEDURES IN THE UTILITY PROCEDURES SECTION IN
*      THEY ARE NOT IN PROCEDURE FORM AND THEY DO NOT RETURN
*      TO THEIR CALLERS. THEY ARE ACCESSED WITH A BRANCH TYPE
*      INSTRUCTION AFTER SETTING THE REGISTERS TO APPROPRIATE
*      PARAMETER VALUES.
*
*      THE REGISTER VALUES REQUIRED FOR EACH ROUTINE ARE
*      DOCUMENTED AT THE START OF EACH ROUTINE. REGISTERS NOT
*      MENTIONED MAY CONTAIN ANY VALUES EXCEPT THAT XR,XL
*      CAN ONLY CONTAIN PROPER COLLECTABLE POINTERS.
*
*      SOME OF THESE ROUTINES WILL TOLERATE GARBAGE POINTERS
*      IN XL,XR ON ENTRY. THIS IS ALWAYS DOCUMENTED AND IN
*      EACH CASE, THE ROUTINE CLEARS THESE GARBAGE VALUES BEFORE
*      EXITING AFTER COMPLETING ITS TASK.
*
*      THE ROUTINES HAVE NAMES CONSISTING OF FIVE LETTERS
*      AND ARE ASSEMBLED IN ALPHABETICAL ORDER.
       EJC
*      ARREF -- ARRAY REFERENCE
*
*      (XL)                  MAY BE NON-COLLECTABLE
*      (XR)                  NUMBER OF SUBSCRIPTS
*      (WB)                  SET ZERO/NONZERO FOR VALUE/NAME
*                            THE VALUE IN WB MUST BE COLLECTABLE
*      STACK                 SUBSCRIPTS AND ARRAY OPERAND
*      BRN  ARREF            JUMP TO CALL FUNCTION
*
*      ARREF CONTINUES BY EXECUTING THE NEXT CODE WORD WITH
*      THE RESULT NAME OR VALUE PLACED ON TOP OF THE STACK.
*      TO DEAL WITH THE PROBLEM OF ACCESSING SUBSCRIPTS IN THE
*      ORDER OF STACKING, XL IS USED AS A SUBSCRIPT POINTER
*      WORKING BELOW THE STACK POINTER.
*
ARREF  RTN
       MOV  XR,WA            COPY NUMBER OF SUBSCRIPTS
       MOV  XS,XT            POINT TO STACK FRONT
       WTB  XR               CONVERT TO BYTE OFFSET
       ADD  XR,XT            POINT TO ARRAY OPERAND ON STACK
       ICA  XT               FINAL VALUE FOR STACK POPPING
       MOV  XT,ARFXS         KEEP FOR LATER
       MOV  -(XT),XR         LOAD ARRAY OPERAND POINTER
       MOV  XR,R_ARF         KEEP ARRAY POINTER
       MOV  XT,XR            SAVE POINTER TO SUBSCRIPTS
       MOV  R_ARF,XL         POINT XL TO POSSIBLE VCBLK OR TBBLK
       MOV  (XL),WC          LOAD FIRST WORD
       BEQ  WC,=B_ART,ARF01  JUMP IF ARBLK
       BEQ  WC,=B_VCT,ARF07  JUMP IF VCBLK
       BEQ  WC,=B_TBT,ARF10  JUMP IF TBBLK
       ERB  235,Subscripted operand is not table or array
*
*      HERE FOR ARRAY (ARBLK)
*
ARF01  BNE  WA,ARNDM(XL),ARF09 JUMP IF WRONG NUMBER OF DIMS
       LDI  INTV0            GET INITIAL SUBSCRIPT OF ZERO
       MOV  XR,XT            POINT BEFORE SUBSCRIPTS
       ZER  WA               INITIAL OFFSET TO BOUNDS
       BRN  ARF03            JUMP INTO LOOP
*
*      LOOP TO COMPUTE SUBSCRIPTS BY MULTIPLICATIONS
*
ARF02  MLI  ARDM2(XR)        MULTIPLY TOTAL BY NEXT DIMENSION
*
*      MERGE HERE FIRST TIME
*
ARF03  MOV  -(XT),XR         LOAD NEXT SUBSCRIPT
       STI  ARFSI            SAVE CURRENT SUBSCRIPT
       LDI  ICVAL(XR)        LOAD INTEGER VALUE IN CASE
       BEQ  (XR),=B_ICL,ARF04 JUMP IF IT WAS AN INTEGER
       EJC
*
*      ARREF (CONTINUED)
*
*
       JSR  GTINT            CONVERT TO INTEGER
       PPM  ARF12            JUMP IF NOT INTEGER
       LDI  ICVAL(XR)        IF OK, LOAD INTEGER VALUE
*
*      HERE WITH INTEGER SUBSCRIPT IN (IA)
*
ARF04  MOV  R_ARF,XR         POINT TO ARRAY
       ADD  WA,XR            OFFSET TO NEXT BOUNDS
       SBI  ARLBD(XR)        SUBTRACT LOW BOUND TO COMPARE
       IOV  ARF13            OUT OF RANGE FAIL IF OVERFLOW
       ILT  ARF13            OUT OF RANGE FAIL IF TOO SMALL
       SBI  ARDIM(XR)        SUBTRACT DIMENSION
       IGE  ARF13            OUT OF RANGE FAIL IF TOO LARGE
       ADI  ARDIM(XR)        ELSE RESTORE SUBSCRIPT OFFSET
       ADI  ARFSI            ADD TO CURRENT TOTAL
       ADD  *ARDMS,WA        POINT TO NEXT BOUNDS
       BNE  XT,XS,ARF02      LOOP BACK IF MORE TO GO
*
*      HERE WITH INTEGER SUBSCRIPT COMPUTED
*
       MFI  WA               GET AS ONE WORD INTEGER
       WTB  WA               CONVERT TO OFFSET
       MOV  R_ARF,XL         POINT TO ARBLK
       ADD  AROFS(XL),WA     ADD OFFSET PAST BOUNDS
       ICA  WA               ADJUST FOR ARPRO FIELD
       BNZ  WB,ARF08         EXIT WITH NAME IF NAME CALL
*
*      MERGE HERE TO GET VALUE FOR VALUE CALL
*
ARF05  JSR  ACESS            GET VALUE
       PPM  ARF13            FAIL IF ACESS FAILS
*
*      RETURN VALUE
*
ARF06  MOV  ARFXS,XS         POP STACK ENTRIES
       ZER  R_ARF            FINISHED WITH ARRAY POINTER
       MOV  XR,-(XS)         STACK RESULT
       LCW  XR               GET NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      ARREF (CONTINUED)
*
*      HERE FOR VECTOR
*
ARF07  BNE  WA,=NUM01,ARF09  ERROR IF MORE THAN 1 SUBSCRIPT
       MOV  (XS),XR          ELSE LOAD SUBSCRIPT
       JSR  GTINT            CONVERT TO INTEGER
       PPM  ARF12            ERROR IF NOT INTEGER
       LDI  ICVAL(XR)        ELSE LOAD INTEGER VALUE
       SBI  INTV1            SUBTRACT FOR ONES OFFSET
       MFI  WA,ARF13         GET SUBSCRIPT AS ONE WORD
       ADD  =VCVLS,WA        ADD OFFSET FOR STANDARD FIELDS
       WTB  WA               CONVERT OFFSET TO BYTES
       BGE  WA,VCLEN(XL),ARF13 FAIL IF OUT OF RANGE SUBSCRIPT
       BZE  WB,ARF05         BACK TO GET VALUE IF VALUE CALL
*
*      RETURN NAME
*
ARF08  MOV  ARFXS,XS         POP STACK ENTRIES
       ZER  R_ARF            FINISHED WITH ARRAY POINTER
       BRN  EXNAM            ELSE EXIT WITH NAME
*
*      HERE IF SUBSCRIPT COUNT IS WRONG
*
ARF09  ERB  236,Array referenced with wrong number of subscripts
*
*      TABLE
*
ARF10  BNE  WA,=NUM01,ARF11  ERROR IF MORE THAN 1 SUBSCRIPT
       MOV  (XS),XR          ELSE LOAD SUBSCRIPT
       JSR  TFIND            CALL TABLE SEARCH ROUTINE
       PPM  ARF13            FAIL IF FAILED
       BNZ  WB,ARF08         EXIT WITH NAME IF NAME CALL
       BRN  ARF06            ELSE EXIT WITH VALUE
*
*      HERE FOR BAD TABLE REFERENCE
*
ARF11  ERB  237,Table referenced with more than one subscript
*
*      HERE FOR BAD SUBSCRIPT
*
ARF12  ERB  238,Array subscript is not integer
*
*      HERE TO SIGNAL FAILURE
*
ARF13  ZER  R_ARF            FINISHED WITH ARRAY POINTER
       BRN  EXFAL            FAIL
       EJC
*
*      CFUNC -- CALL A FUNCTION
*
*      CFUNC IS USED TO CALL A SNOBOL LEVEL FUNCTION. IT IS
*      USED BY THE APPLY FUNCTION (S_APP), THE FUNCTION
*      TRACE ROUTINE (TRXEQ) AND THE MAIN FUNCTION CALL ENTRY
*      (O_FNC, O_FNS). IN THE LATTER CASES, CFUNC IS USED ONLY
*      IF THE NUMBER OF ARGUMENTS IS INCORRECT.
*
*      (XL)                  POINTER TO FUNCTION BLOCK
*      (WA)                  ACTUAL NUMBER OF ARGUMENTS
*      (XS)                  POINTS TO STACKED ARGUMENTS
*      BRN  CFUNC            JUMP TO CALL FUNCTION
*
*      CFUNC CONTINUES BY EXECUTING THE FUNCTION
*
CFUNC  RTN
       BLT  WA,FARGS(XL),CFNC1 JUMP IF TOO FEW ARGUMENTS
       BEQ  WA,FARGS(XL),CFNC3 JUMP IF CORRECT NUMBER OF ARGS
*
*      HERE IF TOO MANY ARGUMENTS SUPPLIED, POP THEM OFF
*
       MOV  WA,WB            COPY ACTUAL NUMBER
       SUB  FARGS(XL),WB     GET NUMBER OF EXTRA ARGS
       WTB  WB               CONVERT TO BYTES
       ADD  WB,XS            POP OFF UNWANTED ARGUMENTS
       BRN  CFNC3            JUMP TO GO OFF TO FUNCTION
*
*      HERE IF TOO FEW ARGUMENTS
*
CFNC1  MOV  FARGS(XL),WB     LOAD REQUIRED NUMBER OF ARGUMENTS
       BEQ  WB,=NINI9,CFNC3  JUMP IF CASE OF VAR NUM OF ARGS
       SUB  WA,WB            CALCULATE NUMBER MISSING
       LCT  WB,WB            SET COUNTER TO CONTROL LOOP
*
*      LOOP TO SUPPLY EXTRA NULL ARGUMENTS
*
CFNC2  MOV  =NULLS,-(XS)     STACK A NULL ARGUMENT
       BCT  WB,CFNC2         LOOP TILL PROPER NUMBER STACKED
*
*      MERGE HERE TO JUMP TO FUNCTION
*
CFNC3  BRI  (XL)             JUMP THROUGH FCODE FIELD
       EJC
*
*      EXFAL -- EXIT SIGNALLING SNOBOL FAILURE
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      BRN  EXFAL            JUMP TO FAIL
*
*      EXFAL CONTINUES BY EXECUTING THE APPROPRIATE FAIL GOTO
*
EXFAL  RTN
       MOV  FLPTR,XS         POP STACK
       MOV  (XS),XR          LOAD FAILURE OFFSET
       ADD  R_COD,XR         POINT TO FAILURE CODE LOCATION
       LCP  XR               SET CODE POINTER
       LCW  XR               LOAD NEXT CODE WORD
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO EXECUTE NEXT CODE WORD
       EJC
*
*      EXINT -- EXIT WITH INTEGER RESULT
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      (IA)                  INTEGER VALUE
*      BRN  EXINT            JUMP TO EXIT WITH INTEGER
*
*      EXINT CONTINUES BY EXECUTING THE NEXT CODE WORD
*      WHICH IT DOES BY FALLING THROUGH TO EXIXR
*
EXINT  RTN
       ZER  XL               CLEAR DUD VALUE
       JSR  ICBLD            BUILD ICBLK
       EJC
*      EXIXR -- EXIT WITH RESULT IN (XR)
*
*      (XR)                  RESULT
*      (XL)                  MAY BE NON-COLLECTABLE
*      BRN  EXIXR            JUMP TO EXIT WITH RESULT IN (XR)
*
*      EXIXR CONTINUES BY EXECUTING THE NEXT CODE WORD
*      WHICH IT DOES BY FALLING THROUGH TO EXITS.
EXIXR  RTN
*
       MOV  XR,-(XS)         STACK RESULT
*
*
*      EXITS -- EXIT WITH RESULT IF ANY STACKED
*
*      (XR,XL)               MAY BE NON-COLLECTABLE
*
*      BRN  EXITS            ENTER EXITS ROUTINE
*
EXITS  RTN
       LCW  XR               LOAD NEXT CODE WORD
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO EXECUTE NEXT CODE WORD
       EJC
*
*      EXNAM -- EXIT WITH NAME IN (XL,WA)
*
*      (XL)                  NAME BASE
*      (WA)                  NAME OFFSET
*      (XR)                  MAY BE NON-COLLECTABLE
*      BRN  EXNAM            JUMP TO EXIT WITH NAME IN (XL,WA)
*
*      EXNAM CONTINUES BY EXECUTING THE NEXT CODE WORD
*
EXNAM  RTN
       MOV  XL,-(XS)         STACK NAME BASE
       MOV  WA,-(XS)         STACK NAME OFFSET
       LCW  XR               LOAD NEXT CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      EXNUL -- EXIT WITH NULL RESULT
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      BRN  EXNUL            JUMP TO EXIT WITH NULL VALUE
*
*      EXNUL CONTINUES BY EXECUTING THE NEXT CODE WORD
*
EXNUL  RTN
       MOV  =NULLS,-(XS)     STACK NULL VALUE
       LCW  XR               LOAD NEXT CODE WORD
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO EXECUTE NEXT CODE WORD
       EJC
.IF    .CNRA
.ELSE
*
*      EXREA -- EXIT WITH REAL RESULT
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      (RA)                  REAL VALUE
*      BRN  EXREA            JUMP TO EXIT WITH REAL VALUE
*
*      EXREA CONTINUES BY EXECUTING THE NEXT CODE WORD
*
EXREA  RTN
       ZER  XL               CLEAR DUD VALUE
       JSR  RCBLD            BUILD RCBLK
       BRN  EXIXR            JUMP TO EXIT WITH RESULT IN XR
.FI
       EJC
*
*      EXSID -- EXIT SETTING ID FIELD
*
*      EXSID IS USED TO EXIT AFTER BUILDING ANY OF THE FOLLOWING
*      BLOCKS (ARBLK, TBBLK, PDBLK, VCBLK). IT SETS THE IDVAL.
*
*      (XR)                  PTR TO BLOCK WITH IDVAL FIELD
*      (XL)                  MAY BE NON-COLLECTABLE
*      BRN  EXSID            JUMP TO EXIT AFTER SETTING ID FIELD
*
*      EXSID CONTINUES BY EXECUTING THE NEXT CODE WORD
*
EXSID  RTN
       MOV  CURID,WA         LOAD CURRENT ID VALUE
       BNE  WA,=CFP_M,EXSI1  JUMP IF NO OVERFLOW
       ZER  WA               ELSE RESET FOR WRAPAROUND
*
*      HERE WITH OLD IDVAL IN WA
*
EXSI1  ICV  WA               BUMP ID VALUE
       MOV  WA,CURID         STORE FOR NEXT TIME
       MOV  WA,IDVAL(XR)     STORE ID VALUE
       BRN  EXIXR            EXIT WITH RESULT IN (XR)
       EJC
*
*      EXVNM -- EXIT WITH NAME OF VARIABLE
*
*      EXVNM EXITS AFTER STACKING A VALUE WHICH IS A NMBLK
*      REFERENCING THE NAME OF A GIVEN NATURAL VARIABLE.
*
*      (XR)                  VRBLK POINTER
*      (XL)                  MAY BE NON-COLLECTABLE
*      BRN  EXVNM            EXIT WITH VRBLK POINTER IN XR
*
EXVNM  RTN
       MOV  XR,XL            COPY NAME BASE POINTER
       MOV  *NMSI_,WA        SET SIZE OF NMBLK
       JSR  ALLOC            ALLOCATE NMBLK
       MOV  =B_NML,(XR)      STORE TYPE WORD
       MOV  XL,NMBAS(XR)     STORE NAME BASE
       MOV  *VRVAL,NMOFS(XR) STORE NAME OFFSET
       BRN  EXIXR            EXIT WITH RESULT IN XR
       EJC
*
*      FLPOP -- FAIL AND POP IN PATTERN MATCHING
*
*      FLPOP POPS THE NODE AND CURSOR ON THE STACK AND THEN
*      DROPS THROUGH INTO FAILP TO CAUSE PATTERN FAILURE
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      BRN  FLPOP            JUMP TO FAIL AND POP STACK
*
FLPOP  RTN
       ADD  *NUM02,XS        POP TWO ENTRIES OFF STACK
       EJC
*
*      FAILP -- FAILURE IN MATCHING PATTERN NODE
*
*      FAILP IS USED AFTER FAILING TO MATCH A PATTERN NODE.
*      SEE PATTERN MATCH ROUTINES FOR DETAILS OF USE.
*
*      (XL,XR)               MAY BE NON-COLLECTABLE
*      BRN  FAILP            SIGNAL FAILURE TO MATCH
*
*      FAILP CONTINUES BY MATCHING AN ALTERNATIVE FROM THE STACK
*
FAILP  RTN
       MOV  (XS)+,XR         LOAD ALTERNATIVE NODE POINTER
       MOV  (XS)+,WB         RESTORE OLD CURSOR
       MOV  (XR),XL          LOAD PCODE ENTRY POINTER
       BRI  XL               JUMP TO EXECUTE CODE FOR NODE
       EJC
*
*      INDIR -- COMPUTE INDIRECT REFERENCE
*
*      (WB)                  NONZERO/ZERO FOR BY NAME/VALUE
*      BRN  INDIR            JUMP TO GET INDIRECT REF ON STACK
*
*      INDIR CONTINUES BY EXECUTING THE NEXT CODE WORD
*
INDIR  RTN
       MOV  (XS)+,XR         LOAD ARGUMENT
       BEQ  (XR),=B_NML,INDR2 JUMP IF A NAME
       JSR  GTNVR            ELSE CONVERT TO VARIABLE
       ERR  239,Indirection operand is not name
       BZE  WB,INDR1         SKIP IF BY VALUE
       MOV  XR,-(XS)         ELSE STACK VRBLK PTR
       MOV  *VRVAL,-(XS)     STACK NAME OFFSET
       LCW  XR               LOAD NEXT CODE WORD
       MOV  (XR),XL          LOAD ENTRY ADDRESS
       BRI  XL               JUMP TO EXECUTE NEXT CODE WORD
*
*      HERE TO GET VALUE OF NATURAL VARIABLE
*
INDR1  BRI  (XR)             JUMP THROUGH VRGET FIELD OF VRBLK
*
*      HERE IF OPERAND IS A NAME
*
INDR2  MOV  NMBAS(XR),XL     LOAD NAME BASE
       MOV  NMOFS(XR),WA     LOAD NAME OFFSET
       BNZ  WB,EXNAM         EXIT IF CALLED BY NAME
       JSR  ACESS            ELSE GET VALUE FIRST
       PPM  EXFAL            FAIL IF ACCESS FAILS
       BRN  EXIXR            ELSE RETURN WITH VALUE IN XR
       EJC
*
*      MATCH -- INITIATE PATTERN MATCH
*
*      (WB)                  MATCH TYPE CODE
*      BRN  MATCH            JUMP TO INITIATE PATTERN MATCH
*
*      MATCH CONTINUES BY EXECUTING THE PATTERN MATCH. SEE
*      PATTERN MATCH ROUTINES (P_XXX) FOR FULL DETAILS.
*
MATCH  RTN
       MOV  (XS)+,XR         LOAD PATTERN OPERAND
       JSR  GTPAT            CONVERT TO PATTERN
       ERR  240,Pattern match right operand is not pattern
       MOV  XR,XL            IF OK, SAVE PATTERN POINTER
       BNZ  WB,MTCH1         JUMP IF NOT MATCH BY NAME
       MOV  (XS),WA          ELSE LOAD NAME OFFSET
       MOV  XL,-(XS)         SAVE PATTERN POINTER
       MOV  NUM02(XS),XL     LOAD NAME BASE
       JSR  ACESS            ACCESS SUBJECT VALUE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  (XS),XL          RESTORE PATTERN POINTER
       MOV  XR,(XS)          STACK SUBJECT STRING VAL FOR MERGE
       ZER  WB               RESTORE TYPE CODE
*
*      MERGE HERE WITH SUBJECT VALUE ON STACK
*
.IF    .CNBF
MTCH1  JSR  GTSTG            CONVERT SUBJECT TO STRING
       ERR  241,Pattern match left operand is not a string
       MOV  WB,-(XS)         STACK MATCH TYPE CODE
.ELSE
MTCH1  MOV  WB,WC            SAVE MATCH TYPE IN WC
       JSR  GTSTB            CONVERT SUBJECT TO STRING
       ERR  241,Pattern match left operand is not a string or buffer
       MOV  WB,R_PMB         SET TO ZERO/BCBLK IF STRING/BUFFER
       MOV  WC,-(XS)         STACK MATCH TYPE CODE
.FI
       MOV  XR,R_PMS         IF OK, STORE SUBJECT STRING POINTER
       MOV  WA,PMSSL         AND LENGTH
       ZER  -(XS)            STACK INITIAL CURSOR (ZERO)
       ZER  WB               SET INITIAL CURSOR
       MOV  XS,PMHBS         SET HISTORY STACK BASE PTR
       ZER  PMDFL            RESET PATTERN ASSIGNMENT FLAG
       MOV  XL,XR            SET INITIAL NODE POINTER
       BNZ  KVANC,MTCH2      JUMP IF ANCHORED
*
*      HERE FOR UNANCHORED
*
       MOV  XR,-(XS)         STACK INITIAL NODE POINTER
       MOV  =NDUNA,-(XS)     STACK POINTER TO ANCHOR MOVE NODE
       BRI  (XR)             START MATCH OF FIRST NODE
*
*      HERE IN ANCHORED MODE
*
MTCH2  ZER  -(XS)            DUMMY CURSOR VALUE
       MOV  =NDABO,-(XS)     STACK POINTER TO ABORT NODE
       BRI  (XR)             START MATCH OF FIRST NODE
       EJC
*
*      RETRN -- RETURN FROM FUNCTION
*
*      (WA)                  STRING POINTER FOR RETURN TYPE
*      BRN  RETRN            JUMP TO RETURN FROM (SNOBOL) FUNC
*
*      RETRN CONTINUES BY EXECUTING THE CODE AT THE RETURN POINT
*      THE STACK IS CLEANED OF ANY GARBAGE LEFT BY OTHER
*      ROUTINES WHICH MAY HAVE ALTERED FLPTR SINCE FUNCTION
*      ENTRY BY USING FLPRT, RESERVED FOR USE ONLY BY
*      FUNCTION CALL AND RETURN.
*
RETRN  RTN
       BNZ  KVFNC,RTN01      JUMP IF NOT LEVEL ZERO
       ERB  242,Function return from level zero
*
*      HERE IF NOT LEVEL ZERO RETURN
*
RTN01  MOV  FLPRT,XS         POP STACK
       ICA  XS               REMOVE FAILURE OFFSET
       MOV  (XS)+,XR         POP PFBLK POINTER
       MOV  (XS)+,FLPTR      POP FAILURE POINTER
       MOV  (XS)+,FLPRT      POP OLD FLPRT
       MOV  (XS)+,WB         POP CODE POINTER OFFSET
       MOV  (XS)+,WC         POP OLD CODE BLOCK POINTER
       ADD  WC,WB            MAKE OLD CODE POINTER ABSOLUTE
       LCP  WB               RESTORE OLD CODE POINTER
       MOV  WC,R_COD         RESTORE OLD CODE BLOCK POINTER
       DCV  KVFNC            DECREMENT FUNCTION LEVEL
       MOV  KVTRA,WB         LOAD TRACE
       ADD  KVFTR,WB         ADD FTRACE
       BZE  WB,RTN06         JUMP IF NO TRACING POSSIBLE
*
*      HERE IF THERE MAY BE A TRACE
*
       MOV  WA,-(XS)         SAVE FUNCTION RETURN TYPE
       MOV  XR,-(XS)         SAVE PFBLK POINTER
       MOV  WA,KVRTN         SET RTNTYPE FOR TRACE FUNCTION
       MOV  R_FNC,XL         LOAD FNCLEVEL TRBLK PTR (IF ANY)
       JSR  KTREX            EXECUTE POSSIBLE FNCLEVEL TRACE
       MOV  PFVBL(XR),XL     LOAD VRBLK PTR (SGD13)
       BZE  KVTRA,RTN02      JUMP IF TRACE IS OFF
       MOV  PFRTR(XR),XR     ELSE LOAD RETURN TRACE TRBLK PTR
       BZE  XR,RTN02         JUMP IF NOT RETURN TRACED
       DCV  KVTRA            ELSE DECREMENT TRACE COUNT
       BZE  TRFNC(XR),RTN03  JUMP IF PRINT TRACE
       MOV  *VRVAL,WA        ELSE SET NAME OFFSET
       MOV  NUM01(XS),KVRTN  MAKE SURE RTNTYPE IS SET RIGHT
       JSR  TRXEQ            EXECUTE FULL TRACE
       EJC
*
*      RETRN (CONTINUED)
*
*      HERE TO TEST FOR FTRACE
*
RTN02  BZE  KVFTR,RTN05      JUMP IF FTRACE IS OFF
       DCV  KVFTR            ELSE DECREMENT FTRACE
*
*      HERE FOR PRINT TRACE OF FUNCTION RETURN
*
RTN03  JSR  PRTSN            PRINT STATEMENT NUMBER
       MOV  NUM01(XS),XR     LOAD RETURN TYPE
       JSR  PRTST            PRINT IT
       MOV  =CH_BL,WA        LOAD BLANK
       JSR  PRTCH            PRINT IT
       MOV  0(XS),XL         LOAD PFBLK PTR
       MOV  PFVBL(XL),XL     LOAD FUNCTION VRBLK PTR
       MOV  *VRVAL,WA        SET VRBLK NAME OFFSET
       BNE  XR,=SCFRT,RTN04  JUMP IF NOT FRETURN CASE
*
*      FOR FRETURN, JUST PRINT FUNCTION NAME
*
       JSR  PRTNM            PRINT NAME
       JSR  PRTNL            TERMINATE PRINT LINE
       BRN  RTN05            MERGE
*
*      HERE FOR RETURN OR NRETURN, PRINT FUNCTION NAME = VALUE
*
RTN04  JSR  PRTNV            PRINT NAME = VALUE
*
*      HERE AFTER COMPLETING TRACE
*
RTN05  MOV  (XS)+,XR         POP PFBLK POINTER
       MOV  (XS)+,WA         POP RETURN TYPE STRING
*
*      MERGE HERE IF NO TRACE REQUIRED
*
RTN06  MOV  WA,KVRTN         SET RTNTYPE KEYWORD
       MOV  PFVBL(XR),XL     LOAD POINTER TO FN VRBLK
       EJC
*      RETRN (CONTINUED)
*
*      GET VALUE OF FUNCTION
*
RTN07  MOV  XL,RTNBP         SAVE BLOCK POINTER
       MOV  VRVAL(XL),XL     LOAD VALUE
       BEQ  (XL),=B_TRT,RTN07 LOOP BACK IF TRAPPED
       MOV  XL,RTNFV         ELSE SAVE FUNCTION RESULT VALUE
       MOV  (XS)+,RTNSV      SAVE ORIGINAL FUNCTION VALUE
.IF    .CNPF
       MOV  FARGS(XR),WB     GET NUMBER OF ARGUMENTS
.ELSE
       MOV  (XS)+,XL         POP SAVED POINTER
       BZE  XL,RTN7C         NO ACTION IF NONE
       BZE  KVPFL,RTN7C      JUMP IF NO PROFILING
       JSR  PRFLU            ELSE PROFILE LAST FUNC STMT
       BEQ  KVPFL,=NUM02,RTN7A BRANCH ON VALUE OF PROFILE KEYWD
*
*      HERE IF &PROFILE = 1. START TIME MUST BE FRIGGED TO
*      APPEAR EARLIER THAN IT ACTUALLY IS, BY AMOUNT USED BEFORE
*      THE CALL.
*
       LDI  PFSTM            LOAD CURRENT TIME
       SBI  ICVAL(XL)        FRIG BY SUBTRACTING SAVED AMOUNT
       BRN  RTN7B            AND MERGE
*
*      HERE IF &PROFILE = 2
*
RTN7A  LDI  ICVAL(XL)        LOAD SAVED TIME
*
*      BOTH PROFILE TYPES MERGE HERE
*
RTN7B  STI  PFSTM            STORE BACK CORRECT START TIME
*
*      MERGE HERE IF NO PROFILING
*
RTN7C  MOV  FARGS(XR),WB     GET NUMBER OF ARGS
.FI
       ADD  PFNLO(XR),WB     ADD NUMBER OF LOCALS
       BZE  WB,RTN10         JUMP IF NO ARGS/LOCALS
       LCT  WB,WB            ELSE SET LOOP COUNTER
       ADD  PFLEN(XR),XR     AND POINT TO END OF PFBLK
*
*      LOOP TO RESTORE FUNCTIONS AND LOCALS
*
RTN08  MOV  -(XR),XL         LOAD NEXT VRBLK POINTER
*
*      LOOP TO FIND VALUE BLOCK
*
RTN09  MOV  XL,WA            SAVE BLOCK POINTER
       MOV  VRVAL(XL),XL     LOAD POINTER TO NEXT VALUE
       BEQ  (XL),=B_TRT,RTN09 LOOP BACK IF TRAPPED
       MOV  WA,XL            ELSE RESTORE LAST BLOCK POINTER
       MOV  (XS)+,VRVAL(XL)  RESTORE OLD VARIABLE VALUE
       BCT  WB,RTN08         LOOP TILL ALL PROCESSED
*
*      NOW RESTORE FUNCTION VALUE AND EXIT
*
RTN10  MOV  RTNBP,XL         RESTORE PTR TO LAST FUNCTION BLOCK
       MOV  RTNSV,VRVAL(XL)  RESTORE OLD FUNCTION VALUE
       MOV  RTNFV,XR         RELOAD FUNCTION RESULT
       MOV  R_COD,XL         POINT TO NEW CODE BLOCK
       MOV  KVSTN,KVLST      SET LASTNO FROM STNO
       MOV  CDSTM(XL),KVSTN  RESET PROPER STNO VALUE
.IF    .CSLN
       MOV  KVLIN,KVLLN      SET LASTLINE FROM LINE
       MOV  CDSLN(XL),KVLIN  RESET PROPER LINE VALUE
.FI
       MOV  KVRTN,WA         LOAD RETURN TYPE
       BEQ  WA,=SCRTN,EXIXR  EXIT WITH RESULT IN XR IF RETURN
       BEQ  WA,=SCFRT,EXFAL  FAIL IF FRETURN
       EJC
*
*      RETRN (CONTINUED)
*
*      HERE FOR NRETURN
*
       BEQ  (XR),=B_NML,RTN11 JUMP IF IS A NAME
       JSR  GTNVR            ELSE TRY CONVERT TO VARIABLE NAME
       ERR  243,Function result in NRETURN is not name
       MOV  XR,XL            IF OK, COPY VRBLK (NAME BASE) PTR
       MOV  *VRVAL,WA        SET NAME OFFSET
       BRN  RTN12            AND MERGE
*
*      HERE IF RETURNED RESULT IS A NAME
*
RTN11  MOV  NMBAS(XR),XL     LOAD NAME BASE
       MOV  NMOFS(XR),WA     LOAD NAME OFFSET
*
*      MERGE HERE WITH RETURNED NAME IN (XL,WA)
*
RTN12  MOV  XL,XR            PRESERVE XL
       LCW  WB               LOAD NEXT WORD
       MOV  XR,XL            RESTORE XL
       BEQ  WB,=OFNE_,EXNAM  EXIT IF CALLED BY NAME
       MOV  WB,-(XS)         ELSE SAVE CODE WORD
       JSR  ACESS            GET VALUE
       PPM  EXFAL            FAIL IF ACCESS FAILS
       MOV  XR,XL            IF OK, COPY RESULT
       MOV  (XS),XR          RELOAD NEXT CODE WORD
       MOV  XL,(XS)          STORE RESULT ON STACK
       MOV  (XR),XL          LOAD ROUTINE ADDRESS
       BRI  XL               JUMP TO EXECUTE NEXT CODE WORD
       EJC
*
*      STCOV -- SIGNAL STATEMENT COUNTER OVERFLOW
*
*      BRN  STCOV            JUMP TO SIGNAL STATEMENT COUNT OFLO
*
*      PERMIT UP TO 10 MORE STATEMENTS TO BE OBEYED SO THAT
*      SETEXIT TRAP CAN REGAIN CONTROL.
*      STCOV CONTINUES BY ISSUING THE ERROR MESSAGE
*
STCOV  RTN
       ICV  ERRFT            FATAL ERROR
       LDI  INTVT            GET 10
       ADI  KVSTL            ADD TO FORMER LIMIT
       STI  KVSTL            STORE AS NEW STLIMIT
       LDI  INTVT            GET 10
       STI  KVSTC            SET AS NEW COUNT
       JSR  STGCC            RECOMPUTE COUNTDOWN COUNTERS
       ERB  244,Statement count exceeds value of STLIMIT keyword
       EJC
*
*      STMGO -- START EXECUTION OF NEW STATEMENT
*
*      (XR)                  POINTER TO CDBLK FOR NEW STATEMENT
*      BRN  STMGO            JUMP TO EXECUTE NEW STATEMENT
*
*      STMGO CONTINUES BY EXECUTING THE NEXT STATEMENT
*
STMGO  RTN
       MOV  XR,R_COD         SET NEW CODE BLOCK POINTER
       DCV  STMCT            SEE IF TIME TO CHECK SOMETHING
       BZE  STMCT,STGO2      JUMP IF SO
       MOV  KVSTN,KVLST      SET LASTNO
       MOV  CDSTM(XR),KVSTN  SET STNO
.IF    .CSLN
       MOV  KVLIN,KVLLN      SET LASTLINE
       MOV  CDSLN(XR),KVLIN  SET LINE
.FI
       ADD  *CDCOD,XR        POINT TO FIRST CODE WORD
       LCP  XR               SET CODE POINTER
*
*      HERE TO EXECUTE FIRST CODE WORD OF STATEMENT
*
STGO1  LCW  XR               LOAD NEXT CODE WORD
       ZER  XL               CLEAR GARBAGE XL
       BRI  (XR)             EXECUTE IT
*
*      CHECK PROFILING, POLLING, STLIMIT, STATEMENT TRACING
*
STGO2  BZE  KVPFL,STGO3      SKIP IF NO PROFILING
       JSR  PRFLU            ELSE PROFILE THE STATEMENT IN KVSTN
*
*      HERE WHEN FINISHED WITH PROFILING
*
STGO3  MOV  KVSTN,KVLST      SET LASTNO
       MOV  CDSTM(XR),KVSTN  SET STNO
.IF    .CSLN
       MOV  KVLIN,KVLLN      SET LASTLINE
       MOV  CDSLN(XR),KVLIN  SET LINE
.FI
       ADD  *CDCOD,XR        POINT TO FIRST CODE WORD
       LCP  XR               SET CODE POINTER
.IF    .CPOL
*
*      HERE TO CHECK FOR POLLING
*
       MOV  STMCS,-(XS)      SAVE PRESENT COUNT START ON STACK
       DCV  POLCT            POLL INTERVAL WITHIN STMCT
       BNZ  POLCT,STGO4      JUMP IF NOT POLL TIME YET
       ZER  WA               =0 FOR POLL
       MOV  KVSTN,WB         STATEMENT NUMBER
       MOV  XR,XL            MAKE COLLECTABLE
       JSR  SYSPL            ALLOW INTERACTIVE ACCESS
       ERR  320,User interrupt
       PPM                   SINGLE STEP
       PPM                   EXPRESSION EVALUATION
       MOV  XL,XR            RESTORE CODE BLOCK POINTER
       MOV  WA,POLCS         POLL INTERVAL START VALUE
       JSR  STGCC            RECOMPUTE COUNTER VALUES
.FI
*
*      CHECK STATEMENT LIMIT
*
STGO4  LDI  KVSTC            GET STMT COUNT
       ILT  STGO5            OMIT COUNTING IF NEGATIVE
       MTI  (XS)+            RELOAD START VALUE OF COUNTER
       NGI                   NEGATE
       ADI  KVSTC            STMT COUNT MINUS COUNTER
       STI  KVSTC            REPLACE IT
       ILE  STCOV            FAIL IF STLIMIT REACHED
       BZE  R_STC,STGO5      JUMP IF NO STATEMENT TRACE
       ZER  XR               CLEAR GARBAGE VALUE IN XR
       MOV  R_STC,XL         LOAD POINTER TO STCOUNT TRBLK
       JSR  KTREX            EXECUTE KEYWORD TRACE
*
*      RESET STMGO COUNTER
*
STGO5  MOV  STMCS,STMCT      RESET COUNTER
       BRN  STGO1            FETCH NEXT CODE WORD
       EJC
*
*      STOPR -- TERMINATE RUN
*
*      (XR)                  POINTS TO ENDING MESSAGE
*      BRN STOPR             JUMP TO TERMINATE RUN
*
*      TERMINATE RUN AND PRINT STATISTICS.  ON ENTRY XR POINTS
*      TO ENDING MESSAGE OR IS ZERO IF MESSAGE  PRINTED ALREADY.
*
STOPR  RTN
.IF    .CSAX
       BZE  XR,STPRA         SKIP IF SYSAX ALREADY CALLED
       JSR  SYSAX            CALL AFTER EXECUTION PROC
STPRA  ADD  RSMEM,DNAME      USE THE RESERVE MEMORY
.ELSE
       ADD  RSMEM,DNAME      USE THE RESERVE MEMORY
.FI
       BNE  XR,=ENDMS,STPR0  SKIP IF NOT NORMAL END MESSAGE
       BNZ  EXSTS,STPR3      SKIP IF EXEC STATS SUPPRESSED
       ZER  ERICH            CLEAR ERRORS TO INT.CH. FLAG
*
*      LOOK TO SEE IF AN ENDING MESSAGE IS SUPPLIED
*
STPR0  JSR  PRTPG            EJECT PRINTER
       BZE  XR,STPR1         SKIP IF NO MESSAGE
       JSR  PRTST            PRINT MESSAGE
*
*      MERGE HERE IF NO MESSAGE TO PRINT
*
STPR1  JSR  PRTIS            PRINT BLANK LINE
.IF    .CSFN
       BNZ  GBCFL,STPR5      IF IN GARBAGE COLLECTION, SKIP
       MOV  =STPM7,XR        POINT TO MESSAGE /IN FILE XXX/
       JSR  PRTST            PRINT IT
       MOV  =PRTMF,PROFS     SET COLUMN OFFSET
       MOV  KVSTN,WC         GET STATEMENT NUMBER
       JSR  FILNM            GET FILE NAME
       MOV  XL,XR            PREPARE TO PRINT
       JSR  PRTST            PRINT FILE NAME
       JSR  PRTIS            PRINT TO INTERACTIVE CHANNEL
.FI
.IF    .CSLN
.IF    .CSFN
.ELSE
       BNZ  GBCFL,STPR5      IF IN GARBAGE COLLECTION, SKIP
.FI
       MOV  R_COD,XR         GET CODE POINTER
       MTI  CDSLN(XR)        GET SOURCE LINE NUMBER
       MOV  =STPM6,XR        POINT TO MESSAGE /IN LINE XXX/
       JSR  PRTMX            PRINT IT
.FI
STPR5  MTI  KVSTN            GET STATEMENT NUMBER
       MOV  =STPM1,XR        POINT TO MESSAGE /IN STATEMENT XXX/
       JSR  PRTMX            PRINT IT
       JSR  SYSTM            GET CURRENT TIME
       SBI  TIMSX            MINUS START TIME = ELAPSED EXEC TIM
       STI  STPTI            SAVE FOR LATER
       MOV  =STPM3,XR        POINT TO MSG /EXECUTION TIME MSEC /
       JSR  PRTMX            PRINT IT
       LDI  KVSTL            GET STATEMENT LIMIT
       ILT  STPR2            SKIP IF NEGATIVE
       SBI  KVSTC            MINUS COUNTER = COURSE COUNT
       STI  STPSI            SAVE
       MOV  STMCS,WA         REFINE WITH COUNTER START VALUE
       SUB  STMCT,WA         MINUS CURRENT COUNTER
       MTI  WA               CONVERT TO INTEGER
       ADI  STPSI            ADD IN COURSE COUNT
       STI  STPSI            SAVE
       MOV  =STPM2,XR        POINT TO MESSAGE /STMTS EXECUTED/
       JSR  PRTMX            PRINT IT
.IF    .CTMD
.ELSE
       LDI  STPTI            RELOAD ELAPSED TIME
       MLI  INTTH            *1000 (MICROSECS)
       IOV  STPR2            JUMP IF WE CANNOT COMPUTE
       DVI  STPSI            DIVIDE BY STATEMENT COUNT
       IOV  STPR2            JUMP IF OVERFLOW
       MOV  =STPM4,XR        POINT TO MSG (MCSEC PER STATEMENT /
       JSR  PRTMX            PRINT IT
.FI
       EJC
*
*      STOPR (CONTINUED)
*
*      MERGE TO SKIP MESSAGE (OVERFLOW OR NEGATIVE STLIMIT)
*
STPR2  MTI  GBCNT            LOAD COUNT OF COLLECTIONS
       MOV  =STPM5,XR        POINT TO MESSAGE /REGENERATIONS /
       JSR  PRTMX            PRINT IT
       JSR  PRTMM            PRINT MEMORY USAGE
       JSR  PRTIS            ONE MORE BLANK FOR LUCK
*
*      CHECK IF DUMP REQUESTED
*
.IF    .CNPF
STPR3  MOV  KVDMP,XR         LOAD DUMP KEYWORD
.ELSE
STPR3  JSR  PRFLR            PRINT PROFILE IF WANTED
*
       MOV  KVDMP,XR         LOAD DUMP KEYWORD
.FI
       JSR  DUMPR            EXECUTE DUMP IF REQUESTED
       MOV  R_FCB,XL         GET FCBLK CHAIN HEAD
       MOV  KVABE,WA         LOAD ABEND VALUE
       MOV  KVCOD,WB         LOAD CODE VALUE
       JSR  SYSEJ            EXIT TO SYSTEM
.IF    .CERA
*
*      HERE AFTER SYSEA CALL AND SUPPRESSING ERROR MSG PRINT
*
STPR4  RTN
       ADD  RSMEM,DNAME      USE THE RESERVE MEMORY
       BZE  EXSTS,STPR1      IF EXECUTION STATS REQUESTED
       BRN  STPR3            CHECK IF DUMP OR PROFILE NEEDED
.FI

       EJC
*
*      SUCCP -- SIGNAL SUCCESSFUL MATCH OF A PATTERN NODE
*
*      SEE PATTERN MATCH ROUTINES FOR DETAILS
*
*      (XR)                  CURRENT NODE
*      (WB)                  CURRENT CURSOR
*      (XL)                  MAY BE NON-COLLECTABLE
*      BRN  SUCCP            SIGNAL SUCCESSFUL PATTERN MATCH
*
*      SUCCP CONTINUES BY MATCHING THE SUCCESSOR NODE
*
SUCCP  RTN
       MOV  PTHEN(XR),XR     LOAD SUCCESSOR NODE
       MOV  (XR),XL          LOAD NODE CODE ENTRY ADDRESS
       BRI  XL               JUMP TO MATCH SUCCESSOR NODE
       EJC
*
*      SYSAB -- PRINT /ABNORMAL END/ AND TERMINATE
*
SYSAB  RTN
       MOV  =ENDAB,XR        POINT TO MESSAGE
       MOV  =NUM01,KVABE     SET ABEND FLAG
       JSR  PRTNL            SKIP TO NEW LINE
       BRN  STOPR            JUMP TO PACK UP
       EJC
*
*      SYSTU -- PRINT /TIME UP/ AND TERMINATE
*
SYSTU  RTN
       MOV  =ENDTU,XR        POINT TO MESSAGE
       MOV  STRTU,WA         GET CHARS /TU/
       MOV  WA,KVCOD         PUT IN KVCOD
       MOV  TIMUP,WA         CHECK STATE OF TIMEUP SWITCH
       MNZ  TIMUP            SET SWITCH
       BNZ  WA,STOPR         STOP RUN IF ALREADY SET
       ERB  245,Translation/execution time expired
       TTL  S P I T B O L -- UTILITY PROCEDURES
*
*      THE FOLLOWING SECTION CONTAINS PROCEDURES WHICH ARE
*      USED FOR VARIOUS PURPOSES THROUGHOUT THE SYSTEM.
*
*      EACH PROCEDURE IS PRECEDED BY A DESCRIPTION OF THE
*      CALLING SEQUENCE. USUALLY THE ARGUMENTS ARE IN REGISTERS
*      BUT ARGUMENTS CAN ALSO OCCUR ON THE STACK AND AS
*      PARAMETERS ASSEMBLED AFTER THE JSR INSTRUCTION.
*
*      THE FOLLOWING CONSIDERATIONS APPLY TO THESE DESCRIPTIONS.
*
*      1)   THE STACK POINTER (XS) IS NOT CHANGED UNLESS THE
*           CHANGE IS EXPLICITLY DOCUMENTED IN THE CALL.
*
*      2)   REGISTERS WHOSE ENTRY VALUES ARE NOT MENTIONED
*           MAY CONTAIN ANY VALUE EXCEPT THAT XL,XR MAY ONLY
*           CONTAIN PROPER (COLLECTABLE) POINTER VALUES.
*           THIS CONDITION ON MEANS THAT THE CALLED ROUTINE
*           MAY IF IT CHOOSES PRESERVE XL,XR BY STACKING.
*
*      3)   REGISTERS NOT MENTIONED ON EXIT CONTAIN THE SAME
*           VALUES AS THEY DID ON ENTRY EXCEPT THAT VALUES IN
*           XR,XL MAY HAVE BEEN RELOCATED BY THE COLLECTOR.
*
*      4)   REGISTERS WHICH ARE DESTROYED ON EXIT MAY CONTAIN
*           ANY VALUE EXCEPT THAT VALUES IN XL,XR ARE PROPER
*           (COLLECTABLE) POINTERS.
*
*      5)   THE CODE POINTER REGISTER POINTS TO THE CURRENT
*           CODE LOCATION ON ENTRY AND IS UNCHANGED ON EXIT.
*
*      IN THE ABOVE DESCRIPTION, A COLLECTABLE POINTER IS ONE
*      WHICH EITHER POINTS OUTSIDE THE DYNAMIC REGION OR
*      POINTS TO THE START OF A BLOCK IN THE DYNAMIC REGION.
*
*      IN THOSE CASES WHERE THE CALLING SEQUENCE CONTAINS
*      PARAMETERS WHICH ARE USED AS ALTERNATE RETURN POINTS,
*      THESE PARAMETERS MAY BE REPLACED BY ERROR CODES
*      ASSEMBLED WITH THE ERR INSTRUCTION. THIS WILL RESULT
*      IN THE POSTING OF THE ERROR IF THE RETURN IS TAKEN.
*
*      THE PROCEDURES ALL HAVE NAMES CONSISTING OF FIVE LETTERS
*      AND ARE IN ALPHABETICAL ORDER BY THEIR NAMES.
       EJC
*
*      ACESS - ACCESS VARIABLE VALUE WITH TRACE/INPUT CHECKS
*
*      ACESS LOADS THE VALUE OF A VARIABLE. TRACE AND INPUT
*      ASSOCIATIONS ARE TESTED FOR AND EXECUTED AS REQUIRED.
*      ACESS ALSO HANDLES THE SPECIAL CASES OF PSEUDO-VARIABLES.
*
*      (XL)                  VARIABLE NAME BASE
*      (WA)                  VARIABLE NAME OFFSET
*      JSR  ACESS            CALL TO ACCESS VALUE
*      PPM  LOC              TRANSFER LOC IF ACCESS FAILURE
*      (XR)                  VARIABLE VALUE
*      (WA,WB,WC)            DESTROYED
*      (XL,RA)               DESTROYED
*
*      FAILURE CAN OCCUR IF AN INPUT ASSOCIATION CAUSES AN END
*      OF FILE CONDITION OR IF THE EVALUATION OF AN EXPRESSION
*      ASSOCIATED WITH AN EXPRESSION VARIABLE FAILS.
*
ACESS  PRC  R,1              ENTRY POINT (RECURSIVE)
       MOV  XL,XR            COPY NAME BASE
       ADD  WA,XR            POINT TO VARIABLE LOCATION
       MOV  (XR),XR          LOAD VARIABLE VALUE
*
*      LOOP HERE TO CHECK FOR SUCCESSIVE TRBLKS
*
ACS02  BNE  (XR),=B_TRT,ACS18 JUMP IF NOT TRAPPED
*
*      HERE IF TRAPPED
*
       BEQ  XR,=TRBKV,ACS12  JUMP IF KEYWORD VARIABLE
       BNE  XR,=TRBEV,ACS05  JUMP IF NOT EXPRESSION VARIABLE
*
*      HERE FOR EXPRESSION VARIABLE, EVALUATE VARIABLE
*
       MOV  EVEXP(XL),XR     LOAD EXPRESSION POINTER
       ZER  WB               EVALUATE BY VALUE
       JSR  EVALX            EVALUATE EXPRESSION
       PPM  ACS04            JUMP IF EVALUATION FAILURE
       BRN  ACS02            CHECK VALUE FOR MORE TRBLKS
       EJC
*
*      ACESS (CONTINUED)
*
*      HERE ON READING END OF FILE
*
ACS03  ADD  *NUM03,XS        POP TRBLK PTR, NAME BASE AND OFFSET
       MOV  XR,DNAMP         POP UNUSED SCBLK
*
*      MERGE HERE WHEN EVALUATION OF EXPRESSION FAILS
*
ACS04  EXI  1                TAKE ALTERNATE (FAILURE) RETURN
*
*      HERE IF NOT KEYWORD OR EXPRESSION VARIABLE
*
ACS05  MOV  TRTYP(XR),WB     LOAD TRAP TYPE CODE
       BNZ  WB,ACS10         JUMP IF NOT INPUT ASSOCIATION
       BZE  KVINP,ACS09      IGNORE INPUT ASSOC IF INPUT IS OFF
*
*      HERE FOR INPUT ASSOCIATION
*
       MOV  XL,-(XS)         STACK NAME BASE
       MOV  WA,-(XS)         STACK NAME OFFSET
       MOV  XR,-(XS)         STACK TRBLK POINTER
       MOV  KVTRM,ACTRM      TEMP TO HOLD TRIM KEYWORD
       MOV  TRFPT(XR),XL     GET FILE CTRL BLK PTR OR ZERO
       BNZ  XL,ACS06         JUMP IF NOT STANDARD INPUT FILE
       BEQ  TRTER(XR),=V_TER,ACS21 JUMP IF TERMINAL
*
*      HERE TO READ FROM STANDARD INPUT FILE
*
       MOV  CSWIN,WA         LENGTH FOR READ BUFFER
       JSR  ALOCS            BUILD STRING OF APPROPRIATE LENGTH
       JSR  SYSRD            READ NEXT STANDARD INPUT IMAGE
       PPM  ACS03            JUMP TO FAIL EXIT IF END OF FILE
       BRN  ACS07            ELSE MERGE WITH OTHER FILE CASE
*
*      HERE FOR INPUT FROM OTHER THAN STANDARD INPUT FILE
*
ACS06  MOV  XL,WA            FCBLK PTR
       JSR  SYSIL            GET INPUT RECORD MAX LENGTH (TO WA)
       BNZ  WC,ACS6A         JUMP IF NOT BINARY FILE
       MOV  WC,ACTRM         DISABLE TRIM FOR BINARY FILE
ACS6A  JSR  ALOCS            ALLOCATE STRING OF CORRECT SIZE
       MOV  XL,WA            FCBLK PTR
       JSR  SYSIN            CALL SYSTEM INPUT ROUTINE
       PPM  ACS03            JUMP TO FAIL EXIT IF END OF FILE
       PPM  ACS22            ERROR
       PPM  ACS23            ERROR
       EJC
*
*      ACESS (CONTINUED)
*
*      MERGE HERE AFTER OBTAINING INPUT RECORD
*
ACS07  MOV  ACTRM,WB         LOAD TRIM INDICATOR
       JSR  TRIMR            TRIM RECORD AS REQUIRED
       MOV  XR,WB            COPY RESULT POINTER
       MOV  (XS),XR          RELOAD POINTER TO TRBLK
*
*      LOOP TO CHASE TO END OF TRBLK CHAIN AND STORE VALUE
*
ACS08  MOV  XR,XL            SAVE POINTER TO THIS TRBLK
       MOV  TRNXT(XR),XR     LOAD FORWARD POINTER
       BEQ  (XR),=B_TRT,ACS08 LOOP IF THIS IS ANOTHER TRBLK
       MOV  WB,TRNXT(XL)     ELSE STORE RESULT AT END OF CHAIN
       MOV  (XS)+,XR         RESTORE INITIAL TRBLK POINTER
       MOV  (XS)+,WA         RESTORE NAME OFFSET
       MOV  (XS)+,XL         RESTORE NAME BASE POINTER
*
*      COME HERE TO MOVE TO NEXT TRBLK
*
ACS09  MOV  TRNXT(XR),XR     LOAD FORWARD PTR TO NEXT VALUE
       BRN  ACS02            BACK TO CHECK IF TRAPPED
*
*      HERE TO CHECK FOR ACCESS TRACE TRBLK
*
ACS10  BNE  WB,=TRTAC,ACS09  LOOP BACK IF NOT ACCESS TRACE
       BZE  KVTRA,ACS09      IGNORE ACCESS TRACE IF TRACE OFF
       DCV  KVTRA            ELSE DECREMENT TRACE COUNT
       BZE  TRFNC(XR),ACS11  JUMP IF PRINT TRACE
       EJC
*
*      ACESS (CONTINUED)
*
*      HERE FOR FULL FUNCTION TRACE
*
       JSR  TRXEQ            CALL ROUTINE TO EXECUTE TRACE
       BRN  ACS09            JUMP FOR NEXT TRBLK
*
*      HERE FOR CASE OF PRINT TRACE
*
ACS11  JSR  PRTSN            PRINT STATEMENT NUMBER
       JSR  PRTNV            PRINT NAME = VALUE
       BRN  ACS09            JUMP BACK FOR NEXT TRBLK
*
*      HERE FOR KEYWORD VARIABLE
*
ACS12  MOV  KVNUM(XL),XR     LOAD KEYWORD NUMBER
       BGE  XR,=K_V__,ACS14  JUMP IF NOT ONE WORD VALUE
       MTI  KVABE(XR)        ELSE LOAD VALUE AS INTEGER
*
*      COMMON EXIT WITH KEYWORD VALUE AS INTEGER IN (IA)
*
ACS13  JSR  ICBLD            BUILD ICBLK
       BRN  ACS18            JUMP TO EXIT
*
*      HERE IF NOT ONE WORD KEYWORD VALUE
*
ACS14  BGE  XR,=K_S__,ACS15  JUMP IF SPECIAL CASE
       SUB  =K_V__,XR        ELSE GET OFFSET
       WTB  XR               CONVERT TO BYTE OFFSET
       ADD  =NDABO,XR        POINT TO PATTERN VALUE
       BRN  ACS18            JUMP TO EXIT
*
*      HERE IF SPECIAL KEYWORD CASE
*
ACS15  MOV  KVRTN,XL         LOAD RTNTYPE IN CASE
       LDI  KVSTL            LOAD STLIMIT IN CASE
       SUB  =K_S__,XR        GET CASE NUMBER
       BSW  XR,K__N_         SWITCH ON KEYWORD NUMBER
.IF    .CSFN
       IFF  K__FL,ACS26      FILE
       IFF  K__LF,ACS27      LASTFILE
.FI
.IF    .CULK
       IFF  K__LC,ACS24      LCASE
       IFF  K__UC,ACS25      UCASE
.FI
       IFF  K__AL,ACS16      JUMP IF ALPHABET
       IFF  K__RT,ACS17      RTNTYPE
       IFF  K__SC,ACS19      STCOUNT
       IFF  K__SL,ACS13      STLIMIT
       IFF  K__ET,ACS20      ERRTEXT
       ESW                   END SWITCH ON KEYWORD NUMBER
       EJC
*
*      ACESS (CONTINUED)
*
.IF    .CULK
*      LCASE
*
ACS24  MOV  =LCASE,XR        LOAD POINTER TO LCASE STRING
       BRN  ACS18            COMMON RETURN
*
*      UCASE
*
ACS25  MOV  =UCASE,XR        LOAD POINTER TO UCASE STRING
       BRN  ACS18            COMMON RETURN
*
.FI
.IF    .CSFN
*      FILE
*
ACS26  MOV  KVSTN,WC         LOAD CURRENT STMT NUMBER
       BRN  ACS28            MERGE TO OBTAIN FILE NAME
*
*      LASTFILE
*
ACS27  MOV  KVLST,WC         LOAD LAST STMT NUMBER
*
*      MERGE HERE TO MAP STATEMENT NUMBER IN WC TO FILE NAME
*
ACS28  JSR  FILNM            OBTAIN FILE NAME FOR THIS STMT
       BRN  ACS17            MERGE TO RETURN STRING IN XL
.FI
*      ALPHABET
*
ACS16  MOV  KVALP,XL         LOAD POINTER TO ALPHABET STRING
*
*      RTNTYPE MERGES HERE
*
ACS17  MOV  XL,XR            COPY STRING PTR TO PROPER REG
*
*      COMMON RETURN POINT
*
ACS18  EXI                   RETURN TO ACESS CALLER
*
*      HERE FOR STCOUNT (IA HAS STLIMIT)
*
ACS19  ILT  ACS29            IF COUNTING SUPPRESSED
       MOV  STMCS,WA         REFINE WITH COUNTER START VALUE
       SUB  STMCT,WA         MINUS CURRENT COUNTER
       MTI  WA               CONVERT TO INTEGER
       ADI  KVSTL            ADD STLIMIT
ACS29  SBI  KVSTC            STCOUNT = LIMIT - LEFT
       BRN  ACS13            MERGE BACK WITH INTEGER RESULT
*
*      ERRTEXT
*
ACS20  MOV  R_ETX,XR         GET ERRTEXT STRING
       BRN  ACS18            MERGE WITH RESULT
*
*      HERE TO READ A RECORD FROM TERMINAL
*
ACS21  MOV  =RILEN,WA        BUFFER LENGTH
       JSR  ALOCS            ALLOCATE BUFFER
       JSR  SYSRI            READ RECORD
       PPM  ACS03            ENDFILE
       BRN  ACS07            MERGE WITH RECORD READ
*
*      ERROR RETURNS
*
ACS22  MOV  XR,DNAMP         POP UNUSED SCBLK
       ERB  202,Input from file caused non-recoverable error
*
ACS23  MOV  XR,DNAMP         POP UNUSED SCBLK
       ERB  203,Input file record has incorrect format
       ENP                   END PROCEDURE ACESS
       EJC
*
*      ACOMP -- COMPARE TWO ARITHMETIC VALUES
*
*      1(XS)                 FIRST ARGUMENT
*      0(XS)                 SECOND ARGUMENT
*      JSR  ACOMP            CALL TO COMPARE VALUES
*      PPM  LOC              TRANSFER LOC IF ARG1 IS NON-NUMERIC
*      PPM  LOC              TRANSFER LOC IF ARG2 IS NON-NUMERIC
*      PPM  LOC              TRANSFER LOC FOR ARG1 LT ARG2
*      PPM  LOC              TRANSFER LOC FOR ARG1 EQ ARG2
*      PPM  LOC              TRANSFER LOC FOR ARG1 GT ARG2
*      (NORMAL RETURN IS NEVER GIVEN)
*      (WA,WB,WC,IA,RA)      DESTROYED
*      (XL,XR)               DESTROYED
*
ACOMP  PRC  N,5              ENTRY POINT
       JSR  ARITH            LOAD ARITHMETIC OPERANDS
       PPM  ACMP7            JUMP IF FIRST ARG NON-NUMERIC
       PPM  ACMP8            JUMP IF SECOND ARG NON-NUMERIC
.IF    .CNRA
.ELSE
       PPM  ACMP4            JUMP IF REAL ARGUMENTS
.FI
*
*      HERE FOR INTEGER ARGUMENTS
*
       SBI  ICVAL(XL)        SUBTRACT TO COMPARE
       IOV  ACMP3            JUMP IF OVERFLOW
       ILT  ACMP5            ELSE JUMP IF ARG1 LT ARG2
       IEQ  ACMP2            JUMP IF ARG1 EQ ARG2
*
*      HERE IF ARG1 GT ARG2
*
ACMP1  EXI  5                TAKE GT EXIT
*
*      HERE IF ARG1 EQ ARG2
*
ACMP2  EXI  4                TAKE EQ EXIT
       EJC
*
*      ACOMP (CONTINUED)
*
*      HERE FOR INTEGER OVERFLOW ON SUBTRACT
*
ACMP3  LDI  ICVAL(XL)        LOAD SECOND ARGUMENT
       ILT  ACMP1            GT IF NEGATIVE
       BRN  ACMP5            ELSE LT
.IF    .CNRA
.ELSE
*
*      HERE FOR REAL OPERANDS
*
ACMP4  SBR  RCVAL(XL)        SUBTRACT TO COMPARE
       ROV  ACMP6            JUMP IF OVERFLOW
       RGT  ACMP1            ELSE JUMP IF ARG1 GT
       REQ  ACMP2            JUMP IF ARG1 EQ ARG2
.FI
*
*      HERE IF ARG1 LT ARG2
*
ACMP5  EXI  3                TAKE LT EXIT
.IF    .CNRA
.ELSE
*
*      HERE IF OVERFLOW ON REAL SUBTRACTION
*
ACMP6  LDR  RCVAL(XL)        RELOAD ARG2
       RLT  ACMP1            GT IF NEGATIVE
       BRN  ACMP5            ELSE LT
.FI
*
*      HERE IF ARG1 NON-NUMERIC
*
ACMP7  EXI  1                TAKE ERROR EXIT
*
*      HERE IF ARG2 NON-NUMERIC
*
ACMP8  EXI  2                TAKE ERROR EXIT
       ENP                   END PROCEDURE ACOMP
       EJC
*
*      ALLOC                 ALLOCATE BLOCK OF DYNAMIC STORAGE
*
*      (WA)                  LENGTH REQUIRED IN BYTES
*      JSR  ALLOC            CALL TO ALLOCATE BLOCK
*      (XR)                  POINTER TO ALLOCATED BLOCK
*
*      A POSSIBLE ALTERNATIVE TO AOV ... AND FOLLOWING STMT IS -
*      MOV  DNAME,XR .  SUB  WA,XR .  BLO XR,DNAMP,ALOC2 .
*      MOV  DNAMP,XR .  ADD  WA,XR
*
ALLOC  PRC  E,0              ENTRY POINT
*
*      COMMON EXIT POINT
*
ALOC1  MOV  DNAMP,XR         POINT TO NEXT AVAILABLE LOC
       AOV  WA,XR,ALOC2      POINT PAST ALLOCATED BLOCK
       BGT  XR,DNAME,ALOC2   JUMP IF NOT ENOUGH ROOM
       MOV  XR,DNAMP         STORE NEW POINTER
       SUB  WA,XR            POINT BACK TO START OF ALLOCATED BK
       EXI                   RETURN TO CALLER
*
*      HERE IF INSUFFICIENT ROOM, TRY A GARBAGE COLLECTION
*
ALOC2  MOV  WB,ALLSV         SAVE WB
ALC2A  ZER  WB               SET NO UPWARD MOVE FOR GBCOL
       JSR  GBCOL            GARBAGE COLLECT
.IF    .CSED
       MOV  XR,WB            REMEMBER NEW SEDIMENT SIZE
.FI
*
*      SEE IF ROOM AFTER GBCOL OR SYSMM CALL
*
ALOC3  MOV  DNAMP,XR         POINT TO FIRST AVAILABLE LOC
       AOV  WA,XR,ALC3A      POINT PAST NEW BLOCK
       BLO  XR,DNAME,ALOC4   JUMP IF THERE IS ROOM NOW
*
*      FAILED AGAIN, SEE IF WE CAN GET MORE CORE
*
ALC3A  JSR  SYSMM            TRY TO GET MORE MEMORY
       WTB  XR               CONVERT TO BAUS (SGD05)
       ADD  XR,DNAME         BUMP PTR BY AMOUNT OBTAINED
       BNZ  XR,ALOC3         JUMP IF GOT MORE CORE
.IF    .CSED
       BZE  DNAMS,ALC3B      JUMP IF THERE WAS NO SEDIMENT
       ZER  DNAMS            TRY COLLECTING THE SEDIMENT
       BRN  ALC2A
*
*      SYSMM FAILED AND THERE WAS NO SEDIMENT TO COLLECT
*
ALC3B  ADD  RSMEM,DNAME      GET THE RESERVE MEMORY
.ELSE
       ADD  RSMEM,DNAME      GET THE RESERVE MEMORY
.FI
       ZER  RSMEM            ONLY PERMISSIBLE ONCE
       ICV  ERRFT            FATAL ERROR
       ERB  204,Memory overflow
       EJC
*
*      HERE AFTER SUCCESSFUL GARBAGE COLLECTION
*
ALOC4  STI  ALLIA            SAVE IA
.IF    .CSED
       MOV  WB,DNAMS         RECORD NEW SEDIMENT SIZE
.FI
       MOV  DNAME,WB         GET DYNAMIC END ADRS
       SUB  DNAMP,WB         COMPUTE FREE STORE
       BTW  WB               CONVERT BYTES TO WORDS
       MTI  WB               PUT FREE STORE IN IA
       MLI  ALFSF            MULTIPLY BY FREE STORE FACTOR
       IOV  ALOC5            JUMP IF OVERFLOWED
       MOV  DNAME,WB         DYNAMIC END ADRS
       SUB  DNAMB,WB         COMPUTE TOTAL AMOUNT OF DYNAMIC
       BTW  WB               CONVERT TO WORDS
       MOV  WB,ALDYN         STORE IT
       SBI  ALDYN            SUBTRACT FROM SCALED UP FREE STORE
       IGT  ALOC5            JUMP IF SUFFICIENT FREE STORE
       JSR  SYSMM            TRY TO GET MORE STORE
       WTB  XR               CONVERT TO BAUS (SGD05)
       ADD  XR,DNAME         ADJUST DYNAMIC END ADRS
*
*      MERGE TO RESTORE IA AND WB
*
ALOC5  LDI  ALLIA            RECOVER IA
       MOV  ALLSV,WB         RESTORE WB
       BRN  ALOC1            JUMP BACK TO EXIT
       ENP                   END PROCEDURE ALLOC
       EJC
.IF    .CNBF
.ELSE
*
*      ALOBF -- ALLOCATE BUFFER
*
*      THIS ROUTINES ALLOCATES A NEW BUFFER.  AS THE BFBLK
*      AND BCBLK COME IN PAIRS, BOTH ARE ALLOCATED HERE,
*      AND XR POINTS TO THE BCBLK ON RETURN.  THE BFBLK
*      AND BCBLK ARE SET TO THE NULL BUFFER, AND THE IDVAL
*      IS ZERO ON RETURN.
*
*      (WA)                  BUFFER SIZE IN CHARACTERS
*      JSR  ALOBF            CALL TO CREATE BUFFER
*      (XR)                  BCBLK PTR
*      (WA,WB)               DESTROYED
*
ALOBF  PRC  E,0              ENTRY POINT
       BGT  WA,KVMXL,ALB01   CHECK FOR MAXLNGTH EXCEEDED
       MOV  WA,WB            HANG ONTO ALLOCATION SIZE
       CTB  WA,BFSI_         GET TOTAL BLOCK SIZE
       ADD  *BCSI_,WA        ADD IN ALLOCATION FOR BCBLK
       JSR  ALLOC            ALLOCATE FRAME
       MOV  =B_BCT,(XR)      SET TYPE
       ZER  IDVAL(XR)        NO ID YET
       ZER  BCLEN(XR)        NO DEFINED LENGTH
       MOV  XL,WA            SAVE XL
       MOV  XR,XL            COPY BCBLK PTR
       ADD  *BCSI_,XL        BIAS PAST PARTIALLY BUILT BCBLK
       MOV  =B_BFT,(XL)      SET BFBLK TYPE WORD
       MOV  WB,BFALC(XL)     SET ALLOCATED SIZE
       MOV  XL,BCBUF(XR)     SET POINTER IN BCBLK
       ZER  BFCHR(XL)        CLEAR FIRST WORD (NULL PAD)
       MOV  WA,XL            RESTORE ENTRY XL
       EXI                   RETURN TO CALLER
*
*      HERE FOR MXLEN EXCEEDED
*
ALB01  ERB  273,BUFFER size exceeds value of MAXLNGTH keyword
       ENP                   END PROCEDURE ALOBF
       EJC
.FI
*
*      ALOCS -- ALLOCATE STRING BLOCK
*
*      ALOCS IS USED TO BUILD A FRAME FOR A STRING BLOCK INTO
*      WHICH THE ACTUAL CHARACTERS ARE PLACED BY THE CALLER.
*      ALL STRINGS ARE CREATED WITH A CALL TO ALOCS (THE
*      EXCEPTIONS OCCUR IN TRIMR AND S_RPL PROCEDURES).
*
*      (WA)                  LENGTH OF STRING TO BE ALLOCATED
*      JSR  ALOCS            CALL TO ALLOCATE SCBLK
*      (XR)                  POINTER TO RESULTING SCBLK
*      (WA)                  DESTROYED
*      (WC)                  CHARACTER COUNT (ENTRY VALUE OF WA)
*
*      THE RESULTING SCBLK HAS THE TYPE WORD AND THE LENGTH
*      FILLED IN AND THE LAST WORD IS CLEARED TO ZERO CHARACTERS
*      TO ENSURE CORRECT RIGHT PADDING OF THE FINAL WORD.
*
ALOCS  PRC  E,0              ENTRY POINT
       BGT  WA,KVMXL,ALCS2   JUMP IF LENGTH EXCEEDS MAXLENGTH
       MOV  WA,WC            ELSE COPY LENGTH
       CTB  WA,SCSI_         COMPUTE LENGTH OF SCBLK IN BYTES
       MOV  DNAMP,XR         POINT TO NEXT AVAILABLE LOCATION
       AOV  WA,XR,ALCS0      POINT PAST BLOCK
       BLO  XR,DNAME,ALCS1   JUMP IF THERE IS ROOM
*
*      INSUFFICIENT MEMORY
*
ALCS0  ZER  XR               ELSE CLEAR GARBAGE XR VALUE
       JSR  ALLOC            AND USE STANDARD ALLOCATOR
       ADD  WA,XR            POINT PAST END OF BLOCK TO MERGE
*
*      MERGE HERE WITH XR POINTING BEYOND NEW BLOCK
*
ALCS1  MOV  XR,DNAMP         SET UPDATED STORAGE POINTER
       ZER  -(XR)            STORE ZERO CHARS IN LAST WORD
       DCA  WA               DECREMENT LENGTH
       SUB  WA,XR            POINT BACK TO START OF BLOCK
       MOV  =B_SCL,(XR)      SET TYPE WORD
       MOV  WC,SCLEN(XR)     STORE LENGTH IN CHARS
       EXI                   RETURN TO ALOCS CALLER
*
*      COME HERE IF STRING IS TOO LONG
*
ALCS2  ERB  205,String length exceeds value of MAXLNGTH keyword
       ENP                   END PROCEDURE ALOCS
       EJC
*
*      ALOST -- ALLOCATE SPACE IN STATIC REGION
*
*      (WA)                  LENGTH REQUIRED IN BYTES
*      JSR  ALOST            CALL TO ALLOCATE SPACE
*      (XR)                  POINTER TO ALLOCATED BLOCK
*      (WB)                  DESTROYED
*
*      NOTE THAT THE CODING ENSURES THAT THE RESULTING VALUE
*      OF STATE IS ALWAYS LESS THAN DNAMB. THIS FACT IS USED
*      IN TESTING A VARIABLE NAME FOR BEING IN THE STATIC REGION
*
ALOST  PRC  E,0              ENTRY POINT
*
*      MERGE BACK HERE AFTER ALLOCATING NEW CHUNK
*
ALST1  MOV  STATE,XR         POINT TO CURRENT END OF AREA
       AOV  WA,XR,ALST2      POINT BEYOND PROPOSED BLOCK
       BGE  XR,DNAMB,ALST2   JUMP IF OVERLAP WITH DYNAMIC AREA
       MOV  XR,STATE         ELSE STORE NEW POINTER
       SUB  WA,XR            POINT BACK TO START OF BLOCK
       EXI                   RETURN TO ALOST CALLER
*
*      HERE IF NO ROOM, PREPARE TO MOVE DYNAMIC STORAGE UP
*
ALST2  MOV  WA,ALSTA         SAVE WA
       BGE  WA,*E_STS,ALST3  SKIP IF REQUESTED CHUNK IS LARGE
       MOV  *E_STS,WA        ELSE SET TO GET LARGE ENOUGH CHUNK
*
*      HERE WITH AMOUNT TO MOVE UP IN WA
*
ALST3  JSR  ALLOC            ALLOCATE BLOCK TO ENSURE ROOM
       MOV  XR,DNAMP         AND DELETE IT
       MOV  WA,WB            COPY MOVE UP AMOUNT
       JSR  GBCOL            CALL GBCOL TO MOVE DYNAMIC AREA UP
.IF    .CSED
       MOV  XR,DNAMS         REMEMBER NEW SEDIMENT SIZE
.FI
       MOV  ALSTA,WA         RESTORE WA
       BRN  ALST1            LOOP BACK TO TRY AGAIN
       ENP                   END PROCEDURE ALOST
       EJC
.IF    .CNBF
.ELSE
*
*      APNDB -- APPEND STRING TO BUFFER
*
*      THIS ROUTINE IS USED BY BUFFER HANDLING ROUTINES TO
*      APPEND DATA TO AN EXISTING BFBLK.
*
*      (XR)                  EXISTING BCBLK TO BE APPENDED
*      (XL)                  CONVERTABLE TO STRING
*      JSR  APNDB            CALL TO APPEND TO BUFFER
*      PPM  LOC              THREAD IF (XL) CANT BE CONVERTED
*      PPM  LOC              IF NOT ENOUGH ROOM
*      (WA,WB)               DESTROYED
*
*      IF MORE CHARACTERS ARE SPECIFIED THAN CAN BE INSERTED,
*      THEN NO ACTION IS TAKEN AND THE SECOND RETURN IS TAKEN.
*
APNDB  PRC  E,2              ENTRY POINT
       MOV  BCLEN(XR),WA     LOAD OFFSET TO INSERT
       ZER  WB               REPLACE SECTION IS NULL
       JSR  INSBF            CALL TO INSERT AT END
       PPM  APN01            CONVERT ERROR
       PPM  APN02            NO ROOM
       EXI                   RETURN TO CALLER
*
*      HERE TO TAKE CONVERT FAILURE EXIT
*
APN01  EXI  1                RETURN TO CALLER ALTERNATE
*
*      HERE FOR NO FIT EXIT
*
APN02  EXI  2                ALTERNATE EXIT TO CALLER
       ENP                   END PROCEDURE APNDB
       EJC
.FI
*
*      ARITH -- FETCH ARITHMETIC OPERANDS
*
*      ARITH IS USED BY FUNCTIONS AND OPERATORS WHICH EXPECT
*      TWO NUMERIC ARGUMENTS (OPERANDS) WHICH MUST BOTH BE
*      INTEGER OR BOTH BE REAL. ARITH FETCHES TWO ARGUMENTS FROM
*      THE STACK AND PERFORMS ANY NECESSARY CONVERSIONS.
*
*      1(XS)                 FIRST ARGUMENT (LEFT OPERAND)
*      0(XS)                 SECOND ARGUMENT (RIGHT OPERAND)
*      JSR  ARITH            CALL TO FETCH NUMERIC ARGUMENTS
*      PPM  LOC              TRANSFER LOC FOR OPND 1 NON-NUMERIC
*      PPM  LOC              TRANSFER LOC FOR OPND 2 NON-NUMERIC
.IF    .CNRA
.ELSE
*      PPM  LOC              TRANSFER LOC FOR REAL OPERANDS
.FI
*
*      FOR INTEGER ARGS, CONTROL RETURNS PAST THE PARAMETERS
*
*      (IA)                  LEFT OPERAND VALUE
*      (XR)                  PTR TO ICBLK FOR LEFT OPERAND
*      (XL)                  PTR TO ICBLK FOR RIGHT OPERAND
*      (XS)                  POPPED TWICE
*      (WA,WB,RA)            DESTROYED
.IF    .CNRA
.ELSE
*
*      FOR REAL ARGUMENTS, CONTROL RETURNS TO THE LOCATION
*      SPECIFIED BY THE THIRD PARAMETER.
*
*      (RA)                  LEFT OPERAND VALUE
*      (XR)                  PTR TO RCBLK FOR LEFT OPERAND
*      (XL)                  PTR TO RCBLK FOR RIGHT OPERAND
*      (WA,WB,WC)            DESTROYED
*      (XS)                  POPPED TWICE
.FI
       EJC
*
*      ARITH (CONTINUED)
*
*      ENTRY POINT
*
.IF    .CNRA
ARITH  PRC  N,2              ENTRY POINT
.ELSE
ARITH  PRC  N,3              ENTRY POINT
.FI
       MOV  (XS)+,XL         LOAD RIGHT OPERAND
       MOV  (XS)+,XR         LOAD LEFT OPERAND
       MOV  (XL),WA          GET RIGHT OPERAND TYPE WORD
       BEQ  WA,=B_ICL,ARTH1  JUMP IF INTEGER
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,ARTH4  JUMP IF REAL
.FI
       MOV  XR,-(XS)         ELSE REPLACE LEFT ARG ON STACK
       MOV  XL,XR            COPY LEFT ARG POINTER
       JSR  GTNUM            CONVERT TO NUMERIC
       PPM  ARTH6            JUMP IF UNCONVERTIBLE
       MOV  XR,XL            ELSE COPY CONVERTED RESULT
       MOV  (XL),WA          GET RIGHT OPERAND TYPE WORD
       MOV  (XS)+,XR         RELOAD LEFT ARGUMENT
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,ARTH4  JUMP IF RIGHT ARG IS REAL
.FI
*
*      HERE IF RIGHT ARG IS AN INTEGER
*
ARTH1  BNE  (XR),=B_ICL,ARTH3 JUMP IF LEFT ARG NOT INTEGER
*
*      EXIT FOR INTEGER CASE
*
ARTH2  LDI  ICVAL(XR)        LOAD LEFT OPERAND VALUE
       EXI                   RETURN TO ARITH CALLER
*
*      HERE FOR RIGHT OPERAND INTEGER, LEFT OPERAND NOT
*
ARTH3  JSR  GTNUM            CONVERT LEFT ARG TO NUMERIC
       PPM  ARTH7            JUMP IF NOT CONVERTIBLE
       BEQ  WA,=B_ICL,ARTH2  JUMP BACK IF INTEGER-INTEGER
.IF    .CNRA
.ELSE
*
*      HERE WE MUST CONVERT REAL-INTEGER TO REAL-REAL
*
       MOV  XR,-(XS)         PUT LEFT ARG BACK ON STACK
       LDI  ICVAL(XL)        LOAD RIGHT ARGUMENT VALUE
       ITR                   CONVERT TO REAL
       JSR  RCBLD            GET REAL BLOCK FOR RIGHT ARG, MERGE
       MOV  XR,XL            COPY RIGHT ARG PTR
       MOV  (XS)+,XR         LOAD LEFT ARGUMENT
       BRN  ARTH5            MERGE FOR REAL-REAL CASE
       EJC
*
*      ARITH (CONTINUED)
*
*      HERE IF RIGHT ARGUMENT IS REAL
*
ARTH4  BEQ  (XR),=B_RCL,ARTH5 JUMP IF LEFT ARG REAL
       JSR  GTREA            ELSE CONVERT TO REAL
       PPM  ARTH7            ERROR IF UNCONVERTIBLE
*
*      HERE FOR REAL-REAL
*
ARTH5  LDR  RCVAL(XR)        LOAD LEFT OPERAND VALUE
       EXI  3                TAKE REAL-REAL EXIT
.FI
*
*      HERE FOR ERROR CONVERTING RIGHT ARGUMENT
*
ARTH6  ICA  XS               POP UNWANTED LEFT ARG
       EXI  2                TAKE APPROPRIATE ERROR EXIT
*
*      HERE FOR ERROR CONVERTING LEFT OPERAND
*
ARTH7  EXI  1                TAKE APPROPRIATE ERROR RETURN
       ENP                   END PROCEDURE ARITH
       EJC
*
*      ASIGN -- PERFORM ASSIGNMENT
*
*      ASIGN PERFORMS THE ASSIGNMENT OF A VALUE TO A VARIABLE
*      WITH APPROPRIATE CHECKS FOR OUTPUT ASSOCIATIONS AND
*      VALUE TRACE ASSOCIATIONS WHICH ARE EXECUTED AS REQUIRED.
*      ASIGN ALSO HANDLES THE SPECIAL CASES OF ASSIGNMENT TO
*      PATTERN AND EXPRESSION VARIABLES.
*
*      (WB)                  VALUE TO BE ASSIGNED
*      (XL)                  BASE POINTER FOR VARIABLE
*      (WA)                  OFFSET FOR VARIABLE
*      JSR  ASIGN            CALL TO ASSIGN VALUE TO VARIABLE
*      PPM  LOC              TRANSFER LOC FOR FAILURE
*      (XR,XL,WA,WB,WC)      DESTROYED
*      (RA)                  DESTROYED
*
*      FAILURE OCCURS IF THE EVALUATION OF AN EXPRESSION
*      ASSOCIATED WITH AN EXPRESSION VARIABLE FAILS.
*
ASIGN  PRC  R,1              ENTRY POINT (RECURSIVE)
*
*      MERGE BACK HERE TO ASSIGN RESULT TO EXPRESSION VARIABLE.
*
ASG01  ADD  WA,XL            POINT TO VARIABLE VALUE
       MOV  (XL),XR          LOAD VARIABLE VALUE
       BEQ  (XR),=B_TRT,ASG02 JUMP IF TRAPPED
       MOV  WB,(XL)          ELSE PERFORM ASSIGNMENT
       ZER  XL               CLEAR GARBAGE VALUE IN XL
       EXI                   AND RETURN TO ASIGN CALLER
*
*      HERE IF VALUE IS TRAPPED
*
ASG02  SUB  WA,XL            RESTORE NAME BASE
       BEQ  XR,=TRBKV,ASG14  JUMP IF KEYWORD VARIABLE
       BNE  XR,=TRBEV,ASG04  JUMP IF NOT EXPRESSION VARIABLE
*
*      HERE FOR ASSIGNMENT TO EXPRESSION VARIABLE
*
       MOV  EVEXP(XL),XR     POINT TO EXPRESSION
       MOV  WB,-(XS)         STORE VALUE TO ASSIGN ON STACK
       MOV  =NUM01,WB        SET FOR EVALUATION BY NAME
       JSR  EVALX            EVALUATE EXPRESSION BY NAME
       PPM  ASG03            JUMP IF EVALUATION FAILS
       MOV  (XS)+,WB         ELSE RELOAD VALUE TO ASSIGN
       BRN  ASG01            LOOP BACK TO PERFORM ASSIGNMENT
       EJC
*
*      ASIGN (CONTINUED)
*
*      HERE FOR FAILURE DURING EXPRESSION EVALUATION
*
ASG03  ICA  XS               REMOVE STACKED VALUE ENTRY
       EXI  1                TAKE FAILURE EXIT
*
*      HERE IF NOT KEYWORD OR EXPRESSION VARIABLE
*
ASG04  MOV  XR,-(XS)         SAVE PTR TO FIRST TRBLK
*
*      LOOP TO CHASE DOWN TRBLK CHAIN AND ASSIGN VALUE AT END
*
ASG05  MOV  XR,WC            SAVE PTR TO THIS TRBLK
       MOV  TRNXT(XR),XR     POINT TO NEXT TRBLK
       BEQ  (XR),=B_TRT,ASG05 LOOP BACK IF ANOTHER TRBLK
       MOV  WC,XR            ELSE POINT BACK TO LAST TRBLK
       MOV  WB,TRVAL(XR)     STORE VALUE AT END OF CHAIN
       MOV  (XS)+,XR         RESTORE PTR TO FIRST TRBLK
*
*      LOOP TO PROCESS TRBLK ENTRIES ON CHAIN
*
ASG06  MOV  TRTYP(XR),WB     LOAD TYPE CODE OF TRBLK
       BEQ  WB,=TRTVL,ASG08  JUMP IF VALUE TRACE
       BEQ  WB,=TRTOU,ASG10  JUMP IF OUTPUT ASSOCIATION
*
*      HERE TO MOVE TO NEXT TRBLK ON CHAIN
*
ASG07  MOV  TRNXT(XR),XR     POINT TO NEXT TRBLK ON CHAIN
       BEQ  (XR),=B_TRT,ASG06 LOOP BACK IF ANOTHER TRBLK
       EXI                   ELSE END OF CHAIN, RETURN TO CALLER
*
*      HERE TO PROCESS VALUE TRACE
*
ASG08  BZE  KVTRA,ASG07      IGNORE VALUE TRACE IF TRACE OFF
       DCV  KVTRA            ELSE DECREMENT TRACE COUNT
       BZE  TRFNC(XR),ASG09  JUMP IF PRINT TRACE
       JSR  TRXEQ            ELSE EXECUTE FUNCTION TRACE
       BRN  ASG07            AND LOOP BACK
       EJC
*
*      ASIGN (CONTINUED)
*
*      HERE FOR PRINT TRACE
*
ASG09  JSR  PRTSN            PRINT STATEMENT NUMBER
       JSR  PRTNV            PRINT NAME = VALUE
       BRN  ASG07            LOOP BACK FOR NEXT TRBLK
*
*      HERE FOR OUTPUT ASSOCIATION
*
ASG10  BZE  KVOUP,ASG07      IGNORE OUTPUT ASSOC IF OUTPUT OFF
ASG1B  MOV  XR,XL            COPY TRBLK POINTER
       MOV  TRNXT(XR),XR     POINT TO NEXT TRBLK
       BEQ  (XR),=B_TRT,ASG1B LOOP BACK IF ANOTHER TRBLK
       MOV  XL,XR            ELSE POINT BACK TO LAST TRBLK
.IF    .CNBF
       MOV  TRVAL(XR),-(XS)  STACK VALUE TO OUTPUT
.ELSE
       MOV  TRVAL(XR),XR     GET VALUE TO OUTPUT
       BEQ  (XR),=B_BCT,ASG11 BRANCH IF BUFFER
       MOV  XR,-(XS)         STACK VALUE TO OUTPUT
.FI
       JSR  GTSTG            CONVERT TO STRING
       PPM  ASG12            GET DATATYPE NAME IF UNCONVERTIBLE
*
*      MERGE WITH STRING OR BUFFER TO OUTPUT IN XR
*
ASG11  MOV  TRFPT(XL),WA     FCBLK PTR
       BZE  WA,ASG13         JUMP IF STANDARD OUTPUT FILE
*
*      HERE FOR OUTPUT TO FILE
*
ASG1A  JSR  SYSOU            CALL SYSTEM OUTPUT ROUTINE
       ERR  206,Output caused file overflow
       ERR  207,Output caused non-recoverable error
       EXI                   ELSE ALL DONE, RETURN TO CALLER
*
*      IF NOT PRINTABLE, GET DATATYPE NAME INSTEAD
*
ASG12  JSR  DTYPE            CALL DATATYPE ROUTINE
       BRN  ASG11            MERGE
*
*      HERE TO PRINT A STRING TO STANDARD OUTPUT OR TERMINAL
*
.IF    .CSOU
ASG13  BEQ  TRTER(XL),=V_TER,ASG1A JUMP IF TERMINAL OUTPUT
       ICV  WA               SIGNAL STANDARD OUTPUT
       BRN  ASG1A            USE SYSOU TO PERFORM OUTPUT
.ELSE
.IF    .CNBF
ASG13  JSR  PRTST            PRINT STRING VALUE
.ELSE
ASG13  BNE  (XR),=B_BCT,ASG1C BRANCH IF NOT BUFFER
       MOV  XR,-(XS)         STACK BUFFER
       JSR  GTSTG            CONVERT TO STRING
       PPM                   ALWAYS SUCCEEDS
ASG1C  JSR  PRTST            PRINT STRING VALUE
.FI
       BEQ  TRTER(XL),=V_TER,ASG20 JUMP IF TERMINAL OUTPUT
       JSR  PRTNL            END OF LINE
       EXI                   RETURN TO CALLER
.FI
       EJC
*
*      ASIGN (CONTINUED)
*
*      HERE FOR KEYWORD ASSIGNMENT
*
ASG14  MOV  KVNUM(XL),XL     LOAD KEYWORD NUMBER
       BEQ  XL,=K_ETX,ASG19  JUMP IF ERRTEXT
       MOV  WB,XR            COPY VALUE TO BE ASSIGNED
       JSR  GTINT            CONVERT TO INTEGER
       ERR  208,Keyword value assigned is not integer
       LDI  ICVAL(XR)        ELSE LOAD VALUE
       BEQ  XL,=K_STL,ASG16  JUMP IF SPECIAL CASE OF STLIMIT
       MFI  WA,ASG18         ELSE GET ADDR INTEGER, TEST OVFLOW
       BGT  WA,MXLEN,ASG18   FAIL IF TOO LARGE
       BEQ  XL,=K_ERT,ASG17  JUMP IF SPECIAL CASE OF ERRTYPE
.IF    .CNPF
.ELSE
       BEQ  XL,=K_PFL,ASG21  JUMP IF SPECIAL CASE OF PROFILE
.FI
       BEQ  XL,=K_MXL,ASG24  JUMP IF SPECIAL CASE OF MAXLNGTH
       BEQ  XL,=K_FLS,ASG26  JUMP IF SPECIAL CASE OF FULLSCAN
       BLT  XL,=K_P__,ASG15  JUMP UNLESS PROTECTED
       ERB  209,Keyword in assignment is protected
*
*      HERE TO DO ASSIGNMENT IF NOT PROTECTED
*
ASG15  MOV  WA,KVABE(XL)     STORE NEW VALUE
       EXI                   RETURN TO ASIGN CALLER
*
*      HERE FOR SPECIAL CASE OF STLIMIT
*
*      SINCE STCOUNT IS MAINTAINED AS (STLIMIT-STCOUNT)
*      IT IS ALSO NECESSARY TO MODIFY STCOUNT APPROPRIATELY.
*
ASG16  SBI  KVSTL            SUBTRACT OLD LIMIT
       ADI  KVSTC            ADD OLD COUNTER
       STI  KVSTC            STORE COURSE COUNTER VALUE
       LDI  KVSTL            CHECK IF COUNTING SUPPRESSED
       ILT  ASG25            DO NOT REFINE IF SO
       MOV  STMCS,WA         REFINE WITH COUNTER BREAKOUT
       SUB  STMCT,WA         VALUES
       MTI  WA               CONVERT TO INTEGER
       NGI                   CURRENT-START VALUE
       ADI  KVSTC            ADD IN COURSE COUNTER VALUE
       STI  KVSTC            SAVE REFINED VALUE
ASG25  LDI  ICVAL(XR)        RELOAD NEW LIMIT VALUE
       STI  KVSTL            STORE NEW LIMIT VALUE
       JSR  STGCC            RECOMPUTE COUNTDOWN COUNTERS
       EXI                   RETURN TO ASIGN CALLER
*
*      HERE FOR SPECIAL CASE OF ERRTYPE
*
ASG17  BLE  WA,=NINI9,ERROR  OK TO SIGNAL IF IN RANGE
*
*      HERE IF VALUE ASSIGNED IS OUT OF RANGE
*
ASG18  ERB  210,Keyword value assigned is negative or too large
*
*      HERE FOR SPECIAL CASE OF ERRTEXT
*
ASG19  MOV  WB,-(XS)         STACK VALUE
       JSR  GTSTG            CONVERT TO STRING
       ERR  211,Value assigned to keyword ERRTEXT not a string
       MOV  XR,R_ETX         MAKE ASSIGNMENT
       EXI                   RETURN TO CALLER
.IF    .CSOU
.ELSE
*
*      PRINT STRING TO TERMINAL
*
ASG20  JSR  PRTTR            PRINT
       EXI                   RETURN
.FI
*
.IF    .CNPF
.ELSE
*      HERE FOR KEYWORD PROFILE
*
ASG21  BGT  WA,=NUM02,ASG18  MOAN IF NOT 0,1, OR 2
       BZE  WA,ASG15         JUST ASSIGN IF ZERO
       BZE  PFDMP,ASG22      BRANCH IF FIRST ASSIGNMENT
       BEQ  WA,PFDMP,ASG23   ALSO IF SAME VALUE AS BEFORE
       ERB  268,Inconsistent value assigned to keyword PROFILE
*
ASG22  MOV  WA,PFDMP         NOTE VALUE ON FIRST ASSIGNMENT
ASG23  MOV  WA,KVPFL         STORE NEW VALUE
       JSR  STGCC            RECOMPUTE COUNTDOWN COUNTS
       JSR  SYSTM            GET THE TIME
       STI  PFSTM            FUDGE SOME KIND OF START TIME
       EXI                   RETURN TO ASIGN CALLER
.FI
*
*      HERE FOR KEYWORD MAXLNGTH
*
ASG24  BGE  WA,=MNLEN,ASG15  IF ACCEPTABLE VALUE
       ERB  287,Value assigned to keyword MAXLNGTH is too small
*
*      HERE FOR KEYWORD FULLSCAN
*
ASG26  BNZ  WA,ASG15         IF ACCEPTABLE VALUE
       ERB  274,Value assigned to keyword FULLSCAN is zero
*
       ENP                   END PROCEDURE ASIGN
       EJC
*
*      ASINP -- ASSIGN DURING PATTERN MATCH
*
*      ASINP IS LIKE ASIGN AND HAS A SIMILAR CALLING SEQUENCE
*      AND EFFECT. THE DIFFERENCE IS THAT THE GLOBAL PATTERN
*      VARIABLES ARE SAVED AND RESTORED IF REQUIRED.
*
*      (XL)                  BASE POINTER FOR VARIABLE
*      (WA)                  OFFSET FOR VARIABLE
*      (WB)                  VALUE TO BE ASSIGNED
*      JSR  ASINP            CALL TO ASSIGN VALUE TO VARIABLE
*      PPM  LOC              TRANSFER LOC IF FAILURE
*      (XR,XL)               DESTROYED
*      (WA,WB,WC,RA)         DESTROYED
*
ASINP  PRC  R,1              ENTRY POINT, RECURSIVE
       ADD  WA,XL            POINT TO VARIABLE
       MOV  (XL),XR          LOAD CURRENT CONTENTS
       BEQ  (XR),=B_TRT,ASNP1 JUMP IF TRAPPED
       MOV  WB,(XL)          ELSE PERFORM ASSIGNMENT
       ZER  XL               CLEAR GARBAGE VALUE IN XL
       EXI                   RETURN TO ASINP CALLER
*
*      HERE IF VARIABLE IS TRAPPED
*
ASNP1  SUB  WA,XL            RESTORE BASE POINTER
       MOV  PMSSL,-(XS)      STACK SUBJECT STRING LENGTH
       MOV  PMHBS,-(XS)      STACK HISTORY STACK BASE PTR
       MOV  R_PMS,-(XS)      STACK SUBJECT STRING POINTER
       MOV  PMDFL,-(XS)      STACK DOT FLAG
       JSR  ASIGN            CALL FULL-BLOWN ASSIGNMENT ROUTINE
       PPM  ASNP2            JUMP IF FAILURE
       MOV  (XS)+,PMDFL      RESTORE DOT FLAG
       MOV  (XS)+,R_PMS      RESTORE SUBJECT STRING POINTER
       MOV  (XS)+,PMHBS      RESTORE HISTORY STACK BASE POINTER
       MOV  (XS)+,PMSSL      RESTORE SUBJECT STRING LENGTH
       EXI                   RETURN TO ASINP CALLER
*
*      HERE IF FAILURE IN ASIGN CALL
*
ASNP2  MOV  (XS)+,PMDFL      RESTORE DOT FLAG
       MOV  (XS)+,R_PMS      RESTORE SUBJECT STRING POINTER
       MOV  (XS)+,PMHBS      RESTORE HISTORY STACK BASE POINTER
       MOV  (XS)+,PMSSL      RESTORE SUBJECT STRING LENGTH
       EXI  1                TAKE FAILURE EXIT
       ENP                   END PROCEDURE ASINP
       EJC
*
*      BLKLN -- DETERMINE LENGTH OF BLOCK
*
*      BLKLN DETERMINES THE LENGTH OF A BLOCK IN DYNAMIC STORE.
*
*      (WA)                  FIRST WORD OF BLOCK
*      (XR)                  POINTER TO BLOCK
*      JSR  BLKLN            CALL TO GET BLOCK LENGTH
*      (WA)                  LENGTH OF BLOCK IN BYTES
*      (XL)                  DESTROYED
*
*      BLKLN IS USED BY THE GARBAGE COLLECTOR AND IS NOT
*      PERMITTED TO CALL GBCOL DIRECTLY OR INDIRECTLY.
*
*      THE FIRST WORD STORED IN THE BLOCK (I.E. AT XR) MAY
*      BE ANYTHING, BUT THE CONTENTS OF WA MUST BE CORRECT.
*
BLKLN  PRC  E,0              ENTRY POINT
       MOV  WA,XL            COPY FIRST WORD
       LEI  XL               GET ENTRY ID (BL_XX)
       BSW  XL,BL___,BLN00   SWITCH ON BLOCK TYPE
       IFF  BL_AR,BLN01      ARBLK
.IF    .CNBF
.ELSE
       IFF  BL_BC,BLN04      BCBLK
       IFF  BL_BF,BLN11      BFBLK
.FI
.IF    .CSLN
       IFF  BL_CD,BLN12      CDBLK
.ELSE
       IFF  BL_CD,BLN01      CDBLK
.FI
       IFF  BL_DF,BLN01      DFBLK
       IFF  BL_EF,BLN01      EFBLK
.IF    .CSLN
       IFF  BL_EX,BLN12      EXBLK
.ELSE
       IFF  BL_EX,BLN01      EXBLK
.FI
       IFF  BL_PF,BLN01      PFBLK
       IFF  BL_TB,BLN01      TBBLK
       IFF  BL_VC,BLN01      VCBLK
       IFF  BL_EV,BLN03      EVBLK
       IFF  BL_KV,BLN03      KVBLK
       IFF  BL_P0,BLN02      P0BLK
       IFF  BL_SE,BLN02      SEBLK
       IFF  BL_NM,BLN03      NMBLK
       IFF  BL_P1,BLN03      P1BLK
       IFF  BL_P2,BLN04      P2BLK
       IFF  BL_TE,BLN04      TEBLK
       IFF  BL_FF,BLN05      FFBLK
       IFF  BL_TR,BLN05      TRBLK
       IFF  BL_CT,BLN06      CTBLK
       IFF  BL_IC,BLN07      ICBLK
       IFF  BL_PD,BLN08      PDBLK
.IF    .CNRA
.ELSE
       IFF  BL_RC,BLN09      RCBLK
.FI
       IFF  BL_SC,BLN10      SCBLK
       ESW                   END OF JUMP TABLE ON BLOCK TYPE
       EJC
*
*      BLKLN (CONTINUED)
*
*      HERE FOR BLOCKS WITH LENGTH IN SECOND WORD
*
BLN00  MOV  NUM01(XR),WA     LOAD LENGTH
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR LENGTH IN THIRD WORD (AR,CD,DF,EF,EX,PF,TB,VC)
*
BLN01  MOV  NUM02(XR),WA     LOAD LENGTH FROM THIRD WORD
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR TWO WORD BLOCKS (P0,SE)
*
BLN02  MOV  *NUM02,WA        LOAD LENGTH (TWO WORDS)
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR THREE WORD BLOCKS (NM,P1,EV,KV)
*
BLN03  MOV  *NUM03,WA        LOAD LENGTH (THREE WORDS)
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR FOUR WORD BLOCKS (P2,TE,BC)
*
BLN04  MOV  *NUM04,WA        LOAD LENGTH (FOUR WORDS)
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR FIVE WORD BLOCKS (FF,TR)
*
BLN05  MOV  *NUM05,WA        LOAD LENGTH
       EXI                   RETURN TO BLKLN CALLER
       EJC
*
*      BLKLN (CONTINUED)
*
*      HERE FOR CTBLK
*
BLN06  MOV  *CTSI_,WA        SET SIZE OF CTBLK
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR ICBLK
*
BLN07  MOV  *ICSI_,WA        SET SIZE OF ICBLK
       EXI                   RETURN TO BLKLN CALLER
*
*      HERE FOR PDBLK
*
BLN08  MOV  PDDFP(XR),XL     POINT TO DFBLK
       MOV  DFPDL(XL),WA     LOAD PDBLK LENGTH FROM DFBLK
       EXI                   RETURN TO BLKLN CALLER
.IF    .CNRA
.ELSE
*
*      HERE FOR RCBLK
*
BLN09  MOV  *RCSI_,WA        SET SIZE OF RCBLK
       EXI                   RETURN TO BLKLN CALLER
.FI
*
*      HERE FOR SCBLK
*
BLN10  MOV  SCLEN(XR),WA     LOAD LENGTH IN CHARACTERS
       CTB  WA,SCSI_         CALCULATE LENGTH IN BYTES
       EXI                   RETURN TO BLKLN CALLER
.IF    .CNBF
.ELSE
*
*      HERE FOR BFBLK
*
BLN11  MOV  BFALC(XR),WA     GET ALLOCATION IN BYTES
       CTB  WA,BFSI_         CALCULATE LENGTH IN BYTES
       EXI                   RETURN TO BLKLN CALLER
.FI
.IF    .CSLN
*
*      HERE FOR LENGTH IN FOURTH WORD (CD,EX)
*
BLN12  MOV  NUM03(XR),WA     LOAD LENGTH FROM CDLEN/EXLEN
       EXI                   RETURN TO BLKLN CALLER
.FI
       ENP                   END PROCEDURE BLKLN
       EJC
*
*      COPYB -- COPY A BLOCK
*
*      (XS)                  BLOCK TO BE COPIED
*      JSR  COPYB            CALL TO COPY BLOCK
*      PPM  LOC              RETURN IF BLOCK HAS NO IDVAL FIELD
*                            NORMAL RETURN IF IDVAL FIELD
*      (XR)                  COPY OF BLOCK
*      (XS)                  POPPED
*      (XL,WA,WB,WC)         DESTROYED
*
COPYB  PRC  N,1              ENTRY POINT
       MOV  (XS),XR          LOAD ARGUMENT
       BEQ  XR,=NULLS,COP10  RETURN ARGUMENT IF IT IS NULL
       MOV  (XR),WA          ELSE LOAD TYPE WORD
       MOV  WA,WB            COPY TYPE WORD
       JSR  BLKLN            GET LENGTH OF ARGUMENT BLOCK
       MOV  XR,XL            COPY POINTER
       JSR  ALLOC            ALLOCATE BLOCK OF SAME SIZE
       MOV  XR,(XS)          STORE POINTER TO COPY
       MVW                   COPY CONTENTS OF OLD BLOCK TO NEW
       ZER  XL               CLEAR GARBAGE XL
       MOV  (XS),XR          RELOAD POINTER TO START OF COPY
       BEQ  WB,=B_TBT,COP05  JUMP IF TABLE
       BEQ  WB,=B_VCT,COP01  JUMP IF VECTOR
       BEQ  WB,=B_PDT,COP01  JUMP IF PROGRAM DEFINED
.IF    .CNBF
.ELSE
       BEQ  WB,=B_BCT,COP11  JUMP IF BUFFER
.FI
       BNE  WB,=B_ART,COP10  RETURN COPY IF NOT ARRAY
*
*      HERE FOR ARRAY (ARBLK)
*
       ADD  AROFS(XR),XR     POINT TO PROTOTYPE FIELD
       BRN  COP02            JUMP TO MERGE
*
*      HERE FOR VECTOR, PROGRAM DEFINED
*
COP01  ADD  *PDFLD,XR        POINT TO PDFLD = VCVLS
*
*      MERGE HERE FOR ARBLK, VCBLK, PDBLK TO DELETE TRAP
*      BLOCKS FROM ALL VALUE FIELDS (THE COPY IS UNTRAPPED)
*
COP02  MOV  (XR),XL          LOAD NEXT POINTER
*
*      LOOP TO GET VALUE AT END OF TRBLK CHAIN
*
COP03  BNE  (XL),=B_TRT,COP04 JUMP IF NOT TRAPPED
       MOV  TRVAL(XL),XL     ELSE POINT TO NEXT VALUE
       BRN  COP03            AND LOOP BACK
       EJC
*
*      COPYB (CONTINUED)
*
*      HERE WITH UNTRAPPED VALUE IN XL
*
COP04  MOV  XL,(XR)+         STORE REAL VALUE, BUMP POINTER
       BNE  XR,DNAMP,COP02   LOOP BACK IF MORE TO GO
       BRN  COP09            ELSE JUMP TO EXIT
*
*      HERE TO COPY A TABLE
*
COP05  ZER  IDVAL(XR)        ZERO ID TO STOP DUMP BLOWING UP
       MOV  *TESI_,WA        SET SIZE OF TEBLK
       MOV  *TBBUK,WC        SET INITIAL OFFSET
*
*      LOOP THROUGH BUCKETS IN TABLE
*
COP06  MOV  (XS),XR          LOAD TABLE POINTER
       BEQ  WC,TBLEN(XR),COP09 JUMP TO EXIT IF ALL DONE
       MOV  WC,WB            ELSE COPY OFFSET
       SUB  *TENXT,WB        SUBTRACT LINK OFFSET TO MERGE
       ADD  WB,XR            NEXT BUCKET HEADER LESS LINK OFFSET
       ICA  WC               BUMP OFFSET
*
*      LOOP THROUGH TEBLKS ON ONE CHAIN
*
COP07  MOV  TENXT(XR),XL     LOAD POINTER TO NEXT TEBLK
       MOV  (XS),TENXT(XR)   SET END OF CHAIN POINTER IN CASE
       BEQ  (XL),=B_TBT,COP06 BACK FOR NEXT BUCKET IF CHAIN END
       SUB  WB,XR            POINT TO HEAD OF PREVIOUS BLOCK
       MOV  XR,-(XS)         STACK PTR TO PREVIOUS BLOCK
       MOV  *TESI_,WA        SET SIZE OF TEBLK
       JSR  ALLOC            ALLOCATE NEW TEBLK
       MOV  XR,-(XS)         STACK PTR TO NEW TEBLK
       MVW                   COPY OLD TEBLK TO NEW TEBLK
       MOV  (XS)+,XR         RESTORE POINTER TO NEW TEBLK
       MOV  (XS)+,XL         RESTORE POINTER TO PREVIOUS BLOCK
       ADD  WB,XL            ADD OFFSET BACK IN
       MOV  XR,TENXT(XL)     LINK NEW BLOCK TO PREVIOUS
       MOV  XR,XL            COPY POINTER TO NEW BLOCK
*
*      LOOP TO SET REAL VALUE AFTER REMOVING TRAP CHAIN
*
COP08  MOV  TEVAL(XL),XL     LOAD VALUE
       BEQ  (XL),=B_TRT,COP08 LOOP BACK IF TRAPPED
       MOV  XL,TEVAL(XR)     STORE UNTRAPPED VALUE IN TEBLK
       ZER  WB               ZERO OFFSET WITHIN TEBLK
       BRN  COP07            BACK FOR NEXT TEBLK
*
*      COMMON EXIT POINT
*
COP09  MOV  (XS)+,XR         LOAD POINTER TO BLOCK
       EXI                   RETURN
*
*      ALTERNATIVE RETURN
*
COP10  EXI  1                RETURN
       EJC
.IF    .CNBF
.ELSE
*
*      HERE TO COPY BUFFER
*
COP11  MOV  BCBUF(XR),XL     GET BFBLK PTR
       MOV  BFALC(XL),WA     GET ALLOCATION
       CTB  WA,BFSI_         SET TOTAL SIZE
       MOV  XR,XL            SAVE BCBLK PTR
       JSR  ALLOC            ALLOCATE BFBLK
       MOV  BCBUF(XL),WB     GET OLD BFBLK
       MOV  XR,BCBUF(XL)     SET POINTER TO NEW BFBLK
       MOV  WB,XL            POINT TO OLD BFBLK
       MVW                   COPY BFBLK TOO
       ZER  XL               CLEAR RUBBISH PTR
       BRN  COP09            BRANCH TO EXIT
.FI
       ENP                   END PROCEDURE COPYB
*
*      CDGCG -- GENERATE CODE FOR COMPLEX GOTO
*
*      USED BY CMPIL TO PROCESS COMPLEX GOTO TREE
*
*      (WB)                  MUST BE COLLECTABLE
*      (XR)                  EXPRESSION POINTER
*      JSR  CDGCG            CALL TO GENERATE COMPLEX GOTO
*      (XL,XR,WA)            DESTROYED
*
CDGCG  PRC  E,0              ENTRY POINT
       MOV  CMOPN(XR),XL     GET UNARY GOTO OPERATOR
       MOV  CMROP(XR),XR     POINT TO GOTO OPERAND
       BEQ  XL,=OPDVD,CDGC2  JUMP IF DIRECT GOTO
       JSR  CDGNM            GENERATE OPND BY NAME IF NOT DIRECT
*
*      RETURN POINT
*
CDGC1  MOV  XL,WA            GOTO OPERATOR
       JSR  CDWRD            GENERATE IT
       EXI                   RETURN TO CALLER
*
*      DIRECT GOTO
*
CDGC2  JSR  CDGVL            GENERATE OPERAND BY VALUE
       BRN  CDGC1            MERGE TO RETURN
       ENP                   END PROCEDURE CDGCG
       EJC
*
*      CDGEX -- BUILD EXPRESSION BLOCK
*
*      CDGEX IS PASSED A POINTER TO AN EXPRESSION TREE (SEE
*      EXPAN) AND RETURNS AN EXPRESSION (SEBLK OR EXBLK).
*
.IF    .CEVB
*      (WA)                  0 IF BY VALUE, 1 IF BY NAME
.FI
*      (WC)                  SOME COLLECTABLE VALUE
*      (WB)                  INTEGER IN RANGE 0 LE X LE MXLEN
*      (XL)                  PTR TO EXPRESSION TREE
*      JSR  CDGEX            CALL TO BUILD EXPRESSION
*      (XR)                  PTR TO SEBLK OR EXBLK
*      (XL,WA,WB)            DESTROYED
*
CDGEX  PRC  R,0              ENTRY POINT, RECURSIVE
       BLO  (XL),=B_VR_,CDGX1 JUMP IF NOT VARIABLE
*
*      HERE FOR NATURAL VARIABLE, BUILD SEBLK
*
       MOV  *SESI_,WA        SET SIZE OF SEBLK
       JSR  ALLOC            ALLOCATE SPACE FOR SEBLK
       MOV  =B_SEL,(XR)      SET TYPE WORD
       MOV  XL,SEVAR(XR)     STORE VRBLK POINTER
       EXI                   RETURN TO CDGEX CALLER
*
*      HERE IF NOT VARIABLE, BUILD EXBLK
*
CDGX1  MOV  XL,XR            COPY TREE POINTER
       MOV  WC,-(XS)         SAVE WC
       MOV  CWCOF,XL         SAVE CURRENT OFFSET
.IF    .CEVB
       BZE  WA,CDGX2         JUMP IF BY VALUE
.FI
       MOV  (XR),WA          GET TYPE WORD
       BNE  WA,=B_CMT,CDGX2  CALL BY VALUE IF NOT CMBLK
       BGE  CMTYP(XR),=C__NM,CDGX2 JUMP IF CMBLK ONLY BY VALUE
       EJC
*
*      CDGEX (CONTINUED)
*
*      HERE IF EXPRESSION CAN BE EVALUATED BY NAME
*
       JSR  CDGNM            GENERATE CODE BY NAME
       MOV  =ORNM_,WA        LOAD RETURN BY NAME WORD
       BRN  CDGX3            MERGE WITH VALUE CASE
*
*      HERE IF EXPRESSION CAN ONLY BE EVALUATED BY VALUE
*
CDGX2  JSR  CDGVL            GENERATE CODE BY VALUE
       MOV  =ORVL_,WA        LOAD RETURN BY VALUE WORD
*
*      MERGE HERE TO CONSTRUCT EXBLK
*
CDGX3  JSR  CDWRD            GENERATE RETURN WORD
       JSR  EXBLD            BUILD EXBLK
       MOV  (XS)+,WC         RESTORE WC
       EXI                   RETURN TO CDGEX CALLER
       ENP                   END PROCEDURE CDGEX
       EJC
*
*      CDGNM -- GENERATE CODE BY NAME
*
*      CDGNM IS CALLED DURING THE COMPILATION PROCESS TO
*      GENERATE CODE BY NAME FOR AN EXPRESSION. SEE CDBLK
*      DESCRIPTION FOR DETAILS OF CODE GENERATED. THE INPUT
*      TO CDGNM IS AN EXPRESSION TREE AS GENERATED BY EXPAN.
*
*      CDGNM IS A RECURSIVE PROCEDURE WHICH PROCEEDS BY MAKING
*      RECURSIVE CALLS TO GENERATE CODE FOR OPERANDS.
*
*      (WB)                  INTEGER IN RANGE 0 LE N LE DNAMB
*      (XR)                  PTR TO TREE GENERATED BY EXPAN
*      (WC)                  CONSTANT FLAG (SEE BELOW)
*      JSR  CDGNM            CALL TO GENERATE CODE BY NAME
*      (XR,WA)               DESTROYED
*      (WC)                  SET NON-ZERO IF NON-CONSTANT
*
*      WC IS SET TO A NON-ZERO (COLLECTABLE) VALUE IF THE
*      EXPRESSION FOR WHICH CODE IS GENERATED CANNOT BE
*      EVALUATED AT COMPILE TIME, OTHERWISE WC IS UNCHANGED.
*
*      THE CODE IS GENERATED IN THE CURRENT CCBLK (SEE CDWRD).
*
CDGNM  PRC  R,0              ENTRY POINT, RECURSIVE
       MOV  XL,-(XS)         SAVE ENTRY XL
       MOV  WB,-(XS)         SAVE ENTRY WB
       CHK                   CHECK FOR STACK OVERFLOW
       MOV  (XR),WA          LOAD TYPE WORD
       BEQ  WA,=B_CMT,CGN04  JUMP IF CMBLK
       BHI  WA,=B_VR_,CGN02  JUMP IF SIMPLE VARIABLE
*
*      MERGE HERE FOR OPERAND YIELDING VALUE (E.G. CONSTANT)
*
CGN01  ERB  212,Syntax error: Value used where name is required
*
*      HERE FOR NATURAL VARIABLE REFERENCE
*
CGN02  MOV  =OLVN_,WA        LOAD VARIABLE LOAD CALL
       JSR  CDWRD            GENERATE IT
       MOV  XR,WA            COPY VRBLK POINTER
       JSR  CDWRD            GENERATE VRBLK POINTER
       EJC
*
*      CDGNM (CONTINUED)
*
*      HERE TO EXIT WITH WC SET CORRECTLY
*
CGN03  MOV  (XS)+,WB         RESTORE ENTRY WB
       MOV  (XS)+,XL         RESTORE ENTRY XL
       EXI                   RETURN TO CDGNM CALLER
*
*      HERE FOR CMBLK
*
CGN04  MOV  XR,XL            COPY CMBLK POINTER
       MOV  CMTYP(XR),XR     LOAD CMBLK TYPE
       BGE  XR,=C__NM,CGN01  ERROR IF NOT NAME OPERAND
       BSW  XR,C__NM         ELSE SWITCH ON TYPE
       IFF  C_ARR,CGN05      ARRAY REFERENCE
       IFF  C_FNC,CGN08      FUNCTION CALL
       IFF  C_DEF,CGN09      DEFERRED EXPRESSION
       IFF  C_IND,CGN10      INDIRECT REFERENCE
       IFF  C_KEY,CGN11      KEYWORD REFERENCE
       IFF  C_UBO,CGN08      UNDEFINED BINARY OP
       IFF  C_UUO,CGN08      UNDEFINED UNARY OP
       ESW                   END SWITCH ON CMBLK TYPE
*
*      HERE TO GENERATE CODE FOR ARRAY REFERENCE
*
CGN05  MOV  *CMOPN,WB        POINT TO ARRAY OPERAND
*
*      LOOP TO GENERATE CODE FOR ARRAY OPERAND AND SUBSCRIPTS
*
CGN06  JSR  CMGEN            GENERATE CODE FOR NEXT OPERAND
       MOV  CMLEN(XL),WC     LOAD LENGTH OF CMBLK
       BLT  WB,WC,CGN06      LOOP TILL ALL GENERATED
*
*      GENERATE APPROPRIATE ARRAY CALL
*
       MOV  =OAON_,WA        LOAD ONE-SUBSCRIPT CASE CALL
       BEQ  WC,*CMAR1,CGN07  JUMP TO EXIT IF ONE SUBSCRIPT CASE
       MOV  =OAMN_,WA        ELSE LOAD MULTI-SUBSCRIPT CASE CALL
       JSR  CDWRD            GENERATE CALL
       MOV  WC,WA            COPY CMBLK LENGTH
       BTW  WA               CONVERT TO WORDS
       SUB  =CMVLS,WA        CALCULATE NUMBER OF SUBSCRIPTS
       EJC
*
*      CDGNM (CONTINUED)
*
*      HERE TO EXIT GENERATING WORD (NON-CONSTANT)
*
CGN07  MNZ  WC               SET RESULT NON-CONSTANT
       JSR  CDWRD            GENERATE WORD
       BRN  CGN03            BACK TO EXIT
*
*      HERE TO GENERATE CODE FOR FUNCTIONS AND UNDEFINED OPRS
*
CGN08  MOV  XL,XR            COPY CMBLK POINTER
       JSR  CDGVL            GEN CODE BY VALUE FOR CALL
       MOV  =OFNE_,WA        GET EXTRA CALL FOR BY NAME
       BRN  CGN07            BACK TO GENERATE AND EXIT
*
*      HERE TO GENERATE CODE FOR DEFERED EXPRESSION
*
CGN09  MOV  CMROP(XL),XR     CHECK IF VARIABLE
       BHI  (XR),=B_VR_,CGN02 TREAT *VARIABLE AS SIMPLE VAR
       MOV  XR,XL            COPY PTR TO EXPRESSION TREE
.IF    .CEVB
       MOV  =NUM01,WA        RETURN NAME
.FI
       JSR  CDGEX            ELSE BUILD EXBLK
       MOV  =OLEX_,WA        SET CALL TO LOAD EXPR BY NAME
       JSR  CDWRD            GENERATE IT
       MOV  XR,WA            COPY EXBLK POINTER
       JSR  CDWRD            GENERATE EXBLK POINTER
       BRN  CGN03            BACK TO EXIT
*
*      HERE TO GENERATE CODE FOR INDIRECT REFERENCE
*
CGN10  MOV  CMROP(XL),XR     GET OPERAND
       JSR  CDGVL            GENERATE CODE BY VALUE FOR IT
       MOV  =OINN_,WA        LOAD CALL FOR INDIRECT BY NAME
       BRN  CGN12            MERGE
*
*      HERE TO GENERATE CODE FOR KEYWORD REFERENCE
*
CGN11  MOV  CMROP(XL),XR     GET OPERAND
       JSR  CDGNM            GENERATE CODE BY NAME FOR IT
       MOV  =OKWN_,WA        LOAD CALL FOR KEYWORD BY NAME
*
*      KEYWORD, INDIRECT MERGE HERE
*
CGN12  JSR  CDWRD            GENERATE CODE FOR OPERATOR
       BRN  CGN03            EXIT
       ENP                   END PROCEDURE CDGNM
       EJC
*
*      CDGVL -- GENERATE CODE BY VALUE
*
*      CDGVL IS CALLED DURING THE COMPILATION PROCESS TO
*      GENERATE CODE BY VALUE FOR AN EXPRESSION. SEE CDBLK
*      DESCRIPTION FOR DETAILS OF THE CODE GENERATED. THE INPUT
*      TO CDGVL IS AN EXPRESSION TREE AS GENERATED BY EXPAN.
*
*      CDGVL IS A RECURSIVE PROCEDURE WHICH PROCEEDS BY MAKING
*      RECURSIVE CALLS TO GENERATE CODE FOR OPERANDS.
*
*      (WB)                  INTEGER IN RANGE 0 LE N LE DNAMB
*      (XR)                  PTR TO TREE GENERATED BY EXPAN
*      (WC)                  CONSTANT FLAG (SEE BELOW)
*      JSR  CDGVL            CALL TO GENERATE CODE BY VALUE
*      (XR,WA)               DESTROYED
*      (WC)                  SET NON-ZERO IF NON-CONSTANT
*
*      WC IS SET TO A NON-ZERO (COLLECTABLE) VALUE IF THE
*      EXPRESSION FOR WHICH CODE IS GENERATED CANNOT BE
*      EVALUATED AT COMPILE TIME, OTHERWISE WC IS UNCHANGED.
*
*      IF WC IS NON-ZERO ON ENTRY, THEN PREEVALUATION IS NOT
*      ALLOWED REGARDLESS OF THE NATURE OF THE OPERAND.
*
*      THE CODE IS GENERATED IN THE CURRENT CCBLK (SEE CDWRD).
*
CDGVL  PRC  R,0              ENTRY POINT, RECURSIVE
       MOV  (XR),WA          LOAD TYPE WORD
       BEQ  WA,=B_CMT,CGV01  JUMP IF CMBLK
       BLT  WA,=B_VRA,CGV00  JUMP IF ICBLK, RCBLK, SCBLK
       BNZ  VRLEN(XR),CGVL0  JUMP IF NOT SYSTEM VARIABLE
       MOV  XR,-(XS)         STACK XR
       MOV  VRSVP(XR),XR     POINT TO SVBLK
       MOV  SVBIT(XR),WA     GET SVBLK PROPERTY BITS
       MOV  (XS)+,XR         RECOVER XR
       ANB  BTKWV,WA         CHECK IF CONSTANT KEYWORD VALUE
       BEQ  WA,BTKWV,CGV00   JUMP IF CONSTANT KEYWORD VALUE
*
*      HERE FOR VARIABLE VALUE REFERENCE
*
CGVL0  MNZ  WC               INDICATE NON-CONSTANT VALUE
*
*      MERGE HERE FOR SIMPLE CONSTANT (ICBLK,RCBLK,SCBLK)
*      AND FOR VARIABLES CORRESPONDING TO CONSTANT KEYWORDS.
*
CGV00  MOV  XR,WA            COPY PTR TO VAR OR CONSTANT
       JSR  CDWRD            GENERATE AS CODE WORD
       EXI                   RETURN TO CALLER
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE FOR TREE NODE (CMBLK)
*
CGV01  MOV  WB,-(XS)         SAVE ENTRY WB
       MOV  XL,-(XS)         SAVE ENTRY XL
       MOV  WC,-(XS)         SAVE ENTRY CONSTANT FLAG
       MOV  CWCOF,-(XS)      SAVE INITIAL CODE OFFSET
       CHK                   CHECK FOR STACK OVERFLOW
*
*      PREPARE TO GENERATE CODE FOR CMBLK. WC IS SET TO THE
*      VALUE OF CSWNO (ZERO IF -OPTIMISE, 1 IF -NOOPT) TO
*      START WITH AND IS RESET NON-ZERO FOR ANY NON-CONSTANT
*      CODE GENERATED. IF IT IS STILL ZERO AFTER GENERATING ALL
*      THE CMBLK CODE, THEN ITS VALUE IS COMPUTED AS THE RESULT.
*
       MOV  XR,XL            COPY CMBLK POINTER
       MOV  CMTYP(XR),XR     LOAD CMBLK TYPE
       MOV  CSWNO,WC         RESET CONSTANT FLAG
       BLE  XR,=C_PR_,CGV02  JUMP IF NOT PREDICATE VALUE
       MNZ  WC               ELSE FORCE NON-CONSTANT CASE
*
*      HERE WITH WC SET APPROPRIATELY
*
CGV02  BSW  XR,C__NV         SWITCH TO APPROPRIATE GENERATOR
       IFF  C_ARR,CGV03      ARRAY REFERENCE
       IFF  C_FNC,CGV05      FUNCTION CALL
       IFF  C_DEF,CGV14      DEFERRED EXPRESSION
       IFF  C_SEL,CGV15      SELECTION
       IFF  C_IND,CGV31      INDIRECT REFERENCE
       IFF  C_KEY,CGV27      KEYWORD REFERENCE
       IFF  C_UBO,CGV29      UNDEFINED BINOP
       IFF  C_UUO,CGV30      UNDEFINED UNOP
       IFF  C_BVL,CGV18      BINOPS WITH VAL OPDS
       IFF  C_ALT,CGV18      ALTERNATION
       IFF  C_UVL,CGV19      UNOPS WITH VALU OPND
       IFF  C_ASS,CGV21      ASSIGNMENT
       IFF  C_CNC,CGV24      CONCATENATION
       IFF  C_CNP,CGV24      CONCATENATION (NOT PATTERN MATCH)
       IFF  C_UNM,CGV27      UNOPS WITH NAME OPND
       IFF  C_BVN,CGV26      BINARY _ AND .
       IFF  C_INT,CGV31      INTERROGATION
       IFF  C_NEG,CGV28      NEGATION
       IFF  C_PMT,CGV18      PATTERN MATCH
       ESW                   END SWITCH ON CMBLK TYPE
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE TO GENERATE CODE FOR ARRAY REFERENCE
*
CGV03  MOV  *CMOPN,WB        SET OFFSET TO ARRAY OPERAND
*
*      LOOP TO GENERATE CODE FOR ARRAY OPERAND AND SUBSCRIPTS
*
CGV04  JSR  CMGEN            GEN VALUE CODE FOR NEXT OPERAND
       MOV  CMLEN(XL),WC     LOAD CMBLK LENGTH
       BLT  WB,WC,CGV04      LOOP BACK IF MORE TO GO
*
*      GENERATE CALL TO APPROPRIATE ARRAY REFERENCE ROUTINE
*
       MOV  =OAOV_,WA        SET ONE SUBSCRIPT CALL IN CASE
       BEQ  WC,*CMAR1,CGV32  JUMP TO EXIT IF 1-SUB CASE
       MOV  =OAMV_,WA        ELSE SET CALL FOR MULTI-SUBSCRIPTS
       JSR  CDWRD            GENERATE CALL
       MOV  WC,WA            COPY LENGTH OF CMBLK
       SUB  *CMVLS,WA        SUBTRACT STANDARD LENGTH
       BTW  WA               GET NUMBER OF WORDS
       BRN  CGV32            JUMP TO GENERATE SUBSCRIPT COUNT
*
*      HERE TO GENERATE CODE FOR FUNCTION CALL
*
CGV05  MOV  *CMVLS,WB        SET OFFSET TO FIRST ARGUMENT
*
*      LOOP TO GENERATE CODE FOR ARGUMENTS
*
CGV06  BEQ  WB,CMLEN(XL),CGV07 JUMP IF ALL GENERATED
       JSR  CMGEN            ELSE GEN VALUE CODE FOR NEXT ARG
       BRN  CGV06            BACK TO GENERATE NEXT ARGUMENT
*
*      HERE TO GENERATE ACTUAL FUNCTION CALL
*
CGV07  SUB  *CMVLS,WB        GET NUMBER OF ARG PTRS (BYTES)
       BTW  WB               CONVERT BYTES TO WORDS
       MOV  CMOPN(XL),XR     LOAD FUNCTION VRBLK POINTER
       BNZ  VRLEN(XR),CGV12  JUMP IF NOT SYSTEM FUNCTION
       MOV  VRSVP(XR),XL     LOAD SVBLK PTR IF SYSTEM VAR
       MOV  SVBIT(XL),WA     LOAD BIT MASK
       ANB  BTFFC,WA         TEST FOR FAST FUNCTION CALL ALLOWED
       ZRB  WA,CGV12         JUMP IF NOT
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE IF FAST FUNCTION CALL IS ALLOWED
*
       MOV  SVBIT(XL),WA     RELOAD BIT INDICATORS
       ANB  BTPRE,WA         TEST FOR PREEVALUATION OK
       NZB  WA,CGV08         JUMP IF PREEVALUATION PERMITTED
       MNZ  WC               ELSE SET RESULT NON-CONSTANT
*
*      TEST FOR CORRECT NUMBER OF ARGS FOR FAST CALL
*
CGV08  MOV  VRFNC(XR),XL     LOAD PTR TO SVFNC FIELD
       MOV  FARGS(XL),WA     LOAD SVNAR FIELD VALUE
       BEQ  WA,WB,CGV11      JUMP IF ARGUMENT COUNT IS CORRECT
       BHI  WA,WB,CGV09      JUMP IF TOO FEW ARGUMENTS GIVEN
*
*      HERE IF TOO MANY ARGUMENTS, PREPARE TO GENERATE O_POPS
*
       SUB  WA,WB            GET NUMBER OF EXTRA ARGS
       LCT  WB,WB            SET AS COUNT TO CONTROL LOOP
       MOV  =OPOP_,WA        SET POP CALL
       BRN  CGV10            JUMP TO COMMON LOOP
*
*      HERE IF TOO FEW ARGUMENTS, PREPARE TO GENERATE NULLS
*
CGV09  SUB  WB,WA            GET NUMBER OF MISSING ARGUMENTS
       LCT  WB,WA            LOAD AS COUNT TO CONTROL LOOP
       MOV  =NULLS,WA        LOAD PTR TO NULL CONSTANT
*
*      LOOP TO GENERATE CALLS TO FIX ARGUMENT COUNT
*
CGV10  JSR  CDWRD            GENERATE ONE CALL
       BCT  WB,CGV10         LOOP TILL ALL GENERATED
*
*      HERE AFTER ADJUSTING ARG COUNT AS REQUIRED
*
CGV11  MOV  XL,WA            COPY POINTER TO SVFNC FIELD
       BRN  CGV36            JUMP TO GENERATE CALL
       EJC
*
*      CDGVL (CONTINUED)
*
*      COME HERE IF FAST CALL IS NOT PERMITTED
*
CGV12  MOV  =OFNS_,WA        SET ONE ARG CALL IN CASE
       BEQ  WB,=NUM01,CGV13  JUMP IF ONE ARG CASE
       MOV  =OFNC_,WA        ELSE LOAD CALL FOR MORE THAN 1 ARG
       JSR  CDWRD            GENERATE IT
       MOV  WB,WA            COPY ARGUMENT COUNT
*
*      ONE ARG CASE MERGES HERE
*
CGV13  JSR  CDWRD            GENERATE =O_FNS OR ARG COUNT
       MOV  XR,WA            COPY VRBLK POINTER
       BRN  CGV32            JUMP TO GENERATE VRBLK PTR
*
*      HERE FOR DEFERRED EXPRESSION
*
CGV14  MOV  CMROP(XL),XL     POINT TO EXPRESSION TREE
.IF    .CEVB
       ZER  WA               RETURN VALUE
.FI
       JSR  CDGEX            BUILD EXBLK OR SEBLK
       MOV  XR,WA            COPY BLOCK PTR
       JSR  CDWRD            GENERATE PTR TO EXBLK OR SEBLK
       BRN  CGV34            JUMP TO EXIT, CONSTANT TEST
*
*      HERE TO GENERATE CODE FOR SELECTION
*
CGV15  ZER  -(XS)            ZERO PTR TO CHAIN OF FORWARD JUMPS
       ZER  -(XS)            ZERO PTR TO PREV O_SLC FORWARD PTR
       MOV  *CMVLS,WB        POINT TO FIRST ALTERNATIVE
       MOV  =OSLA_,WA        SET INITIAL CODE WORD
*
*      0(XS)                 IS THE OFFSET TO THE PREVIOUS WORD
*                            WHICH REQUIRES FILLING IN WITH AN
*                            OFFSET TO THE FOLLOWING O_SLC,O_SLD
*
*      1(XS)                 IS THE HEAD OF A CHAIN OF OFFSET
*                            POINTERS INDICATING THOSE LOCATIONS
*                            TO BE FILLED WITH OFFSETS PAST
*                            THE END OF ALL THE ALTERNATIVES
*
CGV16  JSR  CDWRD            GENERATE O_SLC (O_SLA FIRST TIME)
       MOV  CWCOF,(XS)       SET CURRENT LOC AS PTR TO FILL IN
       JSR  CDWRD            GENERATE GARBAGE WORD THERE FOR NOW
       JSR  CMGEN            GEN VALUE CODE FOR ALTERNATIVE
       MOV  =OSLB_,WA        LOAD O_SLB POINTER
       JSR  CDWRD            GENERATE O_SLB CALL
       MOV  NUM01(XS),WA     LOAD OLD CHAIN PTR
       MOV  CWCOF,NUM01(XS)  SET CURRENT LOC AS NEW CHAIN HEAD
       JSR  CDWRD            GENERATE FORWARD CHAIN LINK
       EJC
*
*      CDGVL (CONTINUED)
*
*      NOW TO FILL IN THE SKIP OFFSET TO O_SLC,O_SLD
*
       MOV  (XS),XR          LOAD OFFSET TO WORD TO PLUG
       ADD  R_CCB,XR         POINT TO ACTUAL LOCATION TO PLUG
       MOV  CWCOF,(XR)       PLUG PROPER OFFSET IN
       MOV  =OSLC_,WA        LOAD O_SLC PTR FOR NEXT ALTERNATIVE
       MOV  WB,XR            COPY OFFSET (DESTROY GARBAGE XR)
       ICA  XR               BUMP EXTRA TIME FOR TEST
       BLT  XR,CMLEN(XL),CGV16 LOOP BACK IF NOT LAST ALTERNATIVE
*
*      HERE TO GENERATE CODE FOR LAST ALTERNATIVE
*
       MOV  =OSLD_,WA        GET HEADER CALL
       JSR  CDWRD            GENERATE O_SLD CALL
       JSR  CMGEN            GENERATE CODE FOR LAST ALTERNATIVE
       ICA  XS               POP OFFSET PTR
       MOV  (XS)+,XR         LOAD CHAIN PTR
*
*      LOOP TO PLUG OFFSETS PAST STRUCTURE
*
CGV17  ADD  R_CCB,XR         MAKE NEXT PTR ABSOLUTE
       MOV  (XR),WA          LOAD FORWARD PTR
       MOV  CWCOF,(XR)       PLUG REQUIRED OFFSET
       MOV  WA,XR            COPY FORWARD PTR
       BNZ  WA,CGV17         LOOP BACK IF MORE TO GO
       BRN  CGV33            ELSE JUMP TO EXIT (NOT CONSTANT)
*
*      HERE FOR BINARY OPS WITH VALUE OPERANDS
*
CGV18  MOV  CMLOP(XL),XR     LOAD LEFT OPERAND POINTER
       JSR  CDGVL            GEN VALUE CODE FOR LEFT OPERAND
*
*      HERE FOR UNARY OPS WITH VALUE OPERAND (BINOPS MERGE)
*
CGV19  MOV  CMROP(XL),XR     LOAD RIGHT (ONLY) OPERAND PTR
       JSR  CDGVL            GEN CODE BY VALUE
       EJC
*
*      CDGVL (CONTINUED)
*
*      MERGE HERE TO GENERATE OPERATOR CALL FROM CMOPN FIELD
*
CGV20  MOV  CMOPN(XL),WA     LOAD OPERATOR CALL POINTER
       BRN  CGV36            JUMP TO GENERATE IT WITH CONS TEST
*
*      HERE FOR ASSIGNMENT
*
CGV21  MOV  CMLOP(XL),XR     LOAD LEFT OPERAND POINTER
       BLO  (XR),=B_VR_,CGV22 JUMP IF NOT VARIABLE
*
*      HERE FOR ASSIGNMENT TO SIMPLE VARIABLE
*
       MOV  CMROP(XL),XR     LOAD RIGHT OPERAND PTR
       JSR  CDGVL            GENERATE CODE BY VALUE
       MOV  CMLOP(XL),WA     RELOAD LEFT OPERAND VRBLK PTR
       ADD  *VRSTO,WA        POINT TO VRSTO FIELD
       BRN  CGV32            JUMP TO GENERATE STORE PTR
*
*      HERE IF NOT SIMPLE VARIABLE ASSIGNMENT
*
CGV22  JSR  EXPAP            TEST FOR PATTERN MATCH ON LEFT SIDE
       PPM  CGV23            JUMP IF NOT PATTERN MATCH
*
*      HERE FOR PATTERN REPLACEMENT
*
       MOV  CMROP(XR),CMLOP(XL) SAVE PATTERN PTR IN SAFE PLACE
       MOV  CMLOP(XR),XR     LOAD SUBJECT PTR
       JSR  CDGNM            GEN CODE BY NAME FOR SUBJECT
       MOV  CMLOP(XL),XR     LOAD PATTERN PTR
       JSR  CDGVL            GEN CODE BY VALUE FOR PATTERN
       MOV  =OPMN_,WA        LOAD MATCH BY NAME CALL
       JSR  CDWRD            GENERATE IT
       MOV  CMROP(XL),XR     LOAD REPLACEMENT VALUE PTR
       JSR  CDGVL            GEN CODE BY VALUE
       MOV  =ORPL_,WA        LOAD REPLACE CALL
       BRN  CGV32            JUMP TO GEN AND EXIT (NOT CONSTANT)
*
*      HERE FOR ASSIGNMENT TO COMPLEX VARIABLE
*
CGV23  MNZ  WC               INHIBIT PRE-EVALUATION
       JSR  CDGNM            GEN CODE BY NAME FOR LEFT SIDE
       BRN  CGV31            MERGE WITH UNOP CIRCUIT
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE FOR CONCATENATION
*
CGV24  MOV  CMLOP(XL),XR     LOAD LEFT OPERAND PTR
       BNE  (XR),=B_CMT,CGV18 ORDINARY BINOP IF NOT CMBLK
       MOV  CMTYP(XR),WB     LOAD CMBLK TYPE CODE
       BEQ  WB,=C_INT,CGV25  SPECIAL CASE IF INTERROGATION
       BEQ  WB,=C_NEG,CGV25  OR NEGATION
       BNE  WB,=C_FNC,CGV18  ELSE ORDINARY BINOP IF NOT FUNCTION
       MOV  CMOPN(XR),XR     ELSE LOAD FUNCTION VRBLK PTR
       BNZ  VRLEN(XR),CGV18  ORDINARY BINOP IF NOT SYSTEM VAR
       MOV  VRSVP(XR),XR     ELSE POINT TO SVBLK
       MOV  SVBIT(XR),WA     LOAD BIT INDICATORS
       ANB  BTPRD,WA         TEST FOR PREDICATE FUNCTION
       ZRB  WA,CGV18         ORDINARY BINOP IF NOT
*
*      HERE IF LEFT ARG OF CONCATENATION IS PREDICATE FUNCTION
*
CGV25  MOV  CMLOP(XL),XR     RELOAD LEFT ARG
       JSR  CDGVL            GEN CODE BY VALUE
       MOV  =OPOP_,WA        LOAD POP CALL
       JSR  CDWRD            GENERATE IT
       MOV  CMROP(XL),XR     LOAD RIGHT OPERAND
       JSR  CDGVL            GEN CODE BY VALUE AS RESULT CODE
       BRN  CGV33            EXIT (NOT CONSTANT)
*
*      HERE TO GENERATE CODE FOR PATTERN, IMMEDIATE ASSIGNMENT
*
CGV26  MOV  CMLOP(XL),XR     LOAD LEFT OPERAND
       JSR  CDGVL            GEN CODE BY VALUE, MERGE
*
*      HERE FOR UNOPS WITH ARG BY NAME (BINARY _ . MERGE)
*
CGV27  MOV  CMROP(XL),XR     LOAD RIGHT OPERAND PTR
       JSR  CDGNM            GEN CODE BY NAME FOR RIGHT ARG
       MOV  CMOPN(XL),XR     GET OPERATOR CODE WORD
       BNE  (XR),=O_KWV,CGV20 GEN CALL UNLESS KEYWORD VALUE
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE FOR KEYWORD BY VALUE. THIS IS CONSTANT ONLY IF
*      THE OPERAND IS ONE OF THE SPECIAL SYSTEM VARIABLES WITH
*      THE SVCKW BIT SET TO INDICATE A CONSTANT KEYWORD VALUE.
*      NOTE THAT THE ONLY CONSTANT OPERAND BY NAME IS A VARIABLE
*
       BNZ  WC,CGV20         GEN CALL IF NON-CONSTANT (NOT VAR)
       MNZ  WC               ELSE SET NON-CONSTANT IN CASE
       MOV  CMROP(XL),XR     LOAD PTR TO OPERAND VRBLK
       BNZ  VRLEN(XR),CGV20  GEN (NON-CONSTANT) IF NOT SYS VAR
       MOV  VRSVP(XR),XR     ELSE LOAD PTR TO SVBLK
       MOV  SVBIT(XR),WA     LOAD BIT MASK
       ANB  BTCKW,WA         TEST FOR CONSTANT KEYWORD
       ZRB  WA,CGV20         GO GEN IF NOT CONSTANT
       ZER  WC               ELSE SET RESULT CONSTANT
       BRN  CGV20            AND JUMP BACK TO GENERATE CALL
*
*      HERE TO GENERATE CODE FOR NEGATION
*
CGV28  MOV  =ONTA_,WA        GET INITIAL WORD
       JSR  CDWRD            GENERATE IT
       MOV  CWCOF,WB         SAVE NEXT OFFSET
       JSR  CDWRD            GENERATE GUNK WORD FOR NOW
       MOV  CMROP(XL),XR     LOAD RIGHT OPERAND PTR
       JSR  CDGVL            GEN CODE BY VALUE
       MOV  =ONTB_,WA        LOAD END OF EVALUATION CALL
       JSR  CDWRD            GENERATE IT
       MOV  WB,XR            COPY OFFSET TO WORD TO PLUG
       ADD  R_CCB,XR         POINT TO ACTUAL WORD TO PLUG
       MOV  CWCOF,(XR)       PLUG WORD WITH CURRENT OFFSET
       MOV  =ONTC_,WA        LOAD FINAL CALL
       BRN  CGV32            JUMP TO GENERATE IT (NOT CONSTANT)
*
*      HERE TO GENERATE CODE FOR UNDEFINED BINARY OPERATOR
*
CGV29  MOV  CMLOP(XL),XR     LOAD LEFT OPERAND PTR
       JSR  CDGVL            GENERATE CODE BY VALUE
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE TO GENERATE CODE FOR UNDEFINED UNARY OPERATOR
*
CGV30  MOV  =C_UO_,WB        SET UNOP CODE + 1
       SUB  CMTYP(XL),WB     SET NUMBER OF ARGS (1 OR 2)
*
*      MERGE HERE FOR UNDEFINED OPERATORS
*
       MOV  CMROP(XL),XR     LOAD RIGHT (ONLY) OPERAND POINTER
       JSR  CDGVL            GEN VALUE CODE FOR RIGHT OPERAND
       MOV  CMOPN(XL),XR     LOAD POINTER TO OPERATOR DV
       MOV  DVOPN(XR),XR     LOAD POINTER OFFSET
       WTB  XR               CONVERT WORD OFFSET TO BYTES
       ADD  =R_UBA,XR        POINT TO PROPER FUNCTION PTR
       SUB  *VRFNC,XR        SET STANDARD FUNCTION OFFSET
       BRN  CGV12            MERGE WITH FUNCTION CALL CIRCUIT
*
*      HERE TO GENERATE CODE FOR INTERROGATION, INDIRECTION
*
CGV31  MNZ  WC               SET NON CONSTANT
       BRN  CGV19            MERGE
*
*      HERE TO EXIT GENERATING A WORD, RESULT NOT CONSTANT
*
CGV32  JSR  CDWRD            GENERATE WORD, MERGE
*
*      HERE TO EXIT WITH NO WORD GENERATED, NOT CONSTANT
*
CGV33  MNZ  WC               INDICATE RESULT IS NOT CONSTANT
*
*      COMMON EXIT POINT
*
CGV34  ICA  XS               POP INITIAL CODE OFFSET
       MOV  (XS)+,WA         RESTORE OLD CONSTANT FLAG
       MOV  (XS)+,XL         RESTORE ENTRY XL
       MOV  (XS)+,WB         RESTORE ENTRY WB
       BNZ  WC,CGV35         JUMP IF NOT CONSTANT
       MOV  WA,WC            ELSE RESTORE ENTRY CONSTANT FLAG
*
*      HERE TO RETURN AFTER DEALING WITH WC SETTING
*
CGV35  EXI                   RETURN TO CDGVL CALLER
*
*      EXIT HERE TO GENERATE WORD AND TEST FOR CONSTANT
*
CGV36  JSR  CDWRD            GENERATE WORD
       BNZ  WC,CGV34         JUMP TO EXIT IF NOT CONSTANT
       EJC
*
*      CDGVL (CONTINUED)
*
*      HERE TO PREEVALUATE CONSTANT SUB-EXPRESSION
*
       MOV  =ORVL_,WA        LOAD CALL TO RETURN VALUE
       JSR  CDWRD            GENERATE IT
       MOV  (XS),XL          LOAD INITIAL CODE OFFSET
       JSR  EXBLD            BUILD EXBLK FOR EXPRESSION
       ZER  WB               SET TO EVALUATE BY VALUE
       JSR  EVALX            EVALUATE EXPRESSION
       PPM                   SHOULD NOT FAIL
       MOV  (XR),WA          LOAD TYPE WORD OF RESULT
       BLO  WA,=P_AAA,CGV37  JUMP IF NOT PATTERN
       MOV  =OLPT_,WA        ELSE LOAD SPECIAL PATTERN LOAD CALL
       JSR  CDWRD            GENERATE IT
*
*      MERGE HERE TO GENERATE POINTER TO RESULTING CONSTANT
*
CGV37  MOV  XR,WA            COPY CONSTANT POINTER
       JSR  CDWRD            GENERATE PTR
       ZER  WC               SET RESULT CONSTANT
       BRN  CGV34            JUMP BACK TO EXIT
       ENP                   END PROCEDURE CDGVL
       EJC
*
*      CDWRD -- GENERATE ONE WORD OF CODE
*
*      CDWRD WRITES ONE WORD INTO THE CURRENT CODE BLOCK UNDER
*      CONSTRUCTION. A NEW, LARGER, BLOCK IS ALLOCATED IF THERE
*      IS INSUFFICIENT ROOM IN THE CURRENT BLOCK. CDWRD ENSURES
.IF    .CSLN
*      THAT THERE ARE AT LEAST FOUR WORDS LEFT IN THE BLOCK
.ELSE
*      THAT THERE ARE AT LEAST THREE WORDS LEFT IN THE BLOCK
.FI
*      AFTER ENTERING THE NEW WORD. THIS GUARANTEES THAT ANY
*      EXTRA SPACE AT THE END CAN BE SPLIT OFF AS A CCBLK.
*
*      (WA)                  WORD TO BE GENERATED
*      JSR  CDWRD            CALL TO GENERATE WORD
*
CDWRD  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE ENTRY XR
       MOV  WA,-(XS)         SAVE CODE WORD TO BE GENERATED
*
*      MERGE BACK HERE AFTER ALLOCATING LARGER BLOCK
*
CDWD1  MOV  R_CCB,XR         LOAD PTR TO CCBLK BEING BUILT
       BNZ  XR,CDWD2         JUMP IF BLOCK ALLOCATED
*
*      HERE WE ALLOCATE AN ENTIRELY FRESH BLOCK
*
       MOV  *E_CBS,WA        LOAD INITIAL LENGTH
       JSR  ALLOC            ALLOCATE CCBLK
       MOV  =B_CCT,(XR)      STORE TYPE WORD
       MOV  *CCCOD,CWCOF     SET INITIAL OFFSET
       MOV  WA,CCLEN(XR)     STORE BLOCK LENGTH
.IF    .CSLN
       ZER  CCSLN(XR)        ZERO LINE NUMBER
.FI
       MOV  XR,R_CCB         STORE PTR TO NEW BLOCK
*
*      HERE WE HAVE A BLOCK WE CAN USE
*
CDWD2  MOV  CWCOF,WA         LOAD CURRENT OFFSET
.IF    .CSLN
       ADD  *NUM05,WA        ADJUST FOR TEST (FIVE WORDS)
.ELSE
       ADD  *NUM04,WA        ADJUST FOR TEST (FOUR WORDS)
.FI
       BLO  WA,CCLEN(XR),CDWD4 JUMP IF ROOM IN THIS BLOCK
*
*      HERE IF NO ROOM IN CURRENT BLOCK
*
       BGE  WA,MXLEN,CDWD5   JUMP IF ALREADY AT MAX SIZE
       ADD  *E_CBS,WA        ELSE GET NEW SIZE
       MOV  XL,-(XS)         SAVE ENTRY XL
       MOV  XR,XL            COPY POINTER
       BLT  WA,MXLEN,CDWD3   JUMP IF NOT TOO LARGE
       MOV  MXLEN,WA         ELSE RESET TO MAX ALLOWED SIZE
       EJC
*
*      CDWRD (CONTINUED)
*
*      HERE WITH NEW BLOCK SIZE IN WA
*
CDWD3  JSR  ALLOC            ALLOCATE NEW BLOCK
       MOV  XR,R_CCB         STORE POINTER TO NEW BLOCK
       MOV  =B_CCT,(XR)+     STORE TYPE WORD IN NEW BLOCK
       MOV  WA,(XR)+         STORE BLOCK LENGTH
.IF    .CSLN
       MOV  CCSLN(XL),(XR)+  COPY SOURCE LINE NUMBER WORD
.FI
       ADD  *CCUSE,XL        POINT TO CCUSE,CCCOD FIELDS IN OLD
       MOV  (XL),WA          LOAD CCUSE VALUE
       MVW                   COPY USEFUL WORDS FROM OLD BLOCK
       MOV  (XS)+,XL         RESTORE XL
       BRN  CDWD1            MERGE BACK TO TRY AGAIN
*
*      HERE WITH ROOM IN CURRENT BLOCK
*
CDWD4  MOV  CWCOF,WA         LOAD CURRENT OFFSET
       ICA  WA               GET NEW OFFSET
       MOV  WA,CWCOF         STORE NEW OFFSET
       MOV  WA,CCUSE(XR)     STORE IN CCBLK FOR GBCOL
       DCA  WA               RESTORE PTR TO THIS WORD
       ADD  WA,XR            POINT TO CURRENT ENTRY
       MOV  (XS)+,WA         RELOAD WORD TO GENERATE
       MOV  WA,(XR)          STORE WORD IN BLOCK
       MOV  (XS)+,XR         RESTORE ENTRY XR
       EXI                   RETURN TO CALLER
*
*      HERE IF COMPILED CODE IS TOO LONG FOR CDBLK
*
CDWD5  ERB  213,Syntax error: Statement is too complicated.
       ENP                   END PROCEDURE CDWRD
       EJC
*
*      CMGEN -- GENERATE CODE FOR CMBLK PTR
*
*      CMGEN IS A SUBSIDIARY PROCEDURE USED TO GENERATE VALUE
*      CODE FOR A CMBLK PTR FROM THE MAIN CODE GENERATORS.
*
*      (XL)                  CMBLK POINTER
*      (WB)                  OFFSET TO POINTER IN CMBLK
*      JSR  CMGEN            CALL TO GENERATE CODE
*      (XR,WA)               DESTROYED
*      (WB)                  BUMPED BY ONE WORD
*
CMGEN  PRC  R,0              ENTRY POINT, RECURSIVE
       MOV  XL,XR            COPY CMBLK POINTER
       ADD  WB,XR            POINT TO CMBLK POINTER
       MOV  (XR),XR          LOAD CMBLK POINTER
       JSR  CDGVL            GENERATE CODE BY VALUE
       ICA  WB               BUMP OFFSET
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE CMGEN
       EJC
*
*      CMPIL (COMPILE SOURCE CODE)
*
*      CMPIL IS USED TO CONVERT SNOBOL4 SOURCE CODE TO INTERNAL
*      FORM (SEE CDBLK FORMAT). IT IS USED BOTH FOR THE INITIAL
*      COMPILE AND AT RUN TIME BY THE CODE AND CONVERT FUNCTIONS
*      THIS PROCEDURE HAS CONTROL FOR THE ENTIRE DURATION OF
*      INITIAL COMPILATION. AN ERROR IN ANY PROCEDURE CALLED
*      DURING COMPILATION WILL LEAD FIRST TO THE ERROR SECTION
*      AND ULTIMATELY BACK HERE FOR RESUMED COMPILATION. THE
*      RE-ENTRY POINTS AFTER AN ERROR ARE SPECIALLY LABELLED -
*
*      CMPCE                 RESUME AFTER CONTROL CARD ERROR
*      CMPLE                 RESUME AFTER LABEL ERROR
*      CMPSE                 RESUME AFTER STATEMENT ERROR
*
*      JSR  CMPIL            CALL TO COMPILE CODE
*      (XR)                  PTR TO CDBLK FOR ENTRY STATEMENT
*      (XL,WA,WB,WC,RA)      DESTROYED
*
*      THE FOLLOWING GLOBAL VARIABLES ARE REFERENCED
*
*      CMPLN                 LINE NUMBER OF FIRST LINE OF
*                            STATEMENT TO BE COMPILED
*
*      CMPSN                 NUMBER OF NEXT STATEMENT
*                            TO BE COMPILED.
*
*      CSWXX                 CONTROL CARD SWITCH VALUES ARE
*                            CHANGED WHEN RELEVANT CONTROL
*                            CARDS ARE MET.
*
*      CWCOF                 OFFSET TO NEXT WORD IN CODE BLOCK
*                            BEING BUILT (SEE CDWRD).
*
*      LSTSN                 NUMBER OF STATEMENT MOST RECENTLY
*                            COMPILED (INITIALLY SET TO ZERO).
*
*      R_CIM                 CURRENT (INITIAL) COMPILER IMAGE
*                            (ZERO FOR INITIAL COMPILE CALL)
*
*      R_CNI                 USED TO POINT TO FOLLOWING IMAGE.
*                            (SEE READR PROCEDURE).
*
*      SCNGO                 GOTO SWITCH FOR SCANE PROCEDURE
*
*      SCNIL                 LENGTH OF CURRENT IMAGE EXCLUDING
*                            CHARACTERS REMOVED BY -INPUT.
*
*      SCNPT                 CURRENT SCAN OFFSET, SEE SCANE.
*
*      SCNRS                 RESCAN SWITCH FOR SCANE PROCEDURE.
*
*      SCNSE                 OFFSET (IN R_CIM) OF MOST RECENTLY
*                            SCANNED ELEMENT. SET ZERO IF NOT
*                            CURRENTLY SCANNING ITEMS
       EJC
*
*      CMPIL (CONTINUED)
*
*      STAGE               STGIC  INITIAL COMPILE IN PROGRESS
*                          STGXC  CODE/CONVERT COMPILE
*                          STGEV  BUILDING EXBLK FOR EVAL
*                          STGXT  EXECUTE TIME (OUTSIDE COMPILE)
*                          STGCE  INITIAL COMPILE AFTER END LINE
*                          STGXE  EXECUTE COMPILE AFTER END LINE
*
*      CMPIL ALSO USES A FIXED NUMBER OF LOCATIONS ON THE
*      MAIN STACK AS FOLLOWS. (THE DEFINITIONS OF THE ACTUAL
*      OFFSETS ARE IN THE DEFINITIONS SECTION).
*
*      CMSTM(XS)             POINTER TO EXPAN TREE FOR BODY OF
*                            STATEMENT (SEE EXPAN PROCEDURE).
*
*      CMSGO(XS)             POINTER TO TREE REPRESENTATION OF
*                            SUCCESS GOTO (SEE PROCEDURE SCNGO)
*                            ZERO IF NO SUCCESS GOTO IS GIVEN
*
*      CMFGO(XS)             LIKE CMSGO FOR FAILURE GOTO.
*
*      CMCGO(XS)             SET NON-ZERO ONLY IF THERE IS A
*                            CONDITIONAL GOTO. USED FOR -FAIL,
*                            -NOFAIL CODE GENERATION.
*
*      CMPCD(XS)             POINTER TO CDBLK FOR PREVIOUS
*                            STATEMENT. ZERO FOR 1ST STATEMENT.
*
*      CMFFP(XS)             SET NON-ZERO IF CDFAL IN PREVIOUS
*                            CDBLK NEEDS FILLING WITH FORWARD
*                            POINTER, ELSE SET TO ZERO.
*
*      CMFFC(XS)             SAME AS CMFFP FOR CURRENT CDBLK
*
*      CMSOP(XS)             OFFSET TO WORD IN PREVIOUS CDBLK
*                            TO BE FILLED IN WITH FORWARD PTR
*                            TO NEXT CDBLK FOR SUCCESS GOTO.
*                            ZERO IF NO FILL IN IS REQUIRED.
*
*      CMSOC(XS)             SAME AS CMSOP FOR CURRENT CDBLK.
*
*      CMLBL(XS)             POINTER TO VRBLK FOR LABEL OF
*                            CURRENT STATEMENT. ZERO IF NO LABEL
*
*      CMTRA(XS)             POINTER TO CDBLK FOR ENTRY STMNT.
       EJC
*
*      CMPIL (CONTINUED)
*
*      ENTRY POINT
*
CMPIL  PRC  E,0              ENTRY POINT
       LCT  WB,=CMNEN        SET NUMBER OF STACK WORK LOCATIONS
*
*      LOOP TO INITIALIZE STACK WORKING LOCATIONS
*
CMP00  ZER  -(XS)            STORE A ZERO, MAKE ONE ENTRY
       BCT  WB,CMP00         LOOP BACK UNTIL ALL SET
       MOV  XS,CMPXS         SAVE STACK POINTER FOR ERROR SEC
       SSS  CMPSS            SAVE S-R STACK POINTER IF ANY
*
*      LOOP THROUGH STATEMENTS
*
CMP01  MOV  SCNPT,WB         SET SCAN POINTER OFFSET
       MOV  WB,SCNSE         SET START OF ELEMENT LOCATION
       MOV  =OCER_,WA        POINT TO COMPILE ERROR CALL
       JSR  CDWRD            GENERATE AS TEMPORARY CDFAL
       BLT  WB,SCNIL,CMP04   JUMP IF CHARS LEFT ON THIS IMAGE
*
*      LOOP HERE AFTER COMMENT OR CONTROL CARD
*      ALSO SPECIAL ENTRY AFTER CONTROL CARD ERROR
*
CMPCE  ZER  XR               CLEAR POSSIBLE GARBAGE XR VALUE
.IF    .CINC
       BNZ  CNIND,CMPC2      IF WITHIN INCLUDE FILE
.FI
       BNE  STAGE,=STGIC,CMP02 SKIP UNLESS INITIAL COMPILE
CMPC2  JSR  READR            READ NEXT INPUT IMAGE
       BZE  XR,CMP09         JUMP IF NO INPUT AVAILABLE
       JSR  NEXTS            ACQUIRE NEXT SOURCE IMAGE
       MOV  CMPSN,LSTSN      STORE STMT NO FOR USE BY LISTR
       MOV  RDCLN,CMPLN      STORE LINE NUMBER AT START OF STMT
       ZER  SCNPT            RESET SCAN POINTER
       BRN  CMP04            GO PROCESS IMAGE
*
*      FOR EXECUTE TIME COMPILE, PERMIT EMBEDDED CONTROL CARDS
*      AND COMMENTS (BY SKIPPING TO NEXT SEMI-COLON)
*
CMP02  MOV  R_CIM,XR         GET CURRENT IMAGE
       MOV  SCNPT,WB         GET CURRENT OFFSET
       PLC  XR,WB            PREPARE TO GET CHARS
*
*      SKIP TO SEMI-COLON
*
CMP03  BGE  SCNPT,SCNIL,CMP09 END LOOP IF END OF IMAGE
       LCH  WC,(XR)+         GET CHAR
       ICV  SCNPT            ADVANCE OFFSET
       BNE  WC,=CH_SM,CMP03  LOOP IF NOT SEMI-COLON
       EJC
*
*      CMPIL (CONTINUED)
*
*      HERE WITH IMAGE AVAILABLE TO SCAN. NOTE THAT IF THE INPUT
*      STRING IS NULL, THEN EVERYTHING IS OK SINCE NULL IS
*      ACTUALLY ASSEMBLED AS A WORD OF BLANKS.
*
CMP04  MOV  R_CIM,XR         POINT TO CURRENT IMAGE
       MOV  SCNPT,WB         LOAD CURRENT OFFSET
       MOV  WB,WA            COPY FOR LABEL SCAN
       PLC  XR,WB            POINT TO FIRST CHARACTER
       LCH  WC,(XR)+         LOAD FIRST CHARACTER
       BEQ  WC,=CH_SM,CMP12  NO LABEL IF SEMICOLON
       BEQ  WC,=CH_AS,CMPCE  LOOP BACK IF COMMENT CARD
       BEQ  WC,=CH_MN,CMP32  JUMP IF CONTROL CARD
       MOV  R_CIM,R_CMP      ABOUT TO DESTROY R_CIM
       MOV  =CMLAB,XL        POINT TO LABEL WORK STRING
       MOV  XL,R_CIM         SCANE IS TO SCAN WORK STRING
       PSC  XL               POINT TO FIRST CHARACTER POSITION
       SCH  WC,(XL)+         STORE CHAR JUST LOADED
       MOV  =CH_SM,WC        GET A SEMICOLON
       SCH  WC,(XL)          STORE AFTER FIRST CHAR
       CSC  XL               FINISHED CHARACTER STORING
       ZER  XL               CLEAR POINTER
       ZER  SCNPT            START AT FIRST CHARACTER
       MOV  SCNIL,-(XS)      PRESERVE IMAGE LENGTH
       MOV  =NUM02,SCNIL     READ 2 CHARS AT MOST
       JSR  SCANE            SCAN FIRST CHAR FOR TYPE
       MOV  (XS)+,SCNIL      RESTORE IMAGE LENGTH
       MOV  XL,WC            NOTE RETURN CODE
       MOV  R_CMP,XL         GET OLD R_CIM
       MOV  XL,R_CIM         PUT IT BACK
       MOV  WB,SCNPT         REINSTATE OFFSET
       BNZ  SCNBL,CMP12      BLANK SEEN - CANT BE LABEL
       MOV  XL,XR            POINT TO CURRENT IMAGE
       PLC  XR,WB            POINT TO FIRST CHAR AGAIN
       BEQ  WC,=T_VAR,CMP06  OK IF LETTER
       BEQ  WC,=T_CON,CMP06  OK IF DIGIT
*
*      DROP IN OR JUMP FROM ERROR SECTION IF SCANE FAILED
*
CMPLE  MOV  R_CMP,R_CIM      POINT TO BAD LINE
       ERB  214,Bad label or misplaced continuation line
*
*      LOOP TO SCAN LABEL
*
CMP05  BEQ  WC,=CH_SM,CMP07  SKIP IF SEMICOLON
       ICV  WA               BUMP OFFSET
       BEQ  WA,SCNIL,CMP07   JUMP IF END OF IMAGE (LABEL END)
       EJC
*
*      CMPIL (CONTINUED)
*
*      ENTER LOOP AT THIS POINT
*
CMP06  LCH  WC,(XR)+         ELSE LOAD NEXT CHARACTER
.IF    .CAHT
       BEQ  WC,=CH_HT,CMP07  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WC,=CH_VT,CMP07  JUMP IF VERTICAL TAB
.FI
       BNE  WC,=CH_BL,CMP05  LOOP BACK IF NON-BLANK
*
*      HERE AFTER SCANNING OUT LABEL
*
CMP07  MOV  WA,SCNPT         SAVE UPDATED SCAN OFFSET
       SUB  WB,WA            GET LENGTH OF LABEL
       BZE  WA,CMP12         SKIP IF LABEL LENGTH ZERO
       ZER  XR               CLEAR GARBAGE XR VALUE
       JSR  SBSTR            BUILD SCBLK FOR LABEL NAME
       JSR  GTNVR            LOCATE/CONTRUCT VRBLK
       PPM                   DUMMY (IMPOSSIBLE) ERROR RETURN
       MOV  XR,CMLBL(XS)     STORE LABEL POINTER
       BNZ  VRLEN(XR),CMP11  JUMP IF NOT SYSTEM LABEL
       BNE  VRSVP(XR),=V_END,CMP11 JUMP IF NOT END LABEL
*
*      HERE FOR END LABEL SCANNED OUT
*
       ADD  =STGND,STAGE     ADJUST STAGE APPROPRIATELY
       JSR  SCANE            SCAN OUT NEXT ELEMENT
       BEQ  XL,=T_SMC,CMP10  JUMP IF END OF IMAGE
       BNE  XL,=T_VAR,CMP08  ELSE ERROR IF NOT VARIABLE
*
*      HERE CHECK FOR VALID INITIAL TRANSFER
*
       BEQ  VRLBL(XR),=STNDL,CMP08 JUMP IF NOT DEFINED (ERROR)
       MOV  VRLBL(XR),CMTRA(XS) ELSE SET INITIAL ENTRY POINTER
       JSR  SCANE            SCAN NEXT ELEMENT
       BEQ  XL,=T_SMC,CMP10  JUMP IF OK (END OF IMAGE)
*
*      HERE FOR BAD TRANSFER LABEL
*
CMP08  ERB  215,Syntax error: Undefined or erroneous entry label
*
*      HERE FOR END OF INPUT (NO END LABEL DETECTED)
*
CMP09  ZER  XR               CLEAR GARBAGE XR VALUE
       ADD  =STGND,STAGE     ADJUST STAGE APPROPRIATELY
       BEQ  STAGE,=STGXE,CMP10 JUMP IF CODE CALL (OK)
       ERB  216,Syntax error: Missing END line
*
*      HERE AFTER PROCESSING END LINE (MERGE HERE ON END ERROR)
*
CMP10  MOV  =OSTP_,WA        SET STOP CALL POINTER
       JSR  CDWRD            GENERATE AS STATEMENT CALL
       BRN  CMPSE            JUMP TO GENERATE AS FAILURE
       EJC
*
*      CMPIL (CONTINUED)
*
*      HERE AFTER PROCESSING LABEL OTHER THAN END
*
CMP11  BNE  STAGE,=STGIC,CMP12 JUMP IF CODE CALL - REDEF. OK
       BEQ  VRLBL(XR),=STNDL,CMP12 ELSE CHECK FOR REDEFINITION
       ZER  CMLBL(XS)        LEAVE FIRST LABEL DECLN UNDISTURBED
       ERB  217,Syntax error: Duplicate label
*
*      HERE AFTER DEALING WITH LABEL
*      NULL STATEMENTS AND STATEMENTS JUST CONTAINING A
*      CONSTANT SUBJECT ARE OPTIMIZED OUT BY RESETTING THE
*      CURRENT CCBLK TO EMPTY.
*
CMP12  ZER  WB               SET FLAG FOR STATEMENT BODY
       JSR  EXPAN            GET TREE FOR STATEMENT BODY
       MOV  XR,CMSTM(XS)     STORE FOR LATER USE
       ZER  CMSGO(XS)        CLEAR SUCCESS GOTO POINTER
       ZER  CMFGO(XS)        CLEAR FAILURE GOTO POINTER
       ZER  CMCGO(XS)        CLEAR CONDITIONAL GOTO FLAG
       JSR  SCANE            SCAN NEXT ELEMENT
       BEQ  XL,=T_COL,CMP13  JUMP IF COLON (GOTO)
       BNZ  CSWNO,CMP18      JUMP IF NOT OPTIMIZING
       BNZ  CMLBL(XS),CMP18  JUMP IF LABEL PRESENT
       MOV  CMSTM(XS),XR     LOAD TREE PTR FOR STATEMENT BODY
       MOV  (XR),WA          LOAD TYPE WORD
       BEQ  WA,=B_CMT,CMP18  JUMP IF CMBLK
       BGE  WA,=B_VRA,CMP18  JUMP IF NOT ICBLK, SCBLK, OR RCBLK
       MOV  R_CCB,XL         LOAD PTR TO CCBLK
       MOV  *CCCOD,CCUSE(XL) RESET USE OFFSET IN CCBLK
       MOV  *CCCOD,CWCOF     AND IN GLOBAL
       ICV  CMPSN            BUMP STATEMENT NUMBER
       BRN  CMP01            GENERATE NO CODE FOR STATEMENT
*
*      LOOP TO PROCESS GOTO FIELDS
*
CMP13  MNZ  SCNGO            SET GOTO FLAG
       JSR  SCANE            SCAN NEXT ELEMENT
       BEQ  XL,=T_SMC,CMP31  JUMP IF NO FIELDS LEFT
       BEQ  XL,=T_SGO,CMP14  JUMP IF S FOR SUCCESS GOTO
       BEQ  XL,=T_FGO,CMP16  JUMP IF F FOR FAILURE GOTO
*
*      HERE FOR UNCONDITIONAL GOTO (I.E. NOT F OR S)
*
       MNZ  SCNRS            SET TO RESCAN ELEMENT NOT F,S
       JSR  SCNGF            SCAN OUT GOTO FIELD
       BNZ  CMFGO(XS),CMP17  ERROR IF FGOTO ALREADY
       MOV  XR,CMFGO(XS)     ELSE SET AS FGOTO
       BRN  CMP15            MERGE WITH SGOTO CIRCUIT
*
*      HERE FOR SUCCESS GOTO
*
CMP14  JSR  SCNGF            SCAN SUCCESS GOTO FIELD
       MOV  =NUM01,CMCGO(XS) SET CONDITIONAL GOTO FLAG
*
*      UNCONTIONAL GOTO MERGES HERE
*
CMP15  BNZ  CMSGO(XS),CMP17  ERROR IF SGOTO ALREADY GIVEN
       MOV  XR,CMSGO(XS)     ELSE SET SGOTO
       BRN  CMP13            LOOP BACK FOR NEXT GOTO FIELD
*
*      HERE FOR FAILURE GOTO
*
CMP16  JSR  SCNGF            SCAN GOTO FIELD
       MOV  =NUM01,CMCGO(XS) SET CONDITONAL GOTO FLAG
       BNZ  CMFGO(XS),CMP17  ERROR IF FGOTO ALREADY GIVEN
       MOV  XR,CMFGO(XS)     ELSE STORE FGOTO POINTER
       BRN  CMP13            LOOP BACK FOR NEXT FIELD
       EJC
*
*      CMPIL (CONTINUED)
*
*      HERE FOR DUPLICATED GOTO FIELD
*
CMP17  ERB  218,Syntax error: Duplicated goto field
*
*      HERE TO GENERATE CODE
*
CMP18  ZER  SCNSE            STOP POSITIONAL ERROR FLAGS
       MOV  CMSTM(XS),XR     LOAD TREE PTR FOR STATEMENT BODY
       ZER  WB               COLLECTABLE VALUE FOR WB FOR CDGVL
       ZER  WC               RESET CONSTANT FLAG FOR CDGVL
       JSR  EXPAP            TEST FOR PATTERN MATCH
       PPM  CMP19            JUMP IF NOT PATTERN MATCH
       MOV  =OPMS_,CMOPN(XR) ELSE SET PATTERN MATCH POINTER
       MOV  =C_PMT,CMTYP(XR)
*
*      HERE AFTER DEALING WITH SPECIAL PATTERN MATCH CASE
*
CMP19  JSR  CDGVL            GENERATE CODE FOR BODY OF STATEMENT
       MOV  CMSGO(XS),XR     LOAD SGOTO POINTER
       MOV  XR,WA            COPY IT
       BZE  XR,CMP21         JUMP IF NO SUCCESS GOTO
       ZER  CMSOC(XS)        CLEAR SUCCESS OFFSET FILLIN PTR
       BHI  XR,STATE,CMP20   JUMP IF COMPLEX GOTO
*
*      HERE FOR SIMPLE SUCCESS GOTO (LABEL)
*
       ADD  *VRTRA,WA        POINT TO VRTRA FIELD AS REQUIRED
       JSR  CDWRD            GENERATE SUCCESS GOTO
       BRN  CMP22            JUMP TO DEAL WITH FGOTO
*
*      HERE FOR COMPLEX SUCCESS GOTO
*
CMP20  BEQ  XR,CMFGO(XS),CMP22 NO CODE IF SAME AS FGOTO
       ZER  WB               ELSE SET OK VALUE FOR CDGVL IN WB
       JSR  CDGCG            GENERATE CODE FOR SUCCESS GOTO
       BRN  CMP22            JUMP TO DEAL WITH FGOTO
*
*      HERE FOR NO SUCCESS GOTO
*
CMP21  MOV  CWCOF,CMSOC(XS)  SET SUCCESS FILL IN OFFSET
       MOV  =OCER_,WA        POINT TO COMPILE ERROR CALL
       JSR  CDWRD            GENERATE AS TEMPORARY VALUE
       EJC
*
*      CMPIL (CONTINUED)
*
*      HERE TO DEAL WITH FAILURE GOTO
*
CMP22  MOV  CMFGO(XS),XR     LOAD FAILURE GOTO POINTER
       MOV  XR,WA            COPY IT
       ZER  CMFFC(XS)        SET NO FILL IN REQUIRED YET
       BZE  XR,CMP23         JUMP IF NO FAILURE GOTO GIVEN
       ADD  *VRTRA,WA        POINT TO VRTRA FIELD IN CASE
       BLO  XR,STATE,CMPSE   JUMP TO GEN IF SIMPLE FGOTO
*
*      HERE FOR COMPLEX FAILURE GOTO
*
       MOV  CWCOF,WB         SAVE OFFSET TO O_GOF CALL
       MOV  =OGOF_,WA        POINT TO FAILURE GOTO CALL
       JSR  CDWRD            GENERATE
       MOV  =OFIF_,WA        POINT TO FAIL IN FAIL WORD
       JSR  CDWRD            GENERATE
       JSR  CDGCG            GENERATE CODE FOR FAILURE GOTO
       MOV  WB,WA            COPY OFFSET TO O_GOF FOR CDFAL
       MOV  =B_CDC,WB        SET COMPLEX CASE CDTYP
       BRN  CMP25            JUMP TO BUILD CDBLK
*
*      HERE IF NO FAILURE GOTO GIVEN
*
CMP23  MOV  =OUNF_,WA        LOAD UNEXPECTED FAILURE CALL IN CAS
       MOV  CSWFL,WC         GET -NOFAIL FLAG
       ORB  CMCGO(XS),WC     CHECK IF CONDITIONAL GOTO
       ZRB  WC,CMPSE         JUMP IF -NOFAIL AND NO COND. GOTO
       MNZ  CMFFC(XS)        ELSE SET FILL IN FLAG
       MOV  =OCER_,WA        AND SET COMPILE ERROR FOR TEMPORARY
*
*      MERGE HERE WITH CDFAL VALUE IN WA, SIMPLE CDBLK
*      ALSO SPECIAL ENTRY AFTER STATEMENT ERROR
*
CMPSE  MOV  =B_CDS,WB        SET CDTYP FOR SIMPLE CASE
       EJC
*
*      CMPIL (CONTINUED)
*
*      MERGE HERE TO BUILD CDBLK
*
*      (WA)                  CDFAL VALUE TO BE GENERATED
*      (WB)                  CDTYP VALUE TO BE GENERATED
*
*      AT THIS STAGE, WE CHOP OFF AN APPROPRIATE CHUNK OF THE
*      CURRENT CCBLK AND CONVERT IT INTO A CDBLK. THE REMAINDER
*      OF THE CCBLK IS REFORMATTED TO BE THE NEW CCBLK.
*
CMP25  MOV  R_CCB,XR         POINT TO CCBLK
       MOV  CMLBL(XS),XL     GET POSSIBLE LABEL POINTER
       BZE  XL,CMP26         SKIP IF NO LABEL
       ZER  CMLBL(XS)        CLEAR FLAG FOR NEXT STATEMENT
       MOV  XR,VRLBL(XL)     PUT CDBLK PTR IN VRBLK LABEL FIELD
*
*      MERGE AFTER DOING LABEL
*
CMP26  MOV  WB,(XR)          SET TYPE WORD FOR NEW CDBLK
       MOV  WA,CDFAL(XR)     SET FAILURE WORD
       MOV  XR,XL            COPY POINTER TO CCBLK
       MOV  CCUSE(XR),WB     LOAD LENGTH GEN (= NEW CDLEN)
       MOV  CCLEN(XR),WC     LOAD TOTAL CCBLK LENGTH
       ADD  WB,XL            POINT PAST CDBLK
       SUB  WB,WC            GET LENGTH LEFT FOR CHOP OFF
       MOV  =B_CCT,(XL)      SET TYPE CODE FOR NEW CCBLK AT END
       MOV  *CCCOD,CCUSE(XL) SET INITIAL CODE OFFSET
       MOV  *CCCOD,CWCOF     REINITIALISE CWCOF
       MOV  WC,CCLEN(XL)     SET NEW LENGTH
       MOV  XL,R_CCB         SET NEW CCBLK POINTER
.IF    .CSLN
       ZER  CCSLN(XL)        INITIALIZE NEW LINE NUMBER
       MOV  CMPLN,CDSLN(XR)  SET LINE NUMBER IN OLD BLOCK
.FI
       MOV  CMPSN,CDSTM(XR)  SET STATEMENT NUMBER
       ICV  CMPSN            BUMP STATEMENT NUMBER
*
*      SET POINTERS IN PREVIOUS CODE BLOCK AS REQUIRED
*
       MOV  CMPCD(XS),XL     LOAD PTR TO PREVIOUS CDBLK
       BZE  CMFFP(XS),CMP27  JUMP IF NO FAILURE FILL IN REQUIRED
       MOV  XR,CDFAL(XL)     ELSE SET FAILURE PTR IN PREVIOUS
*
*      HERE TO DEAL WITH SUCCESS FORWARD POINTER
*
CMP27  MOV  CMSOP(XS),WA     LOAD SUCCESS OFFSET
       BZE  WA,CMP28         JUMP IF NO FILL IN REQUIRED
       ADD  WA,XL            ELSE POINT TO FILL IN LOCATION
       MOV  XR,(XL)          STORE FORWARD POINTER
       ZER  XL               CLEAR GARBAGE XL VALUE
       EJC
*
*      CMPIL (CONTINUED)
*
*      NOW SET FILL IN POINTERS FOR THIS STATEMENT
*
CMP28  MOV  CMFFC(XS),CMFFP(XS) COPY FAILURE FILL IN FLAG
       MOV  CMSOC(XS),CMSOP(XS) COPY SUCCESS FILL IN OFFSET
       MOV  XR,CMPCD(XS)     SAVE PTR TO THIS CDBLK
       BNZ  CMTRA(XS),CMP29  JUMP IF INITIAL ENTRY ALREADY SET
       MOV  XR,CMTRA(XS)     ELSE SET PTR HERE AS DEFAULT
*
*      HERE AFTER COMPILING ONE STATEMENT
*
CMP29  BLT  STAGE,=STGCE,CMP01 JUMP IF NOT END LINE JUST DONE
       BZE  CSWLS,CMP30      SKIP IF -NOLIST
       JSR  LISTR            LIST LAST LINE
*
*      RETURN
*
CMP30  MOV  CMTRA(XS),XR     LOAD INITIAL ENTRY CDBLK POINTER
       ADD  *CMNEN,XS        POP WORK LOCATIONS OFF STACK
       EXI                   AND RETURN TO CMPIL CALLER
*
*      HERE AT END OF GOTO FIELD
*
CMP31  MOV  CMFGO(XS),WB     GET FAIL GOTO
       ORB  CMSGO(XS),WB     OR IN SUCCESS GOTO
       BNZ  WB,CMP18         OK IF NON-NULL FIELD
       ERB  219,Syntax error: Empty goto field
*
*      CONTROL CARD FOUND
*
CMP32  ICV  WB               POINT PAST CH_MN
       JSR  CNCRD            PROCESS CONTROL CARD
       ZER  SCNSE            CLEAR START OF ELEMENT LOC.
       BRN  CMPCE            LOOP FOR NEXT STATEMENT
       ENP                   END PROCEDURE CMPIL
       EJC
*
*      CNCRD -- CONTROL CARD PROCESSOR
*
*      CALLED TO DEAL WITH CONTROL CARDS
*
*      R_CIM                 POINTS TO CURRENT IMAGE
*      (WB)                  OFFSET TO 1ST CHAR OF CONTROL CARD
*      JSR  CNCRD            CALL TO PROCESS CONTROL CARDS
*      (XL,XR,WA,WB,WC,IA)   DESTROYED
*
CNCRD  PRC  E,0              ENTRY POINT
       MOV  WB,SCNPT         OFFSET FOR CONTROL CARD SCAN
       MOV  =CCNOC,WA        NUMBER OF CHARS FOR COMPARISON
       CTW  WA,0             CONVERT TO WORD COUNT
       MOV  WA,CNSWC         SAVE WORD COUNT
*
*      LOOP HERE IF MORE THAN ONE CONTROL CARD
*
CNC01  BGE  SCNPT,SCNIL,CNC09 RETURN IF END OF IMAGE
       MOV  R_CIM,XR         POINT TO IMAGE
       PLC  XR,SCNPT         CHAR PTR FOR FIRST CHAR
       LCH  WA,(XR)+         GET FIRST CHAR
.IF    .CULC
       FLC  WA               FOLD TO UPPER CASE
.FI
       BEQ  WA,=CH_LI,CNC07  SPECIAL CASE OF -INXXX
CNC0A  MNZ  SCNCC            SET FLAG FOR SCANE
       JSR  SCANE            SCAN CARD NAME
       ZER  SCNCC            CLEAR SCANE FLAG
       BNZ  XL,CNC06         FAIL UNLESS CONTROL CARD NAME
       MOV  =CCNOC,WA        NO. OF CHARS TO BE COMPARED
.IF    .CICC
       BLT  SCLEN(XR),WA,CNC08  FAIL IF TOO FEW CHARS
.ELSE
       BLT  SCLEN(XR),WA,CNC06  FAIL IF TOO FEW CHARS
.FI
       MOV  XR,XL            POINT TO CONTROL CARD NAME
       ZER  WB               ZERO OFFSET FOR SUBSTRING
       JSR  SBSTR            EXTRACT SUBSTRING FOR COMPARISON
.IF    .CULC
       MOV  SCLEN(XR),WA     RELOAD LENGTH
       JSR  FLSTG            FOLD TO UPPER CASE
.FI
       MOV  XR,CNSCC         KEEP CONTROL CARD SUBSTRING PTR
       MOV  =CCNMS,XR        POINT TO LIST OF STANDARD NAMES
       ZER  WB               INITIALISE NAME OFFSET
       LCT  WC,=CC_NC        NUMBER OF STANDARD NAMES
*
*      TRY TO MATCH NAME
*
CNC02  MOV  CNSCC,XL         POINT TO NAME
       LCT  WA,CNSWC         COUNTER FOR INNER LOOP
       BRN  CNC04            JUMP INTO LOOP
*
*      INNER LOOP TO MATCH CARD NAME CHARS
*
CNC03  ICA  XR               BUMP STANDARD NAMES PTR
       ICA  XL               BUMP NAME POINTER
*
*      HERE TO INITIATE THE LOOP
*
CNC04  CNE  SCHAR(XL),(XR),CNC05 COMP. UP TO CFP_C CHARS AT ONCE
       BCT  WA,CNC03         LOOP IF MORE WORDS TO COMPARE
       EJC
*
*      CNCRD (CONTINUED)
*
*      MATCHED - BRANCH ON CARD OFFSET
*
       MOV  WB,XL            GET NAME OFFSET
.IF    .CICC
       BSW  XL,CC_NC,CNC08   SWITCH
.ELSE
       BSW  XL,CC_NC,CNC06   SWITCH
.FI
.IF    .CULC
       IFF  CC_CA,CNC37      -CASE
.FI
.IF    .CCMC
       IFF  CC_CO,CNC39      -COMPARE
.FI
       IFF  CC_DO,CNC10      -DOUBLE
       IFF  CC_DU,CNC11      -DUMP
.IF    .CINC
       IFF  CC_CP,CNC41      -COPY
.FI
       IFF  CC_EJ,CNC12      -EJECT
       IFF  CC_ER,CNC13      -ERRORS
       IFF  CC_EX,CNC14      -EXECUTE
       IFF  CC_FA,CNC15      -FAIL
.IF    .CINC
       IFF  CC_IN,CNC41      -INCLUDE
.FI
.IF    .CSLN
       IFF  CC_LN,CNC44      -LINE
.FI
       IFF  CC_LI,CNC16      -LIST
       IFF  CC_NR,CNC17      -NOERRORS
       IFF  CC_NX,CNC18      -NOEXECUTE
       IFF  CC_NF,CNC19      -NOFAIL
       IFF  CC_NL,CNC20      -NOLIST
       IFF  CC_NO,CNC21      -NOOPT
       IFF  CC_NP,CNC22      -NOPRINT
       IFF  CC_OP,CNC24      -OPTIMISE
       IFF  CC_PR,CNC25      -PRINT
       IFF  CC_SI,CNC27      -SINGLE
       IFF  CC_SP,CNC28      -SPACE
       IFF  CC_ST,CNC31      -STITLE
       IFF  CC_TI,CNC32      -TITLE
       IFF  CC_TR,CNC36      -TRACE
       ESW                   END SWITCH
*
*      NOT MATCHED YET. ALIGN STD NAMES PTR AND TRY AGAIN
*
CNC05  ICA  XR               BUMP STANDARD NAMES PTR
       BCT  WA,CNC05         LOOP
       ICV  WB               BUMP NAMES OFFSET
       BCT  WC,CNC02         CONTINUE IF MORE NAMES
.IF    .CICC
       BRN  CNC08            IGNORE UNRECOGNIZED CONTROL CARD
.FI
*
*      INVALID CONTROL CARD NAME
*
CNC06  ERB  247,Invalid control statement
*
*      SPECIAL PROCESSING FOR -INXXX
*
CNC07  LCH  WA,(XR)+         GET NEXT CHAR
.IF    .CULC
       FLC  WA               FOLD TO UPPER CASE
.FI
       BNE  WA,=CH_LN,CNC0A  IF NOT LETTER N
       LCH  WA,(XR)          GET THIRD CHAR
       BLT  WA,=CH_D0,CNC0A  IF NOT DIGIT
       BGT  WA,=CH_D9,CNC0A  IF NOT DIGIT
       ADD  =NUM02,SCNPT     BUMP OFFSET PAST -IN
       JSR  SCANE            SCAN INTEGER AFTER -IN
       MOV  XR,-(XS)         STACK SCANNED ITEM
       JSR  GTSMI            CHECK IF INTEGER
       PPM  CNC06            FAIL IF NOT INTEGER
       PPM  CNC06            FAIL IF NEGATIVE OR LARGE
       MOV  XR,CSWIN         KEEP INTEGER
       EJC
*
*      CNCRD (CONTINUED)
*
*      CHECK FOR MORE CONTROL CARDS BEFORE RETURNING
*
CNC08  MOV  SCNPT,WA         PRESERVE IN CASE XEQ TIME COMPILE
       JSR  SCANE            LOOK FOR COMMA
       BEQ  XL,=T_CMA,CNC01  LOOP IF COMMA FOUND
       MOV  WA,SCNPT         RESTORE SCNPT IN CASE XEQ TIME
*
*      RETURN POINT
*
CNC09  EXI                   RETURN
*
*      -DOUBLE
*
CNC10  MNZ  CSWDB            SET SWITCH
       BRN  CNC08            MERGE
*
*      -DUMP
*      THIS IS USED FOR SYSTEM DEBUGGING . IT HAS THE EFFECT OF
*      PRODUCING A CORE DUMP AT COMPILATION TIME
*
CNC11  JSR  SYSDM            CALL DUMPER
       BRN  CNC09            FINISHED
*
*      -EJECT
*
CNC12  BZE  CSWLS,CNC09      RETURN IF -NOLIST
       JSR  PRTPS            EJECT
       JSR  LISTT            LIST TITLE
       BRN  CNC09            FINISHED
*
*      -ERRORS
*
CNC13  ZER  CSWER            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -EXECUTE
*
CNC14  ZER  CSWEX            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -FAIL
*
CNC15  MNZ  CSWFL            SET SWITCH
       BRN  CNC08            MERGE
*
*      -LIST
*
CNC16  MNZ  CSWLS            SET SWITCH
       BEQ  STAGE,=STGIC,CNC08 DONE IF COMPILE TIME
*
*      LIST CODE LINE IF EXECUTE TIME COMPILE
*
       ZER  LSTPF            PERMIT LISTING
       JSR  LISTR            LIST LINE
       BRN  CNC08            MERGE
       EJC
*
*      CNCRD (CONTINUED)
*
*      -NOERRORS
*
CNC17  MNZ  CSWER            SET SWITCH
       BRN  CNC08            MERGE
*
*      -NOEXECUTE
*
CNC18  MNZ  CSWEX            SET SWITCH
       BRN  CNC08            MERGE
*
*      -NOFAIL
*
CNC19  ZER  CSWFL            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -NOLIST
*
CNC20  ZER  CSWLS            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -NOOPTIMISE
*
CNC21  MNZ  CSWNO            SET SWITCH
       BRN  CNC08            MERGE
*
*      -NOPRINT
*
CNC22  ZER  CSWPR            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -OPTIMISE
*
CNC24  ZER  CSWNO            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -PRINT
*
CNC25  MNZ  CSWPR            SET SWITCH
       BRN  CNC08            MERGE
       EJC
*
*      CNCRD (CONTINUED)
*
*      -SINGLE
*
CNC27  ZER  CSWDB            CLEAR SWITCH
       BRN  CNC08            MERGE
*
*      -SPACE
*
CNC28  BZE  CSWLS,CNC09      RETURN IF -NOLIST
       JSR  SCANE            SCAN INTEGER AFTER -SPACE
       MOV  =NUM01,WC        1 SPACE IN CASE
       BEQ  XR,=T_SMC,CNC29  JUMP IF NO INTEGER
       MOV  XR,-(XS)         STACK IT
       JSR  GTSMI            CHECK INTEGER
       PPM  CNC06            FAIL IF NOT INTEGER
       PPM  CNC06            FAIL IF NEGATIVE OR LARGE
       BNZ  WC,CNC29         JUMP IF NON ZERO
       MOV  =NUM01,WC        ELSE 1 SPACE
*
*      MERGE WITH COUNT OF LINES TO SKIP
*
CNC29  ADD  WC,LSTLC         BUMP LINE COUNT
       LCT  WC,WC            CONVERT TO LOOP COUNTER
       BLT  LSTLC,LSTNP,CNC30 JUMP IF FITS ON PAGE
       JSR  PRTPS            EJECT
       JSR  LISTT            LIST TITLE
       BRN  CNC09            MERGE
*
*      SKIP LINES
*
CNC30  JSR  PRTNL            PRINT A BLANK
       BCT  WC,CNC30         LOOP
       BRN  CNC09            MERGE
       EJC
*
*      CNCRD (CONTINUED)
*
*      -STITL
*
CNC31  MOV  =R_STL,CNR_T     PTR TO R_STL
       BRN  CNC33            MERGE
*
*      -TITLE
*
CNC32  MOV  =NULLS,R_STL     CLEAR SUBTITLE
       MOV  =R_TTL,CNR_T     PTR TO R_TTL
*
*      COMMON PROCESSING FOR -TITLE, -STITL
*
CNC33  MOV  =NULLS,XR        NULL IN CASE NEEDED
       MNZ  CNTTL            SET FLAG FOR NEXT LISTR CALL
       MOV  =CCOFS,WB        OFFSET TO TITLE/SUBTITLE
       MOV  SCNIL,WA         INPUT IMAGE LENGTH
       BLO  WA,WB,CNC34      JUMP IF NO CHARS LEFT
       SUB  WB,WA            NO OF CHARS TO EXTRACT
       MOV  R_CIM,XL         POINT TO IMAGE
       JSR  SBSTR            GET TITLE/SUBTITLE
*
*      STORE TITLE/SUBTITLE
*
CNC34  MOV  CNR_T,XL         POINT TO STORAGE LOCATION
       MOV  XR,(XL)          STORE TITLE/SUBTITLE
       BEQ  XL,=R_STL,CNC09  RETURN IF STITL
       BNZ  PRECL,CNC09      RETURN IF EXTENDED LISTING
       BZE  PRICH,CNC09      RETURN IF REGULAR PRINTER
       MOV  SCLEN(XR),XL     GET LENGTH OF TITLE
       MOV  XL,WA            COPY IT
       BZE  XL,CNC35         JUMP IF NULL
       ADD  =NUM10,XL        INCREMENT
       BHI  XL,PRLEN,CNC09   USE DEFAULT LSTP0 VAL IF TOO LONG
       ADD  =NUM04,WA        POINT JUST PAST TITLE
*
*      STORE OFFSET TO PAGE NN MESSAGE FOR SHORT TITLE
*
CNC35  MOV  WA,LSTPO         STORE OFFSET
       BRN  CNC09            RETURN
*
*      -TRACE
*      PROVIDED FOR SYSTEM DEBUGGING.  TOGGLES THE SYSTEM LABEL
*      TRACE SWITCH AT COMPILE TIME
*
CNC36  JSR  SYSTT            TOGGLE SWITCH
       BRN  CNC08            MERGE
.IF    .CULC
*
*      -CASE
*      SETS VALUE OF KVCAS SO THAT NAMES ARE FOLDED OR NOT
*      DURING COMPILATION.
*
CNC37  JSR  SCANE            SCAN INTEGER AFTER -CASE
       ZER  WC               GET 0 IN CASE NONE THERE
       BEQ  XL,=T_SMC,CNC38  SKIP IF NO INTEGER
       MOV  XR,-(XS)         STACK IT
       JSR  GTSMI            CHECK INTEGER
       PPM  CNC06            FAIL IF NOT INTEGER
       PPM  CNC06            FAIL IF NEGATIVE OR TOO LARGE
CNC38  MOV  WC,KVCAS         STORE NEW CASE VALUE
       BRN  CNC09            MERGE
.FI
.IF    .CCMC
*
*      -COMPARE
*
*      SETS VALUE OF KVCOM SO THAT STRING COMPARISONS MAY
*      FOLLOW COLLATION SEQUENCE DETERMINED BY THE INTERFACE.
*
CNC39  JSR  SCANE            SCAN INTEGER AFTER -COMPARE
       ZER  WC               GET 0 IN CASE NONE THERE
       BEQ  XL,=T_SMC,CNC40  SKIP IF NO INTEGER
       MOV  XR,-(XS)         STACK IT
       JSR  GTSMI            CHECK INTEGER
       PPM  CNC06            FAIL IF NOT INTEGER
       PPM  CNC06            FAIL IF NEGATIVE OR TOO LARGE
CNC40  MOV  WC,KVCOM         STORE NEW COMPARE VALUE
       BRN  CNC09            MERGE
.FI
.IF    .CINC
*
*      -INCLUDE
*
CNC41  MNZ  SCNCC            SET FLAG FOR SCANE
       JSR  SCANE            SCAN QUOTED FILE NAME
       ZER  SCNCC            CLEAR SCANE FLAG
       BNE  XL,=T_CON,CNC06  IF NOT CONSTANT
       BNE  (XR),=B_SCL,CNC06  IF NOT STRING CONSTANT
       MOV  XR,R_IFN         SAVE FILE NAME
       MOV  R_INC,XL         EXAMINE INCLUDE FILE NAME TABLE
       ZER  WB               LOOKUP BY VALUE
       JSR  TFIND            DO LOOKUP
       PPM                   NEVER FAILS
       BEQ  XR,=INTON,CNC09  IGNORE IF ALREADY IN TABLE
       MNZ  WB               SET FOR TRIM
       MOV  R_IFN,XR         FILE NAME
       JSR  TRIMR            REMOVE TRAILING BLANKS
       MOV  R_INC,XL         INCLUDE FILE NAME TABLE
       MNZ  WB               LOOKUP BY NAME THIS TIME
       JSR  TFIND            DO LOOKUP
       PPM                   NEVER FAILS
       MOV  =INTON,TEVAL(XL) MAKE TABLE VALUE INTEGER 1
       ICV  CNIND            INCREASE NESTING LEVEL
       MOV  CNIND,WA         LOAD NEW NEST LEVEL
       BGT  WA,=CCINM,CNC42  FAIL IF EXCESSIVE NESTING
.IF    .CSFN
*
*      RECORD THE NAME AND LINE NUMBER OF THE CURRENT INPUT FILE
*
       MOV  R_IFA,XL         ARRAY OF NESTED FILE NAMES
       ADD  =VCVLB,WA        COMPUTE OFFSET IN WORDS
       WTB  WA               CONVERT TO BYTES
       ADD  WA,XL            POINT TO ELEMENT
       MOV  R_SFC,(XL)       RECORD CURRENT FILE NAME
       MOV  WA,XL            PRESERVE NESTING BYTE OFFSET
       MTI  RDNLN            FETCH SOURCE LINE NUMBER AS INTEGER
       JSR  ICBLD            CONVERT TO ICBLK
       ADD  R_IFL,XL         ENTRY IN NESTED LINE NUMBER ARRAY
       MOV  XR,(XL)          RECORD IN ARRAY
.FI
*
*      HERE TO SWITCH TO INCLUDE FILE NAMED IN R_IFN
*
       MOV  CSWIN,WA         MAX READ LENGTH
       MOV  R_IFN,XL         INCLUDE FILE NAME
       JSR  ALOCS            GET BUFFER FOR COMPLETE FILE NAME
       JSR  SYSIF            OPEN INCLUDE FILE
       PPM  CNC43            COULD NOT OPEN
.IF    .CSFN
*
*      MAKE NOTE OF THE COMPLETE FILE NAME FOR ERROR MESSAGES
*
       ZER  WB               DO NOT TRIM TRAILING BLANKS
       JSR  TRIMR            ADJUST SCBLK FOR ACTUAL LENGTH
       MOV  XR,R_SFC         SAVE PTR TO FILE NAME
       MTI  CMPSN            CURRENT STATEMENT AS INTEGER
       JSR  ICBLD            BUILD ICBLK FOR STMT NUMBER
       MOV  R_SFN,XL         FILE NAME TABLE
       MNZ  WB               LOOKUP STATEMENT NUMBER BY NAME
       JSR  TFIND            ALLOCATE NEW TEBLK
       PPM                   ALWAYS POSSIBLE TO ALLOCATE BLOCK
       MOV  R_SFC,TEVAL(XL)  RECORD FILE NAME AS ENTRY VALUE
.ELSE
       MOV  XR,DNAMP         RELEASE ALLOCATED SCBLK
.FI
       ZER  RDNLN            RESTART LINE COUNTER FOR NEW FILE
       BEQ  STAGE,=STGIC,CNC09 IF INITIAL COMPILE
       BNE  CNIND,=NUM01,CNC09 IF NOT FIRST EXECUTE-TIME NESTING
*
*      HERE FOR -INCLUDE DURING EXECUTE-TIME COMPILE
*
       MOV  R_CIM,R_ICI      REMEMBER CODE ARGUMENT STRING
       MOV  SCNPT,CNSPT      SAVE POSITION IN STRING
       MOV  SCNIL,CNSIL      AND LENGTH OF STRING
       BRN  CNC09            ALL DONE, MERGE
*
*      HERE FOR EXCESSIVE INCLUDE FILE NESTING
*
CNC42  ERB  284,Excessively nested INCLUDE files
*
*      HERE IF INCLUDE FILE COULD NOT BE OPENED
*
CNC43  MOV  XR,DNAMP         RELEASE ALLOCATED SCBLK
       ERB  285,INCLUDE file cannot be opened
*
.FI
.IF    .CSLN
*
*      -LINE N FILENAME
*
CNC44  JSR  SCANE            SCAN INTEGER AFTER -LINE
       BNE  XL,=T_CON,CNC06  JUMP IF NO LINE NUMBER
       BNE  (XR),=B_ICL,CNC06 JUMP IF NOT INTEGER
       LDI  ICVAL(XR)        FETCH INTEGER LINE NUMBER
       ILE  CNC06            ERROR IF NEGATIVE OR ZERO
       BEQ  STAGE,=STGIC,CNC45 SKIP IF INITIAL COMPILE
       MFI  CMPLN            SET DIRECTLY FOR OTHER COMPILES
       BRN  CNC46            NO NEED TO SET RDNLN
CNC45  SBI  INTV1            ADJUST NUMBER BY ONE
       MFI  RDNLN            SAVE LINE NUMBER
.IF    .CSFN
CNC46  MNZ  SCNCC            SET FLAG FOR SCANE
       JSR  SCANE            SCAN QUOTED FILE NAME
       ZER  SCNCC            CLEAR SCANE FLAG
       BEQ  XL,=T_SMC,CNC47  DONE IF NO FILE NAME
       BNE  XL,=T_CON,CNC06  ERROR IF NOT CONSTANT
       BNE  (XR),=B_SCL,CNC06 IF NOT STRING CONSTANT
       JSR  NEWFN            RECORD NEW FILE NAME
       BRN  CNC09            MERGE
*
*      HERE IF FILE NAME NOT PRESENT
*
CNC47  DCV  SCNPT            SET TO RESCAN THE TERMINATOR
       BRN  CNC09            MERGE
.ELSE
CNC46  BRN  CNC09            MERGE
.FI
.FI
       ENP                   END PROCEDURE CNCRD
       EJC
.IF    .CENG
*
*      ENEVS -- EVALUATE STRING EXPRESSION FOR ENGINE
*
*      ENEVS IS USED BY THE EXTERNAL INTERFACE TO EVALUATE A
*      STRING EXPRESSION, TYPICALLY FOR AN ENGINE WISHING TO
*      OBTAIN THE VALUE OF A VARIABLE OR EXPRESSION.
*
.IF    .CEVB
*      (WB)                  0 IF BY VALUE, 1 IF BY NAME
.FI
*      (XR)                  SCBLK FOR STRING TO EVALUATE
*      JSR  ENEVS            CALL TO CONVERT AND EVALUATE
*      (XR)                  POINTER TO RESULT
*                            = 0 IF EXPRESSION EVALUATION FAILED
*                            = 1 IF CONVERSION TO EXPRESSION FAILED
*
ENEVS  PRC  R,0              ENTRY POINT (RECURSIVE)
.IF    .CEVB
       MOV  WB,-(XS)         SAVE VALUE/NAME FLAG
.FI
       JSR  GTEXP            CONVERT TO EXPRESSION
       PPM  ENEV2            CONVERSION FAILS
.IF    .CEVB
       MOV  (XS)+,WB         RECOVER VALUE/NAME FLAG
.FI
       JSR  EVALX            EVALUATE EXPRESSION BY VALUE
       PPM  ENEV1            EVALUATION FAILS
       EXI
*
*      HERE IF EXPRESSION EVALUATION FAILED
*
ENEV1  ZER  XR               RETURN ZERO RESULT
       EXI
*
*      HERE IF CONVERSION TO EXPRESSION FAILED
*
.IF    .CEVB
ENEV2  ICA  XS               DISCARD VALUE/NAME FLAG
       MOV  =NUM01,XR        RETURN INTEGER ONE RESULT
.ELSE
ENEV2  MOV  =NUM01,XR        RETURN INTEGER ONE RESULT
.FI
       EXI
       ENP
       EJC
*
*      ENGTS -- GET STRING FOR ENGINE
*
*      ENGTS IS PASSED AN OBJECT AND RETURNS A STRING WITH
*      ANY NECESSARY CONVERSIONS PERFORMED.
*
*      (XR)                  INPUT ARGUMENT
*      JSR  ENGTS            CALL TO CONVERT TO STRING
*      (XR)                  POINTER TO RESULTING STRING
*                            =0 IF CONVERSION NOT POSSIBLE
*
ENGTS  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         STACK ARGUMENT TO CONVERT
       JSR  GTSTG            CONVERT TO STRING
       PPM  ENGT1            CONVERT IMPOSSIBLE
       EXI
*
*      HERE IF UNABLE TO CONVERT TO STRING
*
ENGT1  ZER  XR               RETURN ZERO
       EXI
       ENP
       EJC
.FI
*
*      DFFNC -- DEFINE FUNCTION
*
*      DFFNC IS CALLED WHENEVER A NEW FUNCTION IS ASSIGNED TO
*      A VARIABLE. IT DEALS WITH EXTERNAL FUNCTION USE COUNTS.
*
*      (XR)                  POINTER TO VRBLK
*      (XL)                  POINTER TO NEW FUNCTION BLOCK
*      JSR  DFFNC            CALL TO DEFINE FUNCTION
*      (WA,WB)               DESTROYED
*
DFFNC  PRC  E,0              ENTRY POINT
.IF    .CNLD
.ELSE
       BNE  (XL),=B_EFC,DFFN1 SKIP IF NEW FUNCTION NOT EXTERNAL
       ICV  EFUSE(XL)        ELSE INCREMENT ITS USE COUNT
*
*      HERE AFTER DEALING WITH NEW FUNCTION USE COUNT
*
DFFN1  MOV  XR,WA            SAVE VRBLK POINTER
       MOV  VRFNC(XR),XR     LOAD OLD FUNCTION POINTER
       BNE  (XR),=B_EFC,DFFN2 JUMP IF OLD FUNCTION NOT EXTERNAL
       MOV  EFUSE(XR),WB     ELSE GET USE COUNT
       DCV  WB               DECREMENT
       MOV  WB,EFUSE(XR)     STORE DECREMENTED VALUE
       BNZ  WB,DFFN2         JUMP IF USE COUNT STILL NON-ZERO
       JSR  SYSUL            ELSE CALL SYSTEM UNLOAD FUNCTION
*
*      HERE AFTER DEALING WITH OLD FUNCTION USE COUNT
*
DFFN2  MOV  WA,XR            RESTORE VRBLK POINTER
.FI
       MOV  XL,WA            COPY FUNCTION BLOCK PTR
       BLT  XR,=R_YYY,DFFN3  SKIP CHECKS IF OPSYN OP DEFINITION
       BNZ  VRLEN(XR),DFFN3  JUMP IF NOT SYSTEM VARIABLE
*
*      FOR SYSTEM VARIABLE, CHECK FOR ILLEGAL REDEFINITION
*
       MOV  VRSVP(XR),XL     POINT TO SVBLK
       MOV  SVBIT(XL),WB     LOAD BIT INDICATORS
       ANB  BTFNC,WB         IS IT A SYSTEM FUNCTION
       ZRB  WB,DFFN3         REDEF OK IF NOT
       ERB  248,Attempted redefinition of system function
*
*      HERE IF REDEFINITION IS PERMITTED
*
DFFN3  MOV  WA,VRFNC(XR)     STORE NEW FUNCTION POINTER
       MOV  WA,XL            RESTORE FUNCTION BLOCK POINTER
       EXI                   RETURN TO DFFNC CALLER
       ENP                   END PROCEDURE DFFNC
       EJC
*
*      DTACH -- DETACH I/O ASSOCIATED NAMES
*
*      DETACHES TRBLKS FROM I/O ASSOCIATED VARIABLES, REMOVES
*      ENTRY FROM IOCHN CHAIN ATTACHED TO FILEARG1 VRBLK AND MAY
*      REMOVE VRBLK ACCESS AND STORE TRAPS.
*      INPUT, OUTPUT, TERMINAL ARE HANDLED SPECIALLY.
*
*      (XL)                  I/O ASSOC. VBL NAME BASE PTR
*      (WA)                  OFFSET TO NAME
*      JSR  DTACH            CALL FOR DETACH OPERATION
*      (XL,XR,WA,WB,WC)      DESTROYED
*
DTACH  PRC  E,0              ENTRY POINT
       MOV  XL,DTCNB         STORE NAME BASE (GBCOL NOT CALLED)
       ADD  WA,XL            POINT TO NAME LOCATION
       MOV  XL,DTCNM         STORE IT
*
*      LOOP TO SEARCH FOR I/O TRBLK
*
DTCH1  MOV  XL,XR            COPY NAME POINTER
*
*      CONTINUE AFTER BLOCK DELETION
*
DTCH2  MOV  (XL),XL          POINT TO NEXT VALUE
       BNE  (XL),=B_TRT,DTCH6 JUMP AT CHAIN END
       MOV  TRTYP(XL),WA     GET TRAP BLOCK TYPE
       BEQ  WA,=TRTIN,DTCH3  JUMP IF INPUT
       BEQ  WA,=TRTOU,DTCH3  JUMP IF OUTPUT
       ADD  *TRNXT,XL        POINT TO NEXT LINK
       BRN  DTCH1            LOOP
*
*      DELETE AN OLD ASSOCIATION
*
DTCH3  MOV  TRVAL(XL),(XR)   DELETE TRBLK
       MOV  XL,WA            DUMP XL ...
       MOV  XR,WB            ... AND XR
       MOV  TRTRF(XL),XL     POINT TO TRTRF TRAP BLOCK
       BZE  XL,DTCH5         JUMP IF NO IOCHN
       BNE  (XL),=B_TRT,DTCH5 JUMP IF INPUT, OUTPUT, TERMINAL
*
*      LOOP TO SEARCH IOCHN CHAIN FOR NAME PTR
*
DTCH4  MOV  XL,XR            REMEMBER LINK PTR
       MOV  TRTRF(XL),XL     POINT TO NEXT LINK
       BZE  XL,DTCH5         JUMP IF END OF CHAIN
       MOV  IONMB(XL),WC     GET NAME BASE
       ADD  IONMO(XL),WC     ADD OFFSET
       BNE  WC,DTCNM,DTCH4   LOOP IF NO MATCH
       MOV  TRTRF(XL),TRTRF(XR) REMOVE NAME FROM CHAIN
       EJC
*
*      DTACH (CONTINUED)
*
*      PREPARE TO RESUME I/O TRBLK SCAN
*
DTCH5  MOV  WA,XL            RECOVER XL ...
       MOV  WB,XR            ... AND XR
       ADD  *TRVAL,XL        POINT TO VALUE FIELD
       BRN  DTCH2            CONTINUE
*
*      EXIT POINT
*
DTCH6  MOV  DTCNB,XR         POSSIBLE VRBLK PTR
       JSR  SETVR            RESET VRBLK IF NECESSARY
       EXI                   RETURN
       ENP                   END PROCEDURE DTACH
       EJC
*
*      DTYPE -- GET DATATYPE NAME
*
*      (XR)                  OBJECT WHOSE DATATYPE IS REQUIRED
*      JSR  DTYPE            CALL TO GET DATATYPE
*      (XR)                  RESULT DATATYPE
*
DTYPE  PRC  E,0              ENTRY POINT
       BEQ  (XR),=B_PDT,DTYP1   JUMP IF PROG.DEFINED
       MOV  (XR),XR          LOAD TYPE WORD
       LEI  XR               GET ENTRY POINT ID (BLOCK CODE)
       WTB  XR               CONVERT TO BYTE OFFSET
       MOV  SCNMT(XR),XR     LOAD TABLE ENTRY
       EXI                   EXIT TO DTYPE CALLER
*
*      HERE IF PROGRAM DEFINED
*
DTYP1  MOV  PDDFP(XR),XR     POINT TO DFBLK
       MOV  DFNAM(XR),XR     GET DATATYPE NAME FROM DFBLK
       EXI                   RETURN TO DTYPE CALLER
       ENP                   END PROCEDURE DTYPE
       EJC
*
*      DUMPR -- PRINT DUMP OF STORAGE
*
*      (XR)                  DUMP ARGUMENT (SEE BELOW)
*      JSR  DUMPR            CALL TO PRINT DUMP
*      (XR,XL)               DESTROYED
*      (WA,WB,WC,RA)         DESTROYED
*
*      THE DUMP ARGUMENT HAS THE FOLLOWING SIGNIFICANCE
*
*      DMARG = 0             NO DUMP PRINTED
*      DMARG = 1             PARTIAL DUMP (NAT VARS, KEYWORDS)
*      DMARG = 2             FULL DUMP (ARRAYS, TABLES, ETC.)
*      DMARG = 3             FULL DUMP + NULL VARIABLES
*      DMARG GE 4            CORE DUMP
*
*      SINCE DUMPR SCRAMBLES STORE, IT IS NOT PERMISSIBLE TO
*      COLLECT IN MID-DUMP. HENCE A COLLECT IS DONE INITIALLY
*      AND THEN IF STORE RUNS OUT AN ERROR MESSAGE IS PRODUCED.
*
DUMPR  PRC  E,0              ENTRY POINT
       BZE  XR,DMP28         SKIP DUMP IF ARGUMENT IS ZERO
       BGT  XR,=NUM03,DMP29  JUMP IF CORE DUMP REQUIRED
       ZER  XL               CLEAR XL
       ZER  WB               ZERO MOVE OFFSET
       MOV  XR,DMARG         SAVE DUMP ARGUMENT
.IF    .CSED
       ZER  DNAMS            COLLECT SEDIMENT TOO
.FI
       JSR  GBCOL            COLLECT GARBAGE
       JSR  PRTPG            EJECT PRINTER
       MOV  =DMHDV,XR        POINT TO HEADING FOR VARIABLES
       JSR  PRTST            PRINT IT
       JSR  PRTNL            TERMINATE PRINT LINE
       JSR  PRTNL            AND PRINT A BLANK LINE
*
*      FIRST ALL NATURAL VARIABLE BLOCKS (VRBLK) WHOSE VALUES
*      ARE NON-NULL ARE LINKED IN LEXICAL ORDER USING DMVCH AS
*      THE CHAIN HEAD AND CHAINING THROUGH THE VRGET FIELDS.
*      NOTE THAT THIS SCRAMBLES STORE IF THE PROCESS IS
*      INTERRUPTED BEFORE COMPLETION E.G. BY EXCEEDING TIME  OR
*      PRINT LIMITS. SINCE THE SUBSEQUENT CORE DUMPS AND
*      FAILURES IF EXECUTION IS RESUMED ARE VERY CONFUSING, THE
*      EXECUTION TIME ERROR ROUTINE CHECKS FOR THIS EVENT AND
*      ATTEMPTS AN UNSCRAMBLE. SIMILAR PRECAUTIONS SHOULD BE
*      OBSERVED IF TRANSLATE TIME DUMPING IS IMPLEMENTED.
*
       ZER  DMVCH            SET NULL CHAIN TO START
       MOV  HSHTB,WA         POINT TO HASH TABLE
*
*      LOOP THROUGH HEADERS IN HASH TABLE
*
DMP00  MOV  WA,XR            COPY HASH BUCKET POINTER
       ICA  WA               BUMP POINTER
       SUB  *VRNXT,XR        SET OFFSET TO MERGE
*
*      LOOP THROUGH VRBLKS ON ONE CHAIN
*
DMP01  MOV  VRNXT(XR),XR     POINT TO NEXT VRBLK ON CHAIN
       BZE  XR,DMP09         JUMP IF END OF THIS HASH CHAIN
       MOV  XR,XL            ELSE COPY VRBLK POINTER
       EJC
*
*      DUMPR (CONTINUED)
*
*      LOOP TO FIND VALUE AND SKIP IF NULL
*
DMP02  MOV  VRVAL(XL),XL     LOAD VALUE
       BEQ  DMARG,=NUM03,DMP2A SKIP NULL VALUE CHECK IF DUMP(3)
       BEQ  XL,=NULLS,DMP01  LOOP FOR NEXT VRBLK IF NULL VALUE
DMP2A  BEQ  (XL),=B_TRT,DMP02 LOOP BACK IF VALUE IS TRAPPED
*
*      NON-NULL VALUE, PREPARE TO SEARCH CHAIN
*
       MOV  XR,WC            SAVE VRBLK POINTER
       ADD  *VRSOF,XR        ADJUST PTR TO BE LIKE SCBLK PTR
       BNZ  SCLEN(XR),DMP03  JUMP IF NON-SYSTEM VARIABLE
       MOV  VRSVO(XR),XR     ELSE LOAD PTR TO NAME IN SVBLK
*
*      HERE WITH NAME POINTER FOR NEW BLOCK IN XR
*
DMP03  MOV  XR,WB            SAVE POINTER TO CHARS
       MOV  WA,DMPSV         SAVE HASH BUCKET POINTER
       MOV  =DMVCH,WA        POINT TO CHAIN HEAD
*
*      LOOP TO SEARCH CHAIN FOR CORRECT INSERTION POINT
*
DMP04  MOV  WA,DMPCH         SAVE CHAIN POINTER
       MOV  WA,XL            COPY IT
       MOV  (XL),XR          LOAD POINTER TO NEXT ENTRY
       BZE  XR,DMP08         JUMP IF END OF CHAIN TO INSERT
       ADD  *VRSOF,XR        ELSE GET NAME PTR FOR CHAINED VRBLK
       BNZ  SCLEN(XR),DMP05  JUMP IF NOT SYSTEM VARIABLE
       MOV  VRSVO(XR),XR     ELSE POINT TO NAME IN SVBLK
*
*      HERE PREPARE TO COMPARE THE NAMES
*
*      (WA)                  SCRATCH
*      (WB)                  POINTER TO STRING OF ENTERING VRBLK
*      (WC)                  POINTER TO ENTERING VRBLK
*      (XR)                  POINTER TO STRING OF CURRENT BLOCK
*      (XL)                  SCRATCH
*
DMP05  MOV  WB,XL            POINT TO ENTERING VRBLK STRING
       MOV  SCLEN(XL),WA     LOAD ITS LENGTH
       PLC  XL               POINT TO CHARS OF ENTERING STRING
.IF    .CCMC
       MOV  WB,DMPSB         SAVE WB
       MOV  SCLEN(XR),WB     LENGTH OF OLD STRING
       PLC  XR               POINT TO CHARS OF OLD STRING
       JSR  SYSCM            GENERALIZED LEXICAL COMPARE
       PPM  DMP06            STRING TOO LONG, TREAT LIKE EQ
       PPM  DMP06            ENTERING STRING LT OLD STRING
       PPM  DMP07            ENTERING STRING GT OLD STRING
*
*      HERE WHEN ENTERING STRING LE OLD STRING
*
DMP06  MOV  DMPSB,WB         RESTORE WB
       BRN  DMP08            FOUND INSERTION POINT
       EJC
*
*      DUMPR (CONTINUED)
*
*      HERE WE MOVE OUT ON THE CHAIN
*
DMP07  MOV  DMPSB,WB         RESTORE WB
       MOV  DMPCH,XL         COPY CHAIN POINTER
.ELSE
       BHI  WA,SCLEN(XR),DMP06 JUMP IF ENTERING LENGTH HIGH
       PLC  XR               ELSE POINT TO CHARS OF OLD STRING
       CMC  DMP08,DMP07      COMPARE, INSERT IF NEW IS LLT OLD
       BRN  DMP08            OR IF LEQ (WE HAD SHORTER LENGTH)
*
*      HERE WHEN NEW LENGTH IS LONGER THAN OLD LENGTH
*
DMP06  MOV  SCLEN(XR),WA     LOAD SHORTER LENGTH
       PLC  XR               POINT TO CHARS OF OLD STRING
       CMC  DMP08,DMP07      COMPARE, INSERT IF NEW ONE LOW
       EJC
*
*      DUMPR (CONTINUED)
*
*      HERE WE MOVE OUT ON THE CHAIN
*
DMP07  MOV  DMPCH,XL         COPY CHAIN POINTER
.FI
       MOV  (XL),WA          MOVE TO NEXT ENTRY ON CHAIN
       BRN  DMP04            LOOP BACK
*
*      HERE AFTER LOCATING THE PROPER INSERTION POINT
*
DMP08  MOV  DMPCH,XL         COPY CHAIN POINTER
       MOV  DMPSV,WA         RESTORE HASH BUCKET POINTER
       MOV  WC,XR            RESTORE VRBLK POINTER
       MOV  (XL),VRGET(XR)   LINK VRBLK TO REST OF CHAIN
       MOV  XR,(XL)          LINK VRBLK INTO CURRENT CHAIN LOC
       BRN  DMP01            LOOP BACK FOR NEXT VRBLK
*
*      HERE AFTER PROCESSING ALL VRBLKS ON ONE CHAIN
*
DMP09  BNE  WA,HSHTE,DMP00   LOOP BACK IF MORE BUCKETS TO GO
*
*      LOOP TO GENERATE DUMP OF NATURAL VARIABLE VALUES
*
DMP10  MOV  DMVCH,XR         LOAD POINTER TO NEXT ENTRY ON CHAIN
       BZE  XR,DMP11         JUMP IF END OF CHAIN
       MOV  (XR),DMVCH       ELSE UPDATE CHAIN PTR TO NEXT ENTRY
       JSR  SETVR            RESTORE VRGET FIELD
       MOV  XR,XL            COPY VRBLK POINTER (NAME BASE)
       MOV  *VRVAL,WA        SET OFFSET FOR VRBLK NAME
       JSR  PRTNV            PRINT NAME = VALUE
       BRN  DMP10            LOOP BACK TILL ALL PRINTED
*
*      PREPARE TO PRINT KEYWORDS
*
DMP11  JSR  PRTNL            PRINT BLANK LINE
       JSR  PRTNL            AND ANOTHER
       MOV  =DMHDK,XR        POINT TO KEYWORD HEADING
       JSR  PRTST            PRINT HEADING
       JSR  PRTNL            END LINE
       JSR  PRTNL            PRINT ONE BLANK LINE
       MOV  =VDMKW,XL        POINT TO LIST OF KEYWORD SVBLK PTRS
       EJC
*
*      DUMPR (CONTINUED)
*
*      LOOP TO DUMP KEYWORD VALUES
*
DMP12  MOV  (XL)+,XR         LOAD NEXT SVBLK PTR FROM TABLE
       BZE  XR,DMP13         JUMP IF END OF LIST
.IF    .CCMK
       BEQ  XR,=NUM01,DMP12  &COMPARE IGNORED IF NOT IMPLEMENTED
.FI
       MOV  =CH_AM,WA        LOAD AMPERSAND
       JSR  PRTCH            PRINT AMPERSAND
       JSR  PRTST            PRINT KEYWORD NAME
       MOV  SVLEN(XR),WA     LOAD NAME LENGTH FROM SVBLK
       CTB  WA,SVCHS         GET LENGTH OF NAME
       ADD  WA,XR            POINT TO SVKNM FIELD
       MOV  (XR),DMPKN       STORE IN DUMMY KVBLK
       MOV  =TMBEB,XR        POINT TO BLANK-EQUAL-BLANK
       JSR  PRTST            PRINT IT
       MOV  XL,DMPSV         SAVE TABLE POINTER
       MOV  =DMPKB,XL        POINT TO DUMMY KVBLK
       MOV  =B_KVT,(XL)      BUILD TYPE WORD
       MOV  =TRBKV,KVVAR(XL) BUILD PTR TO DUMMY TRACE BLOCK
       MOV  *KVVAR,WA        SET ZERO OFFSET
       JSR  ACESS            GET KEYWORD VALUE
       PPM                   FAILURE IS IMPOSSIBLE
       JSR  PRTVL            PRINT KEYWORD VALUE
       JSR  PRTNL            TERMINATE PRINT LINE
       MOV  DMPSV,XL         RESTORE TABLE POINTER
       BRN  DMP12            LOOP BACK TILL ALL PRINTED
*
*      HERE AFTER COMPLETING PARTIAL DUMP
*
DMP13  BEQ  DMARG,=NUM01,DMP27 EXIT IF PARTIAL DUMP COMPLETE
       MOV  DNAMB,XR         ELSE POINT TO FIRST DYNAMIC BLOCK
*
*      LOOP THROUGH BLOCKS IN DYNAMIC STORAGE
*
DMP14  BEQ  XR,DNAMP,DMP27   JUMP IF END OF USED REGION
       MOV  (XR),WA          ELSE LOAD FIRST WORD OF BLOCK
       BEQ  WA,=B_VCT,DMP16  JUMP IF VECTOR
       BEQ  WA,=B_ART,DMP17  JUMP IF ARRAY
       BEQ  WA,=B_PDT,DMP18  JUMP IF PROGRAM DEFINED
       BEQ  WA,=B_TBT,DMP19  JUMP IF TABLE
.IF    .CNBF
.ELSE
       BEQ  WA,=B_BCT,DMP30  JUMP IF BUFFER
.FI
*
*      MERGE HERE TO MOVE TO NEXT BLOCK
*
DMP15  JSR  BLKLN            GET LENGTH OF BLOCK
       ADD  WA,XR            POINT PAST THIS BLOCK
       BRN  DMP14            LOOP BACK FOR NEXT BLOCK
       EJC
*
*      DUMPR (CONTINUED)
*
*      HERE FOR VECTOR
*
DMP16  MOV  *VCVLS,WB        SET OFFSET TO FIRST VALUE
       BRN  DMP19            JUMP TO MERGE
*
*      HERE FOR ARRAY
*
DMP17  MOV  AROFS(XR),WB     SET OFFSET TO ARPRO FIELD
       ICA  WB               BUMP TO GET OFFSET TO VALUES
       BRN  DMP19            JUMP TO MERGE
*
*      HERE FOR PROGRAM DEFINED
*
DMP18  MOV  *PDFLD,WB        POINT TO VALUES, MERGE
*
*      HERE FOR TABLE (OTHERS MERGE)
*
DMP19  BZE  IDVAL(XR),DMP15  IGNORE BLOCK IF ZERO ID VALUE
       JSR  BLKLN            ELSE GET BLOCK LENGTH
       MOV  XR,XL            COPY BLOCK POINTER
       MOV  WA,DMPSV         SAVE LENGTH
       MOV  WB,WA            COPY OFFSET TO FIRST VALUE
       JSR  PRTNL            PRINT BLANK LINE
       MOV  WA,DMPSA         PRESERVE OFFSET
       JSR  PRTVL            PRINT BLOCK VALUE (FOR TITLE)
       MOV  DMPSA,WA         RECOVER OFFSET
       JSR  PRTNL            END PRINT LINE
       BEQ  (XR),=B_TBT,DMP22 JUMP IF TABLE
       DCA  WA               POINT BEFORE FIRST WORD
*
*      LOOP TO PRINT CONTENTS OF ARRAY, VECTOR, OR PROGRAM DEF
*
DMP20  MOV  XL,XR            COPY BLOCK POINTER
       ICA  WA               BUMP OFFSET
       ADD  WA,XR            POINT TO NEXT VALUE
       BEQ  WA,DMPSV,DMP14   EXIT IF END (XR PAST BLOCK)
       SUB  *VRVAL,XR        SUBTRACT OFFSET TO MERGE INTO LOOP
*
*      LOOP TO FIND VALUE AND IGNORE NULLS
*
DMP21  MOV  VRVAL(XR),XR     LOAD NEXT VALUE
       BEQ  DMARG,=NUM03,DMP2B SKIP NULL VALUE CHECK IF DUMP(3)
       BEQ  XR,=NULLS,DMP20  LOOP BACK IF NULL VALUE
DMP2B  BEQ  (XR),=B_TRT,DMP21 LOOP BACK IF TRAPPED
       JSR  PRTNV            ELSE PRINT NAME = VALUE
       BRN  DMP20            LOOP BACK FOR NEXT FIELD
       EJC
*
*      DUMPR (CONTINUED)
*
*      HERE TO DUMP A TABLE
*
DMP22  MOV  *TBBUK,WC        SET OFFSET TO FIRST BUCKET
       MOV  *TEVAL,WA        SET NAME OFFSET FOR ALL TEBLKS
*
*      LOOP THROUGH TABLE BUCKETS
*
DMP23  MOV  XL,-(XS)         SAVE TBBLK POINTER
       ADD  WC,XL            POINT TO NEXT BUCKET HEADER
       ICA  WC               BUMP BUCKET OFFSET
       SUB  *TENXT,XL        SUBTRACT OFFSET TO MERGE INTO LOOP
*
*      LOOP TO PROCESS TEBLKS ON ONE CHAIN
*
DMP24  MOV  TENXT(XL),XL     POINT TO NEXT TEBLK
       BEQ  XL,(XS),DMP26    JUMP IF END OF CHAIN
       MOV  XL,XR            ELSE COPY TEBLK POINTER
*
*      LOOP TO FIND VALUE AND IGNORE IF NULL
*
DMP25  MOV  TEVAL(XR),XR     LOAD NEXT VALUE
       BEQ  XR,=NULLS,DMP24  IGNORE IF NULL VALUE
       BEQ  (XR),=B_TRT,DMP25 LOOP BACK IF TRAPPED
       MOV  WC,DMPSV         ELSE SAVE OFFSET POINTER
       JSR  PRTNV            PRINT NAME = VALUE
       MOV  DMPSV,WC         RELOAD OFFSET
       BRN  DMP24            LOOP BACK FOR NEXT TEBLK
*
*      HERE TO MOVE TO NEXT HASH CHAIN
*
DMP26  MOV  (XS)+,XL         RESTORE TBBLK POINTER
       BNE  WC,TBLEN(XL),DMP23 LOOP BACK IF MORE BUCKETS TO GO
       MOV  XL,XR            ELSE COPY TABLE POINTER
       ADD  WC,XR            POINT TO FOLLOWING BLOCK
       BRN  DMP14            LOOP BACK TO PROCESS NEXT BLOCK
*
*      HERE AFTER COMPLETING DUMP
*
DMP27  JSR  PRTPG            EJECT PRINTER
*
*      MERGE HERE IF NO DUMP GIVEN (DMARG=0)
*
DMP28  EXI                   RETURN TO DUMP CALLER
*
*      CALL SYSTEM CORE DUMP ROUTINE
*
DMP29  JSR  SYSDM            CALL IT
       BRN  DMP28            RETURN
.IF    .CNBF
.ELSE
       EJC
*
*      DUMPR (CONTINUED)
*
*      HERE TO DUMP BUFFER BLOCK
*
DMP30  JSR  PRTNL            PRINT BLANK LINE
       JSR  PRTVL            PRINT VALUE ID FOR TITLE
       JSR  PRTNL            FORCE NEW LINE
       MOV  =CH_DQ,WA        LOAD DOUBLE QUOTE
       JSR  PRTCH            PRINT IT
       MOV  BCLEN(XR),WC     LOAD DEFINED LENGTH
       BZE  WC,DMP32         SKIP CHARACTERS IF NONE
       LCT  WC,WC            LOAD COUNT FOR LOOP
       MOV  XR,WB            SAVE BCBLK PTR
       MOV  BCBUF(XR),XR     POINT TO BFBLK
       PLC  XR               GET SET TO LOAD CHARACTERS
*
*      LOOP HERE STUFFING CHARACTERS IN OUTPUT STREAM
*
DMP31  LCH  WA,(XR)+         GET NEXT CHARACTER
       JSR  PRTCH            STUFF IT
       BCT  WC,DMP31         BRANCH FOR NEXT ONE
       MOV  WB,XR            RESTORE BCBLK POINTER
*
*      MERGE TO STUFF CLOSING QUOTE MARK
*
DMP32  MOV  =CH_DQ,WA        STUFF QUOTE
       JSR  PRTCH            PRINT IT
       JSR  PRTNL            PRINT NEW LINE
       MOV  (XR),WA          GET FIRST WD FOR BLKLN
       BRN  DMP15            MERGE TO GET NEXT BLOCK
.FI
       ENP                   END PROCEDURE DUMPR
       EJC
*
*      ERMSG -- PRINT ERROR CODE AND ERROR MESSAGE
*
*      KVERT                 ERROR CODE
*      JSR  ERMSG            CALL TO PRINT MESSAGE
*      (XR,XL,WA,WB,WC,IA)   DESTROYED
*
ERMSG  PRC  E,0              ENTRY POINT
       MOV  KVERT,WA         LOAD ERROR CODE
       MOV  =ERMMS,XR        POINT TO ERROR MESSAGE /ERROR/
       JSR  PRTST            PRINT IT
       JSR  ERTEX            GET ERROR MESSAGE TEXT
       ADD  =THSND,WA        BUMP ERROR CODE FOR PRINT
       MTI  WA               FAIL CODE IN INT ACC
       MOV  PROFS,WB         SAVE CURRENT BUFFER POSITION
       JSR  PRTIN            PRINT CODE (NOW HAVE ERROR1XXX)
       MOV  PRBUF,XL         POINT TO PRINT BUFFER
       PSC  XL,WB            POINT TO THE 1
       MOV  =CH_BL,WA        LOAD A BLANK
       SCH  WA,(XL)          STORE BLANK OVER 1 (ERROR XXX)
       CSC  XL               COMPLETE STORE CHARACTERS
       ZER  XL               CLEAR GARBAGE POINTER IN XL
       MOV  XR,WA            KEEP ERROR TEXT
       MOV  =ERMNS,XR        POINT TO / -- /
       JSR  PRTST            PRINT IT
       MOV  WA,XR            GET ERROR TEXT AGAIN
       JSR  PRTST            PRINT ERROR MESSAGE TEXT
       JSR  PRTIS            PRINT LINE
       JSR  PRTIS            PRINT BLANK LINE
       EXI                   RETURN TO ERMSG CALLER
       ENP                   END PROCEDURE ERMSG
       EJC
*
*      ERTEX -- GET ERROR MESSAGE TEXT
*
*      (WA)                  ERROR CODE
*      JSR  ERTEX            CALL TO GET ERROR TEXT
*      (XR)                  PTR TO ERROR TEXT IN DYNAMIC
*      (R_ETX)               COPY OF PTR TO ERROR TEXT
*      (XL,WC,IA)            DESTROYED
*
ERTEX  PRC  E,0              ENTRY POINT
       MOV  WA,ERTWA         SAVE WA
       MOV  WB,ERTWB         SAVE WB
       JSR  SYSEM            GET FAILURE MESSAGE TEXT
       MOV  XR,XL            COPY POINTER TO IT
       MOV  SCLEN(XR),WA     GET LENGTH OF STRING
       BZE  WA,ERT02         JUMP IF NULL
       ZER  WB               OFFSET OF ZERO
       JSR  SBSTR            COPY INTO DYNAMIC STORE
       MOV  XR,R_ETX         STORE FOR RELOCATION
*
*      RETURN
*
ERT01  MOV  ERTWB,WB         RESTORE WB
       MOV  ERTWA,WA         RESTORE WA
       EXI                   RETURN TO CALLER
*
*      RETURN ERRTEXT CONTENTS INSTEAD OF NULL
*
ERT02  MOV  R_ETX,XR         GET ERRTEXT
       BRN  ERT01            RETURN
       ENP
       EJC
*
*      EVALI -- EVALUATE INTEGER ARGUMENT
*
*      EVALI IS USED BY PATTERN PRIMITIVES LEN,TAB,RTAB,POS,RPOS
*      WHEN THEIR ARGUMENT IS AN EXPRESSION VALUE.
*
*      (XR)                  NODE POINTER
*      (WB)                  CURSOR
*      JSR  EVALI            CALL TO EVALUATE INTEGER
*      PPM  LOC              TRANSFER LOC FOR NON-INTEGER ARG
*      PPM  LOC              TRANSFER LOC FOR OUT OF RANGE ARG
*      PPM  LOC              TRANSFER LOC FOR EVALUATION FAILURE
*      PPM  LOC              TRANSFER LOC FOR SUCCESSFUL EVAL
*      (THE NORMAL RETURN IS NEVER TAKEN)
*      (XR)                  PTR TO NODE WITH INTEGER ARGUMENT
*      (WC,XL,RA)            DESTROYED
*
*      ON RETURN, THE NODE POINTED TO HAS THE INTEGER ARGUMENT
*      IN PARM1 AND THE PROPER SUCCESSOR POINTER IN PTHEN.
*      THIS ALLOWS MERGING WITH THE NORMAL (INTEGER ARG) CASE.
*
EVALI  PRC  R,4              ENTRY POINT (RECURSIVE)
       JSR  EVALP            EVALUATE EXPRESSION
       PPM  EVLI1            JUMP ON FAILURE
       MOV  XL,-(XS)         STACK RESULT FOR GTSMI
       MOV  PTHEN(XR),XL     LOAD SUCCESSOR POINTER
       MOV  XR,EVLIO         SAVE ORIGINAL NODE POINTER
       MOV  WC,EVLIF         ZERO IF SIMPLE ARGUMENT
       JSR  GTSMI            CONVERT ARG TO SMALL INTEGER
       PPM  EVLI2            JUMP IF NOT INTEGER
       PPM  EVLI3            JUMP IF OUT OF RANGE
       MOV  XR,EVLIV         STORE RESULT IN SPECIAL DUMMY NODE
       MOV  =EVLIN,XR        POINT TO DUMMY NODE WITH RESULT
       MOV  =P_LEN,(XR)      DUMMY PATTERN BLOCK PCODE
       MOV  XL,PTHEN(XR)     STORE SUCCESSOR POINTER
       EXI  4                TAKE SUCCESSFUL EXIT
*
*      HERE IF EVALUATION FAILS
*
EVLI1  EXI  3                TAKE FAILURE RETURN
*
*      HERE IF ARGUMENT IS NOT INTEGER
*
EVLI2  EXI  1                TAKE NON-INTEGER ERROR EXIT
*
*      HERE IF ARGUMENT IS OUT OF RANGE
*
EVLI3  EXI  2                TAKE OUT-OF-RANGE ERROR EXIT
       ENP                   END PROCEDURE EVALI
       EJC
*
*      EVALP -- EVALUATE EXPRESSION DURING PATTERN MATCH
*
*      EVALP IS USED TO EVALUATE AN EXPRESSION (BY VALUE) DURING
*      A PATTERN MATCH. THE EFFECT IS LIKE EVALX, BUT PATTERN
*      VARIABLES ARE STACKED AND RESTORED IF NECESSARY.
*
*      EVALP ALSO DIFFERS FROM EVALX IN THAT IF THE RESULT IS
*      AN EXPRESSION IT IS REEVALUATED. THIS OCCURS REPEATEDLY.
*
*      TO SUPPORT OPTIMIZATION OF POS AND RPOS, EVALP USES WC
*      TO SIGNAL THE CALLER FOR THE CASE OF A SIMPLE VRBLK
*      THAT IS NOT AN EXPRESSION AND IS NOT TRAPPED.  BECAUSE
*      THIS CASE CANNOT HAVE ANY SIDE EFFECTS, OPTIMIZATION IS
*      POSSIBLE.
*
*      (XR)                  NODE POINTER
*      (WB)                  PATTERN MATCH CURSOR
*      JSR  EVALP            CALL TO EVALUATE EXPRESSION
*      PPM  LOC              TRANSFER LOC IF EVALUATION FAILS
*      (XL)                  RESULT
*      (WA)                  FIRST WORD OF RESULT BLOCK
*      (WC)                  ZERO IF SIMPLE VRBLK, ELSE NON-ZERO
*      (XR,WB)               DESTROYED (FAILURE CASE ONLY)
*      (RA)                  DESTROYED
*
*      THE EXPRESSION POINTER IS STORED IN PARM1 OF THE NODE
*
*      CONTROL RETURNS TO FAILP ON FAILURE OF EVALUATION
*
EVALP  PRC  R,1              ENTRY POINT (RECURSIVE)
       MOV  PARM1(XR),XL     LOAD EXPRESSION POINTER
       BEQ  (XL),=B_EXL,EVLP1 JUMP IF EXBLK CASE
*
*      HERE FOR CASE OF SEBLK
*
*      WE CAN GIVE A FAST RETURN IF THE VALUE OF THE VRBLK IS
*      NOT AN EXPRESSION AND IS NOT TRAPPED.
*
       MOV  SEVAR(XL),XL     LOAD VRBLK POINTER
       MOV  VRVAL(XL),XL     LOAD VALUE OF VRBLK
       MOV  (XL),WA          LOAD FIRST WORD OF VALUE
       BHI  WA,=B_T__,EVLP3  JUMP IF NOT SEBLK, TRBLK OR EXBLK
*
*      HERE FOR EXBLK OR SEBLK WITH EXPR VALUE OR TRAPPED VALUE
*
EVLP1  CHK                   CHECK FOR STACK SPACE
       MOV  XR,-(XS)         STACK NODE POINTER
       MOV  WB,-(XS)         STACK CURSOR
       MOV  R_PMS,-(XS)      STACK SUBJECT STRING POINTER
       MOV  PMSSL,-(XS)      STACK SUBJECT STRING LENGTH
       MOV  PMDFL,-(XS)      STACK DOT FLAG
       MOV  PMHBS,-(XS)      STACK HISTORY STACK BASE POINTER
       MOV  PARM1(XR),XR     LOAD EXPRESSION POINTER
       EJC
*
*      EVALP (CONTINUED)
*
*      LOOP BACK HERE TO REEVALUATE EXPRESSION RESULT
*
EVLP2  ZER  WB               SET FLAG FOR BY VALUE
       JSR  EVALX            EVALUATE EXPRESSION
       PPM  EVLP4            JUMP ON FAILURE
       MOV  (XR),WA          ELSE LOAD FIRST WORD OF VALUE
       BLO  WA,=B_E__,EVLP2  LOOP BACK TO REEVALUATE EXPRESSION
*
*      HERE TO RESTORE PATTERN VALUES AFTER SUCCESSFUL EVAL
*
       MOV  XR,XL            COPY RESULT POINTER
       MOV  (XS)+,PMHBS      RESTORE HISTORY STACK BASE POINTER
       MOV  (XS)+,PMDFL      RESTORE DOT FLAG
       MOV  (XS)+,PMSSL      RESTORE SUBJECT STRING LENGTH
       MOV  (XS)+,R_PMS      RESTORE SUBJECT STRING POINTER
       MOV  (XS)+,WB         RESTORE CURSOR
       MOV  (XS)+,XR         RESTORE NODE POINTER
       MOV  XR,WC            NON-ZERO FOR SIMPLE VRBLK
       EXI                   RETURN TO EVALP CALLER
*
*      HERE TO RETURN AFTER SIMPLE VRBLK CASE
*
EVLP3  ZER  WC               SIMPLE VRBLK, NO SIDE EFFECTS
       EXI                   RETURN TO EVALP CALLER
*
*      HERE FOR FAILURE DURING EVALUATION
*
EVLP4  MOV  (XS)+,PMHBS      RESTORE HISTORY STACK BASE POINTER
       MOV  (XS)+,PMDFL      RESTORE DOT FLAG
       MOV  (XS)+,PMSSL      RESTORE SUBJECT STRING LENGTH
       MOV  (XS)+,R_PMS      RESTORE SUBJECT STRING POINTER
       ADD  *NUM02,XS        REMOVE NODE PTR, CURSOR
       EXI  1                TAKE FAILURE EXIT
       ENP                   END PROCEDURE EVALP
       EJC
*
*      EVALS -- EVALUATE STRING ARGUMENT
*
*      EVALS IS USED BY SPAN, ANY, NOTANY, BREAK, BREAKX WHEN
*      THEY ARE PASSED AN EXPRESSION ARGUMENT.
*
*      (XR)                  NODE POINTER
*      (WB)                  CURSOR
*      JSR  EVALS            CALL TO EVALUATE STRING
*      PPM  LOC              TRANSFER LOC FOR NON-STRING ARG
*      PPM  LOC              TRANSFER LOC FOR EVALUATION FAILURE
*      PPM  LOC              TRANSFER LOC FOR SUCCESSFUL EVAL
*      (THE NORMAL RETURN IS NEVER TAKEN)
*      (XR)                  PTR TO NODE WITH PARMS SET
*      (XL,WC,RA)            DESTROYED
*
*      ON RETURN, THE NODE POINTED TO HAS A CHARACTER TABLE
*      POINTER IN PARM1 AND A BIT MASK IN PARM2. THE PROPER
*      SUCCESSOR IS STORED IN PTHEN OF THIS NODE. THUS IT IS
*      OK FOR MERGING WITH THE NORMAL (MULTI-CHAR STRING) CASE.
*
EVALS  PRC  R,3              ENTRY POINT (RECURSIVE)
       JSR  EVALP            EVALUATE EXPRESSION
       PPM  EVLS1            JUMP IF EVALUATION FAILS
       MOV  PTHEN(XR),-(XS)  SAVE SUCCESSOR POINTER
       MOV  WB,-(XS)         SAVE CURSOR
       MOV  XL,-(XS)         STACK RESULT PTR FOR PATST
       ZER  WB               DUMMY PCODE FOR ONE CHAR STRING
       ZER  WC               DUMMY PCODE FOR EXPRESSION ARG
       MOV  =P_BRK,XL        APPROPRIATE PCODE FOR OUR USE
       JSR  PATST            CALL ROUTINE TO BUILD NODE
       PPM  EVLS2            JUMP IF NOT STRING
       MOV  (XS)+,WB         RESTORE CURSOR
       MOV  (XS)+,PTHEN(XR)  STORE SUCCESSOR POINTER
       EXI  3                TAKE SUCCESS RETURN
*
*      HERE IF EVALUATION FAILS
*
EVLS1  EXI  2                TAKE FAILURE RETURN
*
*      HERE IF ARGUMENT IS NOT STRING
*
EVLS2  ADD  *NUM02,XS        POP SUCCESSOR AND CURSOR
       EXI  1                TAKE NON-STRING ERROR EXIT
       ENP                   END PROCEDURE EVALS
       EJC
*
*      EVALX -- EVALUATE EXPRESSION
*
*      EVALX IS CALLED TO EVALUATE AN EXPRESSION
*
*      (XR)                  POINTER TO EXBLK OR SEBLK
*      (WB)                  0 IF BY VALUE, 1 IF BY NAME
*      JSR  EVALX            CALL TO EVALUATE EXPRESSION
*      PPM  LOC              TRANSFER LOC IF EVALUATION FAILS
*      (XR)                  RESULT IF CALLED BY VALUE
*      (XL,WA)               RESULT NAME BASE,OFFSET IF BY NAME
*      (XR)                  DESTROYED (NAME CASE ONLY)
*      (XL,WA)               DESTROYED (VALUE CASE ONLY)
*      (WB,WC,RA)            DESTROYED
*
EVALX  PRC  R,1              ENTRY POINT, RECURSIVE
       BEQ  (XR),=B_EXL,EVLX2 JUMP IF EXBLK CASE
*
*      HERE FOR SEBLK
*
       MOV  SEVAR(XR),XL     LOAD VRBLK POINTER (NAME BASE)
       MOV  *VRVAL,WA        SET NAME OFFSET
       BNZ  WB,EVLX1         JUMP IF CALLED BY NAME
       JSR  ACESS            CALL ROUTINE TO ACCESS VALUE
       PPM  EVLX9            JUMP IF FAILURE ON ACCESS
*
*      MERGE HERE TO EXIT FOR SEBLK CASE
*
EVLX1  EXI                   RETURN TO EVALX CALLER
       EJC
*
*      EVALX (CONTINUED)
*
*      HERE FOR FULL EXPRESSION (EXBLK) CASE
*
*      IF AN ERROR OCCURS IN THE EXPRESSION CODE AT EXECUTION
*      TIME, CONTROL IS PASSED VIA ERROR SECTION TO EXFAL
*      WITHOUT RETURNING TO THIS ROUTINE.
*      THE FOLLOWING ENTRIES ARE MADE ON THE STACK BEFORE
*      GIVING CONTROL TO THE EXPRESSION CODE
*
*                            EVALX RETURN POINT
*                            SAVED VALUE OF R_COD
*                            CODE POINTER (-R_COD)
*                            SAVED VALUE OF FLPTR
*                            0 IF BY VALUE, 1 IF BY NAME
*      FLPTR --------------- *EXFLC, FAIL OFFSET IN EXBLK
*
EVLX2  SCP  WC               GET CODE POINTER
       MOV  R_COD,WA         LOAD CODE BLOCK POINTER
       SUB  WA,WC            GET CODE POINTER AS OFFSET
       MOV  WA,-(XS)         STACK OLD CODE BLOCK POINTER
       MOV  WC,-(XS)         STACK RELATIVE CODE OFFSET
       MOV  FLPTR,-(XS)      STACK OLD FAILURE POINTER
       MOV  WB,-(XS)         STACK NAME/VALUE INDICATOR
       MOV  *EXFLC,-(XS)     STACK NEW FAIL OFFSET
       MOV  FLPTR,GTCEF      KEEP IN CASE OF ERROR
       MOV  R_COD,R_GTC      KEEP CODE BLOCK POINTER SIMILARLY
       MOV  XS,FLPTR         SET NEW FAILURE POINTER
       MOV  XR,R_COD         SET NEW CODE BLOCK POINTER
       MOV  KVSTN,EXSTM(XR)  REMEMBER STMNT NUMBER
       ADD  *EXCOD,XR        POINT TO FIRST CODE WORD
       LCP  XR               SET CODE POINTER
       BNE  STAGE,=STGXT,EVLX0 JUMP IF NOT EXECUTION TIME
       MOV  =STGEE,STAGE     EVALUATING EXPRESSION
*
*      HERE TO EXECUTE FIRST CODE WORD OF EXPRESSION
*
EVLX0  ZER  XL               CLEAR GARBAGE XL
       LCW  XR               LOAD FIRST CODE WORD
       BRI  (XR)             EXECUTE IT
       EJC
*
*      EVALX (CONTINUED)
*
*      COME HERE IF SUCCESSFUL RETURN BY VALUE (SEE O_RVL)
*
EVLX3  MOV  (XS)+,XR         LOAD VALUE
       BZE  NUM01(XS),EVLX5  JUMP IF CALLED BY VALUE
       ERB  249,Expression evaluated by name returned value
*
*      HERE FOR EXPRESSION RETURNING BY NAME (SEE O_RNM)
*
EVLX4  MOV  (XS)+,WA         LOAD NAME OFFSET
       MOV  (XS)+,XL         LOAD NAME BASE
       BNZ  NUM01(XS),EVLX5  JUMP IF CALLED BY NAME
       JSR  ACESS            ELSE ACCESS VALUE FIRST
       PPM  EVLX6            JUMP IF FAILURE DURING ACCESS
*
*      HERE AFTER LOADING CORRECT RESULT INTO XR OR XL,WA
*
EVLX5  ZER  WB               NOTE SUCCESSFUL
       BRN  EVLX7            MERGE
*
*      HERE FOR FAILURE IN EXPRESSION EVALUATION (SEE O_FEX)
*
EVLX6  MNZ  WB               NOTE UNSUCCESSFUL
*
*      RESTORE ENVIRONMENT
*
EVLX7  BNE  STAGE,=STGEE,EVLX8 SKIP IF WAS NOT PREVIOUSLY XT
       MOV  =STGXT,STAGE     EXECUTE TIME
*
*      MERGE WITH STAGE SET UP
*
EVLX8  ADD  *NUM02,XS        POP NAME/VALUE INDICATOR, *EXFAL
       MOV  (XS)+,FLPTR      RESTORE OLD FAILURE POINTER
       MOV  (XS)+,WC         LOAD CODE OFFSET
       ADD  (XS),WC          MAKE CODE POINTER ABSOLUTE
       MOV  (XS)+,R_COD      RESTORE OLD CODE BLOCK POINTER
       LCP  WC               RESTORE OLD CODE POINTER
       BZE  WB,EVLX1         JUMP FOR SUCCESSFUL RETURN
*
*      MERGE HERE FOR FAILURE IN SEBLK CASE
*
EVLX9  EXI  1                TAKE FAILURE EXIT
       ENP                   END OF PROCEDURE EVALX
       EJC
*
*      EXBLD -- BUILD EXBLK
*
*      EXBLD IS USED TO BUILD AN EXPRESSION BLOCK FROM THE
*      CODE COMPILED MOST RECENTLY IN THE CURRENT CCBLK.
*
*      (XL)                  OFFSET IN CCBLK TO START OF CODE
*      (WB)                  INTEGER IN RANGE 0 LE N LE MXLEN
*      JSR  EXBLD            CALL TO BUILD EXBLK
*      (XR)                  PTR TO CONSTRUCTED EXBLK
*      (WA,WB,XL)            DESTROYED
*
EXBLD  PRC  E,0              ENTRY POINT
       MOV  XL,WA            COPY OFFSET TO START OF CODE
       SUB  *EXCOD,WA        CALC REDUCTION IN OFFSET IN EXBLK
       MOV  WA,-(XS)         STACK FOR LATER
       MOV  CWCOF,WA         LOAD FINAL OFFSET
       SUB  XL,WA            COMPUTE LENGTH OF CODE
       ADD  *EXSI_,WA        ADD SPACE FOR STANDARD FIELDS
       JSR  ALLOC            ALLOCATE SPACE FOR EXBLK
       MOV  XR,-(XS)         SAVE POINTER TO EXBLK
       MOV  =B_EXL,EXTYP(XR) STORE TYPE WORD
       ZER  EXSTM(XR)        ZEROISE STMNT NUMBER FIELD
.IF    .CSLN
       MOV  CMPLN,EXSLN(XR)  SET LINE NUMBER FIELD
.FI
       MOV  WA,EXLEN(XR)     STORE LENGTH
       MOV  =OFEX_,EXFLC(XR) STORE FAILURE WORD
       ADD  *EXSI_,XR        SET XR FOR MVW
       MOV  XL,CWCOF         RESET OFFSET TO START OF CODE
       ADD  R_CCB,XL         POINT TO START OF CODE
       SUB  *EXSI_,WA        LENGTH OF CODE TO MOVE
       MOV  WA,-(XS)         STACK LENGTH OF CODE
       MVW                   MOVE CODE TO EXBLK
       MOV  (XS)+,WA         GET LENGTH OF CODE
       BTW  WA               CONVERT BYTE COUNT TO WORD COUNT
       LCT  WA,WA            PREPARE COUNTER FOR LOOP
       MOV  (XS),XL          COPY EXBLK PTR, DONT UNSTACK
       ADD  *EXCOD,XL        POINT TO CODE ITSELF
       MOV  NUM01(XS),WB     GET REDUCTION IN OFFSET
*
*      THIS LOOP SEARCHES FOR NEGATION AND SELECTION CODE SO
*      THAT THE OFFSETS COMPUTED WHILST CODE WAS IN CODE BLOCK
*      CAN BE TRANSFORMED TO REDUCED VALUES APPLICABLE IN AN
*      EXBLK.
*
EXBL1  MOV  (XL)+,XR         GET NEXT CODE WORD
       BEQ  XR,=OSLA_,EXBL3  JUMP IF SELECTION FOUND
       BEQ  XR,=ONTA_,EXBL3  JUMP IF NEGATION FOUND
       BCT  WA,EXBL1         LOOP TO END OF CODE
*
*      NO SELECTION FOUND OR MERGE TO EXIT ON TERMINATION
*
EXBL2  MOV  (XS)+,XR         POP EXBLK PTR INTO XR
       MOV  (XS)+,XL         POP REDUCTION CONSTANT
       EXI                   RETURN TO CALLER
       EJC
*
*      EXBLD (CONTINUED)
*
*      SELECTION OR NEGATION FOUND
*      REDUCE THE OFFSETS AS NEEDED. OFFSETS OCCUR IN WORDS
*      FOLLOWING CODE WORDS -
*           =ONTA_, =OSLA_, =OSLB_, =OSLC_
*
EXBL3  SUB  WB,(XL)+         ADJUST OFFSET
       BCT  WA,EXBL4         DECREMENT COUNT
*
EXBL4  BCT  WA,EXBL5         DECREMENT COUNT
*
*      CONTINUE SEARCH FOR MORE OFFSETS
*
EXBL5  MOV  (XL)+,XR         GET NEXT CODE WORD
       BEQ  XR,=OSLA_,EXBL3  JUMP IF OFFSET FOUND
       BEQ  XR,=OSLB_,EXBL3  JUMP IF OFFSET FOUND
       BEQ  XR,=OSLC_,EXBL3  JUMP IF OFFSET FOUND
       BEQ  XR,=ONTA_,EXBL3  JUMP IF OFFSET FOUND
       BCT  WA,EXBL5         LOOP
       BRN  EXBL2            MERGE TO RETURN
       ENP                   END PROCEDURE EXBLD
       EJC
*
*      EXPAN -- ANALYZE EXPRESSION
*
*      THE EXPRESSION ANALYZER (EXPAN) PROCEDURE IS USED TO SCAN
*      AN EXPRESSION AND CONVERT IT INTO A TREE REPRESENTATION.
*      SEE THE DESCRIPTION OF CMBLK IN THE STRUCTURES SECTION
*      FOR DETAILED FORMAT OF TREE BLOCKS.
*
*      THE ANALYZER USES A SIMPLE PRECEDENCE SCHEME IN WHICH
*      OPERANDS AND OPERATORS ARE PLACED ON A SINGLE STACK
*      AND CONDENSATIONS ARE MADE WHEN LOW PRECEDENCE OPERATORS
*      ARE STACKED AFTER A HIGHER PRECEDENCE OPERATOR. A GLOBAL
*      VARIABLE (IN WB) KEEPS TRACK OF THE LEVEL AS FOLLOWS.
*
*      0    SCANNING OUTER LEVEL OF STATEMENT OR EXPRESSION
*      1    SCANNING OUTER LEVEL OF NORMAL GOTO
*      2    SCANNING OUTER LEVEL OF DIRECT GOTO
*      3    SCANNING INSIDE ARRAY BRACKETS
*      4    SCANNING INSIDE GROUPING PARENTHESES
*      5    SCANNING INSIDE FUNCTION PARENTHESES
*
*      THIS VARIABLE IS SAVED ON THE STACK ON ENCOUNTERING A
*      GROUPING AND RESTORED AT THE END OF THE GROUPING.
*
*      ANOTHER GLOBAL VARIABLE (IN WC) COUNTS THE NUMBER OF
*      ITEMS AT ONE GROUPING LEVEL AND IS INCREMENTED FOR EACH
*      COMMA ENCOUNTERED. IT IS STACKED WITH THE LEVEL INDICATOR
*
*      THE SCAN IS CONTROLLED BY A THREE STATE FINITE MACHINE.
*      A GLOBAL VARIABLE STORED IN WA IS THE CURRENT STATE.
*
*      WA=0                  NOTHING SCANNED AT THIS LEVEL
*      WA=1                  OPERAND EXPECTED
*      WA=2                  OPERATOR EXPECTED
*
*      (WB)                  CALL TYPE (SEE BELOW)
*      JSR  EXPAN            CALL TO ANALYZE EXPRESSION
*      (XR)                  POINTER TO RESULTING TREE
*      (XL,WA,WB,WC,RA)      DESTROYED
*
*      THE ENTRY VALUE OF WB INDICATES THE CALL TYPE AS FOLLOWS.
*
*      0    SCANNING EITHER THE MAIN BODY OF A STATEMENT OR THE
*           TEXT OF AN EXPRESSION (FROM EVAL CALL). VALID
*           TERMINATORS ARE COLON, SEMICOLON. THE RESCAN FLAG IS
*           SET TO RETURN THE TERMINATOR ON THE NEXT SCANE CALL.
*
*      1    SCANNING A NORMAL GOTO. THE ONLY VALID
*           TERMINATOR IS A RIGHT PAREN.
*
*      2    SCANNING A DIRECT GOTO. THE ONLY VALID
*           TERMINATOR IS A RIGHT BRACKET.
       EJC
*
*      EXPAN (CONTINUED)
*
*      ENTRY POINT
*
EXPAN  PRC  E,0              ENTRY POINT
       ZER  -(XS)            SET TOP OF STACK INDICATOR
       ZER  WA               SET INITIAL STATE TO ZERO
       ZER  WC               ZERO COUNTER VALUE
*
*      LOOP HERE FOR SUCCESSIVE ENTRIES
*
EXP01  JSR  SCANE            SCAN NEXT ELEMENT
       ADD  WA,XL            ADD STATE TO SYNTAX CODE
       BSW  XL,T_NES         SWITCH ON ELEMENT TYPE/STATE
       IFF  T_VA0,EXP03      VARIABLE, S=0
       IFF  T_VA1,EXP03      VARIABLE, STATE ONE
       IFF  T_VA2,EXP04      VARIABLE, S=2
       IFF  T_CO0,EXP03      CONSTANT, S=0
       IFF  T_CO1,EXP03      CONSTANT, S=1
       IFF  T_CO2,EXP04      CONSTANT, S=2
       IFF  T_LP0,EXP06      LEFT PAREN, S=0
       IFF  T_LP1,EXP06      LEFT PAREN, S=1
       IFF  T_LP2,EXP04      LEFT PAREN, S=2
       IFF  T_FN0,EXP10      FUNCTION, S=0
       IFF  T_FN1,EXP10      FUNCTION, S=1
       IFF  T_FN2,EXP04      FUNCTION, S=2
       IFF  T_RP0,EXP02      RIGHT PAREN, S=0
       IFF  T_RP1,EXP05      RIGHT PAREN, S=1
       IFF  T_RP2,EXP12      RIGHT PAREN, S=2
       IFF  T_LB0,EXP08      LEFT BRKT, S=0
       IFF  T_LB1,EXP08      LEFT BRKT, S=1
       IFF  T_LB2,EXP09      LEFT BRKT, S=2
       IFF  T_RB0,EXP02      RIGHT BRKT, S=0
       IFF  T_RB1,EXP05      RIGHT BRKT, S=1
       IFF  T_RB2,EXP18      RIGHT BRKT, S=2
       IFF  T_UO0,EXP27      UNOP, S=0
       IFF  T_UO1,EXP27      UNOP, S=1
       IFF  T_UO2,EXP04      UNOP, S=2
       IFF  T_BO0,EXP05      BINOP, S=0
       IFF  T_BO1,EXP05      BINOP, S=1
       IFF  T_BO2,EXP26      BINOP, S=2
       IFF  T_CM0,EXP02      COMMA, S=0
       IFF  T_CM1,EXP05      COMMA, S=1
       IFF  T_CM2,EXP11      COMMA, S=2
       IFF  T_CL0,EXP02      COLON, S=0
       IFF  T_CL1,EXP05      COLON, S=1
       IFF  T_CL2,EXP19      COLON, S=2
       IFF  T_SM0,EXP02      SEMICOLON, S=0
       IFF  T_SM1,EXP05      SEMICOLON, S=1
       IFF  T_SM2,EXP19      SEMICOLON, S=2
       ESW                   END SWITCH ON ELEMENT TYPE/STATE
       EJC
*
*      EXPAN (CONTINUED)
*
*      HERE FOR RBR,RPR,COL,SMC,CMA IN STATE 0
*
*      SET TO RESCAN THE TERMINATOR ENCOUNTERED AND CREATE
*      A NULL CONSTANT (CASE OF OMITTED NULL)
*
EXP02  MNZ  SCNRS            SET TO RESCAN ELEMENT
       MOV  =NULLS,XR        POINT TO NULL, MERGE
*
*      HERE FOR VAR OR CON IN STATES 0,1
*
*      STACK THE VARIABLE/CONSTANT AND SET STATE=2
*
EXP03  MOV  XR,-(XS)         STACK POINTER TO OPERAND
       MOV  =NUM02,WA        SET STATE 2
       BRN  EXP01            JUMP FOR NEXT ELEMENT
*
*      HERE FOR VAR,CON,LPR,FNC,UOP IN STATE 2
*
*      WE RESCAN THE ELEMENT AND CREATE A CONCATENATION OPERATOR
*      THIS IS THE CASE OF THE BLANK CONCATENATION OPERATOR.
*
EXP04  MNZ  SCNRS            SET TO RESCAN ELEMENT
       MOV  =OPDVC,XR        POINT TO CONCAT OPERATOR DV
       BZE  WB,EXP4A         OK IF AT TOP LEVEL
       MOV  =OPDVP,XR        ELSE POINT TO UNMISTAKABLE CONCAT.
*
*      MERGE HERE WHEN XR SET UP WITH PROPER CONCATENATION DVBLK
*
EXP4A  BNZ  SCNBL,EXP26      MERGE BOP IF BLANKS, ELSE ERROR
*      DCV  SCNSE            ADJUST START OF ELEMENT LOCATION
       ERB  220,Syntax error: Missing operator
*
*      HERE FOR CMA,RPR,RBR,COL,SMC,BOP(S=1) BOP(S=0)
*
*      THIS IS AN ERRONOUS CONTRUCTION
*

*EXP05 DCV  SCNSE            ADJUST START OF ELEMENT LOCATION
EXP05  ERB  221,Syntax error: Missing operand
*
*      HERE FOR LPR (S=0,1)
*
EXP06  MOV  =NUM04,XL        SET NEW LEVEL INDICATOR
       ZER  XR               SET ZERO VALUE FOR CMOPN
       EJC
*
*      EXPAN (CONTINUED)
*
*      MERGE HERE TO STORE OLD LEVEL ON STACK AND START NEW ONE
*
EXP07  MOV  XR,-(XS)         STACK CMOPN VALUE
       MOV  WC,-(XS)         STACK OLD COUNTER
       MOV  WB,-(XS)         STACK OLD LEVEL INDICATOR
       CHK                   CHECK FOR STACK OVERFLOW
       ZER  WA               SET NEW STATE TO ZERO
       MOV  XL,WB            SET NEW LEVEL INDICATOR
       MOV  =NUM01,WC        INITIALIZE NEW COUNTER
       BRN  EXP01            JUMP TO SCAN NEXT ELEMENT
*
*      HERE FOR LBR (S=0,1)
*
*      THIS IS AN ILLEGAL USE OF LEFT BRACKET
*
EXP08  ERB  222,Syntax error: Invalid use of left bracket
*
*      HERE FOR LBR (S=2)
*
*      SET NEW LEVEL AND START TO SCAN SUBSCRIPTS
*
EXP09  MOV  (XS)+,XR         LOAD ARRAY PTR FOR CMOPN
       MOV  =NUM03,XL        SET NEW LEVEL INDICATOR
       BRN  EXP07            JUMP TO STACK OLD AND START NEW
*
*      HERE FOR FNC (S=0,1)
*
*      STACK OLD LEVEL AND START TO SCAN ARGUMENTS
*
EXP10  MOV  =NUM05,XL        SET NEW LEV INDIC (XR=VRBLK=CMOPN)
       BRN  EXP07            JUMP TO STACK OLD AND START NEW
*
*      HERE FOR CMA (S=2)
*
*      INCREMENT ARGUMENT COUNT AND CONTINUE
*
EXP11  ICV  WC               INCREMENT COUNTER
       JSR  EXPDM            DUMP OPERATORS AT THIS LEVEL
       ZER  -(XS)            SET NEW LEVEL FOR PARAMETER
       ZER  WA               SET NEW STATE
       BGT  WB,=NUM02,EXP01  LOOP BACK UNLESS OUTER LEVEL
       ERB  223,Syntax error: Invalid use of comma
       EJC
*
*      EXPAN (CONTINUED)
*
*      HERE FOR RPR (S=2)
*
*      AT OUTER LEVEL IN A NORMAL GOTO THIS IS A TERMINATOR
*      OTHERWISE IT MUST TERMINATE A FUNCTION OR GROUPING
*
EXP12  BEQ  WB,=NUM01,EXP20  END OF NORMAL GOTO
       BEQ  WB,=NUM05,EXP13  END OF FUNCTION ARGUMENTS
       BEQ  WB,=NUM04,EXP14  END OF GROUPING / SELECTION
       ERB  224,Syntax error: Unbalanced right parenthesis
*
*      HERE AT END OF FUNCTION ARGUMENTS
*
EXP13  MOV  =C_FNC,XL        SET CMTYP VALUE FOR FUNCTION
       BRN  EXP15            JUMP TO BUILD CMBLK
*
*      HERE FOR END OF GROUPING
*
EXP14  BEQ  WC,=NUM01,EXP17  JUMP IF END OF GROUPING
       MOV  =C_SEL,XL        ELSE SET CMTYP FOR SELECTION
*
*      MERGE HERE TO BUILD CMBLK FOR LEVEL JUST SCANNED AND
*      TO POP UP TO THE PREVIOUS SCAN LEVEL BEFORE CONTINUING.
*
EXP15  JSR  EXPDM            DUMP OPERATORS AT THIS LEVEL
       MOV  WC,WA            COPY COUNT
       ADD  =CMVLS,WA        ADD FOR STANDARD FIELDS AT START
       WTB  WA               CONVERT LENGTH TO BYTES
       JSR  ALLOC            ALLOCATE SPACE FOR CMBLK
       MOV  =B_CMT,(XR)      STORE TYPE CODE FOR CMBLK
       MOV  XL,CMTYP(XR)     STORE CMBLK NODE TYPE INDICATOR
       MOV  WA,CMLEN(XR)     STORE LENGTH
       ADD  WA,XR            POINT PAST END OF BLOCK
       LCT  WC,WC            SET LOOP COUNTER
*
*      LOOP TO MOVE REMAINING WORDS TO CMBLK
*
EXP16  MOV  (XS)+,-(XR)      MOVE ONE OPERAND PTR FROM STACK
       MOV  (XS)+,WB         POP TO OLD LEVEL INDICATOR
       BCT  WC,EXP16         LOOP TILL ALL MOVED
       EJC
*
*      EXPAN (CONTINUED)
*
*      COMPLETE CMBLK AND STACK POINTER TO IT ON STACK
*
       SUB  *CMVLS,XR        POINT BACK TO START OF BLOCK
       MOV  (XS)+,WC         RESTORE OLD COUNTER
       MOV  (XS),CMOPN(XR)   STORE OPERAND PTR IN CMBLK
       MOV  XR,(XS)          STACK CMBLK POINTER
       MOV  =NUM02,WA        SET NEW STATE
       BRN  EXP01            BACK FOR NEXT ELEMENT
*
*      HERE AT END OF A PARENTHESIZED EXPRESSION
*
EXP17  JSR  EXPDM            DUMP OPERATORS AT THIS LEVEL
       MOV  (XS)+,XR         RESTORE XR
       MOV  (XS)+,WB         RESTORE OUTER LEVEL
       MOV  (XS)+,WC         RESTORE OUTER COUNT
       MOV  XR,(XS)          STORE OPND OVER UNUSED CMOPN VAL
       MOV  =NUM02,WA        SET NEW STATE
       BRN  EXP01            BACK FOR NEXT ELE8ENT
*
*      HERE FOR RBR (S=2)
*
*      AT OUTER LEVEL IN A DIRECT GOTO, THIS IS A TERMINATOR.
*      OTHERWISE IT MUST TERMINATE A SUBSCRIPT LIST.
*
EXP18  MOV  =C_ARR,XL        SET CMTYP FOR ARRAY REFERENCE
       BEQ  WB,=NUM03,EXP15  JUMP TO BUILD CMBLK IF END ARRAYREF
       BEQ  WB,=NUM02,EXP20  JUMP IF END OF DIRECT GOTO
       ERB  225,Syntax error: Unbalanced right bracket
       EJC
*
*      EXPAN (CONTINUED)
*
*      HERE FOR COL,SMC (S=2)
*
*      ERROR UNLESS TERMINATING STATEMENT BODY AT OUTER LEVEL
*
EXP19  MNZ  SCNRS            RESCAN TERMINATOR
       MOV  WB,XL            COPY LEVEL INDICATOR
       BSW  XL,6             SWITCH ON LEVEL INDICATOR
       IFF  0,EXP20          NORMAL OUTER LEVEL
       IFF  1,EXP22          FAIL IF NORMAL GOTO
       IFF  2,EXP23          FAIL IF DIRECT GOTO
       IFF  3,EXP24          FAIL ARRAY BRACKETS
       IFF  4,EXP21          FAIL IF IN GROUPING
       IFF  5,EXP21          FAIL FUNCTION ARGS
       ESW                   END SWITCH ON LEVEL
*
*      HERE AT NORMAL END OF EXPRESSION
*
EXP20  JSR  EXPDM            DUMP REMAINING OPERATORS
       MOV  (XS)+,XR         LOAD TREE POINTER
       ICA  XS               POP OFF BOTTOM OF STACK MARKER
       EXI                   RETURN TO EXPAN CALLER
*
*      MISSING RIGHT PAREN
*
EXP21  ERB  226,Syntax error: Missing right paren
*
*      MISSING RIGHT PAREN IN GOTO FIELD
*
EXP22  ERB  227,Syntax error: Right paren missing from goto
*
*      MISSING BRACKET IN GOTO
*
EXP23  ERB  228,Syntax error: Right bracket missing from goto
*
*      MISSING ARRAY BRACKET
*
EXP24  ERB  229,Syntax error: Missing right array bracket
       EJC
*
*      EXPAN (CONTINUED)
*
*      LOOP HERE WHEN AN OPERATOR CAUSES AN OPERATOR DUMP
*
EXP25  MOV  XR,EXPSV
       JSR  EXPOP            POP ONE OPERATOR
       MOV  EXPSV,XR         RESTORE OP DV POINTER AND MERGE
*
*      HERE FOR BOP (S=2)
*
*      REMOVE OPERATORS (CONDENSE) FROM STACK UNTIL NO MORE
*      LEFT AT THIS LEVEL OR TOP ONE HAS LOWER PRECEDENCE.
*      LOOP HERE TILL THIS CONDITION IS MET.
*
EXP26  MOV  NUM01(XS),XL     LOAD OPERATOR DVPTR FROM STACK
       BLE  XL,=NUM05,EXP27  JUMP IF BOTTOM OF STACK LEVEL
       BLT  DVRPR(XR),DVLPR(XL),EXP25 ELSE POP IF NEW PREC IS LO
*
*      HERE FOR UOP (S=0,1)
*
*      BINARY OPERATOR MERGES AFTER PRECEDENCE CHECK
*
*      THE OPERATOR DV IS STORED ON THE STACK AND THE SCAN
*      CONTINUES AFTER SETTING THE SCAN STATE TO ONE.
*
EXP27  MOV  XR,-(XS)         STACK OPERATOR DVPTR ON STACK
       CHK                   CHECK FOR STACK OVERFLOW
       MOV  =NUM01,WA        SET NEW STATE
       BNE  XR,=OPDVS,EXP01  BACK FOR NEXT ELEMENT UNLESS =
*
*      HERE FOR SPECIAL CASE OF BINARY =. THE SYNTAX ALLOWS A
*      NULL RIGHT ARGUMENT FOR THIS OPERATOR TO BE LEFT
*      OUT. ACCORDINGLY WE RESET TO STATE ZERO TO GET PROPER
*      ACTION ON A TERMINATOR (SUPPLY A NULL CONSTANT).
*
       ZER  WA               SET STATE ZERO
       BRN  EXP01            JUMP FOR NEXT ELEMENT
       ENP                   END PROCEDURE EXPAN
       EJC
*
*      EXPAP -- TEST FOR PATTERN MATCH TREE
*
*      EXPAP IS PASSED AN EXPRESSION TREE TO DETERMINE IF IT
*      IS A PATTERN MATCH. THE FOLLOWING ARE RECOGIZED AS
*      MATCHES IN THE CONTEXT OF THIS CALL.
*
*      1)   AN EXPLICIT USE OF BINARY QUESTION MARK
*      2)   A CONCATENATION
*      3)   AN ALTERNATION WHOSE LEFT OPERAND IS A CONCATENATION
*
*      (XR)                  PTR TO EXPAN TREE
*      JSR  EXPAP            CALL TO TEST FOR PATTERN MATCH
*      PPM  LOC              TRANSFER LOC IF NOT A PATTERN MATCH
*      (WA)                  DESTROYED
*      (XR)                  UNCHANGED (IF NOT MATCH)
*      (XR)                  PTR TO BINARY OPERATOR BLK IF MATCH
*
EXPAP  PRC  E,1              ENTRY POINT
       MOV  XL,-(XS)         SAVE XL
       BNE  (XR),=B_CMT,EXPP2 NO MATCH IF NOT COMPLEX
       MOV  CMTYP(XR),WA     ELSE LOAD TYPE CODE
       BEQ  WA,=C_CNC,EXPP1  CONCATENATION IS A MATCH
       BEQ  WA,=C_PMT,EXPP1  BINARY QUESTION MARK IS A MATCH
       BNE  WA,=C_ALT,EXPP2  ELSE NOT MATCH UNLESS ALTERNATION
*
*      HERE FOR ALTERNATION. CHANGE (A B) / C TO A QM (B / C)
*
       MOV  CMLOP(XR),XL     LOAD LEFT OPERAND POINTER
       BNE  (XL),=B_CMT,EXPP2 NOT MATCH IF LEFT OPND NOT COMPLEX
       BNE  CMTYP(XL),=C_CNC,EXPP2 NOT MATCH IF LEFT OP NOT CONC
       MOV  CMROP(XL),CMLOP(XR) XR POINTS TO (B / C)
       MOV  XR,CMROP(XL)     SET XL OPNDS TO A, (B / C)
       MOV  XL,XR            POINT TO THIS ALTERED NODE
*
*      EXIT HERE FOR PATTERN MATCH
*
EXPP1  MOV  (XS)+,XL         RESTORE ENTRY XL
       EXI                   GIVE PATTERN MATCH RETURN
*
*      EXIT HERE IF NOT PATTERN MATCH
*
EXPP2  MOV  (XS)+,XL         RESTORE ENTRY XL
       EXI  1                GIVE NON-MATCH RETURN
       ENP                   END PROCEDURE EXPAP
       EJC
*
*      EXPDM -- DUMP OPERATORS AT CURRENT LEVEL (FOR EXPAN)
*
*      EXPDM USES EXPOP TO CONDENSE ALL OPERATORS AT THIS SYNTAX
*      LEVEL. THE STACK BOTTOM IS RECOGNIZED FROM THE LEVEL
*      VALUE WHICH IS SAVED ON THE TOP OF THE STACK.
*
*      JSR  EXPDM            CALL TO DUMP OPERATORS
*      (XS)                  POPPED AS REQUIRED
*      (XR,WA)               DESTROYED
*
EXPDM  PRC  N,0              ENTRY POINT
       MOV  XL,R_EXS         SAVE XL VALUE
*
*      LOOP TO DUMP OPERATORS
*
EXDM1  BLE  NUM01(XS),=NUM05,EXDM2 JUMP IF STACK BOTTOM (SAVED LEVEL
       JSR  EXPOP            ELSE POP ONE OPERATOR
       BRN  EXDM1            AND LOOP BACK
*
*      HERE AFTER POPPING ALL OPERATORS
*
EXDM2  MOV  R_EXS,XL         RESTORE XL
       ZER  R_EXS            RELEASE SAVE LOCATION
       EXI                   RETURN TO EXPDM CALLER
       ENP                   END PROCEDURE EXPDM
       EJC
*
*      EXPOP-- POP OPERATOR (FOR EXPAN)
*
*      EXPOP IS USED BY THE EXPAN ROUTINE TO CONDENSE ONE
*      OPERATOR FROM THE TOP OF THE SYNTAX STACK. AN APPROPRIATE
*      CMBLK IS BUILT FOR THE OPERATOR (UNARY OR BINARY) AND A
*      POINTER TO THIS CMBLK IS STACKED.
*
*      EXPOP IS ALSO USED BY SCNGF (GOTO FIELD SCAN) PROCEDURE
*
*      JSR  EXPOP            CALL TO POP OPERATOR
*      (XS)                  POPPED APPROPRIATELY
*      (XR,XL,WA)            DESTROYED
*
EXPOP  PRC  N,0              ENTRY POINT
       MOV  NUM01(XS),XR     LOAD OPERATOR DV POINTER
       BEQ  DVLPR(XR),=LLUNO,EXPO2 JUMP IF UNARY
*
*      HERE FOR BINARY OPERATOR
*
       MOV  *CMBS_,WA        SET SIZE OF BINARY OPERATOR CMBLK
       JSR  ALLOC            ALLOCATE SPACE FOR CMBLK
       MOV  (XS)+,CMROP(XR)  POP AND STORE RIGHT OPERAND PTR
       MOV  (XS)+,XL         POP AND LOAD OPERATOR DV PTR
       MOV  (XS),CMLOP(XR)   STORE LEFT OPERAND POINTER
*
*      COMMON EXIT POINT
*
EXPO1  MOV  =B_CMT,(XR)      STORE TYPE CODE FOR CMBLK
       MOV  DVTYP(XL),CMTYP(XR) STORE CMBLK NODE TYPE CODE
       MOV  XL,CMOPN(XR)     STORE DVPTR (=PTR TO DAC O_XXX)
       MOV  WA,CMLEN(XR)     STORE CMBLK LENGTH
       MOV  XR,(XS)          STORE RESULTING NODE PTR ON STACK
       EXI                   RETURN TO EXPOP CALLER
*
*      HERE FOR UNARY OPERATOR
*
EXPO2  MOV  *CMUS_,WA        SET SIZE OF UNARY OPERATOR CMBLK
       JSR  ALLOC            ALLOCATE SPACE FOR CMBLK
       MOV  (XS)+,CMROP(XR)  POP AND STORE OPERAND POINTER
       MOV  (XS),XL          LOAD OPERATOR DV POINTER
       BRN  EXPO1            MERGE BACK TO EXIT
       ENP                   END PROCEDURE EXPOP
       EJC
.IF    .CSFN
*
*      FILNM -- OBTAIN FILE NAME FROM STATEMENT NUMBER
*
*      FILNM TAKES A STATEMENT NUMBER AND EXAMINES THE FILE NAME
*      TABLE POINTED TO BY R_SFN TO FIND THE NAME OF THE FILE
*      CONTAINING THE GIVEN STATEMENT.  TABLE ENTRIES ARE
*      ARRANGED IN ORDER OF ASCENDING STATEMENT NUMBER (THERE
*      IS ONLY ONE HASH BUCKET IN THIS TABLE).  ELEMENTS ARE
*      ADDED TO THE TABLE EACH TIME THERE IS A CHANGE IN
*      FILE NAME, RECORDING THE THEN CURRENT STATEMENT NUMBER.
*
*      TO FIND THE FILE NAME, THE LINKED LIST OF TEBLKS IS
*      SCANNED FOR AN ELEMENT CONTAINING A SUBSCRIPT (STATEMENT
*      NUMBER) GREATER THAN THE ARGUMENT STATEMENT NUMBER, OR
*      THE END OF CHAIN.  WHEN THIS CONDITION IS MET, THE
*      PREVIOUS TEBLK CONTAINS THE DESIRED FILE NAME AS ITS
*      VALUE ENTRY.
*
*      (WC)                  STATEMENT NUMBER
*      JSR  FILNM            CALL TO OBTAIN FILE NAME
*      (XL)                  FILE NAME (SCBLK)
*      (IA)                  DESTROYED
*
FILNM  PRC  E,0              ENTRY POINT
       MOV  WB,-(XS)         PRESERVE WB
       BZE  WC,FILN3         RETURN NULLS IF STNO IS ZERO
       MOV  R_SFN,XL         FILE NAME TABLE
       BZE  XL,FILN3         IF NO TABLE
       MOV  TBBUK(XL),WB     GET BUCKET ENTRY
       BEQ  WB,R_SFN,FILN3   JUMP IF NO TEBLKS ON CHAIN
       MOV  XR,-(XS)         PRESERVE XR
       MOV  WB,XR            PREVIOUS BLOCK POINTER
       MOV  WC,-(XS)         PRESERVE STMT NUMBER
*
*      LOOP THROUGH TEBLKS ON HASH CHAIN
*
FILN1  MOV  XR,XL            NEXT ELEMENT TO EXAMINE
       MOV  TESUB(XL),XR     LOAD SUBSCRIPT VALUE (AN ICBLK)
       LDI  ICVAL(XR)        LOAD THE STATEMENT NUMBER
       MFI  WC               CONVERT TO ADDRESS CONSTANT
       BLT  (XS),WC,FILN2    COMPARE ARG WITH TEBLK STMT NUMBER
*
*      HERE IF DESIRED STMT NUMBER IS GE TEBLK STMT NUMBER
*
       MOV  XL,WB            SAVE PREVIOUS ENTRY POINTER
       MOV  TENXT(XL),XR     POINT TO NEXT TEBLK ON CHAIN
       BNE  XR,R_SFN,FILN1   JUMP IF THERE IS ONE
*
*      HERE IF CHAIN EXHAUSTED OR DESIRED BLOCK FOUND.
*
FILN2  MOV  WB,XL            PREVIOUS TEBLK
       MOV  TEVAL(XL),XL     GET PTR TO FILE NAME SCBLK
       MOV  (XS)+,WC         RESTORE STMT NUMBER
       MOV  (XS)+,XR         RESTORE XR
       MOV  (XS)+,WB         RESTORE WB
       EXI
*
*      NO TABLE OR NO TABLE ENTRIES
*
FILN3  MOV  (XS)+,WB         RESTORE WB
       MOV  =NULLS,XL        RETURN NULL STRING
       EXI
       ENP
       EJC
.FI
*
.IF    .CULC
*
*      FLSTG -- FOLD STRING TO UPPER CASE
*
*      FLSTG FOLDS A CHARACTER STRING CONTAINING LOWER CASE
*      CHARACCTERS TO ONE CONTAINING UPPER CASE CHARACTERS.
*      FOLDING IS ONLY DONE IF &CASE (KVCAS) IS NOT ZERO.
*
*      (XR)                  STRING ARGUMENT
*      (WA)                  LENGTH OF STRING
*      JSR  FLSTG            CALL TO FOLD STRING
*      (XR)                  RESULT STRING (POSSIBLY ORIGINAL)
*      (WC)                  DESTROYED
*
FLSTG  PRC  E,0              ENTRY POINT
       BZE  KVCAS,FST99      SKIP IF &CASE IS 0
       MOV  XL,-(XS)         SAVE XL ACROSS CALL
       MOV  XR,-(XS)         SAVE ORIGINAL SCBLK PTR
       JSR  ALOCS            ALLOCATE NEW STRING BLOCK
       MOV  (XS),XL          POINT TO ORIGINAL SCBLK
       MOV  XR,-(XS)         SAVE POINTER TO NEW SCBLK
       PLC  XL               POINT TO ORIGINAL CHARS
       PSC  XR               POINT TO NEW CHARS
       ZER  -(XS)            INIT DID FOLD FLAG
       LCT  WC,WC            LOAD LOOP COUNTER
FST01  LCH  WA,(XL)+         LOAD CHARACTER
       BLT  WA,=CH__A,FST02  SKIP IF LESS THAN LC A
       BGT  WA,=CH___,FST02  SKIP IF GREATER THAN LC Z
       FLC  WA               FOLD CHARACTER TO UPPER CASE
       MNZ  (XS)             SET DID FOLD CHARACTER FLAG
FST02  SCH  WA,(XR)+         STORE (POSSIBLY FOLDED) CHARACTER
       BCT  WC,FST01         LOOP THRU ENTIRE STRING
       CSC  XR               COMPLETE STORE CHARACTERS
       MOV  (XS)+,XR         SEE IF ANY CHANGE
       BNZ  XR,FST10         SKIP IF FOLDING DONE (NO CHANGE)
       MOV  (XS)+,DNAMP      DO NOT NEED NEW SCBLK
       MOV  (XS)+,XR         RETURN ORIGINAL SCBLK
       BRN  FST20            MERGE BELOW
FST10  MOV  (XS)+,XR         RETURN NEW SCBLK
       ICA  XS               THROW AWAY ORIGINAL SCBLK POINTER
FST20  MOV  SCLEN(XR),WA     RELOAD STRING LENGTH
       MOV  (XS)+,XL         RESTORE XL
FST99  EXI                   RETURN
       ENP
       EJC
.FI
*
*      GBCOL -- PERFORM GARBAGE COLLECTION
*
*      GBCOL PERFORMS A GARBAGE COLLECTION ON THE DYNAMIC REGION
*      ALL BLOCKS WHICH ARE NO LONGER IN USE ARE ELIMINATED
*      BY MOVING BLOCKS WHICH ARE IN USE DOWN AND RESETTING
*      DNAMP, THE POINTER TO THE NEXT AVAILABLE LOCATION.
*
*      (WB)                  MOVE OFFSET (SEE BELOW)
*      JSR  GBCOL            CALL TO COLLECT GARBAGE
.IF    .CSED
*      (XR)                  SEDIMENT SIZE AFTER COLLECTION
.ELSE
*      (XR)                  DESTROYED
.FI
*
*      THE FOLLOWING CONDITIONS MUST BE MET AT THE TIME WHEN
*      GBCOL IS CALLED.
*
*      1)   ALL POINTERS TO BLOCKS IN THE DYNAMIC AREA MUST BE
*           ACCESSIBLE TO THE GARBAGE COLLECTOR. THIS MEANS
*           THAT THEY MUST OCCUR IN ONE OF THE FOLLOWING.
*
*           A)               MAIN STACK, WITH CURRENT TOP
*                            ELEMENT BEING INDICATED BY XS
*
*           B)               IN RELOCATABLE FIELDS OF VRBLKS.
*
*           C)               IN REGISTER XL AT THE TIME OF CALL
*
*           E)               IN THE SPECIAL REGION OF WORKING
*                            STORAGE WHERE NAMES BEGIN WITH R_.
*
*      2)   ALL POINTERS MUST POINT TO THE START OF BLOCKS WITH
*           THE SOLE EXCEPTION OF THE CONTENTS OF THE CODE
*           POINTER REGISTER WHICH POINTS INTO THE R_COD BLOCK.
*
*      3)   NO LOCATION WHICH APPEARS TO CONTAIN A POINTER
*           INTO THE DYNAMIC REGION MAY OCCUR UNLESS IT IS IN
*           FACT A POINTER TO THE START OF THE BLOCK. HOWEVER
*           POINTERS OUTSIDE THIS AREA MAY OCCUR AND WILL
*           NOT BE CHANGED BY THE GARBAGE COLLECTOR.
*           IT IS ESPECIALLY IMPORTANT TO MAKE SURE THAT XL
*           DOES NOT CONTAIN A GARBAGE VALUE FROM SOME PROCESS
*           CARRIED OUT BEFORE THE CALL TO THE COLLECTOR.
*
*      GBCOL HAS THE CAPABILITY OF MOVING THE FINAL COMPACTED
*      RESULT UP IN MEMORY (WITH ADDRESSES ADJUSTED ACCORDINGLY)
*      THIS IS USED TO ADD SPACE TO THE STATIC REGION. THE
*      ENTRY VALUE OF WB IS THE NUMBER OF BYTES TO MOVE UP.
*      THE CALLER MUST GUARANTEE THAT THERE IS ENOUGH ROOM.
*      FURTHERMORE THE VALUE IN WB IF IT IS NON-ZERO, MUST BE AT
*      LEAST 256 SO THAT THE MWB INSTRUCTION CONDITIONS ARE MET.
       EJC
*
*      GBCOL (CONTINUED)
*
*      THE ALGORITHM, WHICH IS A MODIFICATION OF THE LISP-2
*      GARBAGE COLLECTOR DEVISED BY R.DEWAR AND K.BELCHER
*      TAKES THREE PASSES AS FOLLOWS.
*
*      1)   ALL POINTERS IN MEMORY ARE SCANNED AND BLOCKS IN USE
*           DETERMINED FROM THIS SCAN. NOTE THAT THIS PROCEDURE
*           IS RECURSIVE AND USES THE MAIN STACK FOR LINKAGE.
*           THE MARKING PROCESS IS THUS SIMILAR TO THAT USED IN
*           A STANDARD LISP COLLECTOR. HOWEVER THE METHOD OF
*           ACTUALLY MARKING THE BLOCKS IS DIFFERENT.
*
*           THE FIRST FIELD OF A BLOCK NORMALLY CONTAINS A
*           CODE ENTRY POINT POINTER. SUCH AN ENTRY POINTER
*           CAN BE DISTINGUISHED FROM THE ADDRESS OF ANY POINTER
*           TO BE PROCESSED BY THE COLLECTOR. DURING GARBAGE
*           COLLECTION, THIS WORD IS USED TO BUILD A BACK CHAIN
*           OF POINTERS THROUGH FIELDS WHICH POINT TO THE BLOCK.
*           THE END OF THE CHAIN IS MARKED BY THE OCCURENCE
*           OF THE WORD WHICH USED TO BE IN THE FIRST WORD OF
*           THE BLOCK. THIS BACKCHAIN SERVES BOTH AS A MARK
*           INDICATING THAT THE BLOCK IS IN USE AND AS A LIST OF
*           REFERENCES FOR THE RELOCATION PHASE.
*
*      2)   STORAGE IS SCANNED SEQUENTIALLY TO DISCOVER WHICH
*           BLOCKS ARE CURRENTLY IN USE AS INDICATED BY THE
*           PRESENCE OF A BACKCHAIN. TWO POINTERS ARE MAINTAINED
*           ONE SCANS THROUGH LOOKING AT EACH BLOCK. THE OTHER
*           IS INCREMENTED ONLY FOR BLOCKS FOUND TO BE IN USE.
*           IN THIS WAY, THE EVENTUAL LOCATION OF EACH BLOCK CAN
*           BE DETERMINED WITHOUT ACTUALLY MOVING ANY BLOCKS.
*           AS EACH BLOCK WHICH IS IN USE IS PROCESSED, THE BACK
*           CHAIN IS USED TO RESET ALL POINTERS WHICH POINT TO
*           THIS BLOCK TO CONTAIN ITS NEW ADDRESS, I.E. THE
*           ADDRESS IT WILL OCCUPY AFTER THE BLOCKS ARE MOVED.
*           THE FIRST WORD OF THE BLOCK, TAKEN FROM THE END OF
*           THE CHAIN IS RESTORED AT THIS POINT.
*
*           DURING PASS 2, THE COLLECTOR BUILDS BLOCKS WHICH
*           DESCRIBE THE REGIONS OF STORAGE WHICH ARE TO BE
*           MOVED IN THE THIRD PASS. THERE IS ONE DESCRIPTOR FOR
*           EACH CONTIGUOUS SET OF GOOD BLOCKS. THE DESCRIPTOR
*           IS BUILT JUST BEHIND THE BLOCK TO BE MOVED AND
*           CONTAINS A POINTER TO THE NEXT BLOCK AND THE NUMBER
*           OF WORDS TO BE MOVED.
*
*      3)   IN THE THIRD AND FINAL PASS, THE MOVE DESCRIPTOR
*           BLOCKS BUILT IN PASS TWO ARE USED TO ACTUALLY MOVE
*           THE BLOCKS DOWN TO THE BOTTOM OF THE DYNAMIC REGION.
*           THE COLLECTION IS THEN COMPLETE AND THE NEXT
*           AVAILABLE LOCATION POINTER IS RESET.
       EJC
*
*      GBCOL (CONTINUED)
*
.IF    .CSED
*      THE GARBAGE COLLECTOR ALSO RECOGNIZES THE CONCEPT OF
*      SEDIMENT.  SEDIMENT IS DEFINED AS LONG-LIVED OBJECTS
*      WHICH PERCIPITATE TO THE BOTTOM OF DYNAMIC STORAGE.
*      MOVING THESE OBJECTS DURING REPEATED COLLECTIONS IS
*      INEFFICIENT.  IT ALSO CONTRIBUTES TO THRASHING ON
*      SYSTEMS WITH VIRTUAL MEMORY.  IN A TYPICAL WORST-CASE
*      SITUATION, THERE MAY BE SEVERAL MEGABYTES OF LIVE OBJECTS
*      IN THE SEDIMENT, AND ONLY A FEW DEAD OBJECTS IN NEED OF
*      COLLECTION.  WITHOUT RECOGNISING SEDIMENT, THE STANDARD
*      COLLECTOR WOULD MOVE THOSE MEGABYTES OF OBJECTS DOWNWARD
*      TO SQUEEZE OUT THE DEAD OBJECTS.  THIS TYPE OF MOVE
*      WOULD RESULT IN EXCESSIVE THRASING FOR VERY LITTLE MEMORY
*      GAIN.
*
*      SCANNING OF BLOCKS IN THE SEDIMENT CANNOT BE AVOIDED
*      ENTIRELY, BECAUSE THESE BLOCKS MAY CONTAIN POINTERS TO
*      LIVE OBJECTS ABOVE THE SEDIMENT.  HOWEVER, SEDIMENT
*      BLOCKS NEED NOT BE LINKED TO A BACK CHAIN AS DESCRIBED
*      IN PASS ONE ABOVE.  SINCE THESE BLOCKS WILL NOT BE MOVED,
*      POINTERS TO THEM DO NOT NEED TO BE ADJUSTED.  ELIMINATING
*      UNNECESSARY BACK CHAIN LINKS INCREASES LOCALITY OF
*      REFERENCE, IMPROVING VIRTUAL MEMORY PERFORMANCE.
*
*      BECAUSE BACK CHAINS ARE USED TO MARK BLOCKS WHOSE CON-
*      TENTS HAVE BEEN PROCESSED, A DIFFERENT MARKING SYSTEM
.IF    .CEPP
*      IS NEEDED FOR BLOCKS IN THE SEDIMENT.  SINCE BLOCK TYPE
*      WORDS POINT TO ODD-PARITY ENTRY ADDRESSES, MERELY INCRE-
*      MENTING THE TYPE WORD SERVES TO MARK THE BLOCK AS PRO-
*      CESSED.  DURING PASS THREE, THE TYPE WORDS ARE DECRE-
*      MENTED TO RESTORE THEM TO THEIR ORIGINAL VALUE.
.ELSE
*      IS NEEDED FOR BLOCKS IN THE SEDIMENT.  ALL BLOCK TYPE
*      WORDS NORMALLY LIE IN THE RANGE B_AAA TO P_YYY.  BLOCKS
*      CAN BE MARKED BY ADDING AN OFFSET (CREATED IN GBCMK) TO
*      MOVE TYPE WORDS OUT OF THIS RANGE.  DURING PASS THREE THE
*      OFFSET IS SUBTRACTED TO RESTORE THEM TO THEIR ORIGINAL
*      VALUE.
.FI
       EJC
*
*      GBCOL (CONTINUED)
*
*
*      THE VARIABLE DNAMS CONTAINS THE NUMBER OF BYTES OF MEMORY
*      CURRENTLY IN THE SEDIMENT.  SETTING DNAMS TO ZERO WILL
*      ELIMINATE THE SEDIMENT AND FORCE IT TO BE INCLUDED IN A
*      FULL GARBAGE COLLECTION.  GBCOL RETURNS A SUGGESTED NEW
*      VALUE FOR DNAMS (USUALLY DNAMP-DNAMB) IN XR WHICH THE
*      CALLER CAN STORE IN DNAMS IF IT WISHES TO MAINTAIN THE
*      SEDIMENT.  THAT IS, DATA REMAINING AFTER A GARBAGE
*      COLLECTION IS CONSIDERED TO BE SEDIMENT.  IF ONE ACCEPTS
*      THE COMMON LORE THAT MOST OBJECTS ARE EITHER VERY SHORT-
*      OR VERY LONG-LIVED, THEN THIS NAIVE SETTING OF DNAMS
*      PROBABLY INCLUDES SOME SHORT-LIVED OBJECTS TOWARD THE END
*      OF THE SEDIMENT.
*
*      KNOWING WHEN TO RESET DNAMS TO ZERO TO COLLECT THE SEDI-
*      MENT IS NOT PRECISELY KNOWN.  WE FORCE IT TO ZERO PRIOR
*      TO PRODUCING A DUMP, WHEN GBCOL IS INVOKED BY COLLECT()
*      (SO THAT THE SEDIMENT IS INVISIBLE TO THE USER), WHEN
*      SYSMM IS UNABLE TO OBTAIN ADDITIONAL MEMORY, AND WHEN
*      GBCOL IS CALLED TO RELOCATE THE DYNAMIC AREA UP IN MEMORY
*      (TO MAKE ROOM FOR ENLARGING THE STATIC AREA).  IF THERE
*      ARE NO OTHER RESET SITUATIONS, THIS LEADS TO THE INEXO-
*      RABLE GROWTH OF THE SEDIMENT, POSSIBLE FORCING A MODEST
*      PROGRAM TO BEGIN TO USE VIRTUAL MEMORY THAT IT OTHERWISE
*      WOULD NOT.
*
*      AS WE SCAN SEDIMENT BLOCKS IN PASS THREE, WE MAINTAIN
*      AGGREGATE COUNTS OF THE AMOUNT OF DEAD AND LIVE STORAGE,
*      WHICH IS USED TO DECIDE WHEN TO RESET DNAMS.  WHEN THE
*      RATIO OF FREE STORAGE FOUND IN THE SEDIMENT TO TOTAL
*      SEDIMENT SIZE EXCEEDS A THRESHOLD, THE SEDIMENT IS MARKED
*      FOR COLLECTION ON THE NEXT GBCOL CALL.
*
.FI
       EJC
*
*      GBCOL (CONTINUED)
*
GBCOL  PRC  E,0              ENTRY POINT
*Z-
       BNZ  DMVCH,GBC14      FAIL IF IN MID-DUMP
       MNZ  GBCFL            NOTE GBCOL ENTERED
       MOV  WA,GBSVA         SAVE ENTRY WA
       MOV  WB,GBSVB         SAVE ENTRY WB
       MOV  WC,GBSVC         SAVE ENTRY WC
       MOV  XL,-(XS)         SAVE ENTRY XL
       SCP  WA               GET CODE POINTER VALUE
       SUB  R_COD,WA         MAKE RELATIVE
       LCP  WA               AND RESTORE
.IF    .CSED
       BZE  WB,GBC0A         CHECK THERE IS NO MOVE OFFSET
       ZER  DNAMS            COLLECT SEDIMENT IF MUST MOVE IT
GBC0A  MOV  DNAMB,WA         START OF DYNAMIC AREA
       ADD  DNAMS,WA         SIZE OF SEDIMENT
       MOV  WA,GBCSD         FIRST LOCATION PAST SEDIMENT
.IF    .CEPP
.ELSE
       MOV  =P_YYY,WA        LAST ENTRY POINT
       ICV  WA               ADDRESS PAST LAST ENTRY POINT
       SUB  =B_AAA,WA        SIZE OF ENTRY POINT AREA
       MOV  WA,GBCMK         USE TO MARK PROCESSED SED. BLOCKS
.FI
.FI
.IF    .CGBC
*
*      INFORM SYSGC THAT COLLECTION TO COMMENCE
*
       MNZ  XR               NON-ZERO FLAGS START OF COLLECTION
       MOV  DNAMB,WA         START OF DYNAMIC AREA
       MOV  DNAMP,WB         NEXT AVAILABLE LOCATION
       MOV  DNAME,WC         LAST AVAILABLE LOCATION + 1
       JSR  SYSGC            INFORM OF COLLECTION
.FI
*
*      PROCESS STACK ENTRIES
*
       MOV  XS,XR            POINT TO STACK FRONT
       MOV  STBAS,XL         POINT PAST END OF STACK
       BGE  XL,XR,GBC00      OK IF D-STACK
       MOV  XL,XR            REVERSE IF ...
       MOV  XS,XL            ... U-STACK
*
*      PROCESS THE STACK
*
GBC00  JSR  GBCPF            PROCESS POINTERS ON STACK
*
*      PROCESS SPECIAL WORK LOCATIONS
*
       MOV  =R_AAA,XR        POINT TO START OF RELOCATABLE LOCS
       MOV  =R_YYY,XL        POINT PAST END OF RELOCATABLE LOCS
       JSR  GBCPF            PROCESS WORK FIELDS
*
*      PREPARE TO PROCESS VARIABLE BLOCKS
*
       MOV  HSHTB,WA         POINT TO FIRST HASH SLOT POINTER
*
*      LOOP THROUGH HASH SLOTS
*
GBC01  MOV  WA,XL            POINT TO NEXT SLOT
       ICA  WA               BUMP BUCKET POINTER
       MOV  WA,GBCNM         SAVE BUCKET POINTER
       EJC
*
*      GBCOL (CONTINUED)
*
*      LOOP THROUGH VARIABLES ON ONE HASH CHAIN
*
GBC02  MOV  (XL),XR          LOAD PTR TO NEXT VRBLK
       BZE  XR,GBC03         JUMP IF END OF CHAIN
       MOV  XR,XL            ELSE COPY VRBLK POINTER
       ADD  *VRVAL,XR        POINT TO FIRST RELOC FLD
       ADD  *VRNXT,XL        POINT PAST LAST (AND TO LINK PTR)
       JSR  GBCPF            PROCESS RELOC FIELDS IN VRBLK
       BRN  GBC02            LOOP BACK FOR NEXT BLOCK
*
*      HERE AT END OF ONE HASH CHAIN
*
GBC03  MOV  GBCNM,WA         RESTORE BUCKET POINTER
       BNE  WA,HSHTE,GBC01   LOOP BACK IF MORE BUCKETS TO GO
       EJC
*
*      GBCOL (CONTINUED)
*
*      NOW WE ARE READY TO START PASS TWO. REGISTERS ARE USED
*      AS FOLLOWS IN PASS TWO.
*
*      (XR)                  SCANS THROUGH ALL BLOCKS
*      (WC)                  POINTER TO EVENTUAL LOCATION
*
*      THE MOVE DESCRIPTION BLOCKS BUILT IN THIS PASS HAVE
*      THE FOLLOWING FORMAT.
*
*      WORD 1                POINTER TO NEXT MOVE BLOCK,
*                            ZERO IF END OF CHAIN OF BLOCKS
*
*      WORD 2                LENGTH OF BLOCKS TO BE MOVED IN
*                            BYTES. SET TO THE ADDRESS OF THE
*                            FIRST BYTE WHILE ACTUALLY SCANNING
*                            THE BLOCKS.
*
*      THE FIRST ENTRY ON THIS CHAIN IS A SPECIAL ENTRY
*      CONSISTING OF THE TWO WORDS GBCNM AND GBCNS. AFTER
*      BUILDING THE CHAIN OF MOVE DESCRIPTORS, GBCNM POINTS TO
*      THE FIRST REAL MOVE BLOCK, AND GBCNS IS THE LENGTH OF
*      BLOCKS IN USE AT THE START OF STORAGE WHICH NEED NOT
*      BE MOVED SINCE THEY ARE IN THE CORRECT POSITION.
*
.IF    .CSED
       MOV  DNAMB,XR         POINT TO FIRST BLOCK
       ZER  WB               ACCUMULATE SIZE OF DEAD BLOCKS
GBC04  BEQ  XR,GBCSD,GBC4C   JUMP IF END OF SEDIMENT
       MOV  (XR),WA          ELSE GET FIRST WORD
.IF    .CEPP
       BOD  WA,GBC4B         JUMP IF ENTRY POINTER (UNUSED)
       DCV  WA               RESTORE ENTRY POINTER
.ELSE
       BHI  WA,=P_YYY,GBC4A  SKIP IF NOT ENTRY PTR (IN USE)
       BHI  WA,=B_AAA,GBC4B  JUMP IF ENTRY POINTER (UNUSED)
GBC4A  SUB  GBCMK,WA         RESTORE ENTRY POINTER
.FI
       MOV  WA,(XR)          RESTORE FIRST WORD
       JSR  BLKLN            GET LENGTH OF THIS BLOCK
       ADD  WA,XR            BUMP ACTUAL POINTER
       BRN  GBC04            CONTINUE SCAN THROUGH SEDIMENT
*
*      HERE FOR UNUSED SEDIMENT BLOCK
*
GBC4B  JSR  BLKLN            GET LENGTH OF THIS BLOCK
       ADD  WA,XR            BUMP ACTUAL POINTER
       ADD  WA,WB            COUNT SIZE OF UNUSED BLOCKS
       BRN  GBC04            CONTINUE SCAN THROUGH SEDIMENT
*
*      HERE AT END OF SEDIMENT.  REMEMBER SIZE OF FREE BLOCKS
*      WITHIN THE SEDIMENT.  THIS WILL BE USED LATER TO DECIDE
*      HOW TO SET THE SEDIMENT SIZE RETURNED TO CALLER.
*
*      THEN SCAN REST OF DYNAMIC AREA ABOVE SEDIMENT.
*
*      (WB) = AGGREGATE SIZE OF FREE BLOCKS IN SEDIMENT
*      (XR) = FIRST LOCATION PAST SEDIMENT
*
GBC4C  MOV  WB,GBCSF         SIZE OF SEDIMENT FREE SPACE
.ELSE
       MOV  DNAMB,XR         POINT TO FIRST BLOCK
.FI
       MOV  XR,WC            SET AS FIRST EVENTUAL LOCATION
       ADD  GBSVB,WC         ADD OFFSET FOR EVENTUAL MOVE UP
       ZER  GBCNM            CLEAR INITIAL FORWARD POINTER
       MOV  =GBCNM,GBCLM     INITIALIZE PTR TO LAST MOVE BLOCK
       MOV  XR,GBCNS         INITIALIZE FIRST ADDRESS
*
*      LOOP THROUGH A SERIES OF BLOCKS IN USE
*
GBC05  BEQ  XR,DNAMP,GBC07   JUMP IF END OF USED REGION
       MOV  (XR),WA          ELSE GET FIRST WORD
.IF    .CEPP
       BOD  WA,GBC07         JUMP IF ENTRY POINTER (UNUSED)
.ELSE
       BHI  WA,=P_YYY,GBC06  SKIP IF NOT ENTRY PTR (IN USE)
       BHI  WA,=B_AAA,GBC07  JUMP IF ENTRY POINTER (UNUSED)
.FI
*
*      HERE FOR BLOCK IN USE, LOOP TO RELOCATE REFERENCES
*
GBC06  MOV  WA,XL            COPY POINTER
       MOV  (XL),WA          LOAD FORWARD POINTER
       MOV  WC,(XL)          RELOCATE REFERENCE
.IF    .CEPP
       BEV  WA,GBC06         LOOP BACK IF NOT END OF CHAIN
.ELSE
       BHI  WA,=P_YYY,GBC06  LOOP BACK IF NOT END OF CHAIN
       BLO  WA,=B_AAA,GBC06  LOOP BACK IF NOT END OF CHAIN
.FI
       EJC
*
*      GBCOL (CONTINUED)
*
*      AT END OF CHAIN, RESTORE FIRST WORD AND BUMP PAST
*
       MOV  WA,(XR)          RESTORE FIRST WORD
       JSR  BLKLN            GET LENGTH OF THIS BLOCK
       ADD  WA,XR            BUMP ACTUAL POINTER
       ADD  WA,WC            BUMP EVENTUAL POINTER
       BRN  GBC05            LOOP BACK FOR NEXT BLOCK
*
*      HERE AT END OF A SERIES OF BLOCKS IN USE
*
GBC07  MOV  XR,WA            COPY POINTER PAST LAST BLOCK
       MOV  GBCLM,XL         POINT TO PREVIOUS MOVE BLOCK
       SUB  NUM01(XL),WA     SUBTRACT STARTING ADDRESS
       MOV  WA,NUM01(XL)     STORE LENGTH OF BLOCK TO BE MOVED
*
*      LOOP THROUGH A SERIES OF BLOCKS NOT IN USE
*
GBC08  BEQ  XR,DNAMP,GBC10   JUMP IF END OF USED REGION
       MOV  (XR),WA          ELSE LOAD FIRST WORD OF NEXT BLOCK
.IF    .CEPP
       BEV  WA,GBC09         JUMP IF IN USE
.ELSE
       BHI  WA,=P_YYY,GBC09  JUMP IF IN USE
       BLO  WA,=B_AAA,GBC09  JUMP IF IN USE
.FI
       JSR  BLKLN            ELSE GET LENGTH OF NEXT BLOCK
       ADD  WA,XR            PUSH POINTER
       BRN  GBC08            AND LOOP BACK
*
*      HERE FOR A BLOCK IN USE AFTER PROCESSING A SERIES OF
*      BLOCKS WHICH WERE NOT IN USE, BUILD NEW MOVE BLOCK.
*
GBC09  SUB  *NUM02,XR        POINT 2 WORDS BEHIND FOR MOVE BLOCK
       MOV  GBCLM,XL         POINT TO PREVIOUS MOVE BLOCK
       MOV  XR,(XL)          SET FORWARD PTR IN PREVIOUS BLOCK
       ZER  (XR)             ZERO FORWARD PTR OF NEW BLOCK
       MOV  XR,GBCLM         REMEMBER ADDRESS OF THIS BLOCK
       MOV  XR,XL            COPY PTR TO MOVE BLOCK
       ADD  *NUM02,XR        POINT BACK TO BLOCK IN USE
       MOV  XR,NUM01(XL)     STORE STARTING ADDRESS
       BRN  GBC06            JUMP TO PROCESS BLOCK IN USE
       EJC
*
*      GBCOL (CONTINUED)
*
*      HERE FOR PASS THREE -- ACTUALLY MOVE THE BLOCKS DOWN
*
*      (XL)                  POINTER TO OLD LOCATION
*      (XR)                  POINTER TO NEW LOCATION
*
.IF    .CSED
GBC10  MOV  GBCSD,XR         POINT TO STORAGE ABOVE SEDIMENT
.ELSE
GBC10  MOV  DNAMB,XR         POINT TO START OF STORAGE
.FI
       ADD  GBCNS,XR         BUMP PAST UNMOVED BLOCKS AT START
*
*      LOOP THROUGH MOVE DESCRIPTORS
*
GBC11  MOV  GBCNM,XL         POINT TO NEXT MOVE BLOCK
       BZE  XL,GBC12         JUMP IF END OF CHAIN
       MOV  (XL)+,GBCNM      MOVE POINTER DOWN CHAIN
       MOV  (XL)+,WA         GET LENGTH TO MOVE
       MVW                   PERFORM MOVE
       BRN  GBC11            LOOP BACK
*
*      NOW TEST FOR MOVE UP
*
GBC12  MOV  XR,DNAMP         SET NEXT AVAILABLE LOC PTR
       MOV  GBSVB,WB         RELOAD MOVE OFFSET
       BZE  WB,GBC13         JUMP IF NO MOVE REQUIRED
       MOV  XR,XL            ELSE COPY OLD TOP OF CORE
       ADD  WB,XR            POINT TO NEW TOP OF CORE
       MOV  XR,DNAMP         SAVE NEW TOP OF CORE POINTER
       MOV  XL,WA            COPY OLD TOP
       SUB  DNAMB,WA         MINUS OLD BOTTOM = LENGTH
       ADD  WB,DNAMB         BUMP BOTTOM TO GET NEW VALUE
       MWB                   PERFORM MOVE (BACKWARDS)
*
*      MERGE HERE TO EXIT
*
GBC13  ZER  XR               CLEAR GARBAGE VALUE IN XR
       MOV  XR,GBCFL         NOTE EXIT FROM GBCOL
.IF    .CGBC
       MOV  DNAMB,WA         START OF DYNAMIC AREA
       MOV  DNAMP,WB         NEXT AVAILABLE LOCATION
       MOV  DNAME,WC         LAST AVAILABLE LOCATION + 1
       JSR  SYSGC            INFORM SYSGC OF COMPLETION
.FI
.IF    .CSED
*
*      DECIDE WHETHER TO MARK SEDIMENT FOR COLLECTION NEXT TIME.
*      THIS IS DONE BY EXAMINING THE RATIO OF PREVIOUS SEDIMENT
*      FREE SPACE TO THE NEW SEDIMENT SIZE.
*
       STI  GBCIA            SAVE IA
       ZER  XR               PRESUME NO SEDIMENT WILL REMAIN
       MOV  GBCSF,WB         FREE SPACE IN SEDIMENT
       BTW  WB               CONVERT BYTES TO WORDS
       MTI  WB               PUT SEDIMENT FREE STORE IN IA
       MLI  GBSED            MULTIPLY BY SEDIMENT FACTOR
       IOV  GB13A            JUMP IF OVERFLOWED
       MOV  DNAMP,WB         END OF DYNAMIC AREA IN USE
       SUB  DNAMB,WB         MINUS START IS SEDIMENT REMAINING
       BTW  WB               CONVERT TO WORDS
       MOV  WB,GBCSF         STORE IT
       SBI  GBCSF            SUBTRACT FROM SCALED UP FREE STORE
       IGT  GB13A            JUMP IF LARGE FREE STORE IN SEDIMNT
       MOV  DNAMP,XR         BELOW THRESHOLD, RETURN SEDIMENT
       SUB  DNAMB,XR         FOR USE BY CALLER
GB13A  LDI  GBCIA            RESTORE IA
.FI
       MOV  GBSVA,WA         RESTORE WA
       MOV  GBSVB,WB         RESTORE WB
       SCP  WC               GET CODE POINTER
       ADD  R_COD,WC         MAKE ABSOLUTE AGAIN
       LCP  WC               AND REPLACE ABSOLUTE VALUE
       MOV  GBSVC,WC         RESTORE WC
       MOV  (XS)+,XL         RESTORE ENTRY XL
       ICV  GBCNT            INCREMENT COUNT OF COLLECTIONS
       EXI                   EXIT TO GBCOL CALLER
*
*      GARBAGE COLLECTION NOT ALLOWED WHILST DUMPING
*
GBC14  ICV  ERRFT            FATAL ERROR
       ERB  250,Insufficient memory to complete dump
       ENP                   END PROCEDURE GBCOL
       EJC
*
*      GBCPF -- PROCESS FIELDS FOR GARBAGE COLLECTOR
*
*      THIS PROCEDURE IS USED BY THE GARBAGE COLLECTOR TO
*      PROCESS FIELDS IN PASS ONE. SEE GBCOL FOR FULL DETAILS.
*
*      (XR)                  PTR TO FIRST LOCATION TO PROCESS
*      (XL)                  PTR PAST LAST LOCATION TO PROCESS
*      JSR  GBCPF            CALL TO PROCESS FIELDS
*      (XR,WA,WB,WC,IA)      DESTROYED
*
*      NOTE THAT ALTHOUGH THIS PROCEDURE USES A RECURSIVE
*      APPROACH, IT CONTROLS ITS OWN STACK AND IS NOT RECURSIVE.
*
GBCPF  PRC  E,0              ENTRY POINT
       ZER  -(XS)            SET ZERO TO MARK BOTTOM OF STACK
       MOV  XL,-(XS)         SAVE END POINTER
*
*      MERGE HERE TO GO DOWN A LEVEL AND START A NEW LOOP
*
*      1(XS)                 NEXT LVL FIELD PTR (0 AT OUTER LVL)
*      0(XS)                 PTR PAST LAST FIELD TO PROCESS
*      (XR)                  PTR TO FIRST FIELD TO PROCESS
*
*      LOOP TO PROCESS SUCCESSIVE FIELDS
*
GPF01  MOV  (XR),XL          LOAD FIELD CONTENTS
       MOV  XR,WC            SAVE FIELD POINTER
.IF    .CRPP
       BOD  XL,GPF2A         JUMP IF NOT PTR INTO DYNAMIC AREA
.FI
       BLT  XL,DNAMB,GPF2A   JUMP IF NOT PTR INTO DYNAMIC AREA
       BGE  XL,DNAMP,GPF2A   JUMP IF NOT PTR INTO DYNAMIC AREA
*
*      HERE WE HAVE A PTR TO A BLOCK IN THE DYNAMIC AREA.
*      LINK THIS FIELD ONTO THE REFERENCE BACKCHAIN.
*
       MOV  (XL),WA          LOAD PTR TO CHAIN (OR ENTRY PTR)
.IF    .CSED
       BLT  XL,GBCSD,GPF1A   DO NOT CHAIN IF WITHIN SEDIMENT
.FI
       MOV  XR,(XL)          SET THIS FIELD AS NEW HEAD OF CHAIN
       MOV  WA,(XR)          SET FORWARD POINTER
*
*      NOW SEE IF THIS BLOCK HAS BEEN PROCESSED BEFORE
*
.IF    .CEPP
GPF1A  BOD  WA,GPF03         JUMP IF NOT ALREADY PROCESSED
.ELSE
GPF1A  BHI  WA,=P_YYY,GPF2A  JUMP IF ALREADY PROCESSED
       BHI  WA,=B_AAA,GPF03  JUMP IF NOT ALREADY PROCESSED
.FI
*
*      HERE TO RESTORE POINTER IN XR TO FIELD JUST PROCESSED
*
GPF02  MOV  WC,XR            RESTORE FIELD POINTER
*
*      HERE TO MOVE TO NEXT FIELD
*
GPF2A  ICA  XR               BUMP TO NEXT FIELD
       BNE  XR,(XS),GPF01    LOOP BACK IF MORE TO GO
       EJC
*
*      GBCPF (CONTINUED)
*
*      HERE WE POP UP A LEVEL AFTER FINISHING A BLOCK
*
       MOV  (XS)+,XL         RESTORE POINTER PAST END
       MOV  (XS)+,XR         RESTORE BLOCK POINTER
       BNZ  XR,GPF2A         CONTINUE LOOP UNLESS OUTER LEVL
       EXI                   RETURN TO CALLER IF OUTER LEVEL
*
*      HERE TO PROCESS AN ACTIVE BLOCK WHICH HAS NOT BEEN DONE
*
.IF    .CSED
*
*      SINCE SEDIMENT BLOCKS ARE NOT MARKED BY PUTTING THEM ON
*      THE BACK CHAIN, THEY MUST BE EXPLICITLY MARKED IN ANOTHER
*      MANNER.  IF ODD PARITY ENTRY POINTS ARE PRESENT, MARK BY
*      TEMPORARILY CONVERTING TO EVEN PARITY.  IF ODD PARITY NOT
*      AVAILABLE, THE ENTRY POINT IS ADJUSTED BY THE VALUE IN
*      GBCMK.
*
GPF03  BGE  XL,GBCSD,GPF3A   IF NOT WITHIN SEDIMENT
.IF    .CEPP
       ICV  (XL)             MARK BY MAKING ENTRY POINT EVEN
.ELSE
       ADD  GBCMK,(XL)       MARK BY BIASING ENTRY POINT
.FI
GPF3A  MOV  XL,XR            COPY BLOCK POINTER
.ELSE
GPF03  MOV  XL,XR            COPY BLOCK POINTER
.FI
       MOV  WA,XL            COPY FIRST WORD OF BLOCK
       LEI  XL               LOAD ENTRY POINT ID (BL_XX)
*
*      BLOCK TYPE SWITCH. NOTE THAT BLOCKS WITH NO RELOCATABLE
*      FIELDS JUST RETURN TO GPF02 HERE TO CONTINUE TO NEXT FLD.
*
       BSW  XL,BL___         SWITCH ON BLOCK TYPE
       IFF  BL_AR,GPF06      ARBLK
.IF    .CNBF
       IFF  BL_BC,GPF02      BCBLK - DUMMY TO FILL OUT IFFS
.ELSE
       IFF  BL_BC,GPF18      BCBLK
.FI
       IFF  BL_BF,GPF02      BFBLK
       IFF  BL_CC,GPF07      CCBLK
.IF    .CSLN
       IFF  BL_CD,GPF19      CDBLK
.ELSE
       IFF  BL_CD,GPF08      CDBLK
.FI
       IFF  BL_CM,GPF04      CMBLK
       IFF  BL_DF,GPF02      DFBLK
       IFF  BL_EV,GPF10      EVBLK
       IFF  BL_EX,GPF17      EXBLK
       IFF  BL_FF,GPF11      FFBLK
       IFF  BL_NM,GPF10      NMBLK
       IFF  BL_P0,GPF10      P0BLK
       IFF  BL_P1,GPF12      P1BLK
       IFF  BL_P2,GPF12      P2BLK
       IFF  BL_PD,GPF13      PDBLK
       IFF  BL_PF,GPF14      PFBLK
       IFF  BL_TB,GPF08      TBBLK
       IFF  BL_TE,GPF15      TEBLK
       IFF  BL_TR,GPF16      TRBLK
       IFF  BL_VC,GPF08      VCBLK
       IFF  BL_XR,GPF09      XRBLK
       IFF  BL_CT,GPF02      CTBLK
       IFF  BL_EF,GPF02      EFBLK
       IFF  BL_IC,GPF02      ICBLK
       IFF  BL_KV,GPF02      KVBLK
       IFF  BL_RC,GPF02      RCBLK
       IFF  BL_SC,GPF02      SCBLK
       IFF  BL_SE,GPF02      SEBLK
       IFF  BL_XN,GPF02      XNBLK
       ESW                   END OF JUMP TABLE
       EJC
*
*      GBCPF (CONTINUED)
*
*      CMBLK
*
GPF04  MOV  CMLEN(XR),WA     LOAD LENGTH
       MOV  *CMTYP,WB        SET OFFSET
*
*      HERE TO PUSH DOWN TO NEW LEVEL
*
*      (WC)                  FIELD PTR AT PREVIOUS LEVEL
*      (XR)                  PTR TO NEW BLOCK
*      (WA)                  LENGTH (RELOC FLDS + FLDS AT START)
*      (WB)                  OFFSET TO FIRST RELOC FIELD
*
GPF05  ADD  XR,WA            POINT PAST LAST RELOC FIELD
       ADD  WB,XR            POINT TO FIRST RELOC FIELD
       MOV  WC,-(XS)         STACK OLD FIELD POINTER
       MOV  WA,-(XS)         STACK NEW LIMIT POINTER
       CHK                   CHECK FOR STACK OVERFLOW
       BRN  GPF01            IF OK, BACK TO PROCESS
*
*      ARBLK
*
GPF06  MOV  ARLEN(XR),WA     LOAD LENGTH
       MOV  AROFS(XR),WB     SET OFFSET TO 1ST RELOC FLD (ARPRO)
       BRN  GPF05            ALL SET
*
*      CCBLK
*
GPF07  MOV  CCUSE(XR),WA     SET LENGTH IN USE
       MOV  *CCUSE,WB        1ST WORD (MAKE SURE AT LEAST ONE)
       BRN  GPF05            ALL SET
       EJC
*
*      GBCPF (CONTINUED)
*
.IF    .CSLN
*      CDBLK
*
GPF19  MOV  CDLEN(XR),WA     LOAD LENGTH
       MOV  *CDFAL,WB        SET OFFSET
       BRN  GPF05            JUMP BACK
*
*      TBBLK, VCBLK
.ELSE
*      CDBLK, TBBLK, VCBLK
.FI
*
GPF08  MOV  OFFS2(XR),WA     LOAD LENGTH
       MOV  *OFFS3,WB        SET OFFSET
       BRN  GPF05            JUMP BACK
*
*      XRBLK
*
GPF09  MOV  XRLEN(XR),WA     LOAD LENGTH
       MOV  *XRPTR,WB        SET OFFSET
       BRN  GPF05            JUMP BACK
*
*      EVBLK, NMBLK, P0BLK
*
GPF10  MOV  *OFFS2,WA        POINT PAST SECOND FIELD
       MOV  *OFFS1,WB        OFFSET IS ONE (ONLY RELOC FLD IS 2)
       BRN  GPF05            ALL SET
*
*      FFBLK
*
GPF11  MOV  *FFOFS,WA        SET LENGTH
       MOV  *FFNXT,WB        SET OFFSET
       BRN  GPF05            ALL SET
*
*      P1BLK, P2BLK
*
GPF12  MOV  *PARM2,WA        LENGTH (PARM2 IS NON-RELOCATABLE)
       MOV  *PTHEN,WB        SET OFFSET
       BRN  GPF05            ALL SET
       EJC
*
*      GBCPF (CONTINUED)
*
*      PDBLK
*
GPF13  MOV  PDDFP(XR),XL     LOAD PTR TO DFBLK
       MOV  DFPDL(XL),WA     GET PDBLK LENGTH
       MOV  *PDFLD,WB        SET OFFSET
       BRN  GPF05            ALL SET
*
*      PFBLK
*
GPF14  MOV  *PFARG,WA        LENGTH PAST LAST RELOC
       MOV  *PFCOD,WB        OFFSET TO FIRST RELOC
       BRN  GPF05            ALL SET
*
*      TEBLK
*
GPF15  MOV  *TESI_,WA        SET LENGTH
       MOV  *TESUB,WB        AND OFFSET
       BRN  GPF05            ALL SET
*
*      TRBLK
*
GPF16  MOV  *TRSI_,WA        SET LENGTH
       MOV  *TRVAL,WB        AND OFFSET
       BRN  GPF05            ALL SET
*
*      EXBLK
*
GPF17  MOV  EXLEN(XR),WA     LOAD LENGTH
       MOV  *EXFLC,WB        SET OFFSET
       BRN  GPF05            JUMP BACK
.IF    .CNBF
.ELSE
*
*      BCBLK
*
GPF18  MOV  *BCSI_,WA        SET LENGTH
       MOV  *BCBUF,WB        AND OFFSET
       BRN  GPF05            ALL SET
.FI
       ENP                   END PROCEDURE GBCPF
       EJC
*Z+
*
*      GTARR -- GET ARRAY
*
*      GTARR IS PASSED AN OBJECT AND RETURNS AN ARRAY IF POSSIBL
*
*      (XR)                  VALUE TO BE CONVERTED
*      (WA)                  0 TO PLACE TABLE ADDRESSES IN ARRAY
*                            NON-ZERO FOR KEYS/VALUES IN ARRAY
*      JSR  GTARR            CALL TO GET ARRAY
*      PPM  LOC              TRANSFER LOC FOR ALL NULL TABLE
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  RESULTING ARRAY
*      (XL,WA,WB,WC)         DESTROYED
*
GTARR  PRC  E,2              ENTRY POINT
       MOV  WA,GTAWA         SAVE WA INDICATOR
       MOV  (XR),WA          LOAD TYPE WORD
       BEQ  WA,=B_ART,GTAR8  EXIT IF ALREADY AN ARRAY
       BEQ  WA,=B_VCT,GTAR8  EXIT IF ALREADY AN ARRAY
       BNE  WA,=B_TBT,GTA9A  ELSE FAIL IF NOT A TABLE (SGD02)
*
*      HERE WE CONVERT A TABLE TO AN ARRAY
*
       MOV  XR,-(XS)         REPLACE TBBLK POINTER ON STACK
       ZER  XR               SIGNAL FIRST PASS
       ZER  WB               ZERO NON-NULL ELEMENT COUNT
*
*      THE FOLLOWING CODE IS EXECUTED TWICE. ON THE FIRST PASS,
*      SIGNALLED BY XR=0, THE NUMBER OF NON-NULL ELEMENTS IN
*      THE TABLE IS COUNTED IN WB. IN THE SECOND PASS, WHERE
*      XR IS A POINTER INTO THE ARBLK, THE NAME AND VALUE ARE
*      ENTERED INTO THE CURRENT ARBLK LOCATION PROVIDED GTAWA
*      IS NON-ZERO.  IF GTAWA IS ZERO, THE ADDRESS OF THE TEBLK
*      IS ENTERED INTO THE ARBLK TWICE (C3.762).
*
GTAR1  MOV  (XS),XL          POINT TO TABLE
       ADD  TBLEN(XL),XL     POINT PAST LAST BUCKET
       SUB  *TBBUK,XL        SET FIRST BUCKET OFFSET
       MOV  XL,WA            COPY ADJUSTED POINTER
*
*      LOOP THROUGH BUCKETS IN TABLE BLOCK
*      NEXT THREE LINES OF CODE RELY ON TENXT HAVING A VALUE
*      1 LESS THAN TBBUK.
*
GTAR2  MOV  WA,XL            COPY BUCKET POINTER
       DCA  WA               DECREMENT BUCKET POINTER
*
*      LOOP THROUGH TEBLKS ON ONE BUCKET CHAIN
*
GTAR3  MOV  TENXT(XL),XL     POINT TO NEXT TEBLK
       BEQ  XL,(XS),GTAR6    JUMP IF CHAIN END (TBBLK PTR)
       MOV  XL,CNVTP         ELSE SAVE TEBLK POINTER
*
*      LOOP TO FIND VALUE DOWN TRBLK CHAIN
*
GTAR4  MOV  TEVAL(XL),XL     LOAD VALUE
       BEQ  (XL),=B_TRT,GTAR4 LOOP TILL VALUE FOUND
       MOV  XL,WC            COPY VALUE
       MOV  CNVTP,XL         RESTORE TEBLK POINTER
       EJC
*
*      GTARR (CONTINUED)
*
*      NOW CHECK FOR NULL AND TEST CASES
*
       BEQ  WC,=NULLS,GTAR3  LOOP BACK TO IGNORE NULL VALUE
       BNZ  XR,GTAR5         JUMP IF SECOND PASS
       ICV  WB               FOR THE FIRST PASS, BUMP COUNT
       BRN  GTAR3            AND LOOP BACK FOR NEXT TEBLK
*
*      HERE IN SECOND PASS
*
GTAR5  BZE  GTAWA,GTA5A      JUMP IF ADDRESS WANTED
       MOV  TESUB(XL),(XR)+  STORE SUBSCRIPT NAME
       MOV  WC,(XR)+         STORE VALUE IN ARBLK
       BRN  GTAR3            LOOP BACK FOR NEXT TEBLK
*
*      HERE TO RECORD TEBLK ADDRESS IN ARBLK.  THIS ALLOWS
*      A SORT ROUTINE TO SORT BY ASCENDING ADDRESS.
*
GTA5A  MOV  XL,(XR)+         STORE TEBLK ADDRESS IN NAME
       MOV  XL,(XR)+         AND VALUE SLOTS
       BRN  GTAR3            LOOP BACK FOR NEXT TEBLK
*
*      HERE AFTER SCANNING TEBLKS ON ONE CHAIN
*
GTAR6  BNE  WA,(XS),GTAR2    LOOP BACK IF MORE BUCKETS TO GO
       BNZ  XR,GTAR7         ELSE JUMP IF SECOND PASS
*
*      HERE AFTER COUNTING NON-NULL ELEMENTS
*
       BZE  WB,GTAR9         FAIL IF NO NON-NULL ELEMENTS
       MOV  WB,WA            ELSE COPY COUNT
       ADD  WB,WA            DOUBLE (TWO WORDS/ELEMENT)
       ADD  =ARVL2,WA        ADD SPACE FOR STANDARD FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       BGT  WA,MXLEN,GTA9B   ERROR IF TOO LONG FOR ARRAY
       JSR  ALLOC            ELSE ALLOCATE SPACE FOR ARBLK
       MOV  =B_ART,(XR)      STORE TYPE WORD
       ZER  IDVAL(XR)        ZERO ID FOR THE MOMENT
       MOV  WA,ARLEN(XR)     STORE LENGTH
       MOV  =NUM02,ARNDM(XR) SET DIMENSIONS = 2
       LDI  INTV1            GET INTEGER ONE
       STI  ARLBD(XR)        STORE AS LBD 1
       STI  ARLB2(XR)        STORE AS LBD 2
       LDI  INTV2            LOAD INTEGER TWO
       STI  ARDM2(XR)        STORE AS DIM 2
       MTI  WB               GET ELEMENT COUNT AS INTEGER
       STI  ARDIM(XR)        STORE AS DIM 1
       ZER  ARPR2(XR)        ZERO PROTOTYPE FIELD FOR NOW
       MOV  *ARPR2,AROFS(XR) SET OFFSET FIELD (SIGNAL PASS 2)
       MOV  XR,WB            SAVE ARBLK POINTER
       ADD  *ARVL2,XR        POINT TO FIRST ELEMENT LOCATION
       BRN  GTAR1            JUMP BACK TO FILL IN ELEMENTS
       EJC
*
*      GTARR (CONTINUED)
*
*      HERE AFTER FILLING IN ELEMENT VALUES
*
GTAR7  MOV  WB,XR            RESTORE ARBLK POINTER
       MOV  WB,(XS)          STORE AS RESULT
*
*      NOW WE NEED THE ARRAY PROTOTYPE WHICH IS OF THE FORM NN,2
*      THIS IS OBTAINED BY BUILDING THE STRING FOR NN02 AND
*      CHANGING THE ZERO TO A COMMA BEFORE STORING IT.
*
       LDI  ARDIM(XR)        GET NUMBER OF ELEMENTS (NN)
       MLI  INTVH            MULTIPLY BY 100
       ADI  INTV2            ADD 2 (NN02)
       JSR  ICBLD            BUILD INTEGER
       MOV  XR,-(XS)         STORE PTR FOR GTSTG
       JSR  GTSTG            CONVERT TO STRING
       PPM                   CONVERT FAIL IS IMPOSSIBLE
       MOV  XR,XL            COPY STRING POINTER
       MOV  (XS)+,XR         RELOAD ARBLK POINTER
       MOV  XL,ARPR2(XR)     STORE PROTOTYPE PTR (NN02)
       SUB  =NUM02,WA        ADJUST LENGTH TO POINT TO ZERO
       PSC  XL,WA            POINT TO ZERO
       MOV  =CH_CM,WB        LOAD A COMMA
       SCH  WB,(XL)          STORE A COMMA OVER THE ZERO
       CSC  XL               COMPLETE STORE CHARACTERS
*
*      NORMAL RETURN
*
GTAR8  EXI                   RETURN TO CALLER
*
*      NULL TABLE NON-CONVERSION RETURN
*
GTAR9  MOV  (XS)+,XR         RESTORE STACK FOR CONV ERR (SGD02)
       EXI  1                RETURN
*
*      IMPOSSIBLE CONVERSION RETURN
*
GTA9A  EXI  2                RETURN
*
*      ARRAY SIZE TOO LARGE
*
GTA9B  ERB  260,Conversion array size exceeds maximum permitted
       ENP                   PROCEDURE GTARR
       EJC
*
*      GTCOD -- CONVERT TO CODE
*
*      (XR)                  OBJECT TO BE CONVERTED
*      JSR  GTCOD            CALL TO CONVERT TO CODE
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULTING CDBLK
*      (XL,WA,WB,WC,RA)      DESTROYED
*
*      IF A SPITBOL ERROR OCCURS DURING COMPILATION OR PRE-
*      EVALUATION, CONTROL IS PASSED VIA ERROR SECTION TO EXFAL
*      WITHOUT RETURNING TO THIS ROUTINE.
*
GTCOD  PRC  E,1              ENTRY POINT
       BEQ  (XR),=B_CDS,GTCD1 JUMP IF ALREADY CODE
       BEQ  (XR),=B_CDC,GTCD1 JUMP IF ALREADY CODE
*
*      HERE WE MUST GENERATE A CDBLK BY COMPILATION
*
       MOV  XR,-(XS)         STACK ARGUMENT FOR GTSTG
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
       PPM  GTCD2            JUMP IF NON-CONVERTIBLE
       MOV  FLPTR,GTCEF      SAVE FAIL PTR IN CASE OF ERROR
       MOV  R_COD,R_GTC      ALSO SAVE CODE PTR
       MOV  XR,R_CIM         ELSE SET IMAGE POINTER
       MOV  WA,SCNIL         SET IMAGE LENGTH
       ZER  SCNPT            SET SCAN POINTER
       MOV  =STGXC,STAGE     SET STAGE FOR EXECUTE COMPILE
       MOV  CMPSN,LSTSN      IN CASE LISTR CALLED
.IF    .CSLN
       ICV  CMPLN            BUMP LINE NUMBER
.FI
       JSR  CMPIL            COMPILE STRING
       MOV  =STGXT,STAGE     RESET STAGE FOR EXECUTE TIME
       ZER  R_CIM            CLEAR IMAGE
*
*      MERGE HERE IF NO CONVERT REQUIRED
*
GTCD1  EXI                   GIVE NORMAL GTCOD RETURN
*
*      HERE IF UNCONVERTIBLE
*
GTCD2  EXI  1                GIVE ERROR RETURN
       ENP                   END PROCEDURE GTCOD
       EJC
*
*      GTEXP -- CONVERT TO EXPRESSION
*
.IF    .CEVB
*      (WB)                  0 IF BY VALUE, 1 IF BY NAME
.FI
*      (XR)                  INPUT VALUE TO BE CONVERTED
*      JSR  GTEXP            CALL TO CONVERT TO EXPRESSION
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULT EXBLK OR SEBLK
*      (XL,WA,WB,WC,RA)      DESTROYED
*
*      IF A SPITBOL ERROR OCCURS DURING COMPILATION OR PRE-
*      EVALUATION, CONTROL IS PASSED VIA ERROR SECTION TO EXFAL
*      WITHOUT RETURNING TO THIS ROUTINE.
*
GTEXP  PRC  E,1              ENTRY POINT
       BLO  (XR),=B_E__,GTEX1 JUMP IF ALREADY AN EXPRESSION
       MOV  XR,-(XS)         STORE ARGUMENT FOR GTSTG
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
       PPM  GTEX2            JUMP IF UNCONVERTIBLE
*
*      CHECK THE LAST CHARACTER OF THE STRING FOR COLON OR
*      SEMICOLON.  THESE CHARACTERS CAN LEGITIMATELY END AN
*      EXPRESSION IN OPEN CODE, SO EXPAN WILL NOT DETECT THEM
*      AS ERRORS, BUT THEY ARE INVALID AS TERMINATORS FOR A
*      STRING THAT IS BEING CONVERTED TO EXPRESSION FORM.
*
       MOV  XR,XL            COPY INPUT STRING POINTER
       PLC  XL,WA            POINT ONE PAST THE STRING END
       LCH  XL,-(XL)         FETCH THE LAST CHARACTER
       BEQ  XL,=CH_CL,GTEX2  ERROR IF IT IS A SEMICOLON
       BEQ  XL,=CH_SM,GTEX2  OR IF IT IS A COLON
*
*      HERE WE CONVERT A STRING BY COMPILATION
*
       MOV  XR,R_CIM         SET INPUT IMAGE POINTER
       ZER  SCNPT            SET SCAN POINTER
       MOV  WA,SCNIL         SET INPUT IMAGE LENGTH
.IF    .CEVB
       MOV  WB,-(XS)         SAVE VALUE/NAME FLAG
.FI
       ZER  WB               SET CODE FOR NORMAL SCAN
       MOV  FLPTR,GTCEF      SAVE FAIL PTR IN CASE OF ERROR
       MOV  R_COD,R_GTC      ALSO SAVE CODE PTR
       MOV  =STGEV,STAGE     ADJUST STAGE FOR COMPILE
       MOV  =T_UOK,SCNTP     INDICATE UNARY OPERATOR ACCEPTABLE
       JSR  EXPAN            BUILD TREE FOR EXPRESSION
       ZER  SCNRS            RESET RESCAN FLAG
.IF    .CEVB
       MOV  (XS)+,WA         RESTORE VALUE/NAME FLAG
.FI
       BNE  SCNPT,SCNIL,GTEX2 ERROR IF NOT END OF IMAGE
       ZER  WB               SET OK VALUE FOR CDGEX CALL
       MOV  XR,XL            COPY TREE POINTER
       JSR  CDGEX            BUILD EXPRESSION BLOCK
       ZER  R_CIM            CLEAR POINTER
       MOV  =STGXT,STAGE     RESTORE STAGE FOR EXECUTE TIME
*
*      MERGE HERE IF NO CONVERSION REQUIRED
*
GTEX1  EXI                   RETURN TO GTEXP CALLER
*
*      HERE IF UNCONVERTIBLE
*
GTEX2  EXI  1                TAKE ERROR EXIT
       ENP                   END PROCEDURE GTEXP
       EJC
*
*      GTINT -- GET INTEGER VALUE
*
*      GTINT IS PASSED AN OBJECT AND RETURNS AN INTEGER AFTER
*      PERFORMING ANY NECESSARY CONVERSIONS.
*
*      (XR)                  VALUE TO BE CONVERTED
*      JSR  GTINT            CALL TO CONVERT TO INTEGER
*      PPM  LOC              TRANSFER LOC FOR CONVERT IMPOSSIBLE
*      (XR)                  RESULTING INTEGER
*      (WC,RA)               DESTROYED
*      (WA,WB)               DESTROYED (ONLY ON CONVERSION ERR)
*      (XR)                  UNCHANGED (ON CONVERT ERROR)
*
GTINT  PRC  E,1              ENTRY POINT
       BEQ  (XR),=B_ICL,GTIN2 JUMP IF ALREADY AN INTEGER
       MOV  WA,GTINA         ELSE SAVE WA
       MOV  WB,GTINB         SAVE WB
       JSR  GTNUM            CONVERT TO NUMERIC
       PPM  GTIN3            JUMP IF UNCONVERTIBLE
.IF    .CNRA
.ELSE
       BEQ  WA,=B_ICL,GTIN1  JUMP IF INTEGER
*
*      HERE WE CONVERT A REAL TO INTEGER
*
       LDR  RCVAL(XR)        LOAD REAL VALUE
       RTI  GTIN3            CONVERT TO INTEGER (ERR IF OVFLOW)
       JSR  ICBLD            IF OK BUILD ICBLK
.FI
*
*      HERE AFTER SUCCESSFUL CONVERSION TO INTEGER
*
GTIN1  MOV  GTINA,WA         RESTORE WA
       MOV  GTINB,WB         RESTORE WB
*
*      COMMON EXIT POINT
*
GTIN2  EXI                   RETURN TO GTINT CALLER
*
*      HERE ON CONVERSION ERROR
*
GTIN3  EXI  1                TAKE CONVERT ERROR EXIT
       ENP                   END PROCEDURE GTINT
       EJC
*
*      GTNUM -- GET NUMERIC VALUE
*
*      GTNUM IS GIVEN AN OBJECT AND RETURNS EITHER AN INTEGER
*      OR A REAL, PERFORMING ANY NECESSARY CONVERSIONS.
*
*      (XR)                  OBJECT TO BE CONVERTED
*      JSR  GTNUM            CALL TO CONVERT TO NUMERIC
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULT (INT OR REAL)
*      (WA)                  FIRST WORD OF RESULT BLOCK
*      (WB,WC,RA)            DESTROYED
*      (XR)                  UNCHANGED (ON CONVERT ERROR)
*
GTNUM  PRC  E,1              ENTRY POINT
       MOV  (XR),WA          LOAD FIRST WORD OF BLOCK
       BEQ  WA,=B_ICL,GTN34  JUMP IF INTEGER (NO CONVERSION)
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,GTN34  JUMP IF REAL (NO CONVERSION)
.FI
*
*      AT THIS POINT THE ONLY POSSIBILITY IS TO CONVERT A STRING
*      TO AN INTEGER OR REAL AS APPROPRIATE.
*
       MOV  XR,-(XS)         STACK ARGUMENT IN CASE CONVERT ERR
       MOV  XR,-(XS)         STACK ARGUMENT FOR GTSTG
.IF    .CNBF
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
.ELSE
       JSR  GTSTB            GET ARGUMENT AS STRING OR BUFFER
.FI
       PPM  GTN36            JUMP IF UNCONVERTIBLE
*
*      INITIALIZE NUMERIC CONVERSION
*
       LDI  INTV0            INITIALIZE INTEGER RESULT TO ZERO
       BZE  WA,GTN32         JUMP TO EXIT WITH ZERO IF NULL
       LCT  WA,WA            SET BCT COUNTER FOR FOLLOWING LOOPS
       ZER  GTNNF            TENTATIVELY INDICATE RESULT +
.IF    .CNRA
.ELSE
       STI  GTNEX            INITIALISE EXPONENT TO ZERO
       ZER  GTNSC            ZERO SCALE IN CASE REAL
       ZER  GTNDF            RESET FLAG FOR DEC POINT FOUND
       ZER  GTNRD            RESET FLAG FOR DIGITS FOUND
       LDR  REAV0            ZERO REAL ACCUM IN CASE REAL
.FI
       PLC  XR               POINT TO ARGUMENT CHARACTERS
*
*      MERGE BACK HERE AFTER IGNORING LEADING BLANK
*
GTN01  LCH  WB,(XR)+         LOAD FIRST CHARACTER
       BLT  WB,=CH_D0,GTN02  JUMP IF NOT DIGIT
       BLE  WB,=CH_D9,GTN06  JUMP IF FIRST CHAR IS A DIGIT
       EJC
*
*      GTNUM (CONTINUED)
*
*      HERE IF FIRST DIGIT IS NON-DIGIT
*
GTN02  BNE  WB,=CH_BL,GTN03  JUMP IF NON-BLANK
GTNA2  BCT  WA,GTN01         ELSE DECR COUNT AND LOOP BACK
       BRN  GTN07            JUMP TO RETURN ZERO IF ALL BLANKS
*
*      HERE FOR FIRST CHARACTER NON-BLANK, NON-DIGIT
*
GTN03  BEQ  WB,=CH_PL,GTN04  JUMP IF PLUS SIGN
.IF    .CAHT
       BEQ  WB,=CH_HT,GTNA2  HORIZONTAL TAB EQUIV TO BLANK
.FI
.IF    .CAVT
       BEQ  WB,=CH_VT,GTNA2  VERTICAL TAB EQUIV TO BLANK
.FI
.IF    .CNRA
       BNE  WB,=CH_MN,GTN36  ELSE FAIL
.ELSE
       BNE  WB,=CH_MN,GTN12  JUMP IF NOT MINUS (MAY BE REAL)
.FI
       MNZ  GTNNF            IF MINUS SIGN, SET NEGATIVE FLAG
*
*      MERGE HERE AFTER PROCESSING SIGN
*
GTN04  BCT  WA,GTN05         JUMP IF CHARS LEFT
       BRN  GTN36            ELSE ERROR
*
*      LOOP TO FETCH CHARACTERS OF AN INTEGER
*
GTN05  LCH  WB,(XR)+         LOAD NEXT CHARACTER
       BLT  WB,=CH_D0,GTN08  JUMP IF NOT A DIGIT
       BGT  WB,=CH_D9,GTN08  JUMP IF NOT A DIGIT
*
*      MERGE HERE FOR FIRST DIGIT
*
GTN06  STI  GTNSI            SAVE CURRENT VALUE
.IF    .CNRA
       CVM  GTN36            CURRENT*10-(NEW DIG) JUMP IF OVFLOW
.ELSE
       CVM  GTN35            CURRENT*10-(NEW DIG) JUMP IF OVFLOW
       MNZ  GTNRD            SET DIGIT READ FLAG
.FI
       BCT  WA,GTN05         ELSE LOOP BACK IF MORE CHARS
*
*      HERE TO EXIT WITH CONVERTED INTEGER VALUE
*
GTN07  BNZ  GTNNF,GTN32      JUMP IF NEGATIVE (ALL SET)
       NGI                   ELSE NEGATE
       INO  GTN32            JUMP IF NO OVERFLOW
       BRN  GTN36            ELSE SIGNAL ERROR
       EJC
*
*      GTNUM (CONTINUED)
*
*      HERE FOR A NON-DIGIT CHARACTER WHILE ATTEMPTING TO
*      CONVERT AN INTEGER, CHECK FOR TRAILING BLANKS OR REAL.
*
GTN08  BEQ  WB,=CH_BL,GTNA9  JUMP IF A BLANK
.IF    .CAHT
       BEQ  WB,=CH_HT,GTNA9  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WB,=CH_VT,GTNA9  JUMP IF VERTICAL TAB
.FI
.IF    .CNRA
       BRN  GTN36            ERROR
.ELSE
       ITR                   ELSE CONVERT INTEGER TO REAL
       NGR                   NEGATE TO GET POSITIVE VALUE
       BRN  GTN12            JUMP TO TRY FOR REAL
.FI
*
*      HERE WE SCAN OUT BLANKS TO END OF STRING
*
GTN09  LCH  WB,(XR)+         GET NEXT CHAR
.IF    .CAHT
       BEQ  WB,=CH_HT,GTNA9  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WB,=CH_VT,GTNA9  JUMP IF VERTICAL TAB
.FI
       BNE  WB,=CH_BL,GTN36  ERROR IF NON-BLANK
GTNA9  BCT  WA,GTN09         LOOP BACK IF MORE CHARS TO CHECK
       BRN  GTN07            RETURN INTEGER IF ALL BLANKS
.IF    .CNRA
.ELSE
*
*      LOOP TO COLLECT MANTISSA OF REAL
*
GTN10  LCH  WB,(XR)+         LOAD NEXT CHARACTER
       BLT  WB,=CH_D0,GTN12  JUMP IF NON-NUMERIC
       BGT  WB,=CH_D9,GTN12  JUMP IF NON-NUMERIC
*
*      MERGE HERE TO COLLECT FIRST REAL DIGIT
*
GTN11  SUB  =CH_D0,WB        CONVERT DIGIT TO NUMBER
       MLR  REAVT            MULTIPLY REAL BY 10.0
       ROV  GTN36            CONVERT ERROR IF OVERFLOW
       STR  GTNSR            SAVE RESULT
       MTI  WB               GET NEW DIGIT AS INTEGER
       ITR                   CONVERT NEW DIGIT TO REAL
       ADR  GTNSR            ADD TO GET NEW TOTAL
       ADD  GTNDF,GTNSC      INCREMENT SCALE IF AFTER DEC POINT
       MNZ  GTNRD            SET DIGIT FOUND FLAG
       BCT  WA,GTN10         LOOP BACK IF MORE CHARS
       BRN  GTN22            ELSE JUMP TO SCALE
       EJC
*
*      GTNUM (CONTINUED)
*
*      HERE IF NON-DIGIT FOUND WHILE COLLECTING A REAL
*
GTN12  BNE  WB,=CH_DT,GTN13  JUMP IF NOT DEC POINT
       BNZ  GTNDF,GTN36      IF DEC POINT, ERROR IF ONE ALREADY
       MOV  =NUM01,GTNDF     ELSE SET FLAG FOR DEC POINT
       BCT  WA,GTN10         LOOP BACK IF MORE CHARS
       BRN  GTN22            ELSE JUMP TO SCALE
*
*      HERE IF NOT DECIMAL POINT
*
GTN13  BEQ  WB,=CH_LE,GTN15  JUMP IF E FOR EXPONENT
       BEQ  WB,=CH_LD,GTN15  JUMP IF D FOR EXPONENT
.IF    .CULC
       BEQ  WB,=CH__E,GTN15  JUMP IF E FOR EXPONENT
       BEQ  WB,=CH__D,GTN15  JUMP IF D FOR EXPONENT
.FI
*
*      HERE CHECK FOR TRAILING BLANKS
*
GTN14  BEQ  WB,=CH_BL,GTNB4  JUMP IF BLANK
.IF    .CAHT
       BEQ  WB,=CH_HT,GTNB4  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WB,=CH_VT,GTNB4  JUMP IF VERTICAL TAB
.FI
       BRN  GTN36            ERROR IF NON-BLANK
*
GTNB4  LCH  WB,(XR)+         GET NEXT CHARACTER
       BCT  WA,GTN14         LOOP BACK TO CHECK IF MORE
       BRN  GTN22            ELSE JUMP TO SCALE
*
*      HERE TO READ AND PROCESS AN EXPONENT
*
GTN15  ZER  GTNES            SET EXPONENT SIGN POSITIVE
       LDI  INTV0            INITIALIZE EXPONENT TO ZERO
       MNZ  GTNDF            RESET NO DEC POINT INDICATION
       BCT  WA,GTN16         JUMP SKIPPING PAST E OR D
       BRN  GTN36            ERROR IF NULL EXPONENT
*
*      CHECK FOR EXPONENT SIGN
*
GTN16  LCH  WB,(XR)+         LOAD FIRST EXPONENT CHARACTER
       BEQ  WB,=CH_PL,GTN17  JUMP IF PLUS SIGN
       BNE  WB,=CH_MN,GTN19  ELSE JUMP IF NOT MINUS SIGN
       MNZ  GTNES            SET SIGN NEGATIVE IF MINUS SIGN
*
*      MERGE HERE AFTER PROCESSING EXPONENT SIGN
*
GTN17  BCT  WA,GTN18         JUMP IF CHARS LEFT
       BRN  GTN36            ELSE ERROR
*
*      LOOP TO CONVERT EXPONENT DIGITS
*
GTN18  LCH  WB,(XR)+         LOAD NEXT CHARACTER
       EJC
*
*      GTNUM (CONTINUED)
*
*      MERGE HERE FOR FIRST EXPONENT DIGIT
*
GTN19  BLT  WB,=CH_D0,GTN20  JUMP IF NOT DIGIT
       BGT  WB,=CH_D9,GTN20  JUMP IF NOT DIGIT
       CVM  GTN36            ELSE CURRENT*10, SUBTRACT NEW DIGIT
       BCT  WA,GTN18         LOOP BACK IF MORE CHARS
       BRN  GTN21            JUMP IF EXPONENT FIELD IS EXHAUSTED
*
*      HERE TO CHECK FOR TRAILING BLANKS AFTER EXPONENT
*
GTN20  BEQ  WB,=CH_BL,GTNC0  JUMP IF BLANK
.IF    .CAHT
       BEQ  WB,=CH_HT,GTNC0  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WC,=CH_VT,GTNC0  JUMP IF VERTICAL TAB
.FI
       BRN  GTN36            ERROR IF NON-BLANK
*
GTNC0  LCH  WB,(XR)+         GET NEXT CHARACTER
       BCT  WA,GTN20         LOOP BACK TILL ALL BLANKS SCANNED
*
*      MERGE HERE AFTER COLLECTING EXPONENT
*
GTN21  STI  GTNEX            SAVE COLLECTED EXPONENT
       BNZ  GTNES,GTN22      JUMP IF IT WAS NEGATIVE
       NGI                   ELSE COMPLEMENT
       IOV  GTN36            ERROR IF OVERFLOW
       STI  GTNEX            AND STORE POSITIVE EXPONENT
*
*      MERGE HERE WITH EXPONENT (0 IF NONE GIVEN)
*
GTN22  BZE  GTNRD,GTN36      ERROR IF NOT DIGITS COLLECTED
       BZE  GTNDF,GTN36      ERROR IF NO EXPONENT OR DEC POINT
       MTI  GTNSC            ELSE LOAD SCALE AS INTEGER
       SBI  GTNEX            SUBTRACT EXPONENT
       IOV  GTN36            ERROR IF OVERFLOW
       ILT  GTN26            JUMP IF WE MUST SCALE UP
*
*      HERE WE HAVE A NEGATIVE EXPONENT, SO SCALE DOWN
*
       MFI  WA,GTN36         LOAD SCALE FACTOR, ERR IF OVFLOW
*
*      LOOP TO SCALE DOWN IN STEPS OF 10**10
*
GTN23  BLE  WA,=NUM10,GTN24  JUMP IF 10 OR LESS TO GO
       DVR  REATT            ELSE DIVIDE BY 10**10
       SUB  =NUM10,WA        DECREMENT SCALE
       BRN  GTN23            AND LOOP BACK
       EJC
*
*      GTNUM (CONTINUED)
*
*      HERE SCALE REST OF WAY FROM POWERS OF TEN TABLE
*
GTN24  BZE  WA,GTN30         JUMP IF SCALED
       LCT  WB,=CFP_R        ELSE GET INDEXING FACTOR
       MOV  =REAV1,XR        POINT TO POWERS OF TEN TABLE
       WTB  WA               CONVERT REMAINING SCALE TO BYTE OFS
*
*      LOOP TO POINT TO POWERS OF TEN TABLE ENTRY
*
GTN25  ADD  WA,XR            BUMP POINTER
       BCT  WB,GTN25         ONCE FOR EACH VALUE WORD
       DVR  (XR)             SCALE DOWN AS REQUIRED
       BRN  GTN30            AND JUMP
*
*      COME HERE TO SCALE RESULT UP (POSITIVE EXPONENT)
*
GTN26  NGI                   GET ABSOLUTE VALUE OF EXPONENT
       IOV  GTN36            ERROR IF OVERFLOW
       MFI  WA,GTN36         ACQUIRE SCALE, ERROR IF OVFLOW
*
*      LOOP TO SCALE UP IN STEPS OF 10**10
*
GTN27  BLE  WA,=NUM10,GTN28  JUMP IF 10 OR LESS TO GO
       MLR  REATT            ELSE MULTIPLY BY 10**10
       ROV  GTN36            ERROR IF OVERFLOW
       SUB  =NUM10,WA        ELSE DECREMENT SCALE
       BRN  GTN27            AND LOOP BACK
*
*      HERE TO SCALE UP REST OF WAY WITH TABLE
*
GTN28  BZE  WA,GTN30         JUMP IF SCALED
       LCT  WB,=CFP_R        ELSE GET INDEXING FACTOR
       MOV  =REAV1,XR        POINT TO POWERS OF TEN TABLE
       WTB  WA               CONVERT REMAINING SCALE TO BYTE OFS
*
*      LOOP TO POINT TO PROPER ENTRY IN POWERS OF TEN TABLE
*
GTN29  ADD  WA,XR            BUMP POINTER
       BCT  WB,GTN29         ONCE FOR EACH WORD IN VALUE
       MLR  (XR)             SCALE UP
       ROV  GTN36            ERROR IF OVERFLOW
       EJC
*
*      GTNUM (CONTINUED)
*
*      HERE WITH REAL VALUE SCALED AND READY EXCEPT FOR SIGN
*
GTN30  BZE  GTNNF,GTN31      JUMP IF POSITIVE
       NGR                   ELSE NEGATE
*
*      HERE WITH PROPERLY SIGNED REAL VALUE IN (RA)
*
GTN31  JSR  RCBLD            BUILD REAL BLOCK
       BRN  GTN33            MERGE TO EXIT
.FI
*
*      HERE WITH PROPERLY SIGNED INTEGER VALUE IN (IA)
*
GTN32  JSR  ICBLD            BUILD ICBLK
*
*      REAL MERGES HERE
*
GTN33  MOV  (XR),WA          LOAD FIRST WORD OF RESULT BLOCK
       ICA  XS               POP ARGUMENT OFF STACK
*
*      COMMON EXIT POINT
*
GTN34  EXI                   RETURN TO GTNUM CALLER
.IF    .CNRA
.ELSE
*
*      COME HERE IF OVERFLOW OCCURS DURING COLLECTION OF INTEGER
*      HAVE TO RESTORE WB WHICH CVM MAY HAVE DESTROYED.
*
GTN35  LCH  WB,-(XR)         RELOAD CURRENT CHARACTER
       LCH  WB,(XR)+         BUMP CHARACTER POINTER
       LDI  GTNSI            RELOAD INTEGER SO FAR
       ITR                   CONVERT TO REAL
       NGR                   MAKE VALUE POSITIVE
       BRN  GTN11            MERGE WITH REAL CIRCUIT
.FI
*
*      HERE FOR UNCONVERTIBLE TO STRING OR CONVERSION ERROR
*
GTN36  MOV  (XS)+,XR         RELOAD ORIGINAL ARGUMENT
       EXI  1                TAKE CONVERT-ERROR EXIT
       ENP                   END PROCEDURE GTNUM
       EJC
*
*      GTNVR -- CONVERT TO NATURAL VARIABLE
*
*      GTNVR LOCATES A VARIABLE BLOCK (VRBLK) GIVEN EITHER AN
*      APPROPRIATE NAME (NMBLK) OR A NON-NULL STRING (SCBLK).
*
*      (XR)                  ARGUMENT
*      JSR  GTNVR            CALL TO CONVERT TO NATURAL VARIABLE
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO VRBLK
*      (WA,WB)               DESTROYED (CONVERSION ERROR ONLY)
*      (WC)                  DESTROYED
*
GTNVR  PRC  E,1              ENTRY POINT
       BNE  (XR),=B_NML,GNV02 JUMP IF NOT NAME
       MOV  NMBAS(XR),XR     ELSE LOAD NAME BASE IF NAME
       BLO  XR,STATE,GNV07   SKIP IF VRBLK (IN STATIC REGION)
*
*      COMMON ERROR EXIT
*
GNV01  EXI  1                TAKE CONVERT-ERROR EXIT
*
*      HERE IF NOT NAME
*
GNV02  MOV  WA,GNVSA         SAVE WA
       MOV  WB,GNVSB         SAVE WB
       MOV  XR,-(XS)         STACK ARGUMENT FOR GTSTG
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
       PPM  GNV01            JUMP IF CONVERSION ERROR
       BZE  WA,GNV01         NULL STRING IS AN ERROR
.IF    .CULC
       JSR  FLSTG            FOLD LOWER CASE TO UPPER CASE
.FI
       MOV  XL,-(XS)         SAVE XL
       MOV  XR,-(XS)         STACK STRING PTR FOR LATER
       MOV  XR,WB            COPY STRING POINTER
       ADD  *SCHAR,WB        POINT TO CHARACTERS OF STRING
       MOV  WB,GNVST         SAVE POINTER TO CHARACTERS
       MOV  WA,WB            COPY LENGTH
       CTW  WB,0             GET NUMBER OF WORDS IN NAME
       MOV  WB,GNVNW         SAVE FOR LATER
       JSR  HASHS            COMPUTE HASH INDEX FOR STRING
       RMI  HSHNB            COMPUTE HASH OFFSET BY TAKING MOD
       MFI  WC               GET AS OFFSET
       WTB  WC               CONVERT OFFSET TO BYTES
       ADD  HSHTB,WC         POINT TO PROPER HASH CHAIN
       SUB  *VRNXT,WC        SUBTRACT OFFSET TO MERGE INTO LOOP
       EJC
*
*      GTNVR (CONTINUED)
*
*      LOOP TO SEARCH HASH CHAIN
*
GNV03  MOV  WC,XL            COPY HASH CHAIN POINTER
       MOV  VRNXT(XL),XL     POINT TO NEXT VRBLK ON CHAIN
       BZE  XL,GNV08         JUMP IF END OF CHAIN
       MOV  XL,WC            SAVE POINTER TO THIS VRBLK
       BNZ  VRLEN(XL),GNV04  JUMP IF NOT SYSTEM VARIABLE
       MOV  VRSVP(XL),XL     ELSE POINT TO SVBLK
       SUB  *VRSOF,XL        ADJUST OFFSET FOR MERGE
*
*      MERGE HERE WITH STRING PTR (LIKE VRBLK) IN XL
*
GNV04  BNE  WA,VRLEN(XL),GNV03 BACK FOR NEXT VRBLK IF LENGTHS NE
       ADD  *VRCHS,XL        ELSE POINT TO CHARS OF CHAIN ENTRY
       LCT  WB,GNVNW         GET WORD COUNTER TO CONTROL LOOP
       MOV  GNVST,XR         POINT TO CHARS OF NEW NAME
*
*      LOOP TO COMPARE CHARACTERS OF THE TWO NAMES
*
GNV05  CNE  (XR),(XL),GNV03  JUMP IF NO MATCH FOR NEXT VRBLK
       ICA  XR               BUMP NEW NAME POINTER
       ICA  XL               BUMP VRBLK IN CHAIN NAME POINTER
       BCT  WB,GNV05         ELSE LOOP TILL ALL COMPARED
       MOV  WC,XR            WE HAVE FOUND A MATCH, GET VRBLK
*
*      EXIT POINT AFTER FINDING VRBLK OR BUILDING NEW ONE
*
GNV06  MOV  GNVSA,WA         RESTORE WA
       MOV  GNVSB,WB         RESTORE WB
       ICA  XS               POP STRING POINTER
       MOV  (XS)+,XL         RESTORE XL
*
*      COMMON EXIT POINT
*
GNV07  EXI                   RETURN TO GTNVR CALLER
*
*      NOT FOUND, PREPARE TO SEARCH SYSTEM VARIABLE TABLE
*
GNV08  ZER  XR               CLEAR GARBAGE XR POINTER
       MOV  WC,GNVHE         SAVE PTR TO END OF HASH CHAIN
       BGT  WA,=NUM09,GNV14  CANNOT BE SYSTEM VAR IF LENGTH GT 9
       MOV  WA,XL            ELSE COPY LENGTH
       WTB  XL               CONVERT TO BYTE OFFSET
       MOV  VSRCH(XL),XL     POINT TO FIRST SVBLK OF THIS LENGTH
       EJC
*
*      GTNVR (CONTINUED)
*
*      LOOP TO SEARCH ENTRIES IN STANDARD VARIABLE TABLE
*
GNV09  MOV  XL,GNVSP         SAVE TABLE POINTER
       MOV  (XL)+,WC         LOAD SVBIT BIT STRING
       MOV  (XL)+,WB         LOAD LENGTH FROM TABLE ENTRY
       BNE  WA,WB,GNV14      JUMP IF END OF RIGHT LENGTH ENTRIES
       LCT  WB,GNVNW         GET WORD COUNTER TO CONTROL LOOP
       MOV  GNVST,XR         POINT TO CHARS OF NEW NAME
*
*      LOOP TO CHECK FOR MATCHING NAMES
*
GNV10  CNE  (XR),(XL),GNV11  JUMP IF NAME MISMATCH
       ICA  XR               ELSE BUMP NEW NAME POINTER
       ICA  XL               BUMP SVBLK POINTER
       BCT  WB,GNV10         ELSE LOOP UNTIL ALL CHECKED
*
*      HERE WE HAVE A MATCH IN THE STANDARD VARIABLE TABLE
*
       ZER  WC               SET VRLEN VALUE ZERO
       MOV  *VRSI_,WA        SET STANDARD SIZE
       BRN  GNV15            JUMP TO BUILD VRBLK
*
*      HERE IF NO MATCH WITH TABLE ENTRY IN SVBLKS TABLE
*
GNV11  ICA  XL               BUMP PAST WORD OF CHARS
       BCT  WB,GNV11         LOOP BACK IF MORE TO GO
       RSH  WC,SVNBT         REMOVE UNINTERESTING BITS
*
*      LOOP TO BUMP TABLE PTR FOR EACH FLAGGED WORD
*
GNV12  MOV  BITS1,WB         LOAD BIT TO TEST
       ANB  WC,WB            TEST FOR WORD PRESENT
       ZRB  WB,GNV13         JUMP IF NOT PRESENT
       ICA  XL               ELSE BUMP TABLE POINTER
*
*      HERE AFTER DEALING WITH ONE WORD (ONE BIT)
*
GNV13  RSH  WC,1             REMOVE BIT ALREADY PROCESSED
       NZB  WC,GNV12         LOOP BACK IF MORE BITS TO TEST
       BRN  GNV09            ELSE LOOP BACK FOR NEXT SVBLK
*
*      HERE IF NOT SYSTEM VARIABLE
*
GNV14  MOV  WA,WC            COPY VRLEN VALUE
       MOV  =VRCHS,WA        LOAD STANDARD SIZE -CHARS
       ADD  GNVNW,WA         ADJUST FOR CHARS OF NAME
       WTB  WA               CONVERT LENGTH TO BYTES
       EJC
*
*      GTNVR (CONTINUED)
*
*      MERGE HERE TO BUILD VRBLK
*
GNV15  JSR  ALOST            ALLOCATE SPACE FOR VRBLK (STATIC)
       MOV  XR,WB            SAVE VRBLK POINTER
       MOV  =STNVR,XL        POINT TO MODEL VARIABLE BLOCK
       MOV  *VRLEN,WA        SET LENGTH OF STANDARD FIELDS
       MVW                   SET INITIAL FIELDS OF NEW BLOCK
       MOV  GNVHE,XL         LOAD POINTER TO END OF HASH CHAIN
       MOV  WB,VRNXT(XL)     ADD NEW BLOCK TO END OF CHAIN
       MOV  WC,(XR)+         SET VRLEN FIELD, BUMP PTR
       MOV  GNVNW,WA         GET LENGTH IN WORDS
       WTB  WA               CONVERT TO LENGTH IN BYTES
       BZE  WC,GNV16         JUMP IF SYSTEM VARIABLE
*
*      HERE FOR NON-SYSTEM VARIABLE -- SET CHARS OF NAME
*
       MOV  (XS),XL          POINT BACK TO STRING NAME
       ADD  *SCHAR,XL        POINT TO CHARS OF NAME
       MVW                   MOVE CHARACTERS INTO PLACE
       MOV  WB,XR            RESTORE VRBLK POINTER
       BRN  GNV06            JUMP BACK TO EXIT
*
*      HERE FOR SYSTEM VARIABLE CASE TO FILL IN FIELDS WHERE
*      NECESSARY FROM THE FIELDS PRESENT IN THE SVBLK.
*
GNV16  MOV  GNVSP,XL         LOAD POINTER TO SVBLK
       MOV  XL,(XR)          SET SVBLK PTR IN VRBLK
       MOV  WB,XR            RESTORE VRBLK POINTER
       MOV  SVBIT(XL),WB     LOAD BIT INDICATORS
       ADD  *SVCHS,XL        POINT TO CHARACTERS OF NAME
       ADD  WA,XL            POINT PAST CHARACTERS
*
*      SKIP PAST KEYWORD NUMBER (SVKNM) IF PRESENT
*
       MOV  BTKNM,WC         LOAD TEST BIT
       ANB  WB,WC            AND TO TEST
       ZRB  WC,GNV17         JUMP IF NO KEYWORD NUMBER
       ICA  XL               ELSE BUMP POINTER
       EJC
*
*      GTNVR (CONTINUED)
*
*      HERE TEST FOR FUNCTION (SVFNC AND SVNAR)
*
GNV17  MOV  BTFNC,WC         GET TEST BIT
       ANB  WB,WC            AND TO TEST
       ZRB  WC,GNV18         SKIP IF NO SYSTEM FUNCTION
       MOV  XL,VRFNC(XR)     ELSE POINT VRFNC TO SVFNC FIELD
       ADD  *NUM02,XL        AND BUMP PAST SVFNC, SVNAR FIELDS
*
*      NOW TEST FOR LABEL (SVLBL)
*
GNV18  MOV  BTLBL,WC         GET TEST BIT
       ANB  WB,WC            AND TO TEST
       ZRB  WC,GNV19         JUMP IF BIT IS OFF (NO SYSTEM LABL)
       MOV  XL,VRLBL(XR)     ELSE POINT VRLBL TO SVLBL FIELD
       ICA  XL               BUMP PAST SVLBL FIELD
*
*      NOW TEST FOR VALUE (SVVAL)
*
GNV19  MOV  BTVAL,WC         LOAD TEST BIT
       ANB  WB,WC            AND TO TEST
       ZRB  WC,GNV06         ALL DONE IF NO VALUE
       MOV  (XL),VRVAL(XR)   ELSE SET INITIAL VALUE
       MOV  =B_VRE,VRSTO(XR) SET ERROR STORE ACCESS
       BRN  GNV06            MERGE BACK TO EXIT TO CALLER
       ENP                   END PROCEDURE GTNVR
       EJC
*
*      GTPAT -- GET PATTERN
*
*      GTPAT IS PASSED AN OBJECT IN (XR) AND RETURNS A
*      PATTERN AFTER PERFORMING ANY NECESSARY CONVERSIONS
*
*      (XR)                  INPUT ARGUMENT
*      JSR  GTPAT            CALL TO CONVERT TO PATTERN
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  RESULTING PATTERN
*      (WA)                  DESTROYED
*      (WB)                  DESTROYED (ONLY ON CONVERT ERROR)
*      (XR)                  UNCHANGED (ONLY ON CONVERT ERROR)
*
GTPAT  PRC  E,1              ENTRY POINT
       BHI  (XR),=P_AAA,GTPT5 JUMP IF PATTERN ALREADY
*
*      HERE IF NOT PATTERN, TRY FOR STRING
*
       MOV  WB,GTPSB         SAVE WB
       MOV  XR,-(XS)         STACK ARGUMENT FOR GTSTG
       JSR  GTSTG            CONVERT ARGUMENT TO STRING
       PPM  GTPT2            JUMP IF IMPOSSIBLE
*
*      HERE WE HAVE A STRING
*
       BNZ  WA,GTPT1         JUMP IF NON-NULL
*
*      HERE FOR NULL STRING. GENERATE POINTER TO NULL PATTERN.
*
       MOV  =NDNTH,XR        POINT TO NOTHEN NODE
       BRN  GTPT4            JUMP TO EXIT
       EJC
*
*      GTPAT (CONTINUED)
*
*      HERE FOR NON-NULL STRING
*
GTPT1  MOV  =P_STR,WB        LOAD PCODE FOR MULTI-CHAR STRING
       BNE  WA,=NUM01,GTPT3  JUMP IF MULTI-CHAR STRING
*
*      HERE FOR ONE CHARACTER STRING, SHARE ONE CHARACTER ANY
*
       PLC  XR               POINT TO CHARACTER
       LCH  WA,(XR)          LOAD CHARACTER
       MOV  WA,XR            SET AS PARM1
       MOV  =P_ANS,WB        POINT TO PCODE FOR 1-CHAR ANY
       BRN  GTPT3            JUMP TO BUILD NODE
*
*      HERE IF ARGUMENT IS NOT CONVERTIBLE TO STRING
*
GTPT2  MOV  =P_EXA,WB        SET PCODE FOR EXPRESSION IN CASE
       BLO  (XR),=B_E__,GTPT3 JUMP TO BUILD NODE IF EXPRESSION
*
*      HERE WE HAVE AN ERROR (CONVERSION IMPOSSIBLE)
*
       EXI  1                TAKE CONVERT ERROR EXIT
*
*      MERGE HERE TO BUILD NODE FOR STRING OR EXPRESSION
*
GTPT3  JSR  PBILD            CALL ROUTINE TO BUILD PATTERN NODE
*
*      COMMON EXIT AFTER SUCCESSFUL CONVERSION
*
GTPT4  MOV  GTPSB,WB         RESTORE WB
*
*      MERGE HERE TO EXIT IF NO CONVERSION REQUIRED
*
GTPT5  EXI                   RETURN TO GTPAT CALLER
       ENP                   END PROCEDURE GTPAT
.IF    .CNRA
.ELSE
       EJC
*
*      GTREA -- GET REAL VALUE
*
*      GTREA IS PASSED AN OBJECT AND RETURNS A REAL VALUE
*      PERFORMING ANY NECESSARY CONVERSIONS.
*
*      (XR)                  OBJECT TO BE CONVERTED
*      JSR  GTREA            CALL TO CONVERT OBJECT TO REAL
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULTING REAL
*      (WA,WB,WC,RA)         DESTROYED
*      (XR)                  UNCHANGED (CONVERT ERROR ONLY)
*
GTREA  PRC  E,1              ENTRY POINT
       MOV  (XR),WA          GET FIRST WORD OF BLOCK
       BEQ  WA,=B_RCL,GTRE2  JUMP IF REAL
       JSR  GTNUM            ELSE CONVERT ARGUMENT TO NUMERIC
       PPM  GTRE3            JUMP IF UNCONVERTIBLE
       BEQ  WA,=B_RCL,GTRE2  JUMP IF REAL WAS RETURNED
*
*      HERE FOR CASE OF AN INTEGER TO CONVERT TO REAL
*
GTRE1  LDI  ICVAL(XR)        LOAD INTEGER
       ITR                   CONVERT TO REAL
       JSR  RCBLD            BUILD RCBLK
*
*      EXIT WITH REAL
*
GTRE2  EXI                   RETURN TO GTREA CALLER
*
*      HERE ON CONVERSION ERROR
*
GTRE3  EXI  1                TAKE CONVERT ERROR EXIT
       ENP                   END PROCEDURE GTREA
.FI
       EJC
*
*      GTSMI -- GET SMALL INTEGER
*
*      GTSMI IS PASSED A SNOBOL OBJECT AND RETURNS AN ADDRESS
*      INTEGER IN THE RANGE (0 LE N LE DNAMB). SUCH A VALUE CAN
*      ONLY BE DERIVED FROM AN INTEGER IN THE APPROPRIATE RANGE.
*      SMALL INTEGERS NEVER APPEAR AS SNOBOL VALUES. HOWEVER,
*      THEY ARE USED INTERNALLY FOR A VARIETY OF PURPOSES.
*
*      -(XS)                 ARGUMENT TO CONVERT (ON STACK)
*      JSR  GTSMI            CALL TO CONVERT TO SMALL INTEGER
*      PPM  LOC              TRANSFER LOC FOR NOT INTEGER
*      PPM  LOC              TRANSFER LOC FOR LT 0, GT DNAMB
*      (XR,WC)               RESULTING SMALL INT (TWO COPIES)
*      (XS)                  POPPED
*      (RA)                  DESTROYED
*      (WA,WB)               DESTROYED (ON CONVERT ERROR ONLY)
*      (XR)                  INPUT ARG (CONVERT ERROR ONLY)
*
GTSMI  PRC  N,2              ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT
       BEQ  (XR),=B_ICL,GTSM1 SKIP IF ALREADY AN INTEGER
*
*      HERE IF NOT AN INTEGER
*
       JSR  GTINT            CONVERT ARGUMENT TO INTEGER
       PPM  GTSM2            JUMP IF CONVERT IS IMPOSSIBLE
*
*      MERGE HERE WITH INTEGER
*
GTSM1  LDI  ICVAL(XR)        LOAD INTEGER VALUE
       MFI  WC,GTSM3         MOVE AS ONE WORD, JUMP IF OVFLOW
       BGT  WC,MXLEN,GTSM3   OR IF TOO LARGE
       MOV  WC,XR            COPY RESULT TO XR
       EXI                   RETURN TO GTSMI CALLER
*
*      HERE IF UNCONVERTIBLE TO INTEGER
*
GTSM2  EXI  1                TAKE NON-INTEGER ERROR EXIT
*
*      HERE IF OUT OF RANGE
*
GTSM3  EXI  2                TAKE OUT-OF-RANGE ERROR EXIT
       ENP                   END PROCEDURE GTSMI
       EJC
.IF    .CNBF
.ELSE
*
*      GTSTB -- GET STRING OR BUFFER
*
*      GTSTB IS PASSED AN OBJECT AND RETURNS IT UNCHANGED IF
*      IT IS A BUFFER BLOCK, ELSE IT RETURNS IT AS A STRING WITH
*      ANY NECESSARY CONVERSIONS PERFORMED.
*
*      -(XS)                 INPUT ARGUMENT (ON STACK)
*      JSR  GTSTB            CALL TO GET BUFFER OR CNVRT TO STG
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULTING SCBLK OR BFBLK
*      (WA)                  LENGTH OF STRING IN CHARACTERS
*      (WB)                  ZERO/BCBLK IF STRING/BUFFER
*      (XS)                  POPPED
*      (RA)                  DESTROYED
*      (XR)                  INPUT ARG (CONVERT ERROR ONLY)
*
GTSTB  PRC  N,1              ENTRY POINT
       MOV  (XS),XR          LOAD ARGUMENT, LEAVE ON STACK
       MOV  (XR),WA          LOAD BLOCK TYPE
       BEQ  WA,=B_SCL,GTSB2  JUMP IF ALREADY A STRING
       BEQ  WA,=B_BCT,GTSB3  JUMP IF ALREADY A BUFFER
       JSR  GTSTG            CONVERT TO STRING
       PPM  GTSB1            CONVERSION FAILED
       ZER  WB               SIGNAL STRING RESULT
       EXI                   CONVERT WITH STRING RESULT
*
*      HERE IF CONVERSION FAILED
*
GTSB1  EXI  1                TAKE CONVERT ERROR EXIT
*
*      HERE IF A STRING ALREADY
*
GTSB2  ICA  XS               POP ARGUMENT
       MOV  SCLEN(XR),WA     LOAD STRING LENGTH
       ZER  WB               SIGNAL STRING RESULT
       EXI                   RETURN WITH STRING RESULT
*
*      HERE IF IT IS ALREADY A BUFFER
*
GTSB3  ICA  XS               POP ARGUMENT
       MOV  BCLEN(XR),WA     LOAD LENGTH OF STRING IN BUFFER
       MOV  XR,WB            RETURN BCBLK POINTER IN WB
       MOV  BCBUF(XR),XR     RETURN BFBLK POINTER IN XR
       EXI                   RETURN WITH BUFFER RESULT
       ENP                   END PROCEDURE GTSTG
       EJC
.FI
*
*      GTSTG -- GET STRING
*
*      GTSTG IS PASSED AN OBJECT AND RETURNS A STRING WITH
*      ANY NECESSARY CONVERSIONS PERFORMED.
*
*      -(XS)                 INPUT ARGUMENT (ON STACK)
*      JSR  GTSTG            CALL TO CONVERT TO STRING
*      PPM  LOC              TRANSFER LOC IF CONVERT IMPOSSIBLE
*      (XR)                  POINTER TO RESULTING STRING
*      (WA)                  LENGTH OF STRING IN CHARACTERS
*      (XS)                  POPPED
*      (RA)                  DESTROYED
*      (XR)                  INPUT ARG (CONVERT ERROR ONLY)
*
GTSTG  PRC  N,1              ENTRY POINT
       MOV  (XS)+,XR         LOAD ARGUMENT, POP STACK
       BEQ  (XR),=B_SCL,GTS30 JUMP IF ALREADY A STRING
*
*      HERE IF NOT A STRING ALREADY
*
GTS01  MOV  XR,-(XS)         RESTACK ARGUMENT IN CASE ERROR
       MOV  XL,-(XS)         SAVE XL
       MOV  WB,GTSVB         SAVE WB
       MOV  WC,GTSVC         SAVE WC
       MOV  (XR),WA          LOAD FIRST WORD OF BLOCK
       BEQ  WA,=B_ICL,GTS05  JUMP TO CONVERT INTEGER
.IF    .CNRA
.ELSE
       BEQ  WA,=B_RCL,GTS10  JUMP TO CONVERT REAL
.FI
       BEQ  WA,=B_NML,GTS03  JUMP TO CONVERT NAME
.IF    .CNBF
.ELSE
       BEQ  WA,=B_BCT,GTS32  JUMP TO CONVERT BUFFER
.FI
*
*      HERE ON CONVERSION ERROR
*
GTS02  MOV  (XS)+,XL         RESTORE XL
       MOV  (XS)+,XR         RELOAD INPUT ARGUMENT
       EXI  1                TAKE CONVERT ERROR EXIT
       EJC
*
*      GTSTG (CONTINUED)
*
*      HERE TO CONVERT A NAME (ONLY POSSIBLE IF NATURAL VAR)
*
GTS03  MOV  NMBAS(XR),XL     LOAD NAME BASE
       BHI  XL,STATE,GTS02   ERROR IF NOT NATURAL VAR (STATIC)
       ADD  *VRSOF,XL        ELSE POINT TO POSSIBLE STRING NAME
       MOV  SCLEN(XL),WA     LOAD LENGTH
       BNZ  WA,GTS04         JUMP IF NOT SYSTEM VARIABLE
       MOV  VRSVO(XL),XL     ELSE POINT TO SVBLK
       MOV  SVLEN(XL),WA     AND LOAD NAME LENGTH
*
*      MERGE HERE WITH STRING IN XR, LENGTH IN WA
*
GTS04  ZER  WB               SET OFFSET TO ZERO
       JSR  SBSTR            USE SBSTR TO COPY STRING
       BRN  GTS29            JUMP TO EXIT
*
*      COME HERE TO CONVERT AN INTEGER
*
GTS05  LDI  ICVAL(XR)        LOAD INTEGER VALUE
.IF    .CNCI
       JSR  SYSCI            CONVERT INTEGER
       MOV  SCLEN(XL),WA     GET LENGTH
       ZER  WB               ZERO OFFSET FOR SBSTR
       JSR  SBSTR            COPY IN RESULT FROM SYSCI
       BRN  GTS29            EXIT
.ELSE
       MOV  =NUM01,GTSSF     SET SIGN FLAG NEGATIVE
       ILT  GTS06            SKIP IF INTEGER IS NEGATIVE
       NGI                   ELSE NEGATE INTEGER
       ZER  GTSSF            AND RESET NEGATIVE FLAG
       EJC
*
*      GTSTG (CONTINUED)
*
*      HERE WITH SIGN FLAG SET AND SIGN FORCED NEGATIVE AS
*      REQUIRED BY THE CVD INSTRUCTION.
*
GTS06  MOV  GTSWK,XR         POINT TO RESULT WORK AREA
       MOV  =NSTMX,WB        INITIALIZE COUNTER TO MAX LENGTH
       PSC  XR,WB            PREPARE TO STORE (RIGHT-LEFT)
*
*      LOOP TO CONVERT DIGITS INTO WORK AREA
*
GTS07  CVD                   CONVERT ONE DIGIT INTO WA
       SCH  WA,-(XR)         STORE IN WORK AREA
       DCV  WB               DECREMENT COUNTER
       INE  GTS07            LOOP IF MORE DIGITS TO GO
       CSC  XR               COMPLETE STORE CHARACTERS
.FI
*
*      MERGE HERE AFTER CONVERTING INTEGER OR REAL INTO WORK
*      AREA. WB IS SET TO NSTMX - (NUMBER OF CHARS IN RESULT).
*
GTS08  MOV  =NSTMX,WA        GET MAX NUMBER OF CHARACTERS
       SUB  WB,WA            COMPUTE LENGTH OF RESULT
       MOV  WA,XL            REMEMBER LENGTH FOR MOVE LATER ON
       ADD  GTSSF,WA         ADD ONE FOR NEGATIVE SIGN IF NEEDED
       JSR  ALOCS            ALLOCATE STRING FOR RESULT
       MOV  XR,WC            SAVE RESULT POINTER FOR THE MOMENT
       PSC  XR               POINT TO CHARS OF RESULT BLOCK
       BZE  GTSSF,GTS09      SKIP IF POSITIVE
       MOV  =CH_MN,WA        ELSE LOAD NEGATIVE SIGN
       SCH  WA,(XR)+         AND STORE IT
       CSC  XR               COMPLETE STORE CHARACTERS
*
*      HERE AFTER DEALING WITH SIGN
*
GTS09  MOV  XL,WA            RECALL LENGTH TO MOVE
       MOV  GTSWK,XL         POINT TO RESULT WORK AREA
       PLC  XL,WB            POINT TO FIRST RESULT CHARACTER
       MVC                   MOVE CHARS TO RESULT STRING
       MOV  WC,XR            RESTORE RESULT POINTER
.IF    .CNRA
.ELSE
       BRN  GTS29            JUMP TO EXIT
       EJC
*
*      GTSTG (CONTINUED)
*
*      HERE TO CONVERT A REAL
*
GTS10  LDR  RCVAL(XR)        LOAD REAL
.IF    .CNCR
       MOV  =NSTMR,WA        MAX NUMBER OF RESULT CHARS
       ZER  XL               CLEAR DUD VALUE
       JSR  ALOCS            ALLOCATE RESULT AREA
       MOV  =CFP_S,WA        SIGNIFICANT DIGITS TO PRODUCE
       ZER  WB               CONVERSION TYPE
       JSR  SYSCR            CONVERT REAL TO STRING
       MOV  WA,SCLEN(XR)     STORE RESULT SIZE
       ZER  WB               NO TRAILING BLANKS TO REMOVE
       JSR  TRIMR            DISCARD EXCESS MEMORY
.ELSE
       ZER  GTSSF            RESET NEGATIVE FLAG
       REQ  GTS31            SKIP IF ZERO
       RGE  GTS11            JUMP IF REAL IS POSITIVE
       MOV  =NUM01,GTSSF     ELSE SET NEGATIVE FLAG
       NGR                   AND GET ABSOLUTE VALUE OF REAL
*
*      NOW SCALE THE REAL TO THE RANGE (0.1 LE X LT 1.0)
*
GTS11  LDI  INTV0            INITIALIZE EXPONENT TO ZERO
*
*      LOOP TO SCALE UP IN STEPS OF 10**10
*
GTS12  STR  GTSRS            SAVE REAL VALUE
       SBR  REAP1            SUBTRACT 0.1 TO COMPARE
       RGE  GTS13            JUMP IF SCALE UP NOT REQUIRED
       LDR  GTSRS            ELSE RELOAD VALUE
       MLR  REATT            MULTIPLY BY 10**10
       SBI  INTVT            DECREMENT EXPONENT BY 10
       BRN  GTS12            LOOP BACK TO TEST AGAIN
*
*      TEST FOR SCALE DOWN REQUIRED
*
GTS13  LDR  GTSRS            RELOAD VALUE
       SBR  REAV1            SUBTRACT 1.0
       RLT  GTS17            JUMP IF NO SCALE DOWN REQUIRED
       LDR  GTSRS            ELSE RELOAD VALUE
*
*      LOOP TO SCALE DOWN IN STEPS OF 10**10
*
GTS14  SBR  REATT            SUBTRACT 10**10 TO COMPARE
       RLT  GTS15            JUMP IF LARGE STEP NOT REQUIRED
       LDR  GTSRS            ELSE RESTORE VALUE
       DVR  REATT            DIVIDE BY 10**10
       STR  GTSRS            STORE NEW VALUE
       ADI  INTVT            INCREMENT EXPONENT BY 10
       BRN  GTS14            LOOP BACK
       EJC
*
*      GTSTG (CONTINUED)
*
*      AT THIS POINT WE HAVE (1.0 LE X LT 10**10)
*      COMPLETE SCALING WITH POWERS OF TEN TABLE
*
GTS15  MOV  =REAV1,XR        POINT TO POWERS OF TEN TABLE
*
*      LOOP TO LOCATE CORRECT ENTRY IN TABLE
*
GTS16  LDR  GTSRS            RELOAD VALUE
       ADI  INTV1            INCREMENT EXPONENT
       ADD  *CFP_R,XR        POINT TO NEXT ENTRY IN TABLE
       SBR  (XR)             SUBTRACT IT TO COMPARE
       RGE  GTS16            LOOP TILL WE FIND A LARGER ENTRY
       LDR  GTSRS            THEN RELOAD THE VALUE
       DVR  (XR)             AND COMPLETE SCALING
       STR  GTSRS            STORE VALUE
*
*      WE ARE NOW SCALED, SO ROUND BY ADDING 0.5 * 10**(-CFP_S)
*
GTS17  LDR  GTSRS            GET VALUE AGAIN
       ADR  GTSRN            ADD ROUNDING FACTOR
       STR  GTSRS            STORE RESULT
*
*      THE ROUNDING OPERATION MAY HAVE PUSHED US UP PAST
*      1.0 AGAIN, SO CHECK ONE MORE TIME.
*
       SBR  REAV1            SUBTRACT 1.0 TO COMPARE
       RLT  GTS18            SKIP IF OK
       ADI  INTV1            ELSE INCREMENT EXPONENT
       LDR  GTSRS            RELOAD VALUE
       DVR  REAVT            DIVIDE BY 10.0 TO RESCALE
       BRN  GTS19            JUMP TO MERGE
*
*      HERE IF ROUNDING DID NOT MUCK UP SCALING
*
GTS18  LDR  GTSRS            RELOAD ROUNDED VALUE
       EJC
*
*      GTSTG (CONTINUED)
*
*      NOW WE HAVE COMPLETED THE SCALING AS FOLLOWS
*
*      (IA)                  SIGNED EXPONENT
*      (RA)                  SCALED REAL (ABSOLUTE VALUE)
*
*      IF THE EXPONENT IS NEGATIVE OR GREATER THAN CFP_S, THEN
*      WE CONVERT THE NUMBER IN THE FORM.
*
*      (NEG SIGN) 0 . (CPF_S DIGITS) E (EXP SIGN) (EXP DIGITS)
*
*      IF THE EXPONENT IS POSITIVE AND LESS THAN OR EQUAL TO
*      CFP_S, THE NUMBER IS CONVERTED IN THE FORM.
*
*      (NEG SIGN) (EXPONENT DIGITS) . (CFP_S-EXPONENT DIGITS)
*
*      IN BOTH CASES, THE FORMATS OBTAINED FROM THE ABOVE
*      RULES ARE MODIFIED BY DELETING TRAILING ZEROS AFTER THE
*      DECIMAL POINT. THERE ARE NO LEADING ZEROS IN THE EXPONENT
*      AND THE EXPONENT SIGN IS ALWAYS PRESENT.
*
GTS19  MOV  =CFP_S,XL        SET NUM DEC DIGITS = CFP_S
       MOV  =CH_MN,GTSES     SET EXPONENT SIGN NEGATIVE
       ILT  GTS21            ALL SET IF EXPONENT IS NEGATIVE
       MFI  WA               ELSE FETCH EXPONENT
       BLE  WA,=CFP_S,GTS20  SKIP IF WE CAN USE SPECIAL FORMAT
       MTI  WA               ELSE RESTORE EXPONENT
       NGI                   SET NEGATIVE FOR CVD
       MOV  =CH_PL,GTSES     SET PLUS SIGN FOR EXPONENT SIGN
       BRN  GTS21            JUMP TO GENERATE EXPONENT
*
*      HERE IF WE CAN USE THE FORMAT WITHOUT AN EXPONENT
*
GTS20  SUB  WA,XL            COMPUTE DIGITS AFTER DECIMAL POINT
       LDI  INTV0            RESET EXPONENT TO ZERO
       EJC
*
*      GTSTG (CONTINUED)
*
*      MERGE HERE AS FOLLOWS
*
*      (IA)                  EXPONENT ABSOLUTE VALUE
*      GTSES                 CHARACTER FOR EXPONENT SIGN
*      (RA)                  POSITIVE FRACTION
*      (XL)                  NUMBER OF DIGITS AFTER DEC POINT
*
GTS21  MOV  GTSWK,XR         POINT TO WORK AREA
       MOV  =NSTMX,WB        SET CHARACTER CTR TO MAX LENGTH
       PSC  XR,WB            PREPARE TO STORE (RIGHT TO LEFT)
       IEQ  GTS23            SKIP EXPONENT IF IT IS ZERO
*
*      LOOP TO GENERATE DIGITS OF EXPONENT
*
GTS22  CVD                   CONVERT A DIGIT INTO WA
       SCH  WA,-(XR)         STORE IN WORK AREA
       DCV  WB               DECREMENT COUNTER
       INE  GTS22            LOOP BACK IF MORE DIGITS TO GO
*
*      HERE GENERATE EXPONENT SIGN AND E
*
       MOV  GTSES,WA         LOAD EXPONENT SIGN
       SCH  WA,-(XR)         STORE IN WORK AREA
       MOV  =CH_LE,WA        GET CHARACTER LETTER E
       SCH  WA,-(XR)         STORE IN WORK AREA
       SUB  =NUM02,WB        DECREMENT COUNTER FOR SIGN AND E
*
*      HERE TO GENERATE THE FRACTION
*
GTS23  MLR  GTSSC            CONVERT REAL TO INTEGER (10**CFP_S)
       RTI                   GET INTEGER (OVERFLOW IMPOSSIBLE)
       NGI                   NEGATE AS REQUIRED BY CVD
*
*      LOOP TO SUPPRESS TRAILING ZEROS
*
GTS24  BZE  XL,GTS27         JUMP IF NO DIGITS LEFT TO DO
       CVD                   ELSE CONVERT ONE DIGIT
       BNE  WA,=CH_D0,GTS26  JUMP IF NOT A ZERO
       DCV  XL               DECREMENT COUNTER
       BRN  GTS24            LOOP BACK FOR NEXT DIGIT
       EJC
*
*      GTSTG (CONTINUED)
*
*      LOOP TO GENERATE DIGITS AFTER DECIMAL POINT
*
GTS25  CVD                   CONVERT A DIGIT INTO WA
*
*      MERGE HERE FIRST TIME
*
GTS26  SCH  WA,-(XR)         STORE DIGIT
       DCV  WB               DECREMENT COUNTER
       DCV  XL               DECREMENT COUNTER
       BNZ  XL,GTS25         LOOP BACK IF MORE TO GO
*
*      HERE GENERATE THE DECIMAL POINT
*
GTS27  MOV  =CH_DT,WA        LOAD DECIMAL POINT
       SCH  WA,-(XR)         STORE IN WORK AREA
       DCV  WB               DECREMENT COUNTER
*
*      HERE GENERATE THE DIGITS BEFORE THE DECIMAL POINT
*
GTS28  CVD                   CONVERT A DIGIT INTO WA
       SCH  WA,-(XR)         STORE IN WORK AREA
       DCV  WB               DECREMENT COUNTER
       INE  GTS28            LOOP BACK IF MORE TO GO
       CSC  XR               COMPLETE STORE CHARACTERS
       BRN  GTS08            ELSE JUMP BACK TO EXIT
.FI
.FI
*
*      EXIT POINT AFTER SUCCESSFUL CONVERSION
*
GTS29  MOV  (XS)+,XL         RESTORE XL
       ICA  XS               POP ARGUMENT
       MOV  GTSVB,WB         RESTORE WB
       MOV  GTSVC,WC         RESTORE WC
*
*      MERGE HERE IF NO CONVERSION REQUIRED
*
GTS30  MOV  SCLEN(XR),WA     LOAD STRING LENGTH
       EXI                   RETURN TO CALLER
.IF    .CNRA
.ELSE
*
*      HERE TO RETURN STRING FOR REAL ZERO
*
GTS31  MOV  =SCRE0,XL        POINT TO STRING
       MOV  =NUM02,WA        2 CHARS
       ZER  WB               ZERO OFFSET
       JSR  SBSTR            COPY STRING
       BRN  GTS29            RETURN
.FI
.IF    .CNBF
.ELSE
       EJC
*
*      HERE TO CONVERT A BUFFER BLOCK
*
GTS32  MOV  XR,XL            COPY ARG PTR
       MOV  BCLEN(XL),WA     GET SIZE TO ALLOCATE
       BZE  WA,GTS33         IF NULL THEN RETURN NULL
       JSR  ALOCS            ALLOCATE STRING FRAME
       MOV  XR,WB            SAVE STRING PTR
       MOV  SCLEN(XR),WA     GET LENGTH TO MOVE
       CTB  WA,0             GET AS MULTIPLE OF WORD SIZE
       MOV  BCBUF(XL),XL     POINT TO BFBLK
       ADD  *SCSI_,XR        POINT TO START OF CHARACTER AREA
       ADD  *BFSI_,XL        POINT TO START OF BUFFER CHARS
       MVW                   COPY WORDS
       MOV  WB,XR            RESTORE SCBLK PTR
       BRN  GTS29            EXIT WITH SCBLK
*
*      HERE WHEN NULL BUFFER IS BEING CONVERTED
*
GTS33  MOV  =NULLS,XR        POINT TO NULL
       BRN  GTS29            EXIT WITH NULL
.FI
       ENP                   END PROCEDURE GTSTG
       EJC
*
*      GTVAR -- GET VARIABLE FOR I/O/TRACE ASSOCIATION
*
*      GTVAR IS USED TO POINT TO AN ACTUAL VARIABLE LOCATION
*      FOR THE DETACH,INPUT,OUTPUT,TRACE,STOPTR SYSTEM FUNCTIONS
*
*      (XR)                  ARGUMENT TO FUNCTION
*      JSR  GTVAR            CALL TO LOCATE VARIABLE POINTER
*      PPM  LOC              TRANSFER LOC IF NOT OK VARIABLE
*      (XL,WA)               NAME BASE,OFFSET OF VARIABLE
*      (XR,RA)               DESTROYED
*      (WB,WC)               DESTROYED (CONVERT ERROR ONLY)
*      (XR)                  INPUT ARG (CONVERT ERROR ONLY)
*
GTVAR  PRC  E,1              ENTRY POINT
       BNE  (XR),=B_NML,GTVR2 JUMP IF NOT A NAME
       MOV  NMOFS(XR),WA     ELSE LOAD NAME OFFSET
       MOV  NMBAS(XR),XL     LOAD NAME BASE
       BEQ  (XL),=B_EVT,GTVR1 ERROR IF EXPRESSION VARIABLE
       BNE  (XL),=B_KVT,GTVR3 ALL OK IF NOT KEYWORD VARIABLE
*
*      HERE ON CONVERSION ERROR
*
GTVR1  EXI  1                TAKE CONVERT ERROR EXIT
*
*      HERE IF NOT A NAME, TRY CONVERT TO NATURAL VARIABLE
*
GTVR2  MOV  WC,GTVRC         SAVE WC
       JSR  GTNVR            LOCATE VRBLK IF POSSIBLE
       PPM  GTVR1            JUMP IF CONVERT ERROR
       MOV  XR,XL            ELSE COPY VRBLK NAME BASE
       MOV  *VRVAL,WA        AND SET OFFSET
       MOV  GTVRC,WC         RESTORE WC
*
*      HERE FOR NAME OBTAINED
*
GTVR3  BHI  XL,STATE,GTVR4   ALL OK IF NOT NATURAL VARIABLE
       BEQ  VRSTO(XL),=B_VRE,GTVR1 ERROR IF PROTECTED VARIABLE
*
*      COMMON EXIT POINT
*
GTVR4  EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE GTVAR
       EJC
*
*      HASHS -- COMPUTE HASH INDEX FOR STRING
*
*      HASHS IS USED TO CONVERT A STRING TO A UNIQUE INTEGER
*      VALUE. THE RESULTING HASH VALUE IS A POSITIVE INTEGER
*      IN THE RANGE 0 TO CFP_M
*
*      (XR)                  STRING TO BE HASHED
*      JSR  HASHS            CALL TO HASH STRING
*      (IA)                  HASH VALUE
*      (XR,WB,WC)            DESTROYED
*
*      THE HASH FUNCTION USED IS AS FOLLOWS.
*
*      START WITH THE LENGTH OF THE STRING (SGD07)
*
*      TAKE THE FIRST E_HNW WORDS OF THE CHARACTERS FROM
*      THE STRING OR ALL THE WORDS IF FEWER THAN E_HNW.
*
*      COMPUTE THE EXCLUSIVE OR OF ALL THESE WORDS TREATING
*      THEM AS ONE WORD BIT STRING VALUES.
*
*      MOVE THE RESULT AS AN INTEGER WITH THE MTI INSTRUCTION.
*
HASHS  PRC  E,0              ENTRY POINT
       MOV  SCLEN(XR),WC     LOAD STRING LENGTH IN CHARACTERS
       MOV  WC,WB            INITIALIZE WITH LENGTH
       BZE  WC,HSHS3         JUMP IF NULL STRING
       ZGB  WB               CORRECT BYTE ORDERING IF NECESSARY
       CTW  WC,0             GET NUMBER OF WORDS OF CHARS
       ADD  *SCHAR,XR        POINT TO CHARACTERS OF STRING
       BLO  WC,=E_HNW,HSHS1  USE WHOLE STRING IF SHORT
       MOV  =E_HNW,WC        ELSE SET TO INVOLVE FIRST E_HNW WDS
*
*      HERE WITH COUNT OF WORDS TO CHECK IN WC
*
HSHS1  LCT  WC,WC            SET COUNTER TO CONTROL LOOP
*
*      LOOP TO COMPUTE EXCLUSIVE OR
*
HSHS2  XOB  (XR)+,WB         EXCLUSIVE OR NEXT WORD OF CHARS
       BCT  WC,HSHS2         LOOP TILL ALL PROCESSED
*
*      MERGE HERE WITH EXCLUSIVE OR IN WB
*
HSHS3  ZGB  WB               ZEROISE UNDEFINED BITS
       ANB  BITSM,WB         ENSURE IN RANGE 0 TO CFP_M
       MTI  WB               MOVE RESULT AS INTEGER
       ZER  XR               CLEAR GARBAGE VALUE IN XR
       EXI                   RETURN TO HASHS CALLER
       ENP                   END PROCEDURE HASHS
       EJC
*
*      ICBLD -- BUILD INTEGER BLOCK
*
*      (IA)                  INTEGER VALUE FOR ICBLK
*      JSR  ICBLD            CALL TO BUILD INTEGER BLOCK
*      (XR)                  POINTER TO RESULT ICBLK
*      (WA)                  DESTROYED
*
ICBLD  PRC  E,0              ENTRY POINT
       MFI  XR,ICBL1         COPY SMALL INTEGERS
       BLE  XR,=NUM02,ICBL3  JUMP IF 0,1 OR 2
*
*      CONSTRUCT ICBLK
*
ICBL1  MOV  DNAMP,XR         LOAD POINTER TO NEXT AVAILABLE LOC
       ADD  *ICSI_,XR        POINT PAST NEW ICBLK
       BLO  XR,DNAME,ICBL2   JUMP IF THERE IS ROOM
       MOV  *ICSI_,WA        ELSE LOAD LENGTH OF ICBLK
       JSR  ALLOC            USE STANDARD ALLOCATOR TO GET BLOCK
       ADD  WA,XR            POINT PAST BLOCK TO MERGE
*
*      MERGE HERE WITH XR POINTING PAST THE BLOCK OBTAINED
*
ICBL2  MOV  XR,DNAMP         SET NEW POINTER
       SUB  *ICSI_,XR        POINT BACK TO START OF BLOCK
       MOV  =B_ICL,(XR)      STORE TYPE WORD
       STI  ICVAL(XR)        STORE INTEGER VALUE IN ICBLK
       EXI                   RETURN TO ICBLD CALLER
*
*      OPTIMISE BY NOT BUILDING ICBLKS FOR SMALL INTEGERS
*
ICBL3  WTB  XR               CONVERT INTEGER TO OFFSET
       MOV  INTAB(XR),XR     POINT TO PRE-BUILT ICBLK
       EXI                   RETURN
       ENP                   END PROCEDURE ICBLD
       EJC
*
*      IDENT -- COMPARE TWO VALUES
*
*      IDENT COMPARES TWO VALUES IN THE SENSE OF THE IDENT
*      DIFFER FUNCTIONS AVAILABLE AT THE SNOBOL LEVEL.
*
*      (XR)                  FIRST ARGUMENT
*      (XL)                  SECOND ARGUMENT
*      JSR  IDENT            CALL TO COMPARE ARGUMENTS
*      PPM  LOC              TRANSFER LOC IF IDENT
*      (NORMAL RETURN IF DIFFER)
*      (XR,XL,WC,RA)         DESTROYED
*
IDENT  PRC  E,1              ENTRY POINT
       BEQ  XR,XL,IDEN7      JUMP IF SAME POINTER (IDENT)
       MOV  (XR),WC          ELSE LOAD ARG 1 TYPE WORD
.IF    .CNBF
       BNE  WC,(XL),IDEN1    DIFFER IF ARG 2 TYPE WORD DIFFER
.ELSE
       BNE  WC,(XL),IDEN0    DIFFER IF ARG 2 TYPE WORD DIFFER
.FI
       BEQ  WC,=B_SCL,IDEN2  JUMP IF STRINGS
       BEQ  WC,=B_ICL,IDEN4  JUMP IF INTEGERS
.IF    .CNRA
.ELSE
       BEQ  WC,=B_RCL,IDEN5  JUMP IF REALS
.FI
       BEQ  WC,=B_NML,IDEN6  JUMP IF NAMES
.IF    .CNBF
.ELSE
       BNE  WC,=B_BCT,IDEN1  JUMP IF NOT BUFFERS
*
*      HERE FOR BUFFERS, IDENT ONLY IF LENGTHS AND CHARS SAME
*
       MOV  BCLEN(XR),WC     LOAD ARG 1 LENGTH
       BNE  WC,BCLEN(XL),IDEN1 DIFFER IF LENGTHS DIFFER
       BZE  WC,IDEN7         IDENTICAL IF LENGTH 0
       MOV  BCBUF(XR),XR     ARG 1 BUFFER BLOCK
       MOV  BCBUF(XL),XL     ARG 2 BUFFER BLOCK
       BRN  IDN2A            COMPARE CHARACTERS
*
*      HERE IF THE TYPE WORDS DIFFER.
*      CHECK IF STRING/BUFFER COMPARISON
*
IDEN0  BEQ  WC,=B_SCL,IDN0A  JUMP IF ARG 1 IS A STRING
       BNE  WC,=B_BCT,IDEN1  JUMP IF ARG 1 NOT STRING OR BUFFER
*
*      HERE IF ARG 1 IS A BUFFER
*
       BNE  (XL),=B_SCL,IDEN1 JUMP IF ARG 2 IS NOT STRING
       MOV  BCLEN(XR),WC     LOAD ARG 1 LENGTH
       BNE  WC,SCLEN(XL),IDEN1 DIFFER IF LENGTHS DIFFER
       BZE  WC,IDEN7         IDENTICAL IF LENGTH 0
       MOV  BCBUF(XR),XR     ARG 1 BUFFER BLOCK
       BRN  IDN2A            COMPARE CHARACTERS
*
*      HERE IF ARG 1 IS A STRING
*
IDN0A  BNE  (XL),=B_BCT,IDEN1 JUMP IF ARG 2 IS NOT BUFFER
       MOV  SCLEN(XR),WC     LOAD ARG 1 LENGTH
       BNE  WC,BCLEN(XL),IDEN1 DIFFER IF LENGTHS DIFFER
       BZE  WC,IDEN7         IDENTICAL IF LENGTH 0
       MOV  BCBUF(XL),XL     ARG 2 BUFFER BLOCK
       BRN  IDN2A            COMPARE CHARACTERS
.FI
*
*      FOR ALL OTHER DATATYPES, MUST BE DIFFER IF XR NE XL
*
*      MERGE HERE FOR DIFFER
*
IDEN1  EXI                   TAKE DIFFER EXIT
*
*      HERE FOR STRINGS, IDENT ONLY IF LENGTHS AND CHARS SAME
*
IDEN2  MOV  SCLEN(XR),WC     LOAD ARG 1 LENGTH
       BNE  WC,SCLEN(XL),IDEN1 DIFFER IF LENGTHS DIFFER
*
*      BUFFER AND STRING COMPARISONS MERGE HERE
*
IDN2A  ADD  *SCHAR,XR        POINT TO CHARS OF ARG 1
       ADD  *SCHAR,XL        POINT TO CHARS OF ARG 2
       CTW  WC,0             GET NUMBER OF WORDS IN STRINGS
       LCT  WC,WC            SET LOOP COUNTER
*
*      LOOP TO COMPARE CHARACTERS. NOTE THAT WC CANNOT BE ZERO
*      SINCE ALL NULL STRINGS POINT TO NULLS AND GIVE XL=XR.
*
IDEN3  CNE  (XR),(XL),IDEN8  DIFFER IF CHARS DO NOT MATCH
       ICA  XR               ELSE BUMP ARG ONE POINTER
       ICA  XL               BUMP ARG TWO POINTER
       BCT  WC,IDEN3         LOOP BACK TILL ALL CHECKED
       EJC
*
*      IDENT (CONTINUED)
*
*      HERE TO EXIT FOR CASE OF TWO IDENT STRINGS
*
       ZER  XL               CLEAR GARBAGE VALUE IN XL
       ZER  XR               CLEAR GARBAGE VALUE IN XR
       EXI  1                TAKE IDENT EXIT
*
*      HERE FOR INTEGERS, IDENT IF SAME VALUES
*
IDEN4  LDI  ICVAL(XR)        LOAD ARG 1
       SBI  ICVAL(XL)        SUBTRACT ARG 2 TO COMPARE
       IOV  IDEN1            DIFFER IF OVERFLOW
       INE  IDEN1            DIFFER IF RESULT IS NOT ZERO
       EXI  1                TAKE IDENT EXIT
.IF    .CNRA
.ELSE
*
*      HERE FOR REALS, IDENT IF SAME VALUES
*
IDEN5  LDR  RCVAL(XR)        LOAD ARG 1
       SBR  RCVAL(XL)        SUBTRACT ARG 2 TO COMPARE
       ROV  IDEN1            DIFFER IF OVERFLOW
       RNE  IDEN1            DIFFER IF RESULT IS NOT ZERO
       EXI  1                TAKE IDENT EXIT
.FI
*
*      HERE FOR NAMES, IDENT IF BASES AND OFFSETS SAME
*
IDEN6  BNE  NMOFS(XR),NMOFS(XL),IDEN1 DIFFER IF DIFFERENT OFFSET
       BNE  NMBAS(XR),NMBAS(XL),IDEN1 DIFFER IF DIFFERENT BASE
*
*      MERGE HERE TO SIGNAL IDENT FOR IDENTICAL POINTERS
*
IDEN7  EXI  1                TAKE IDENT EXIT
*
*      HERE FOR DIFFER STRINGS
*
IDEN8  ZER  XR               CLEAR GARBAGE PTR IN XR
       ZER  XL               CLEAR GARBAGE PTR IN XL
       EXI                   RETURN TO CALLER (DIFFER)
       ENP                   END PROCEDURE IDENT
       EJC
*
*      INOUT - USED TO INITIALISE INPUT AND OUTPUT VARIABLES
*
*      (XL)                  POINTER TO VBL NAME STRING
*      (WB)                  TRBLK TYPE
*      JSR  INOUT            CALL TO PERFORM INITIALISATION
*      (XL)                  VRBLK PTR
*      (XR)                  TRBLK PTR
*      (WA,WC)               DESTROYED
*
*      NOTE THAT TRTER (= TRTRF) FIELD OF STANDARD I/O VARIABLES
*      POINTS TO CORRESPONDING SVBLK NOT TO A TRBLK AS IS THE
*      CASE FOR ORDINARY VARIABLES.
*
INOUT  PRC  E,0              ENTRY POINT
       MOV  WB,-(XS)         STACK TRBLK TYPE
       MOV  SCLEN(XL),WA     GET NAME LENGTH
       ZER  WB               POINT TO START OF NAME
       JSR  SBSTR            BUILD A PROPER SCBLK
       JSR  GTNVR            BUILD VRBLK
       PPM                   NO ERROR RETURN
       MOV  XR,WC            SAVE VRBLK POINTER
       MOV  (XS)+,WB         GET TRTER FIELD
       ZER  XL               ZERO TRFPT
       JSR  TRBLD            BUILD TRBLK
       MOV  WC,XL            RECALL VRBLK POINTER
       MOV  VRSVP(XL),TRTER(XR) STORE SVBLK POINTER
       MOV  XR,VRVAL(XL)     STORE TRBLK PTR IN VRBLK
       MOV  =B_VRA,VRGET(XL) SET TRAPPED ACCESS
       MOV  =B_VRV,VRSTO(XL) SET TRAPPED STORE
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE INOUT
       EJC
.IF    .CNBF
.ELSE
*
*      INSBF -- INSERT STRING IN BUFFER
*
*      THIS ROUTINE WILL REPLACE A SECTION OF A BUFFER WITH THE
*      CONTENTS OF A GIVEN STRING.  IF THE LENGTH OF THE
*      SECTION TO BE REPLACED IS DIFFERENT THAN THE LENGTH OF
*      THE GIVEN STRING, AND THE REPLACEMENT IS NOT AN APPEND,
*      THEN THE UPPER SECTION OF THE BUFFER IS SHIFTED UP OR
*      DOWN TO CREATE THE PROPER SPACE FOR THE INSERT.
*
*      (XR)                  POINTER TO BCBLK
*      (XL)                  OBJECT WHICH IS STRING CONVERTABLE
*      (WA)                  OFFSET OF START OF INSERT IN BUFFER
*      (WB)                  LENGTH OF SECTION TO REPLACE
*      JSR  INSBF            CALL TO INSERT CHARACTERS IN BUFFER
*      PPM  LOC              THREAD IF (XL) NOT CONVERTABLE
*      PPM  LOC              THREAD IF INSERT NOT POSSIBLE
*
*      THE SECOND ALTERNATE EXIT IS TAKEN IF THE INSERT WOULD
*      OVERFLOW THE BUFFER, OR IF THE INSERT IS OUT PAST THE
*      DEFINED END OF THE BUFFER AS GIVEN.
*
INSBF  PRC  E,2              ENTRY POINT
       MOV  WA,INSSA         SAVE ENTRY WA
       MOV  WB,INSSB         SAVE ENTRY WB
       MOV  WC,INSSC         SAVE ENTRY WC
       ADD  WB,WA            ADD TO GET OFFSET PAST REPLACE PART
       MOV  WA,INSAB         SAVE WA+WB
       MOV  BCLEN(XR),WC     GET CURRENT DEFINED LENGTH
       BGT  INSSA,WC,INS07   FAIL IF START OFFSET TOO BIG
       BGT  WA,WC,INS07      FAIL IF FINAL OFFSET TOO BIG
       MOV  XL,-(XS)         SAVE ENTRY XL
       MOV  XR,-(XS)         SAVE BCBLK PTR
       MOV  XL,-(XS)         STACK AGAIN FOR GTSTG OR GTSTB
       BEQ  XR,XL,INS08      B IF INSERTING SAME BUFFER
       JSR  GTSTB            CALL TO GET STRING OR BUFFER
       PPM  INS05            TAKE STRING CONVERT ERR EXIT
*
*      MERGE HERE WITH XR POINTING TO THE SCBLK OR BFBLK OF
*      THE OBJECT BEING INSERTED, AND WA CONTAINING THE
*      NUMBER OF CHARACTERS IN THAT OBJECT.
*
INS09  MOV  XR,XL            SAVE STRING PTR
       MOV  WA,INSLN         SAVE ITS LENGTH
       MOV  (XS),XR          RESTORE BCBLK PTR
       ADD  WC,WA            ADD BUFFER LEN TO STRING LEN
       SUB  INSSB,WA         BIAS OUT COMPONENT BEING REPLACED
       MOV  BCBUF(XR),XR     POINT TO BFBLK
       BGT  WA,BFALC(XR),INS06 FAIL IF RESULT EXCEEDS ALLOCATION
       MOV  (XS),XR          RESTORE BCBLK PTR
       MOV  WC,WA            GET BUFFER LENGTH
       SUB  INSAB,WA         SUBTRACT TO GET SHIFT LENGTH
       ADD  INSLN,WC         ADD LENGTH OF NEW
       SUB  INSSB,WC         SUBTRACT OLD TO GET TOTAL NEW LEN
       MOV  BCLEN(XR),WB     GET OLD BCLEN
       MOV  WC,BCLEN(XR)     STUFF NEW LENGTH
       BZE  WA,INS04         SKIP SHIFT IF NOTHING TO DO
       BEQ  INSSB,INSLN,INS04 SKIP SHIFT IF LENGTHS MATCH
       MOV  BCBUF(XR),XR     POINT TO BFBLK
       MOV  XL,-(XS)         SAVE SCBLK PTR
       BLO  INSSB,INSLN,INS01 BRN IF SHIFT IS FOR MORE ROOM
       EJC
*
*      INSBF (CONTINUED)
*
*      WE ARE SHIFTING THE UPPER SEGMENT DOWN TO COMPACT
*      THE BUFFER.  (THE STRING LENGTH IS SMALLER THAN THE
*      SEGMENT BEING REPLACED.)  REGISTERS ARE SET AS
*
*      (WA)                  MOVE (SHIFT DOWN) LENGTH
*      (WB)                  OLD BCLEN
*      (WC)                  NEW BCLEN
*      (XR)                  BFBLK PTR
*      (XL),(XS)             SCBLK OR BFBLK PTR
*
       MOV  INSSA,WB         GET OFFSET TO INSERT
       ADD  INSLN,WB         ADD INSERT LENGTH TO GET DEST OFF
       MOV  XR,XL            MAKE COPY
       PLC  XL,INSAB         PREPARE SOURCE FOR MOVE
       PSC  XR,WB            PREPARE DESTINATION REG FOR MOVE
       MVC                   MOVE EM OUT
       BRN  INS02            BRANCH TO PAD
*
*      WE ARE SHIFTING THE UPPER SEGMENT UP TO EXPAND
*      THE BUFFER.  (THE STRING LENGTH IS LARGER THAN THE
*      SEGMENT BEING REPLACED.)
*
INS01  MOV  XR,XL            COPY BFBLK PTR
       PLC  XL,WB            SET SOURCE REG FOR MOVE BACKWARDS
       PSC  XR,WC            SET DESTINATION PTR FOR MOVE
       MCB                   MOVE BACKWARDS (POSSIBLE OVERLAP)
*
*      MERGE HERE AFTER MOVE TO ADJUST PADDING AT NEW BUFFER END
*
INS02  MOV  (XS)+,XL         RESTORE SCBLK OR BFBLK PTR
       MOV  WC,WA            COPY NEW BUFFER END
       CTB  WA,0             ROUND OUT
       SUB  WC,WA            SUBTRACT TO GET REMAINDER
       BZE  WA,INS04         NO PAD IF ALREADY EVEN BOUNDARY
       MOV  (XS),XR          GET BCBLK PTR
       MOV  BCBUF(XR),XR     GET BFBLK PTR
       PSC  XR,WC            PREPARE TO PAD
       ZER  WB               CLEAR WB
       LCT  WA,WA            LOAD LOOP COUNT
*
*      LOOP HERE TO STUFF PAD CHARACTERS
*
INS03  SCH  WB,(XR)+         STUFF ZERO PAD
       BCT  WA,INS03         BRANCH FOR MORE
       CSC  XR               COMPLETE STORE CHARACTER
       EJC
*
*      INSBF (CONTINUED)
*
*      MERGE HERE WHEN PADDING OK.  NOW COPY IN THE INSERT
*      STRING TO THE HOLE.
*
INS04  MOV  INSLN,WA         GET INSERT LENGTH
       BZE  WA,INS4B         IF NOTHING TO INSERT
       MOV  (XS),XR          GET BCBLK PTR
       MOV  BCBUF(XR),XR     GET BFBLK PTR
       PLC  XL               PREPARE TO COPY FROM FIRST CHAR
       PSC  XR,INSSA         PREPARE TO STORE IN HOLE
       MVC                   COPY THE CHARACTERS
*
*      CONTINUE HERE AFTER POSSIBLE INSERTION COPY
*
INS4B  MOV  (XS)+,XR         RESTORE ENTRY XR
       MOV  (XS)+,XL         RESTORE ENTRY XL
       MOV  INSSA,WA         RESTORE ENTRY WA
       MOV  INSSB,WB         RESTORE ENTRY WB
       MOV  INSSC,WC         RESTORE ENTRY WC
       EXI                   RETURN TO CALLER
*
*      HERE TO TAKE STRING CONVERT ERROR EXIT
*
INS05  MOV  (XS)+,XR         RESTORE ENTRY XR
       MOV  (XS)+,XL         RESTORE ENTRY XL
       MOV  INSSA,WA         RESTORE ENTRY WA
       MOV  INSSB,WB         RESTORE ENTRY WB
       MOV  INSSC,WC         RESTORE ENTRY WC
       EXI  1                ALTERNATE EXIT
*
*      HERE FOR INVALID OFFSET OR LENGTH
*
INS06  MOV  (XS)+,XR         RESTORE ENTRY XR
       MOV  (XS)+,XL         RESTORE ENTRY XL
*
*      MERGE FOR LENGTH FAILURE EXIT WITH STACK SET
*
INS07  MOV  INSSA,WA         RESTORE ENTRY WA
       MOV  INSSB,WB         RESTORE ENTRY WB
       MOV  INSSC,WC         RESTORE ENTRY WC
       EXI  2                ALTERNATE EXIT
*
*      HERE IF INSERTING THE SAME BUFFER INTO ITSELF.  HAVE
*      TO CONVERT THE INSERTED BUFFER TO AN INTERMEDIATE
*      STRING TO PREVENT GARBLED DATA.
*
INS08  JSR  GTSTG            CALL TO GET STRING
       PPM  INS05            TAKE STRING CONVERT ERR EXIT
       BRN  INS09            MERGE BACK TO PERFORM INSERTION
       ENP                   END PROCEDURE INSBF
       EJC
.FI
*
*      INSTA - USED TO INITIALIZE STRUCTURES IN STATIC REGION
*
*      (XR)                  POINTER TO STARTING STATIC LOCATION
*      JSR  INSTA            CALL TO INITIALIZE STATIC STRUCTURE
*      (XR)                  PTR TO NEXT FREE STATIC LOCATION
*      (WA,WB,WC)            DESTROYED
*
*      NOTE THAT THIS PROCEDURE ESTABLISHES THE POINTERS
*      PRBUF, GTSWK, AND KVALP.
*
INSTA  PRC  E,0              ENTRY POINT
*
*      INITIALIZE PRINT BUFFER WITH BLANK WORDS
*
*Z-
       MOV  PRLEN,WC         NO. OF CHARS IN PRINT BFR
       MOV  XR,PRBUF         PRINT BFR IS PUT AT STATIC START
       MOV  =B_SCL,(XR)+     STORE STRING TYPE CODE
       MOV  WC,(XR)+         AND STRING LENGTH
       CTW  WC,0             GET NUMBER OF WORDS IN BUFFER
       MOV  WC,PRLNW         STORE FOR BUFFER CLEAR
       LCT  WC,WC            WORDS TO CLEAR
*
*      LOOP TO CLEAR BUFFER
*
INST1  MOV  NULLW,(XR)+      STORE BLANK
       BCT  WC,INST1         LOOP
*
*      ALLOCATE WORK AREA FOR GTSTG CONVERSION PROCEDURE
*
       MOV  =NSTMX,WA        GET MAX NUM CHARS IN OUTPUT NUMBER
       CTB  WA,SCSI_         NO OF BYTES NEEDED
       MOV  XR,GTSWK         STORE BFR ADRS
       ADD  WA,XR            BUMP FOR WORK BFR
*
*      BUILD ALPHABET STRING FOR ALPHABET KEYWORD AND REPLACE
*
       MOV  XR,KVALP         SAVE ALPHABET POINTER
       MOV  =B_SCL,(XR)      STRING BLK TYPE
       MOV  =CFP_A,WC        NO OF CHARS IN ALPHABET
       MOV  WC,SCLEN(XR)     STORE AS STRING LENGTH
       MOV  WC,WB            COPY CHAR COUNT
       CTB  WB,SCSI_         NO. OF BYTES NEEDED
       ADD  XR,WB            CURRENT END ADDRESS FOR STATIC
       MOV  WB,WA            SAVE ADRS PAST ALPHABET STRING
       LCT  WC,WC            LOOP COUNTER
       PSC  XR               POINT TO CHARS OF STRING
       ZER  WB               SET INITIAL CHARACTER VALUE
*
*      LOOP TO ENTER CHARACTER CODES IN ORDER
*
INST2  SCH  WB,(XR)+         STORE NEXT CODE
       ICV  WB               BUMP CODE VALUE
       BCT  WC,INST2         LOOP TILL ALL STORED
       CSC  XR               COMPLETE STORE CHARACTERS
       MOV  WA,XR            RETURN CURRENT STATIC PTR
*Z+
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE INSTA
       EJC
*
*      IOFCB -- GET INPUT/OUTPUT FCBLK POINTER
*
*      USED BY ENDFILE, EJECT AND REWIND TO FIND THE FCBLK
*      (IF ANY) CORRESPONDING TO THEIR ARGUMENT.
*
*      -(XS)                 ARGUMENT
*      JSR  IOFCB            CALL TO FIND FCBLK
*      PPM  LOC              ARG IS AN UNSUITABLE NAME
*      PPM  LOC              ARG IS NULL STRING
*      PPM  LOC              ARG FILE NOT FOUND
*      (XS)                  POPPED
*      (XL)                  PTR TO FILEARG1 VRBLK
*      (XR)                  ARGUMENT
*      (WA)                  FCBLK PTR OR 0
*      (WB,WC)               DESTROYED
*
IOFCB  PRC  N,3              ENTRY POINT
       JSR  GTSTG            GET ARG AS STRING
       PPM  IOFC2            FAIL
       MOV  XR,XL            COPY STRING PTR
       JSR  GTNVR            GET AS NATURAL VARIABLE
       PPM  IOFC3            FAIL IF NULL
       MOV  XL,WB            COPY STRING POINTER AGAIN
       MOV  XR,XL            COPY VRBLK PTR FOR RETURN
       ZER  WA               IN CASE NO TRBLK FOUND
*
*      LOOP TO FIND FILE ARG1 TRBLK
*
IOFC1  MOV  VRVAL(XR),XR     GET POSSIBLE TRBLK PTR
       BNE  (XR),=B_TRT,IOFC4 FAIL IF END OF CHAIN
       BNE  TRTYP(XR),=TRTFC,IOFC1 LOOP IF NOT FILE ARG TRBLK
       MOV  TRFPT(XR),WA     GET FCBLK PTR
       MOV  WB,XR            COPY ARG
       EXI                   RETURN
*
*      FAIL RETURN
*
IOFC2  EXI  1                FAIL
*
*      NULL ARG
*
IOFC3  EXI  2                NULL ARG RETURN
*
*      FILE NOT FOUND
*
IOFC4  EXI  3                FILE NOT FOUND RETURN
       ENP                   END PROCEDURE IOFCB
       EJC
*
*      IOPPF -- PROCESS FILEARG2 FOR IOPUT
*
*      (R_XSC)               FILEARG2 PTR
*      JSR  IOPPF            CALL TO PROCESS FILEARG2
*      (XL)                  FILEARG1 PTR
*      (XR)                  FILE ARG2 PTR
*      -(XS)...-(XS)         FIELDS EXTRACTED FROM FILEARG2
*      (WC)                  NO. OF FIELDS EXTRACTED
*      (WB)                  INPUT/OUTPUT FLAG
*      (WA)                  FCBLK PTR OR 0
*
IOPPF  PRC  N,0              ENTRY POINT
       ZER  WB               TO COUNT FIELDS EXTRACTED
*
*      LOOP TO EXTRACT FIELDS
*
IOPP1  MOV  =IODEL,XL        GET DELIMITER
       MOV  XL,WC            COPY IT
       ZER  WA               RETAIN LEADING BLANKS IN FILEARG2
       JSR  XSCAN            GET NEXT FIELD
       MOV  XR,-(XS)         STACK IT
       ICV  WB               INCREMENT COUNT
       BNZ  WA,IOPP1         LOOP
       MOV  WB,WC            COUNT OF FIELDS
       MOV  IOPTT,WB         I/O MARKER
       MOV  R_IOF,WA         FCBLK PTR OR 0
       MOV  R_IO2,XR         FILE ARG2 PTR
       MOV  R_IO1,XL         FILEARG1
       EXI                   RETURN
       ENP                   END PROCEDURE IOPPF
       EJC
*
*      IOPUT -- ROUTINE USED BY INPUT AND OUTPUT
*
*      IOPUT SETS UP INPUT/OUTPUT  ASSOCIATIONS. IT BUILDS
*      SUCH TRACE AND FILE CONTROL BLOCKS AS ARE NECESSARY AND
*      CALLS SYSFC,SYSIO TO PERFORM CHECKS ON THE
*      ARGUMENTS AND TO OPEN THE FILES.
*
*         +-----------+   +---------------+       +-----------+
*      +-.I           I   I               I------.I   =B_XRT  I
*      I  +-----------+   +---------------+       +-----------+
*      I  /           /        (R_FCB)            I    *4     I
*      I  /           /                           +-----------+
*      I  +-----------+   +---------------+       I           I-
*      I  I   NAME    +--.I    =B_TRT     I       +-----------+
*      I  /           /   +---------------+       I           I
*      I   (FIRST ARG)    I =TRTIN/=TRTOU I       +-----------+
*      I                  +---------------+             I
*      I                  I     VALUE     I             I
*      I                  +---------------+             I
*      I                  I(TRTRF) 0   OR I--+          I
*      I                  +---------------+  I          I
*      I                  I(TRFPT) 0   OR I----+        I
*      I                  +---------------+  I I        I
*      I                     (I/O TRBLK)     I I        I
*      I  +-----------+                      I I        I
*      I  I           I                      I I        I
*      I  +-----------+                      I I        I
*      I  I           I                      I I        I
*      I  +-----------+   +---------------+  I I        I
*      I  I           +--.I    =B_TRT     I.-+ I        I
*      I  +-----------+   +---------------+    I        I
*      I  /           /   I    =TRTFC     I    I        I
*      I  /           /   +---------------+    I        I
*      I    (FILEARG1     I     VALUE     I    I        I
*      I         VRBLK)   +---------------+    I        I
*      I                  I(TRTRF) 0   OR I--+ I        .
*      I                  +---------------+  I .  +-----------+
*      I                  I(TRFPT) 0   OR I------./   FCBLK   /
*      I                  +---------------+  I    +-----------+
*      I                       (TRTRF)       I
*      I                                     I
*      I                                     I
*      I                  +---------------+  I
*      I                  I    =B_XRT     I.-+
*      I                  +---------------+
*      I                  I      *5       I
*      I                  +---------------+
*      +------------------I               I
*                         +---------------+       +-----------+
*                         I(TRTRF) O   OR I------.I  =B_XRT   I
*                         +---------------+       +-----------+
*                         I  NAME OFFSET  I       I    ETC    I
*                         +---------------+
*                           (IOCHN - CHAIN OF NAME POINTERS)
       EJC
*
*      IOPUT (CONTINUED)
*
*      NO ADDITIONAL TRAP BLOCKS ARE USED FOR STANDARD INPUT/OUT
*      FILES. OTHERWISE AN I/O TRAP BLOCK IS ATTACHED TO SECOND
*      ARG (FILEARG1) VRBLK. SEE DIAGRAM ABOVE FOR DETAILS OF
*      THE STRUCTURE BUILT.
*
*      -(XS)                 1ST ARG (VBL TO BE ASSOCIATED)
*      -(XS)                 2ND ARG (FILE ARG1)
*      -(XS)                 3RD ARG (FILE ARG2)
*      (WB)                  0 FOR INPUT, 3 FOR OUTPUT ASSOC.
*      JSR  IOPUT            CALL FOR INPUT/OUTPUT ASSOCIATION
*      PPM  LOC              3RD ARG NOT A STRING
*      PPM  LOC              2ND ARG NOT A SUITABLE NAME
*      PPM  LOC              1ST ARG NOT A SUITABLE NAME
*      PPM  LOC              INAPPROPRIATE FILE SPEC FOR I/O
*      PPM  LOC              I/O FILE DOES NOT EXIST
*      PPM  LOC              I/O FILE CANNOT BE READ/WRITTEN
*      PPM  LOC              I/O FCBLK CURRENTLY IN USE
*      (XS)                  POPPED
*      (XL,XR,WA,WB,WC)      DESTROYED
*
IOPUT  PRC  N,7              ENTRY POINT
       ZER  R_IOT            IN CASE NO TRTRF BLOCK USED
       ZER  R_IOF            IN CASE NO FCBLK ALOCATED
       ZER  R_IOP            IN CASE SYSIO FAILS
       MOV  WB,IOPTT         STORE I/O TRACE TYPE
       JSR  XSCNI            PREPARE TO SCAN FILEARG2
       PPM  IOP13            FAIL
       PPM  IOPA0            NULL FILE ARG2
*
IOPA0  MOV  XR,R_IO2         KEEP FILE ARG2
       MOV  WA,XL            COPY LENGTH
       JSR  GTSTG            CONVERT FILEARG1 TO STRING
       PPM  IOP14            FAIL
       MOV  XR,R_IO1         KEEP FILEARG1 PTR
       JSR  GTNVR            CONVERT TO NATURAL VARIABLE
       PPM  IOP00            JUMP IF NULL
       BRN  IOP04            JUMP TO PROCESS NON-NULL ARGS
*
*      NULL FILEARG1
*
IOP00  BZE  XL,IOP01         SKIP IF BOTH ARGS NULL
       JSR  IOPPF            PROCESS FILEARG2
       JSR  SYSFC            CALL FOR FILEARG2 CHECK
       PPM  IOP16            FAIL
       PPM  IOP26            FAIL
       BRN  IOP11            COMPLETE FILE ASSOCIATION
       EJC
*
*      IOPUT (CONTINUED)
*
*      HERE WITH 0 OR FCBLK PTR IN (XL)
*
IOP01  MOV  IOPTT,WB         GET TRACE TYPE
       MOV  R_IOT,XR         GET 0 OR TRTRF PTR
       JSR  TRBLD            BUILD TRBLK
       MOV  XR,WC            COPY TRBLK POINTER
       MOV  (XS)+,XR         GET VARIABLE FROM STACK
       MOV  WC,-(XS)         MAKE TRBLK COLLECTABLE
       JSR  GTVAR            POINT TO VARIABLE
       PPM  IOP15            FAIL
       MOV  (XS)+,WC         RECOVER TRBLK POINTER
       MOV  XL,R_ION         SAVE NAME POINTER
       MOV  XL,XR            COPY NAME POINTER
       ADD  WA,XR            POINT TO VARIABLE
       SUB  *VRVAL,XR        SUBTRACT OFFSET,MERGE INTO LOOP
*
*      LOOP TO END OF TRBLK CHAIN IF ANY
*
IOP02  MOV  XR,XL            COPY BLK PTR
       MOV  VRVAL(XR),XR     LOAD PTR TO NEXT TRBLK
       BNE  (XR),=B_TRT,IOP03    JUMP IF NOT TRAPPED
       BNE  TRTYP(XR),IOPTT,IOP02 LOOP IF NOT SAME ASSOCN
       MOV  TRNXT(XR),XR     GET VALUE AND DELETE OLD TRBLK
*
*      IOPUT (CONTINUED)
*
*      STORE NEW ASSOCIATION
*
IOP03  MOV  WC,VRVAL(XL)     LINK TO THIS TRBLK
       MOV  WC,XL            COPY POINTER
       MOV  XR,TRNXT(XL)     STORE VALUE IN TRBLK
       MOV  R_ION,XR         RESTORE POSSIBLE VRBLK POINTER
       MOV  WA,WB            KEEP OFFSET TO NAME
       JSR  SETVR            IF VRBLK, SET VRGET,VRSTO
       MOV  R_IOT,XR         GET 0 OR TRTRF PTR
       BNZ  XR,IOP19         JUMP IF TRTRF BLOCK EXISTS
       EXI                   RETURN TO CALLER
*
*      NON STANDARD FILE
*      SEE IF AN FCBLK HAS ALREADY BEEN ALLOCATED.
*
IOP04  ZER  WA               IN CASE NO FCBLK FOUND
       EJC
*
*      IOPUT (CONTINUED)
*
*      SEARCH POSSIBLE TRBLK CHAIN TO PICK UP THE FCBLK
*
IOP05  MOV  XR,WB            REMEMBER BLK PTR
       MOV  VRVAL(XR),XR     CHAIN ALONG
       BNE  (XR),=B_TRT,IOP06 JUMP IF END OF TRBLK CHAIN
       BNE  TRTYP(XR),=TRTFC,IOP05 LOOP IF MORE TO GO
       MOV  XR,R_IOT         POINT TO FILE ARG1 TRBLK
       MOV  TRFPT(XR),WA     GET FCBLK PTR FROM TRBLK
*
*      WA = 0 OR FCBLK PTR
*      WB = PTR TO PRECEDING BLK TO WHICH ANY TRTRF BLOCK
*           FOR FILE ARG1 MUST BE CHAINED.
*
IOP06  MOV  WA,R_IOF         KEEP POSSIBLE FCBLK PTR
       MOV  WB,R_IOP         KEEP PRECEDING BLK PTR
       JSR  IOPPF            PROCESS FILEARG2
       JSR  SYSFC            SEE IF FCBLK REQUIRED
       PPM  IOP16            FAIL
       PPM  IOP26            FAIL
       BZE  WA,IOP12         SKIP IF NO NEW FCBLK WANTED
       BLT  WC,=NUM02,IOP6A  JUMP IF FCBLK IN DYNAMIC
       JSR  ALOST            GET IT IN STATIC
       BRN  IOP6B            SKIP
*
*      OBTAIN FCBLK IN DYNAMIC
*
IOP6A  JSR  ALLOC            GET SPACE FOR FCBLK
*
*      MERGE
*
IOP6B  MOV  XR,XL            POINT TO FCBLK
       MOV  WA,WB            COPY ITS LENGTH
       BTW  WB               GET COUNT AS WORDS (SGD APR80)
       LCT  WB,WB            LOOP COUNTER
*
*      CLEAR FCBLK
*
IOP07  ZER  (XR)+            CLEAR A WORD
       BCT  WB,IOP07         LOOP
       BEQ  WC,=NUM02,IOP09  SKIP IF IN STATIC - DONT SET FIELDS
       MOV  =B_XNT,(XL)      STORE XNBLK CODE IN CASE
       MOV  WA,NUM01(XL)     STORE LENGTH
       BNZ  WC,IOP09         JUMP IF XNBLK WANTED
       MOV  =B_XRT,(XL)      XRBLK CODE REQUESTED
*
       EJC
*      IOPUT (CONTINUED)
*
*      COMPLETE FCBLK INITIALISATION
*
IOP09  MOV  R_IOT,XR         GET POSSIBLE TRBLK PTR
       MOV  XL,R_IOF         STORE FCBLK PTR
       BNZ  XR,IOP10         JUMP IF TRBLK ALREADY FOUND
*
*      A NEW TRBLK IS NEEDED
*
       MOV  =TRTFC,WB        TRTYP FOR FCBLK TRAP BLK
       JSR  TRBLD            MAKE THE BLOCK
       MOV  XR,R_IOT         COPY TRTRF PTR
       MOV  R_IOP,XL         POINT TO PRECEDING BLK
       MOV  VRVAL(XL),VRVAL(XR) COPY VALUE FIELD TO TRBLK
       MOV  XR,VRVAL(XL)     LINK NEW TRBLK INTO CHAIN
       MOV  XL,XR            POINT TO PREDECESSOR BLK
       JSR  SETVR            SET TRACE INTERCEPTS
       MOV  VRVAL(XR),XR     RECOVER TRBLK PTR
       BRN  IOP1A            STORE FCBLK PTR
*
*      HERE IF EXISTING TRBLK
*
IOP10  ZER  R_IOP            DO NOT RELEASE IF SYSIO FAILS
*
*      XR IS PTR TO TRBLK, XL IS FCBLK PTR OR 0
*
IOP1A  MOV  R_IOF,TRFPT(XR)  STORE FCBLK PTR
*
*      CALL SYSIO TO COMPLETE FILE ACCESSING
*
IOP11  MOV  R_IOF,WA         COPY FCBLK PTR OR 0
       MOV  IOPTT,WB         GET INPUT/OUTPUT FLAG
       MOV  R_IO2,XR         GET FILE ARG2
       MOV  R_IO1,XL         GET FILE ARG1
       JSR  SYSIO            ASSOCIATE TO THE FILE
       PPM  IOP17            FAIL
       PPM  IOP18            FAIL
       BNZ  R_IOT,IOP01      NOT STD INPUT IF NON-NULL TRTRF BLK
       BNZ  IOPTT,IOP01      JUMP IF OUTPUT
       BZE  WC,IOP01         NO CHANGE TO STANDARD READ LENGTH
       MOV  WC,CSWIN         STORE NEW READ LENGTH FOR STD FILE
       BRN  IOP01            MERGE TO FINISH THE TASK
*
*      SYSFC MAY HAVE RETURNED A POINTER TO A PRIVATE FCBLK
*
IOP12  BNZ  XL,IOP09         JUMP IF PRIVATE FCBLK
       BRN  IOP11            FINISH THE ASSOCIATION
*
*      FAILURE RETURNS
*
IOP13  EXI  1                3RD ARG NOT A STRING
IOP14  EXI  2                2ND ARG UNSUITABLE
IOP15  ICA  XS               DISCARD TRBLK POINTER
       EXI  3                1ST ARG UNSUITABLE
IOP16  EXI  4                FILE SPEC WRONG
IOP26  EXI  7                FCBLK IN USE
*
*      I/O FILE DOES NOT EXIST
*
IOP17  MOV  R_IOP,XR         IS THERE A TRBLK TO RELEASE
       BZE  XR,IOPA7         IF NOT
       MOV  VRVAL(XR),XL     POINT TO TRBLK
       MOV  VRVAL(XL),VRVAL(XR) UNSPLICE IT
       JSR  SETVR            ADJUST TRACE INTERCEPTS
IOPA7  EXI  5                I/O FILE DOES NOT EXIST
*
*      I/O FILE CANNOT BE READ/WRITTEN
*
IOP18  MOV  R_IOP,XR         IS THERE A TRBLK TO RELEASE
       BZE  XR,IOPA7         IF NOT
       MOV  VRVAL(XR),XL     POINT TO TRBLK
       MOV  VRVAL(XL),VRVAL(XR) UNSPLICE IT
       JSR  SETVR            ADJUST TRACE INTERCEPTS
IOPA8  EXI  6                I/O FILE CANNOT BE READ/WRITTEN
       EJC
*
*      IOPUT (CONTINUED)
*
*      ADD TO IOCHN CHAIN OF ASSOCIATED VARIABLES UNLESS
*      ALREADY PRESENT.
*
IOP19  MOV  R_ION,WC         WC = NAME BASE, WB = NAME OFFSET
*
*      SEARCH LOOP
*
IOP20  MOV  TRTRF(XR),XR     NEXT LINK OF CHAIN
       BZE  XR,IOP21         NOT FOUND
       BNE  WC,IONMB(XR),IOP20 NO MATCH
       BEQ  WB,IONMO(XR),IOP22 EXIT IF MATCHED
       BRN  IOP20            LOOP
*
*      NOT FOUND
*
IOP21  MOV  *NUM05,WA        SPACE NEEDED
       JSR  ALLOC            GET IT
       MOV  =B_XRT,(XR)      STORE XRBLK CODE
       MOV  WA,NUM01(XR)     STORE LENGTH
       MOV  WC,IONMB(XR)     STORE NAME BASE
       MOV  WB,IONMO(XR)     STORE NAME OFFSET
       MOV  R_IOT,XL         POINT TO TRTRF BLK
       MOV  TRTRF(XL),WA     GET PTR FIELD CONTENTS
       MOV  XR,TRTRF(XL)     STORE PTR TO NEW BLOCK
       MOV  WA,TRTRF(XR)     COMPLETE THE LINKING
*
*      INSERT FCBLK ON FCBLK CHAIN FOR SYSEJ, SYSXI
*
IOP22  BZE  R_IOF,IOP25      SKIP IF NO FCBLK
       MOV  R_FCB,XL         PTR TO HEAD OF EXISTING CHAIN
*
*      SEE IF FCBLK ALREADY ON CHAIN
*
IOP23  BZE  XL,IOP24         NOT ON IF END OF CHAIN
       BEQ  NUM03(XL),R_IOF,IOP25 DONT DUPLICATE IF FIND IT
       MOV  NUM02(XL),XL     GET NEXT LINK
       BRN  IOP23            LOOP
*
*      NOT FOUND SO ADD AN ENTRY FOR THIS FCBLK
*
IOP24  MOV  *NUM04,WA        SPACE NEEDED
       JSR  ALLOC            GET IT
       MOV  =B_XRT,(XR)      STORE BLOCK CODE
       MOV  WA,NUM01(XR)     STORE LENGTH
       MOV  R_FCB,NUM02(XR)  STORE PREVIOUS LINK IN THIS NODE
       MOV  R_IOF,NUM03(XR)  STORE FCBLK PTR
       MOV  XR,R_FCB         INSERT NODE INTO FCBLK CHAIN
*
*      RETURN
*
IOP25  EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE IOPUT
       EJC
*
*      KTREX -- EXECUTE KEYWORD TRACE
*
*      KTREX IS USED TO EXECUTE A POSSIBLE KEYWORD TRACE. IT
*      INCLUDES THE TEST ON TRACE AND TESTS FOR TRACE ACTIVE.
*
*      (XL)                  PTR TO TRBLK (OR 0 IF UNTRACED)
*      JSR  KTREX            CALL TO EXECUTE KEYWORD TRACE
*      (XL,WA,WB,WC)         DESTROYED
*      (RA)                  DESTROYED
*
KTREX  PRC  R,0              ENTRY POINT (RECURSIVE)
       BZE  XL,KTRX3         IMMEDIATE EXIT IF KEYWORD UNTRACED
       BZE  KVTRA,KTRX3      IMMEDIATE EXIT IF TRACE = 0
       DCV  KVTRA            ELSE DECREMENT TRACE
       MOV  XR,-(XS)         SAVE XR
       MOV  XL,XR            COPY TRBLK POINTER
       MOV  TRKVR(XR),XL     LOAD VRBLK POINTER (NMBAS)
       MOV  *VRVAL,WA        SET NAME OFFSET
       BZE  TRFNC(XR),KTRX1  JUMP IF PRINT TRACE
       JSR  TRXEQ            ELSE EXECUTE FULL TRACE
       BRN  KTRX2            AND JUMP TO EXIT
*
*      HERE FOR PRINT TRACE
*
KTRX1  MOV  XL,-(XS)         STACK VRBLK PTR FOR KWNAM
       MOV  WA,-(XS)         STACK OFFSET FOR KWNAM
       JSR  PRTSN            PRINT STATEMENT NUMBER
       MOV  =CH_AM,WA        LOAD AMPERSAND
       JSR  PRTCH            PRINT AMPERSAND
       JSR  PRTNM            PRINT KEYWORD NAME
       MOV  =TMBEB,XR        POINT TO BLANK-EQUAL-BLANK
       JSR  PRTST            PRINT BLANK-EQUAL-BLANK
       JSR  KWNAM            GET KEYWORD PSEUDO-VARIABLE NAME
       MOV  XR,DNAMP         RESET PTR TO DELETE KVBLK
       JSR  ACESS            GET KEYWORD VALUE
       PPM                   FAILURE IS IMPOSSIBLE
       JSR  PRTVL            PRINT KEYWORD VALUE
       JSR  PRTNL            TERMINATE PRINT LINE
*
*      HERE TO EXIT AFTER COMPLETING TRACE
*
KTRX2  MOV  (XS)+,XR         RESTORE ENTRY XR
*
*      MERGE HERE TO EXIT IF NO TRACE REQUIRED
*
KTRX3  EXI                   RETURN TO KTREX CALLER
       ENP                   END PROCEDURE KTREX
       EJC
*
*      KWNAM -- GET PSEUDO-VARIABLE NAME FOR KEYWORD
*
*      1(XS)                 NAME BASE FOR VRBLK
*      0(XS)                 OFFSET (SHOULD BE *VRVAL)
*      JSR  KWNAM            CALL TO GET PSEUDO-VARIABLE NAME
*      (XS)                  POPPED TWICE
*      (XL,WA)               RESULTING PSEUDO-VARIABLE NAME
*      (XR,WA,WB)            DESTROYED
*
KWNAM  PRC  N,0              ENTRY POINT
       ICA  XS               IGNORE NAME OFFSET
       MOV  (XS)+,XR         LOAD NAME BASE
       BGE  XR,STATE,KWNM1   JUMP IF NOT NATURAL VARIABLE NAME
       BNZ  VRLEN(XR),KWNM1  ERROR IF NOT SYSTEM VARIABLE
       MOV  VRSVP(XR),XR     ELSE POINT TO SVBLK
       MOV  SVBIT(XR),WA     LOAD BIT MASK
       ANB  BTKNM,WA         AND WITH KEYWORD BIT
       ZRB  WA,KWNM1         ERROR IF NO KEYWORD ASSOCIATION
       MOV  SVLEN(XR),WA     ELSE LOAD NAME LENGTH IN CHARACTERS
       CTB  WA,SVCHS         COMPUTE OFFSET TO FIELD WE WANT
       ADD  WA,XR            POINT TO SVKNM FIELD
       MOV  (XR),WB          LOAD SVKNM VALUE
       MOV  *KVSI_,WA        SET SIZE OF KVBLK
       JSR  ALLOC            ALLOCATE KVBLK
       MOV  =B_KVT,(XR)      STORE TYPE WORD
       MOV  WB,KVNUM(XR)     STORE KEYWORD NUMBER
       MOV  =TRBKV,KVVAR(XR) SET DUMMY TRBLK POINTER
       MOV  XR,XL            COPY KVBLK POINTER
       MOV  *KVVAR,WA        SET PROPER OFFSET
       EXI                   RETURN TO KVNAM CALLER
*
*      HERE IF NOT KEYWORD NAME
*
KWNM1  ERB  251,Keyword operand is not name of defined keyword
       ENP                   END PROCEDURE KWNAM
       EJC
*
*      LCOMP-- COMPARE TWO STRINGS LEXICALLY
*
*      1(XS)                 FIRST ARGUMENT
*      0(XS)                 SECOND ARGUMENT
*      JSR  LCOMP            CALL TO COMPARE ARUMENTS
*      PPM  LOC              TRANSFER LOC FOR ARG1 NOT STRING
*      PPM  LOC              TRANSFER LOC FOR ARG2 NOT STRING
*      PPM  LOC              TRANSFER LOC IF ARG1 LLT ARG2
*      PPM  LOC              TRANSFER LOC IF ARG1 LEQ ARG2
*      PPM  LOC              TRANSFER LOC IF ARG1 LGT ARG2
*      (THE NORMAL RETURN IS NEVER TAKEN)
*      (XS)                  POPPED TWICE
*      (XR,XL)               DESTROYED
*      (WA,WB,WC,RA)         DESTROYED
*
LCOMP  PRC  N,5              ENTRY POINT
.IF    .CNBF
       JSR  GTSTG            CONVERT SECOND ARG TO STRING
.ELSE
       JSR  GTSTB            GET SECOND ARG AS STRING OR BUFFER
.FI
       PPM  LCMP6            JUMP IF SECOND ARG NOT STRING
       MOV  XR,XL            ELSE SAVE POINTER
       MOV  WA,WC            AND LENGTH
.IF    .CNBF
       JSR  GTSTG            CONVERT FIRST ARGUMENT TO STRING
.ELSE
       JSR  GTSTB            GET FIRST ARG AS STRING OR BUFFER
.FI
       PPM  LCMP5            JUMP IF NOT STRING
       MOV  WA,WB            SAVE ARG 1 LENGTH
       PLC  XR               POINT TO CHARS OF ARG 1
       PLC  XL               POINT TO CHARS OF ARG 2
.IF    .CCMC
       MOV  WC,WA            ARG 2 LENGTH TO WA
       JSR  SYSCM            COMPARE (XL,WA=ARG2  XR,WB=ARG1)
       ERR  283,String length exceeded for generalized lexical comparison
       PPM  LCMP4            ARG 2 LT ARG 1, LGT EXIT
       PPM  LCMP3            ARG 2 GT ARG 1, LLT EXIT
       EXI  4                ELSE IDENTICAL STRINGS, LEQ EXIT
       EJC
*
*      LCOMP (CONTINUED)
.ELSE
       BLO  WA,WC,LCMP1      JUMP IF ARG 1 LENGTH IS SMALLER
       MOV  WC,WA            ELSE SET ARG 2 LENGTH AS SMALLER
*
*      HERE WITH SMALLER LENGTH IN (WA)
*
LCMP1  BZE  WA,LCMP7         IF NULL STRING, COMPARE LENGTHS
       CMC  LCMP4,LCMP3      COMPARE STRINGS, JUMP IF UNEQUAL
LCMP7  BNE  WB,WC,LCMP2      IF EQUAL, JUMP IF LENGTHS UNEQUAL
       EXI  4                ELSE IDENTICAL STRINGS, LEQ EXIT
       EJC
*
*      LCOMP (CONTINUED)
*
*      HERE IF INITIAL STRINGS IDENTICAL, BUT LENGTHS UNEQUAL
*
LCMP2  BHI  WB,WC,LCMP4      JUMP IF ARG 1 LENGTH GT ARG 2 LENG
.FI
*
*      HERE IF FIRST ARG LLT SECOND ARG
*
LCMP3  EXI  3                TAKE LLT EXIT
*
*      HERE IF FIRST ARG LGT SECOND ARG
*
LCMP4  EXI  5                TAKE LGT EXIT
*
*      HERE IF FIRST ARG IS NOT A STRING
*
LCMP5  EXI  1                TAKE BAD FIRST ARG EXIT
*
*      HERE FOR SECOND ARG NOT A STRING
*
LCMP6  EXI  2                TAKE BAD SECOND ARG ERROR EXIT
       ENP                   END PROCEDURE LCOMP
       EJC
*
*      LISTR -- LIST SOURCE LINE
*
*      LISTR IS USED TO LIST A SOURCE LINE DURING THE INITIAL
*      COMPILATION. IT IS CALLED FROM SCANE AND SCANL.
*
*      JSR  LISTR            CALL TO LIST LINE
*      (XR,XL,WA,WB,WC)      DESTROYED
*
*      GLOBAL LOCATIONS USED BY LISTR
*
*      CNTTL                 FLAG FOR -TITLE, -STITL
*
*      ERLST                 IF LISTING ON ACCOUNT OF AN ERROR
*
.IF    .CINC
*      LSTID                 INCLUDE DEPTH OF CURRENT IMAGE
*
.FI
*      LSTLC                 COUNT LINES ON CURRENT PAGE
*
*      LSTNP                 MAX NUMBER OF LINES/PAGE
*
*      LSTPF                 SET NON-ZERO IF THE CURRENT SOURCE
*                            LINE HAS BEEN LISTED, ELSE ZERO.
*
*      LSTPG                 COMPILER LISTING PAGE NUMBER
*
*      LSTSN                 SET IF STMNT NUM TO BE LISTED
*
*      R_CIM                 POINTER TO CURRENT INPUT LINE.
*
*      R_TTL                 TITLE FOR SOURCE LISTING
*
*      R_STL                 PTR TO SUB-TITLE STRING
*
*      ENTRY POINT
*
LISTR  PRC  E,0              ENTRY POINT
       BNZ  CNTTL,LIST5      JUMP IF -TITLE OR -STITL
       BNZ  LSTPF,LIST4      IMMEDIATE EXIT IF ALREADY LISTED
       BGE  LSTLC,LSTNP,LIST6 JUMP IF NO ROOM
*
*      HERE AFTER PRINTING TITLE (IF NEEDED)
*
LIST0  MOV  R_CIM,XR         LOAD POINTER TO CURRENT IMAGE
       BZE  XR,LIST4         JUMP IF NO IMAGE TO PRINT
       PLC  XR               POINT TO CHARACTERS
       LCH  WA,(XR)          LOAD FIRST CHARACTER
       MOV  LSTSN,XR         LOAD STATEMENT NUMBER
       BZE  XR,LIST2         JUMP IF NO STATEMENT NUMBER
       MTI  XR               ELSE GET STMNT NUMBER AS INTEGER
       BNE  STAGE,=STGIC,LIST1 SKIP IF EXECUTE TIME
       BEQ  WA,=CH_AS,LIST2  NO STMNT NUMBER LIST IF COMMENT
       BEQ  WA,=CH_MN,LIST2  NO STMNT NO. IF CONTROL CARD
*
*      PRINT STATEMENT NUMBER
*
LIST1  JSR  PRTIN            ELSE PRINT STATEMENT NUMBER
       ZER  LSTSN            AND CLEAR FOR NEXT TIME IN
.IF    .CINC
*
*      HERE TO TEST FOR PRINTING INCLUDE DEPTH
*
LIST2  MOV  LSTID,XR         INCLUDE DEPTH OF IMAGE
       BZE  XR,LIST8         IF NOT FROM AN INCLUDE FILE
       MOV  =STNPD,WA        POSITION FOR START OF STATEMENT
       SUB  =NUM03,WA        POSITION TO PLACE INCLUDE DEPTH
       MOV  WA,PROFS         SET AS STARTING POSITION
       MTI  XR               INCLUDE DEPTH AS INTEGER
       JSR  PRTIN            PRINT INCLUDE DEPTH
       EJC
*
*      LISTR (CONTINUED)
*
*      HERE AFTER PRINTING STATEMENT NUMBER AND INCLUDE DEPTH
*
LIST8  MOV  =STNPD,PROFS     POINT PAST STATEMENT NUMBER
.ELSE
       EJC
*
*      LISTR (CONTINUED)
*
*      MERGE HERE AFTER PRINTING STATEMENT NUMBER (IF REQUIRED)
*
LIST2  MOV  =STNPD,PROFS     POINT PAST STATEMENT NUMBER
.FI
       MOV  R_CIM,XR         LOAD POINTER TO CURRENT IMAGE
       JSR  PRTST            PRINT IT
       ICV  LSTLC            BUMP LINE COUNTER
       BNZ  ERLST,LIST3      JUMP IF ERROR COPY TO INT.CH.
       JSR  PRTNL            TERMINATE LINE
       BZE  CSWDB,LIST3      JUMP IF -SINGLE MODE
       JSR  PRTNL            ELSE ADD A BLANK LINE
       ICV  LSTLC            AND BUMP LINE COUNTER
*
*      HERE AFTER PRINTING SOURCE IMAGE
*
LIST3  MNZ  LSTPF            SET FLAG FOR LINE PRINTED
*
*      MERGE HERE TO EXIT
*
LIST4  EXI                   RETURN TO LISTR CALLER
*
*      PRINT TITLE AFTER -TITLE OR -STITL CARD
*
LIST5  ZER  CNTTL            CLEAR FLAG
*
*      EJECT TO NEW PAGE AND LIST TITLE
*
LIST6  JSR  PRTPS            EJECT
       BZE  PRICH,LIST7      SKIP IF LISTING TO REGULAR PRINTER
       BEQ  R_TTL,=NULLS,LIST0 TERMINAL LISTING OMITS NULL TITLE
*
*      LIST TITLE
*
LIST7  JSR  LISTT            LIST TITLE
       BRN  LIST0            MERGE
       ENP                   END PROCEDURE LISTR
       EJC
*
*      LISTT -- LIST TITLE AND SUBTITLE
*
*      USED DURING COMPILATION TO PRINT PAGE HEADING
*
*      JSR  LISTT            CALL TO LIST TITLE
*      (XR,WA)               DESTROYED
*
LISTT  PRC  E,0              ENTRY POINT
       MOV  R_TTL,XR         POINT TO SOURCE LISTING TITLE
       JSR  PRTST            PRINT TITLE
       MOV  LSTPO,PROFS      SET OFFSET
       MOV  =LSTMS,XR        SET PAGE MESSAGE
       JSR  PRTST            PRINT PAGE MESSAGE
       ICV  LSTPG            BUMP PAGE NUMBER
       MTI  LSTPG            LOAD PAGE NUMBER AS INTEGER
       JSR  PRTIN            PRINT PAGE NUMBER
       JSR  PRTNL            TERMINATE TITLE LINE
       ADD  =NUM02,LSTLC     COUNT TITLE LINE AND BLANK LINE
*
*      PRINT SUB-TITLE (IF ANY)
*
       MOV  R_STL,XR         LOAD POINTER TO SUB-TITLE
       BZE  XR,LSTT1         JUMP IF NO SUB-TITLE
       JSR  PRTST            ELSE PRINT SUB-TITLE
       JSR  PRTNL            TERMINATE LINE
       ICV  LSTLC            BUMP LINE COUNT
*
*      RETURN POINT
*
LSTT1  JSR  PRTNL            PRINT A BLANK LINE
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE LISTT
       EJC
.IF    .CSFN
*
*      NEWFN -- RECORD NEW SOURCE FILE NAME
*
*      NEWFN IS USED AFTER SWITCHING TO A NEW INCLUDE FILE, OR
*      AFTER A -LINE STATEMENT WHICH CONTAINS A FILE NAME.
*
*      (XR)                  FILE NAME SCBLK
*      JSR  NEWFN
*      (WA,WB,WC,XL,XR,RA)   DESTROYED
*
*      ON RETURN, THE TABLE THAT MAPS STATEMENT NUMBERS TO FILE
*      NAMES HAS BEEN UPDATED TO INCLUDE THIS NEW FILE NAME AND
*      THE CURRENT STATEMENT NUMBER.  THE ENTRY IS MADE ONLY IF
*      THE FILE NAME HAD CHANGED FROM ITS PREVIOUS VALUE.
*
NEWFN  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE NEW NAME
       MOV  R_SFC,XL         LOAD PREVIOUS NAME
       JSR  IDENT            CHECK FOR EQUALITY
       PPM  NWFN1            JUMP IF IDENTICAL
       MOV  (XS)+,XR         DIFFERENT, RESTORE NAME
       MOV  XR,R_SFC         RECORD CURRENT FILE NAME
       MOV  CMPSN,WB         GET CURRENT STATEMENT
       MTI  WB               CONVERT TO INTEGER
       JSR  ICBLD            BUILD ICBLK FOR STMT NUMBER
       MOV  R_SFN,XL         FILE NAME TABLE
       MNZ  WB               LOOKUP STATEMENT NUMBER BY NAME
       JSR  TFIND            ALLOCATE NEW TEBLK
       PPM                   ALWAYS POSSIBLE TO ALLOCATE BLOCK
       MOV  R_SFC,TEVAL(XL)  RECORD FILE NAME AS ENTRY VALUE
       EXI
*
*     HERE IF NEW NAME AND OLD NAME IDENTICAL
*
NWFN1  ICA  XS               POP STACK
       EXI
       EJC
.FI
*
*      NEXTS -- ACQUIRE NEXT SOURCE IMAGE
*
*      NEXTS IS USED TO ACQUIRE THE NEXT SOURCE IMAGE AT COMPILE
*      TIME. IT ASSUMES THAT A PRIOR CALL TO READR HAS INPUT
*      A LINE IMAGE (SEE PROCEDURE READR). BEFORE THE CURRENT
*      IMAGE IS FINALLY LOST IT MAY BE LISTED HERE.
*
*      JSR  NEXTS            CALL TO ACQUIRE NEXT INPUT LINE
*      (XR,XL,WA,WB,WC)      DESTROYED
*
*      GLOBAL VALUES AFFECTED
*
.IF    .CINC
*      LSTID                 INCLUDE DEPTH OF NEXT IMAGE
*
.FI
*      R_CNI                 ON INPUT, NEXT IMAGE. ON
*                            EXIT RESET TO ZERO
*
*      R_CIM                 ON EXIT, SET TO POINT TO IMAGE
*
*      RDCLN                 CURRENT LN SET FROM NEXT LINE NUM
*
*      SCNIL                 INPUT IMAGE LENGTH ON EXIT
*
*      SCNSE                 RESET TO ZERO ON EXIT
*
*      LSTPF                 SET ON EXIT IF LINE IS LISTED
*
NEXTS  PRC  E,0              ENTRY POINT
       BZE  CSWLS,NXTS2      JUMP IF -NOLIST
       MOV  R_CIM,XR         POINT TO IMAGE
       BZE  XR,NXTS2         JUMP IF NO IMAGE
       PLC  XR               GET CHAR PTR
       LCH  WA,(XR)          GET FIRST CHAR
       BNE  WA,=CH_MN,NXTS1  JUMP IF NOT CTRL CARD
       BZE  CSWPR,NXTS2      JUMP IF -NOPRINT
*
*      HERE TO CALL LISTER
*
NXTS1  JSR  LISTR            LIST LINE
*
*      HERE AFTER POSSIBLE LISTING
*
NXTS2  MOV  R_CNI,XR         POINT TO NEXT IMAGE
       MOV  XR,R_CIM         SET AS NEXT IMAGE
       MOV  RDNLN,RDCLN      SET AS CURRENT LINE NUMBER
.IF    .CINC
       MOV  CNIND,LSTID      SET AS CURRENT INCLUDE DEPTH
.FI
       ZER  R_CNI            CLEAR NEXT IMAGE POINTER
       MOV  SCLEN(XR),WA     GET INPUT IMAGE LENGTH
       MOV  CSWIN,WB         GET MAX ALLOWABLE LENGTH
       BLO  WA,WB,NXTS3      SKIP IF NOT TOO LONG
       MOV  WB,WA            ELSE TRUNCATE
*
*      HERE WITH LENGTH IN (WA)
*
NXTS3  MOV  WA,SCNIL         USE AS RECORD LENGTH
       ZER  SCNSE            RESET SCNSE
       ZER  LSTPF            SET LINE NOT LISTED YET
       EXI                   RETURN TO NEXTS CALLER
       ENP                   END PROCEDURE NEXTS
       EJC
*
*      PATIN -- PATTERN CONSTRUCTION FOR LEN,POS,RPOS,TAB,RTAB
*
*      THESE PATTERN TYPES ALL GENERATE A SIMILAR NODE TYPE. SO
*      THE CONSTRUCTION CODE IS SHARED. SEE FUNCTIONS SECTION
*      FOR ACTUAL ENTRY POINTS FOR THESE FIVE FUNCTIONS.
*
*      (WA)                  PCODE FOR EXPRESSION ARG CASE
*      (WB)                  PCODE FOR INTEGER ARG CASE
*      JSR  PATIN            CALL TO BUILD PATTERN NODE
*      PPM  LOC              TRANSFER LOC FOR NOT INTEGER OR EXP
*      PPM  LOC              TRANSFER LOC FOR INT OUT OF RANGE
*      (XR)                  POINTER TO CONSTRUCTED NODE
*      (XL,WA,WB,WC,IA)      DESTROYED
*
PATIN  PRC  N,2              ENTRY POINT
       MOV  WA,XL            PRESERVE EXPRESSION ARG PCODE
       JSR  GTSMI            TRY TO CONVERT ARG AS SMALL INTEGER
       PPM  PTIN2            JUMP IF NOT INTEGER
       PPM  PTIN3            JUMP IF OUT OF RANGE
*
*      COMMON SUCCESSFUL EXIT POINT
*
PTIN1  JSR  PBILD            BUILD PATTERN NODE
       EXI                   RETURN TO CALLER
*
*      HERE IF ARGUMENT IS NOT AN INTEGER
*
PTIN2  MOV  XL,WB            COPY EXPR ARG CASE PCODE
       BLO  (XR),=B_E__,PTIN1 ALL OK IF EXPRESSION ARG
       EXI  1                ELSE TAKE ERROR EXIT FOR WRONG TYPE
*
*      HERE FOR ERROR OF OUT OF RANGE INTEGER ARGUMENT
*
PTIN3  EXI  2                TAKE OUT-OF-RANGE ERROR EXIT
       ENP                   END PROCEDURE PATIN
       EJC
*
*      PATST -- PATTERN CONSTRUCTION FOR ANY,NOTANY,
*               BREAK,SPAN AND BREAKX PATTERN FUNCTIONS.
*
*      THESE PATTERN FUNCTIONS BUILD SIMILAR TYPES OF NODES AND
*      THE CONSTRUCTION CODE IS SHARED. SEE FUNCTIONS SECTION
*      FOR ACTUAL ENTRY POINTS FOR THESE FIVE PATTERN FUNCTIONS.
*
*      0(XS)                 STRING ARGUMENT
*      (WB)                  PCODE FOR ONE CHAR ARGUMENT
*      (XL)                  PCODE FOR MULTI-CHAR ARGUMENT
*      (WC)                  PCODE FOR EXPRESSION ARGUMENT
*      JSR  PATST            CALL TO BUILD NODE
*      PPM  LOC              IF NOT STRING OR EXPR (OR NULL)
*      (XS)                  POPPED PAST STRING ARGUMENT
*      (XR)                  POINTER TO CONSTRUCTED NODE
*      (XL)                  DESTROYED
*      (WA,WB,WC,RA)         DESTROYED
*
*      NOTE THAT THERE IS A SPECIAL CALL TO PATST IN THE EVALS
*      PROCEDURE WITH A SLIGHTLY DIFFERENT FORM. SEE EVALS
*      FOR DETAILS OF THE FORM OF THIS CALL.
*
PATST  PRC  N,1              ENTRY POINT
       JSR  GTSTG            CONVERT ARGUMENT AS STRING
       PPM  PATS7            JUMP IF NOT STRING
       BZE  WA,PATS7         JUMP IF NULL STRING (CATSPAW)
       BNE  WA,=NUM01,PATS2  JUMP IF NOT ONE CHAR STRING
*
*      HERE FOR ONE CHAR STRING CASE
*
       BZE  WB,PATS2         TREAT AS MULTI-CHAR IF EVALS CALL
       PLC  XR               POINT TO CHARACTER
       LCH  XR,(XR)          LOAD CHARACTER
*
*      COMMON EXIT POINT AFTER SUCCESSFUL CONSTRUCTION
*
PATS1  JSR  PBILD            CALL ROUTINE TO BUILD NODE
       EXI                   RETURN TO PATST CALLER
       EJC
*
*      PATST (CONTINUED)
*
*      HERE FOR MULTI-CHARACTER STRING CASE
*
PATS2  MOV  XL,-(XS)         SAVE MULTI-CHAR PCODE
       MOV  CTMSK,WC         LOAD CURRENT MASK BIT
       BEQ  XR,R_CTS,PATS6   JUMP IF SAME AS LAST STRING C3.738
       MOV  XR,-(XS)         SAVE STRING POINTER
       LSH  WC,1             SHIFT TO NEXT POSITION
       NZB  WC,PATS4         SKIP IF POSITION LEFT IN THIS TBL
*
*      HERE WE MUST ALLOCATE A NEW CHARACTER TABLE
*
       MOV  *CTSI_,WA        SET SIZE OF CTBLK
       JSR  ALLOC            ALLOCATE CTBLK
       MOV  XR,R_CTP         STORE PTR TO NEW CTBLK
       MOV  =B_CTT,(XR)+     STORE TYPE CODE, BUMP PTR
       LCT  WB,=CFP_A        SET NUMBER OF WORDS TO CLEAR
       MOV  BITS0,WC         LOAD ALL ZERO BITS
*
*      LOOP TO CLEAR ALL BITS IN TABLE TO ZEROS
*
PATS3  MOV  WC,(XR)+         MOVE WORD OF ZERO BITS
       BCT  WB,PATS3         LOOP TILL ALL CLEARED
       MOV  BITS1,WC         SET INITIAL BIT POSITION
*
*      MERGE HERE WITH BIT POSITION AVAILABLE
*
PATS4  MOV  WC,CTMSK         SAVE PARM2 (NEW BIT POSITION)
       MOV  (XS)+,XL         RESTORE POINTER TO ARGUMENT STRING
       MOV  XL,R_CTS         SAVE FOR NEXT TIME   C3.738
       MOV  SCLEN(XL),WB     LOAD STRING LENGTH
       BZE  WB,PATS6         JUMP IF NULL STRING CASE
       LCT  WB,WB            ELSE SET LOOP COUNTER
       PLC  XL               POINT TO CHARACTERS IN ARGUMENT
       EJC
*
*      PATST (CONTINUED)
*
*      LOOP TO SET BITS IN COLUMN OF TABLE
*
PATS5  LCH  WA,(XL)+         LOAD NEXT CHARACTER
       WTB  WA               CONVERT TO BYTE OFFSET
       MOV  R_CTP,XR         POINT TO CTBLK
       ADD  WA,XR            POINT TO CTBLK ENTRY
       MOV  WC,WA            COPY BIT MASK
       ORB  CTCHS(XR),WA     OR IN BITS ALREADY SET
       MOV  WA,CTCHS(XR)     STORE RESULTING BIT STRING
       BCT  WB,PATS5         LOOP TILL ALL BITS SET
*
*      COMPLETE PROCESSING FOR MULTI-CHAR STRING CASE
*
PATS6  MOV  R_CTP,XR         LOAD CTBLK PTR AS PARM1 FOR PBILD
       ZER  XL               CLEAR GARBAGE PTR IN XL
       MOV  (XS)+,WB         LOAD PCODE FOR MULTI-CHAR STR CASE
       BRN  PATS1            BACK TO EXIT (WC=BITSTRING=PARM2)
*
*      HERE IF ARGUMENT IS NOT A STRING
*
*      NOTE THAT THE CALL FROM EVALS CANNOT PASS AN EXPRESSION
*      SINCE EVALP ALWAYS REEVALUATES EXPRESSIONS.
*
PATS7  MOV  WC,WB            SET PCODE FOR EXPRESSION ARGUMENT
       BLO  (XR),=B_E__,PATS1 JUMP TO EXIT IF EXPRESSION ARG
       EXI  1                ELSE TAKE WRONG TYPE ERROR EXIT
       ENP                   END PROCEDURE PATST
       EJC
*
*      PBILD -- BUILD PATTERN NODE
*
*      (XR)                  PARM1 (ONLY IF REQUIRED)
*      (WB)                  PCODE FOR NODE
*      (WC)                  PARM2 (ONLY IF REQUIRED)
*      JSR  PBILD            CALL TO BUILD NODE
*      (XR)                  POINTER TO CONSTRUCTED NODE
*      (WA)                  DESTROYED
*
PBILD  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         STACK POSSIBLE PARM1
       MOV  WB,XR            COPY PCODE
       LEI  XR               LOAD ENTRY POINT ID (BL_PX)
       BEQ  XR,=BL_P1,PBLD1  JUMP IF ONE PARAMETER
       BEQ  XR,=BL_P0,PBLD3  JUMP IF NO PARAMETERS
*
*      HERE FOR TWO PARAMETER CASE
*
       MOV  *PCSI_,WA        SET SIZE OF P2BLK
       JSR  ALLOC            ALLOCATE BLOCK
       MOV  WC,PARM2(XR)     STORE SECOND PARAMETER
       BRN  PBLD2            MERGE WITH ONE PARM CASE
*
*      HERE FOR ONE PARAMETER CASE
*
PBLD1  MOV  *PBSI_,WA        SET SIZE OF P1BLK
       JSR  ALLOC            ALLOCATE NODE
*
*      MERGE HERE FROM TWO PARM CASE
*
PBLD2  MOV  (XS),PARM1(XR)   STORE FIRST PARAMETER
       BRN  PBLD4            MERGE WITH NO PARAMETER CASE
*
*      HERE FOR CASE OF NO PARAMETERS
*
PBLD3  MOV  *PASI_,WA        SET SIZE OF P0BLK
       JSR  ALLOC            ALLOCATE NODE
*
*      MERGE HERE FROM OTHER CASES
*
PBLD4  MOV  WB,(XR)          STORE PCODE
       ICA  XS               POP FIRST PARAMETER
       MOV  =NDNTH,PTHEN(XR) SET NOTHEN SUCCESSOR POINTER
       EXI                   RETURN TO PBILD CALLER
       ENP                   END PROCEDURE PBILD
       EJC
*
*      PCONC -- CONCATENATE TWO PATTERNS
*
*      (XL)                  PTR TO RIGHT PATTERN
*      (XR)                  PTR TO LEFT PATTERN
*      JSR  PCONC            CALL TO CONCATENATE PATTERNS
*      (XR)                  PTR TO CONCATENATED PATTERN
*      (XL,WA,WB,WC)         DESTROYED
*
*
*      TO CONCATENATE TWO PATTERNS, ALL SUCCESSORS IN THE LEFT
*      PATTERN WHICH POINT TO THE NOTHEN NODE MUST BE CHANGED TO
*      POINT TO THE RIGHT PATTERN. HOWEVER, THIS MODIFICATION
*      MUST BE PERFORMED ON A COPY OF THE LEFT ARGUMENT RATHER
*      THAN THE LEFT ARGUMENT ITSELF, SINCE THE LEFT ARGUMENT
*      MAY BE POINTED TO BY SOME OTHER VARIABLE VALUE.
*
*      ACCORDINGLY, IT IS NECESSARY TO COPY THE LEFT ARGUMENT.
*      THIS IS NOT A TRIVIAL PROCESS SINCE WE MUST AVOID COPYING
*      NODES MORE THAN ONCE AND THE PATTERN IS A GRAPH STRUCTURE
*      THE FOLLOWING ALGORITHM IS EMPLOYED.
*
*      THE STACK IS USED TO STORE A LIST OF NODES WHICH
*      HAVE ALREADY BEEN COPIED. THE FORMAT OF THE ENTRIES ON
*      THIS LIST CONSISTS OF A TWO WORD BLOCK. THE FIRST WORD
*      IS THE OLD ADDRESS AND THE SECOND WORD IS THE ADDRESS
*      OF THE COPY. THIS LIST IS SEARCHED BY THE PCOPY
*      ROUTINE TO AVOID MAKING DUPLICATE COPIES. A TRICK IS
*      USED TO ACCOMPLISH THE CONCATENATION AT THE SAME TIME.
*      A SPECIAL ENTRY IS MADE TO START WITH ON THE STACK. THIS
*      ENTRY RECORDS THAT THE NOTHEN NODE HAS BEEN COPIED
*      ALREADY AND THE ADDRESS OF ITS COPY IS THE RIGHT PATTERN.
*      THIS AUTOMATICALLY PERFORMS THE CORRECT REPLACEMENTS.
*
PCONC  PRC  E,0              ENTRY POINT
       ZER  -(XS)            MAKE ROOM FOR ONE ENTRY AT BOTTOM
       MOV  XS,WC            STORE POINTER TO START OF LIST
       MOV  =NDNTH,-(XS)     STACK NOTHEN NODE AS OLD NODE
       MOV  XL,-(XS)         STORE RIGHT ARG AS COPY OF NOTHEN
       MOV  XS,XT            INITIALIZE POINTER TO STACK ENTRIES
       JSR  PCOPY            COPY FIRST NODE OF LEFT ARG
       MOV  WA,NUM02(XT)     STORE AS RESULT UNDER LIST
       EJC
*
*      PCONC (CONTINUED)
*
*      THE FOLLOWING LOOP SCANS ENTRIES IN THE LIST AND MAKES
*      SURE THAT THEIR SUCCESSORS HAVE BEEN COPIED.
*
PCNC1  BEQ  XT,XS,PCNC2      JUMP IF ALL ENTRIES PROCESSED
       MOV  -(XT),XR         ELSE LOAD NEXT OLD ADDRESS
       MOV  PTHEN(XR),XR     LOAD POINTER TO SUCCESSOR
       JSR  PCOPY            COPY SUCCESSOR NODE
       MOV  -(XT),XR         LOAD POINTER TO NEW NODE (COPY)
       MOV  WA,PTHEN(XR)     STORE PTR TO NEW SUCCESSOR
*
*      NOW CHECK FOR SPECIAL CASE OF ALTERNATION NODE WHERE
*      PARM1 POINTS TO A NODE AND MUST BE COPIED LIKE PTHEN.
*
       BNE  (XR),=P_ALT,PCNC1 LOOP BACK IF NOT
       MOV  PARM1(XR),XR     ELSE LOAD POINTER TO ALTERNATIVE
       JSR  PCOPY            COPY IT
       MOV  (XT),XR          RESTORE PTR TO NEW NODE
       MOV  WA,PARM1(XR)     STORE PTR TO COPIED ALTERNATIVE
       BRN  PCNC1            LOOP BACK FOR NEXT ENTRY
*
*      HERE AT END OF COPY PROCESS
*
PCNC2  MOV  WC,XS            RESTORE STACK POINTER
       MOV  (XS)+,XR         LOAD POINTER TO COPY
       EXI                   RETURN TO PCONC CALLER
       ENP                   END PROCEDURE PCONC
       EJC
*
*      PCOPY -- COPY A PATTERN NODE
*
*      PCOPY IS CALLED FROM THE PCONC PROCEDURE TO COPY A SINGLE
*      PATTERN NODE. THE COPY IS ONLY CARRIED OUT IF THE NODE
*      HAS NOT BEEN COPIED ALREADY.
*
*      (XR)                  POINTER TO NODE TO BE COPIED
*      (XT)                  PTR TO CURRENT LOC IN COPY LIST
*      (WC)                  POINTER TO LIST OF COPIED NODES
*      JSR  PCOPY            CALL TO COPY A NODE
*      (WA)                  POINTER TO COPY
*      (WB,XR)               DESTROYED
*
PCOPY  PRC  N,0              ENTRY POINT
       MOV  XT,WB            SAVE XT
       MOV  WC,XT            POINT TO START OF LIST
*
*      LOOP TO SEARCH LIST OF NODES COPIED ALREADY
*
PCOP1  DCA  XT               POINT TO NEXT ENTRY ON LIST
       BEQ  XR,(XT),PCOP2    JUMP IF MATCH
       DCA  XT               ELSE SKIP OVER COPIED ADDRESS
       BNE  XT,XS,PCOP1      LOOP BACK IF MORE TO TEST
*
*      HERE IF NOT IN LIST, PERFORM COPY
*
       MOV  (XR),WA          LOAD FIRST WORD OF BLOCK
       JSR  BLKLN            GET LENGTH OF BLOCK
       MOV  XR,XL            SAVE POINTER TO OLD NODE
       JSR  ALLOC            ALLOCATE SPACE FOR COPY
       MOV  XL,-(XS)         STORE OLD ADDRESS ON LIST
       MOV  XR,-(XS)         STORE NEW ADDRESS ON LIST
       CHK                   CHECK FOR STACK OVERFLOW
       MVW                   MOVE WORDS FROM OLD BLOCK TO COPY
       MOV  (XS),WA          LOAD POINTER TO COPY
       BRN  PCOP3            JUMP TO EXIT
*
*      HERE IF WE FIND ENTRY IN LIST
*
PCOP2  MOV  -(XT),WA         LOAD ADDRESS OF COPY FROM LIST
*
*      COMMON EXIT POINT
*
PCOP3  MOV  WB,XT            RESTORE XT
       EXI                   RETURN TO PCOPY CALLER
       ENP                   END PROCEDURE PCOPY
       EJC
.IF    .CNPF
.ELSE
*
*      PRFLR -- PRINT PROFILE
*      PRFLR IS CALLED TO PRINT THE CONTENTS OF THE PROFILE
*      TABLE IN A FAIRLY READABLE TABULAR FORMAT.
*
*      JSR  PRFLR            CALL TO PRINT PROFILE
*      (WA,IA)               DESTROYED
*
PRFLR  PRC  E,0
       BZE  PFDMP,PRFL4      NO PRINTING IF NO PROFILING DONE
       MOV  XR,-(XS)         PRESERVE ENTRY XR
       MOV  WB,PFSVW         AND ALSO WB
       JSR  PRTPG            EJECT
       MOV  =PFMS1,XR        LOAD MSG /PROGRAM PROFILE/
       JSR  PRTST            AND PRINT IT
       JSR  PRTNL            FOLLOWED BY NEWLINE
       JSR  PRTNL            AND ANOTHER
       MOV  =PFMS2,XR        POINT TO FIRST HDR
       JSR  PRTST            PRINT IT
       JSR  PRTNL            NEW LINE
       MOV  =PFMS3,XR        SECOND HDR
       JSR  PRTST            PRINT IT
       JSR  PRTNL            NEW LINE
       JSR  PRTNL            AND ANOTHER BLANK LINE
       ZER  WB               INITIAL STMT COUNT
       MOV  PFTBL,XR         POINT TO TABLE ORIGIN
       ADD  *XNDTA,XR        BIAS PAST XNBLK HEADER (SGD07)
*
*      LOOP HERE TO PRINT SUCCESSIVE ENTRIES
*
PRFL1  ICV  WB               BUMP STMT NR
       LDI  (XR)             LOAD NR OF EXECUTIONS
       IEQ  PRFL3            NO PRINTING IF ZERO
       MOV  =PFPD1,PROFS     POINT WHERE TO PRINT
       JSR  PRTIN            AND PRINT IT
       ZER  PROFS            BACK TO START OF LINE
       MTI  WB               LOAD STMT NR
       JSR  PRTIN            PRINT IT THERE
       MOV  =PFPD2,PROFS     AND PAD PAST COUNT
       LDI  CFP_I(XR)        LOAD TOTAL EXEC TIME
       JSR  PRTIN            PRINT THAT TOO
       LDI  CFP_I(XR)        RELOAD TIME
       MLI  INTTH            CONVERT TO MICROSEC
       IOV  PRFL2            OMIT NEXT BIT IF OVERFLOW
       DVI  (XR)             DIVIDE BY EXECUTIONS
       MOV  =PFPD3,PROFS     PAD LAST PRINT
       JSR  PRTIN            AND PRINT MCSEC/EXECN
*
*      MERGE AFTER PRINTING TIME
*
PRFL2  JSR  PRTNL            THATS ANOTHER LINE
*
*      HERE TO GO TO NEXT ENTRY
*
PRFL3  ADD  *PF_I2,XR        BUMP INDEX PTR (SGD07)
       BLT  WB,PFNTE,PRFL1   LOOP IF MORE STMTS
       MOV  (XS)+,XR         RESTORE CALLERS XR
       MOV  PFSVW,WB         AND WB TOO
*
*      HERE TO EXIT
*
PRFL4  EXI                   RETURN
       ENP                   END OF PRFLR
       EJC
*
*      PRFLU -- UPDATE AN ENTRY IN THE PROFILE TABLE
*
*      ON ENTRY, KVSTN CONTAINS NR OF STMT TO PROFILE
*
*      JSR  PRFLU            CALL TO UPDATE ENTRY
*      (IA)                  DESTROYED
*
PRFLU  PRC  E,0
       BNZ  PFFNC,PFLU4      SKIP IF JUST ENTERED FUNCTION
       MOV  XR,-(XS)         PRESERVE ENTRY XR
       MOV  WA,PFSVW         SAVE WA (SGD07)
       BNZ  PFTBL,PFLU2      BRANCH IF TABLE ALLOCATED
*
*      HERE IF SPACE FOR PROFILE TABLE NOT YET ALLOCATED.
*      CALCULATE SIZE NEEDED, ALLOCATE A STATIC XNBLK, AND
*      INITIALIZE IT ALL TO ZERO.
*      THE TIME TAKEN FOR THIS WILL BE ATTRIBUTED TO THE CURRENT
*      STATEMENT (ASSIGNMENT TO KEYWD PROFILE), BUT SINCE THE
*      TIMING FOR THIS STATEMENT IS UP THE POLE ANYWAY, THIS
*      DOESNT REALLY MATTER...
*
       SUB  =NUM01,PFNTE     ADJUST FOR EXTRA COUNT (SGD07)
       MTI  PFI2A            CONVRT ENTRY SIZE TO INT
       STI  PFSTE            AND STORE SAFELY FOR LATER
       MTI  PFNTE            LOAD TABLE LENGTH AS INTEGER
       MLI  PFSTE            MULTIPLY BY ENTRY SIZE
       MFI  WA               GET BACK ADDRESS-STYLE
       ADD  =NUM02,WA        ADD ON 2 WORD OVERHEAD
       WTB  WA               CONVERT THE WHOLE LOT TO BYTES
       JSR  ALOST            GIMME THE SPACE
       MOV  XR,PFTBL         SAVE BLOCK POINTER
       MOV  =B_XNT,(XR)+     PUT BLOCK TYPE AND ...
       MOV  WA,(XR)+         ... LENGTH INTO HEADER
       MFI  WA               GET BACK NR OF WDS IN DATA AREA
       LCT  WA,WA            LOAD THE COUNTER
*
*      LOOP HERE TO ZERO THE BLOCK DATA
*
PFLU1  ZER  (XR)+            BLANK A WORD
       BCT  WA,PFLU1         AND ALLLLLLL THE REST
*
*      END OF ALLOCATION. MERGE BACK INTO ROUTINE
*
PFLU2  MTI  KVSTN            LOAD NR OF STMT JUST ENDED
       SBI  INTV1            MAKE INTO INDEX OFFSET
       MLI  PFSTE            MAKE OFFSET OF TABLE ENTRY
       MFI  WA               CONVERT TO ADDRESS
       WTB  WA               GET AS BAUS
       ADD  *NUM02,WA        OFFSET INCLUDES TABLE HEADER
       MOV  PFTBL,XR         GET TABLE START
       BGE  WA,NUM01(XR),PFLU3  IF OUT OF TABLE, SKIP IT
       ADD  WA,XR            ELSE POINT TO ENTRY
       LDI  (XR)             GET NR OF EXECUTIONS SO FAR
       ADI  INTV1            NUDGE UP ONE
       STI  (XR)             AND PUT BACK
       JSR  SYSTM            GET TIME NOW
       STI  PFETM            STASH ENDING TIME
       SBI  PFSTM            SUBTRACT START TIME
       ADI  CFP_I(XR)        ADD CUMULATIVE TIME SO FAR
       STI  CFP_I(XR)        AND PUT BACK NEW TOTAL
       LDI  PFETM            LOAD END TIME OF THIS STMT ...
       STI  PFSTM            ... WHICH IS START TIME OF NEXT
*
*      MERGE HERE TO EXIT
*
PFLU3  MOV  (XS)+,XR         RESTORE CALLERS XR
       MOV  PFSVW,WA         RESTORE SAVED REG
       EXI                   AND RETURN
*
*      HERE IF PROFILE IS SUPPRESSED BECAUSE A PROGRAM DEFINED
*      FUNCTION IS ABOUT TO BE ENTERED, AND SO THE CURRENT STMT
*      HAS NOT YET FINISHED
*
PFLU4  ZER  PFFNC            RESET THE CONDITION FLAG
       EXI                   AND IMMEDIATE RETURN
       ENP                   END OF PROCEDURE PRFLU
       EJC
.FI
*
*      PRPAR - PROCESS PRINT PARAMETERS
*
*      (WC)                  IF NONZERO ASSOCIATE TERMINAL ONLY
*      JSR  PRPAR            CALL TO PROCESS PRINT PARAMETERS
*      (XL,XR,WA,WB,WC)      DESTROYED
*
*      SINCE MEMORY ALLOCATION IS UNDECIDED ON INITIAL CALL,
*      TERMINAL CANNOT BE ASSOCIATED. THE ENTRY WITH WC NON-ZERO
*      IS PROVIDED SO A LATER CALL CAN BE MADE TO COMPLETE THIS.
*
PRPAR  PRC  E,0              ENTRY POINT
       BNZ  WC,PRPA8         JUMP TO ASSOCIATE TERMINAL
       JSR  SYSPP            GET PRINT PARAMETERS
       BNZ  WB,PRPA1         JUMP IF LINES/PAGE SPECIFIED
       MOV  =CFP_M,WB        ELSE USE A LARGE VALUE
       RSH  WB,1             BUT NOT TOO LARGE
*
*      STORE LINE COUNT/PAGE
*
PRPA1  MOV  WB,LSTNP         STORE NUMBER OF LINES/PAGE
       MOV  WB,LSTLC         PRETEND PAGE IS FULL INITIALLY
       ZER  LSTPG            CLEAR PAGE NUMBER
       MOV  PRLEN,WB         GET PRIOR LENGTH IF ANY
       BZE  WB,PRPA2         SKIP IF NO LENGTH
       BGT  WA,WB,PRPA3      SKIP STORING IF TOO BIG
*
*      STORE PRINT BUFFER LENGTH
*
PRPA2  MOV  WA,PRLEN         STORE VALUE
*
*      PROCESS BITS OPTIONS
*
PRPA3  MOV  BITS3,WB         BIT 3 MASK
       ANB  WC,WB            GET -NOLIST BIT
       ZRB  WB,PRPA4         SKIP IF CLEAR
       ZER  CSWLS            SET -NOLIST
*
*      CHECK IF FAIL REPORTS GOTO INTERACTIVE CHANNEL
*
PRPA4  MOV  BITS1,WB         BIT 1 MASK
       ANB  WC,WB            GET BIT
       MOV  WB,ERICH         STORE INT. CHAN. ERROR FLAG
       MOV  BITS2,WB         BIT 2 MASK
       ANB  WC,WB            GET BIT
       MOV  WB,PRICH         FLAG FOR STD PRINTER ON INT. CHAN.
       MOV  BITS4,WB         BIT 4 MASK
       ANB  WC,WB            GET BIT
       MOV  WB,CPSTS         FLAG FOR COMPILE STATS SUPPRESSN.
       MOV  BITS5,WB         BIT 5 MASK
       ANB  WC,WB            GET BIT
       MOV  WB,EXSTS         FLAG FOR EXEC STATS SUPPRESSION
       EJC
*
*      PRPAR (CONTINUED)
*
       MOV  BITS6,WB         BIT 6 MASK
       ANB  WC,WB            GET BIT
       MOV  WB,PRECL         EXTENDED/COMPACT LISTING FLAG
       SUB  =NUM08,WA        POINT 8 CHARS FROM LINE END
       ZRB  WB,PRPA5         JUMP IF NOT EXTENDED
       MOV  WA,LSTPO         STORE FOR LISTING PAGE HEADINGS
*
*       CONTINUE OPTION PROCESSING
*
PRPA5  MOV  BITS7,WB         BIT 7 MASK
       ANB  WC,WB            GET BIT 7
       MOV  WB,CSWEX         SET -NOEXECUTE IF NON-ZERO
       MOV  BIT10,WB         BIT 10 MASK
       ANB  WC,WB            GET BIT 10
       MOV  WB,HEADP         PRETEND PRINTED TO OMIT HEADERS
       MOV  BITS9,WB         BIT 9 MASK
       ANB  WC,WB            GET BIT 9
       MOV  WB,PRSTO         KEEP IT AS STD LISTING OPTION
.IF    .CULC
       MOV  WC,WB            COPY FLAGS
       RSH  WB,12            RIGHT JUSTIFY BIT 13
       ANB  BITS1,WB         GET BIT
       MOV  WB,KVCAS         SET -CASE
.FI
       MOV  BIT12,WB         BIT 12 MASK
       ANB  WC,WB            GET BIT 12
       MOV  WB,CSWER         KEEP IT AS ERRORS/NOERRORS OPTION
       ZRB  WB,PRPA6         SKIP IF CLEAR
       MOV  PRLEN,WA         GET PRINT BUFFER LENGTH
       SUB  =NUM08,WA        POINT 8 CHARS FROM LINE END
       MOV  WA,LSTPO         STORE PAGE OFFSET
*
*      CHECK FOR -PRINT/-NOPRINT
*
PRPA6  MOV  BIT11,WB         BIT 11 MASK
       ANB  WC,WB            GET BIT 11
       MOV  WB,CSWPR         SET -PRINT IF NON-ZERO
*
*      CHECK FOR TERMINAL
*
       ANB  BITS8,WC         SEE IF TERMINAL TO BE ACTIVATED
       BNZ  WC,PRPA8         JUMP IF TERMINAL REQUIRED
       BZE  INITR,PRPA9      JUMP IF NO TERMINAL TO DETACH
       MOV  =V_TER,XL        PTR TO /TERMINAL/
       JSR  GTNVR            GET VRBLK POINTER
       PPM                   CANT FAIL
       MOV  =NULLS,VRVAL(XR) CLEAR VALUE OF TERMINAL
       JSR  SETVR            REMOVE ASSOCIATION
       BRN  PRPA9            RETURN
*
*      ASSOCIATE TERMINAL
*
PRPA8  MNZ  INITR            NOTE TERMINAL ASSOCIATED
       BZE  DNAMB,PRPA9      CANT IF MEMORY NOT ORGANISED
       MOV  =V_TER,XL        POINT TO TERMINAL STRING
       MOV  =TRTOU,WB        OUTPUT TRACE TYPE
       JSR  INOUT            ATTACH OUTPUT TRBLK TO VRBLK
       MOV  XR,-(XS)         STACK TRBLK PTR
       MOV  =V_TER,XL        POINT TO TERMINAL STRING
       MOV  =TRTIN,WB        INPUT TRACE TYPE
       JSR  INOUT            ATTACH INPUT TRACE BLK
       MOV  (XS)+,VRVAL(XR)  ADD OUTPUT TRBLK TO CHAIN
*
*      RETURN POINT
*
PRPA9  EXI                   RETURN
       ENP                   END PROCEDURE PRPAR
       EJC
*
*      PRTCH -- PRINT A CHARACTER
*
*      PRTCH IS USED TO PRINT A SINGLE CHARACTER
*
*      (WA)                  CHARACTER TO BE PRINTED
*      JSR  PRTCH            CALL TO PRINT CHARACTER
*
PRTCH  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE XR
       BNE  PROFS,PRLEN,PRCH1 JUMP IF ROOM IN BUFFER
       JSR  PRTNL            ELSE PRINT THIS LINE
*
*      HERE AFTER MAKING SURE WE HAVE ROOM
*
PRCH1  MOV  PRBUF,XR         POINT TO PRINT BUFFER
       PSC  XR,PROFS         POINT TO NEXT CHARACTER LOCATION
       SCH  WA,(XR)          STORE NEW CHARACTER
       CSC  XR               COMPLETE STORE CHARACTERS
       ICV  PROFS            BUMP POINTER
       MOV  (XS)+,XR         RESTORE ENTRY XR
       EXI                   RETURN TO PRTCH CALLER
       ENP                   END PROCEDURE PRTCH
       EJC
*
*      PRTIC -- PRINT TO INTERACTIVE CHANNEL
*
*      PRTIC IS CALLED TO PRINT THE CONTENTS OF THE STANDARD
*      PRINT BUFFER TO THE INTERACTIVE CHANNEL. IT IS ONLY
*      CALLED AFTER PRTST HAS SET UP THE STRING FOR PRINTING.
*      IT DOES NOT CLEAR THE BUFFER.
*
*      JSR  PRTIC            CALL FOR PRINT
*      (WA,WB)               DESTROYED
*
PRTIC  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE XR
       MOV  PRBUF,XR         POINT TO BUFFER
       MOV  PROFS,WA         NO OF CHARS
       JSR  SYSPI            PRINT
       PPM  PRTC2            FAIL RETURN
*
*      RETURN
*
PRTC1  MOV  (XS)+,XR         RESTORE XR
       EXI                   RETURN
*
*      ERROR OCCURED
*
PRTC2  ZER  ERICH            PREVENT LOOPING
       ERB  252,Error on printing to interactive channel
       BRN  PRTC1            RETURN
       ENP                   PROCEDURE PRTIC
       EJC
*
*      PRTIS -- PRINT TO INTERACTIVE AND STANDARD PRINTER
*
*      PRTIS PUTS A LINE FROM THE PRINT BUFFER ONTO THE
*      INTERACTIVE CHANNEL (IF ANY) AND THE STANDARD PRINTER.
*      IT ALWAYS PRINTS TO THE STANDARD PRINTER BUT DOES
*      NOT DUPLICATE LINES IF THE STANDARD PRINTER IS
*      INTERACTIVE.  IT CLEARS DOWN THE PRINT BUFFER.
*
*      JSR  PRTIS            CALL FOR PRINTING
*      (WA,WB)               DESTROYED
*
PRTIS  PRC  E,0              ENTRY POINT
       BNZ  PRICH,PRTS1      JUMP IF STANDARD PRINTER IS INT.CH.
       BZE  ERICH,PRTS1      SKIP IF NOT DOING INT. ERROR REPS.
       JSR  PRTIC            PRINT TO INTERACTIVE CHANNEL
*
*      MERGE AND EXIT
*
PRTS1  JSR  PRTNL            PRINT TO STANDARD PRINTER
       EXI                   RETURN
       ENP                   END PROCEDURE PRTIS
       EJC
*
*      PRTIN -- PRINT AN INTEGER
*
*      PRTIN PRINTS THE INTEGER VALUE WHICH IS IN THE INTEGER
*      ACCUMULATOR. BLOCKS BUILT IN DYNAMIC STORAGE
*      DURING THIS PROCESS ARE IMMEDIATELY DELETED.
*
*      (IA)                  INTEGER VALUE TO BE PRINTED
*      JSR  PRTIN            CALL TO PRINT INTEGER
*      (IA,RA)               DESTROYED
*
PRTIN  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE XR
       JSR  ICBLD            BUILD INTEGER BLOCK
       BLO  XR,DNAMB,PRTI1   JUMP IF ICBLK BELOW DYNAMIC
       BHI  XR,DNAMP,PRTI1   JUMP IF ABOVE DYNAMIC
       MOV  XR,DNAMP         IMMEDIATELY DELETE IT
*
*      DELETE ICBLK FROM DYNAMIC STORE
*
PRTI1  MOV  XR,-(XS)         STACK PTR FOR GTSTG
       JSR  GTSTG            CONVERT TO STRING
       PPM                   CONVERT ERROR IS IMPOSSIBLE
       MOV  XR,DNAMP         RESET POINTER TO DELETE SCBLK
       JSR  PRTST            PRINT INTEGER STRING
       MOV  (XS)+,XR         RESTORE ENTRY XR
       EXI                   RETURN TO PRTIN CALLER
       ENP                   END PROCEDURE PRTIN
       EJC
*
*      PRTMI -- PRINT MESSAGE AND INTEGER
*
*      PRTMI IS USED TO PRINT MESSAGES TOGETHER WITH AN INTEGER
*      VALUE STARTING IN COLUMN 15 (USED BY THE ROUTINES AT
*      THE END OF COMPILATION).
*
*      JSR  PRTMI            CALL TO PRINT MESSAGE AND INTEGER
*
PRTMI  PRC  E,0              ENTRY POINT
       JSR  PRTST            PRINT STRING MESSAGE
       MOV  =PRTMF,PROFS     SET COLUMN OFFSET
       JSR  PRTIN            PRINT INTEGER
       JSR  PRTNL            PRINT LINE
       EXI                   RETURN TO PRTMI CALLER
       ENP                   END PROCEDURE PRTMI
       EJC
*
*      PRTMM -- PRINT MEMORY USED AND AVAILABLE
*
*      PRTMM IS USED TO PROVIDE MEMORY USAGE INFORMATION IN
*      BOTH THE END-OF-COMPILE AND END-OF-RUN STATISTICS.
*
*      JSR  PRTMM            CALL TO PRINT MEMORY STATS
*
PRTMM  PRC  E,0
       MOV  DNAMP,WA         NEXT AVAILABLE LOC
       SUB  STATB,WA         MINUS START
.IF    .CBYT
.ELSE
       BTW  WA               CONVERT TO WORDS
.FI
       MTI  WA               CONVERT TO INTEGER
       MOV  =ENCM1,XR        POINT TO /MEMORY USED (WORDS)/
       JSR  PRTMI            PRINT MESSAGE
       MOV  DNAME,WA         END OF MEMORY
       SUB  DNAMP,WA         MINUS NEXT AVAILABLE LOC
.IF    .CBYT
.ELSE
       BTW  WA               CONVERT TO WORDS
.FI
       MTI  WA               CONVERT TO INTEGER
       MOV  =ENCM2,XR        POINT TO /MEMORY AVAILABLE (WORDS)/
       JSR  PRTMI            PRINT LINE
       EXI                   RETURN TO PRTMM CALLER
       ENP                   END OF PROCEDURE PRTMM
       EJC
*
*      PRTMX  -- AS PRTMI WITH EXTRA COPY TO INTERACTIVE CHAN.
*
*      JSR  PRTMX            CALL FOR PRINTING
*      (WA,WB)               DESTROYED
*
PRTMX  PRC  E,0              ENTRY POINT
       JSR  PRTST            PRINT STRING MESSAGE
       MOV  =PRTMF,PROFS     SET COLUMN OFFSET
       JSR  PRTIN            PRINT INTEGER
       JSR  PRTIS            PRINT LINE
       EXI                   RETURN
       ENP                   END PROCEDURE PRTMX
       EJC
*
*      PRTNL -- PRINT NEW LINE (END PRINT LINE)
*
*      PRTNL PRINTS THE CONTENTS OF THE PRINT BUFFER, RESETS
*      THE BUFFER TO ALL BLANKS AND RESETS THE PRINT POINTER.
*
*      JSR  PRTNL            CALL TO PRINT LINE
*
PRTNL  PRC  R,0              ENTRY POINT
       BNZ  HEADP,PRNL0      WERE HEADERS PRINTED
       JSR  PRTPS            NO - PRINT THEM
*
*      CALL SYSPR
*
PRNL0  MOV  XR,-(XS)         SAVE ENTRY XR
       MOV  WA,PRTSA         SAVE WA
       MOV  WB,PRTSB         SAVE WB
       MOV  PRBUF,XR         LOAD POINTER TO BUFFER
       MOV  PROFS,WA         LOAD NUMBER OF CHARS IN BUFFER
       JSR  SYSPR            CALL SYSTEM PRINT ROUTINE
       PPM  PRNL2            JUMP IF FAILED
       LCT  WA,PRLNW         LOAD LENGTH OF BUFFER IN WORDS
       ADD  *SCHAR,XR        POINT TO CHARS OF BUFFER
       MOV  NULLW,WB         GET WORD OF BLANKS
*
*      LOOP TO BLANK BUFFER
*
PRNL1  MOV  WB,(XR)+         STORE WORD OF BLANKS, BUMP PTR
       BCT  WA,PRNL1         LOOP TILL ALL BLANKED
*
*      EXIT POINT
*
       MOV  PRTSB,WB         RESTORE WB
       MOV  PRTSA,WA         RESTORE WA
       MOV  (XS)+,XR         RESTORE ENTRY XR
       ZER  PROFS            RESET PRINT BUFFER POINTER
       EXI                   RETURN TO PRTNL CALLER
*
*      FILE FULL OR NO OUTPUT FILE FOR LOAD MODULE
*
PRNL2  BNZ  PRTEF,PRNL3      JUMP IF NOT FIRST TIME
       MNZ  PRTEF            MARK FIRST OCCURRENCE
       ERB  253,Print limit exceeded on standard output channel
*
*      STOP AT ONCE
*
PRNL3  MOV  =NINI8,WB        ENDING CODE
       MOV  KVSTN,WA         STATEMENT NUMBER
       MOV  R_FCB,XL         GET FCBLK CHAIN HEAD
       JSR  SYSEJ            STOP
       ENP                   END PROCEDURE PRTNL
       EJC
*
*      PRTNM -- PRINT VARIABLE NAME
*
*      PRTNM IS USED TO PRINT A CHARACTER REPRESENTATION OF THE
*      NAME OF A VARIABLE (NOT A VALUE OF DATATYPE NAME)
*      NAMES OF PSEUDO-VARIABLES MAY NOT BE PASSED TO PRTNM.
*
*      (XL)                  NAME BASE
*      (WA)                  NAME OFFSET
*      JSR  PRTNM            CALL TO PRINT NAME
*      (WB,WC,RA)            DESTROYED
*
PRTNM  PRC  R,0              ENTRY POINT (RECURSIVE, SEE PRTVL)
       MOV  WA,-(XS)         SAVE WA (OFFSET IS COLLECTABLE)
       MOV  XR,-(XS)         SAVE ENTRY XR
       MOV  XL,-(XS)         SAVE NAME BASE
       BHI  XL,STATE,PRN02   JUMP IF NOT NATURAL VARIABLE
*
*      HERE FOR NATURAL VARIABLE NAME, RECOGNIZED BY THE FACT
*      THAT THE NAME BASE POINTS INTO THE STATIC AREA.
*
       MOV  XL,XR            POINT TO VRBLK
       JSR  PRTVN            PRINT NAME OF VARIABLE
*
*      COMMON EXIT POINT
*
PRN01  MOV  (XS)+,XL         RESTORE NAME BASE
       MOV  (XS)+,XR         RESTORE ENTRY VALUE OF XR
       MOV  (XS)+,WA         RESTORE WA
       EXI                   RETURN TO PRTNM CALLER
*
*      HERE FOR CASE OF NON-NATURAL VARIABLE
*
PRN02  MOV  WA,WB            COPY NAME OFFSET
       BNE  (XL),=B_PDT,PRN03 JUMP IF ARRAY OR TABLE
*
*      FOR PROGRAM DEFINED DATATYPE, PRT FLD NAME, LEFT PAREN
*
       MOV  PDDFP(XL),XR     LOAD POINTER TO DFBLK
       ADD  WA,XR            ADD NAME OFFSET
       MOV  PDFOF(XR),XR     LOAD VRBLK POINTER FOR FIELD
       JSR  PRTVN            PRINT FIELD NAME
       MOV  =CH_PP,WA        LOAD LEFT PAREN
       JSR  PRTCH            PRINT CHARACTER
       EJC
*
*      PRTNM (CONTINUED)
*
*      NOW WE PRINT AN IDENTIFYING NAME FOR THE OBJECT IF ONE
*      CAN BE FOUND. THE FOLLOWING CODE SEARCHES FOR A NATURAL
*      VARIABLE WHICH CONTAINS THIS OBJECT AS VALUE. IF SUCH A
*      VARIABLE IS FOUND, ITS NAME IS PRINTED, ELSE THE VALUE
*      OF THE OBJECT (AS PRINTED BY PRTVL) IS USED INSTEAD.
*
*      FIRST WE POINT TO THE PARENT TBBLK IF THIS IS THE CASE OF
*      A TABLE ELEMENT. TO DO THIS, CHASE DOWN THE TRNXT CHAIN.
*
PRN03  BNE  (XL),=B_TET,PRN04 JUMP IF WE GOT THERE (OR NOT TE)
       MOV  TENXT(XL),XL     ELSE MOVE OUT ON CHAIN
       BRN  PRN03            AND LOOP BACK
*
*      NOW WE ARE READY FOR THE SEARCH. TO SPEED THINGS UP IN
*      THE CASE OF CALLS FROM DUMP WHERE THE SAME NAME BASE
*      WILL OCCUR REPEATEDLY WHILE DUMPING AN ARRAY OR TABLE,
*      WE REMEMBER THE LAST VRBLK POINTER FOUND IN PRNMV. SO
*      FIRST CHECK TO SEE IF WE HAVE THIS ONE AGAIN.
*
PRN04  MOV  PRNMV,XR         POINT TO VRBLK WE FOUND LAST TIME
       MOV  HSHTB,WA         POINT TO HASH TABLE IN CASE NOT
       BRN  PRN07            JUMP INTO SEARCH FOR SPECIAL CHECK
*
*      LOOP THROUGH HASH SLOTS
*
PRN05  MOV  WA,XR            COPY SLOT POINTER
       ICA  WA               BUMP SLOT POINTER
       SUB  *VRNXT,XR        INTRODUCE STANDARD VRBLK OFFSET
*
*      LOOP THROUGH VRBLKS ON ONE HASH CHAIN
*
PRN06  MOV  VRNXT(XR),XR     POINT TO NEXT VRBLK ON HASH CHAIN
*
*      MERGE HERE FIRST TIME TO CHECK BLOCK WE FOUND LAST TIME
*
PRN07  MOV  XR,WC            COPY VRBLK POINTER
       BZE  WC,PRN09         JUMP IF CHAIN END (OR PRNMV ZERO)
       EJC
*
*      PRTNM (CONTINUED)
*
*      LOOP TO FIND VALUE (CHASE DOWN POSSIBLE TRBLK CHAIN)
*
PRN08  MOV  VRVAL(XR),XR     LOAD VALUE
       BEQ  (XR),=B_TRT,PRN08 LOOP IF THAT WAS A TRBLK
*
*      NOW WE HAVE THE VALUE, IS THIS THE BLOCK WE WANT
*
       BEQ  XR,XL,PRN10      JUMP IF THIS MATCHES THE NAME BASE
       MOV  WC,XR            ELSE POINT BACK TO THAT VRBLK
       BRN  PRN06            AND LOOP BACK
*
*      HERE TO MOVE TO NEXT HASH SLOT
*
PRN09  BLT  WA,HSHTE,PRN05   LOOP BACK IF MORE TO GO
       MOV  XL,XR            ELSE NOT FOUND, COPY VALUE POINTER
       JSR  PRTVL            PRINT VALUE
       BRN  PRN11            AND MERGE AHEAD
*
*      HERE WHEN WE FIND A MATCHING ENTRY
*
PRN10  MOV  WC,XR            COPY VRBLK POINTER
       MOV  XR,PRNMV         SAVE FOR NEXT TIME IN
       JSR  PRTVN            PRINT VARIABLE NAME
*
*      MERGE HERE IF NO ENTRY FOUND
*
PRN11  MOV  (XL),WC          LOAD FIRST WORD OF NAME BASE
       BNE  WC,=B_PDT,PRN13  JUMP IF NOT PROGRAM DEFINED
*
*      FOR PROGRAM DEFINED DATATYPE, ADD RIGHT PAREN AND EXIT
*
       MOV  =CH_RP,WA        LOAD RIGHT PAREN, MERGE
*
*      MERGE HERE TO PRINT FINAL RIGHT PAREN OR BRACKET
*
PRN12  JSR  PRTCH            PRINT FINAL CHARACTER
       MOV  WB,WA            RESTORE NAME OFFSET
       BRN  PRN01            MERGE BACK TO EXIT
       EJC
*
*      PRTNM (CONTINUED)
*
*      HERE FOR ARRAY OR TABLE
*
PRN13  MOV  =CH_BB,WA        LOAD LEFT BRACKET
       JSR  PRTCH            AND PRINT IT
       MOV  (XS),XL          RESTORE BLOCK POINTER
       MOV  (XL),WC          LOAD TYPE WORD AGAIN
       BNE  WC,=B_TET,PRN15  JUMP IF NOT TABLE
*
*      HERE FOR TABLE, PRINT SUBSCRIPT VALUE
*
       MOV  TESUB(XL),XR     LOAD SUBSCRIPT VALUE
       MOV  WB,XL            SAVE NAME OFFSET
       JSR  PRTVL            PRINT SUBSCRIPT VALUE
       MOV  XL,WB            RESTORE NAME OFFSET
*
*      MERGE HERE FROM ARRAY CASE TO PRINT RIGHT BRACKET
*
PRN14  MOV  =CH_RB,WA        LOAD RIGHT BRACKET
       BRN  PRN12            MERGE BACK TO PRINT IT
*
*      HERE FOR ARRAY OR VECTOR, TO PRINT SUBSCRIPT(S)
*
PRN15  MOV  WB,WA            COPY NAME OFFSET
       BTW  WA               CONVERT TO WORDS
       BEQ  WC,=B_ART,PRN16  JUMP IF ARBLK
*
*      HERE FOR VECTOR
*
       SUB  =VCVLB,WA        ADJUST FOR STANDARD FIELDS
       MTI  WA               MOVE TO INTEGER ACCUM
       JSR  PRTIN            PRINT LINEAR SUBSCRIPT
       BRN  PRN14            MERGE BACK FOR RIGHT BRACKET
       EJC
*
*      PRTNM (CONTINUED)
*
*      HERE FOR ARRAY. FIRST CALCULATE ABSOLUTE SUBSCRIPT
*      OFFSETS BY SUCCESSIVE DIVISIONS BY THE DIMENSION VALUES.
*      THIS MUST BE DONE RIGHT TO LEFT SINCE THE ELEMENTS ARE
*      STORED ROW-WISE. THE SUBSCRIPTS ARE STACKED AS INTEGERS.
*
PRN16  MOV  AROFS(XL),WC     LOAD LENGTH OF BOUNDS INFO
       ICA  WC               ADJUST FOR ARPRO FIELD
       BTW  WC               CONVERT TO WORDS
       SUB  WC,WA            GET LINEAR ZERO-ORIGIN SUBSCRIPT
       MTI  WA               GET INTEGER VALUE
       LCT  WA,ARNDM(XL)     SET NUM OF DIMENSIONS AS LOOP COUNT
       ADD  AROFS(XL),XL     POINT PAST BOUNDS INFORMATION
       SUB  *ARLBD,XL        SET OK OFFSET FOR PROPER PTR LATER
*
*      LOOP TO STACK SUBSCRIPT OFFSETS
*
PRN17  SUB  *ARDMS,XL        POINT TO NEXT SET OF BOUNDS
       STI  PRNSI            SAVE CURRENT OFFSET
       RMI  ARDIM(XL)        GET REMAINDER ON DIVIDING BY DIMENS
       MFI  -(XS)            STORE ON STACK (ONE WORD)
       LDI  PRNSI            RELOAD ARGUMENT
       DVI  ARDIM(XL)        DIVIDE TO GET QUOTIENT
       BCT  WA,PRN17         LOOP TILL ALL STACKED
       ZER  XR               SET OFFSET TO FIRST SET OF BOUNDS
       LCT  WB,ARNDM(XL)     LOAD COUNT OF DIMS TO CONTROL LOOP
       BRN  PRN19            JUMP INTO PRINT LOOP
*
*      LOOP TO PRINT SUBSCRIPTS FROM STACK ADJUSTING BY ADDING
*      THE APPROPRIATE LOW BOUND VALUE FROM THE ARBLK
*
PRN18  MOV  =CH_CM,WA        LOAD A COMMA
       JSR  PRTCH            PRINT IT
*
*      MERGE HERE FIRST TIME IN (NO COMMA REQUIRED)
*
PRN19  MTI  (XS)+            LOAD SUBSCRIPT OFFSET AS INTEGER
       ADD  XR,XL            POINT TO CURRENT LBD
       ADI  ARLBD(XL)        ADD LBD TO GET SIGNED SUBSCRIPT
       SUB  XR,XL            POINT BACK TO START OF ARBLK
       JSR  PRTIN            PRINT SUBSCRIPT
       ADD  *ARDMS,XR        BUMP OFFSET TO NEXT BOUNDS
       BCT  WB,PRN18         LOOP BACK TILL ALL PRINTED
       BRN  PRN14            MERGE BACK TO PRINT RIGHT BRACKET
       ENP                   END PROCEDURE PRTNM
       EJC
*
*      PRTNV -- PRINT NAME VALUE
*
*      PRTNV IS USED BY THE TRACE AND DUMP ROUTINES TO PRINT
*      A LINE OF THE FORM
*
*      NAME = VALUE
*
*      NOTE THAT THE NAME INVOLVED CAN NEVER BE A PSEUDO-VAR
*
*      (XL)                  NAME BASE
*      (WA)                  NAME OFFSET
*      JSR  PRTNV            CALL TO PRINT NAME = VALUE
*      (WB,WC,RA)            DESTROYED
*
PRTNV  PRC  E,0              ENTRY POINT
       JSR  PRTNM            PRINT ARGUMENT NAME
       MOV  XR,-(XS)         SAVE ENTRY XR
       MOV  WA,-(XS)         SAVE NAME OFFSET (COLLECTABLE)
       MOV  =TMBEB,XR        POINT TO BLANK EQUAL BLANK
       JSR  PRTST            PRINT IT
       MOV  XL,XR            COPY NAME BASE
       ADD  WA,XR            POINT TO VALUE
       MOV  (XR),XR          LOAD VALUE POINTER
       JSR  PRTVL            PRINT VALUE
       JSR  PRTNL            TERMINATE LINE
       MOV  (XS)+,WA         RESTORE NAME OFFSET
       MOV  (XS)+,XR         RESTORE ENTRY XR
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE PRTNV
       EJC
*
*      PRTPG  -- PRINT A PAGE THROW
*
*      PRINTS A PAGE THROW OR A FEW BLANK LINES ON THE STANDARD
*      LISTING CHANNEL DEPENDING ON THE LISTING OPTIONS CHOSEN.
*
*      JSR  PRTPG            CALL FOR PAGE EJECT
*
PRTPG  PRC  E,0              ENTRY POINT
       BEQ  STAGE,=STGXT,PRP01 JUMP IF EXECUTION TIME
       BZE  LSTLC,PRP06      RETURN IF TOP OF PAGE ALREADY
       ZER  LSTLC            CLEAR LINE COUNT
*
*      CHECK TYPE OF LISTING
*
PRP01  MOV  XR,-(XS)         PRESERVE XR
       BNZ  PRSTD,PRP02      EJECT IF FLAG SET
       BNZ  PRICH,PRP03      JUMP IF INTERACTIVE LISTING CHANNEL
       BZE  PRECL,PRP03      JUMP IF COMPACT LISTING
*
*      PERFORM AN EJECT
*
PRP02  JSR  SYSEP            EJECT
       BRN  PRP04            MERGE
*
*      COMPACT OR INTERACTIVE CHANNEL LISTING. CANT PRINT
*      BLANKS UNTIL CHECK MADE FOR HEADERS PRINTED AND FLAG SET.
*
*
PRP03  MOV  HEADP,XR         REMEMBER HEADP
       MNZ  HEADP            SET TO AVOID REPEATED PRTPG CALLS
       JSR  PRTNL            PRINT BLANK LINE
       JSR  PRTNL            PRINT BLANK LINE
       JSR  PRTNL            PRINT BLANK LINE
       MOV  =NUM03,LSTLC     COUNT BLANK LINES
       MOV  XR,HEADP         RESTORE HEADER FLAG
       EJC
*
*      PRPTG (CONTINUED)
*
*      PRINT THE HEADING
*
PRP04  BNZ  HEADP,PRP05      JUMP IF HEADER LISTED
       MNZ  HEADP            MARK HEADERS PRINTED
       MOV  XL,-(XS)         KEEP XL
       MOV  =HEADR,XR        POINT TO LISTING HEADER
       JSR  PRTST            PLACE IT
       JSR  SYSID            GET SYSTEM IDENTIFICATION
       JSR  PRTST            APPEND EXTRA CHARS
       JSR  PRTNL            PRINT IT
       MOV  XL,XR            EXTRA HEADER LINE
       JSR  PRTST            PLACE IT
       JSR  PRTNL            PRINT IT
       JSR  PRTNL            PRINT A BLANK
       JSR  PRTNL            AND ANOTHER
       ADD  =NUM04,LSTLC     FOUR HEADER LINES PRINTED
       MOV  (XS)+,XL         RESTORE XL
*
*      MERGE IF HEADER NOT PRINTED
*
PRP05  MOV  (XS)+,XR         RESTORE XR
*
*      RETURN
*
PRP06  EXI                   RETURN
       ENP                   END PROCEDURE PRTPG
       EJC
*
*      PRTPS - PRINT PAGE WITH TEST FOR STANDARD LISTING OPTION
*
*      IF THE STANDARD LISTING OPTION IS SELECTED, INSIST THAT
*      AN EJECT BE DONE
*
*      JSR  PRTPS            CALL FOR EJECT
*
PRTPS  PRC  E,0              ENTRY POINT
       MOV  PRSTO,PRSTD      COPY OPTION FLAG
       JSR  PRTPG            PRINT PAGE
       ZER  PRSTD            CLEAR FLAG
       EXI                   RETURN
       ENP                   END PROCEDURE PRTPS
       EJC
*
*      PRTSN -- PRINT STATEMENT NUMBER
*
*      PRTSN IS USED TO INITIATE A PRINT TRACE LINE BY PRINTING
*      ASTERISKS AND THE CURRENT STATEMENT NUMBER. THE ACTUAL
*      FORMAT OF THE OUTPUT GENERATED IS.
*
*      ***NNNNN**** III.....IIII
*
*      NNNNN IS THE STATEMENT NUMBER WITH LEADING ZEROS REPLACED
*      BY ASTERISKS (E.G. *******9****)
*
*      III...III REPRESENTS A VARIABLE LENGTH OUTPUT CONSISTING
*      OF A NUMBER OF LETTER I CHARACTERS EQUAL TO FNCLEVEL.
*
*      JSR  PRTSN            CALL TO PRINT STATEMENT NUMBER
*      (WC)                  DESTROYED
*
PRTSN  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE ENTRY XR
       MOV  WA,PRSNA         SAVE ENTRY WA
       MOV  =TMASB,XR        POINT TO ASTERISKS
       JSR  PRTST            PRINT ASTERISKS
       MOV  =NUM04,PROFS     POINT INTO MIDDLE OF ASTERISKS
       MTI  KVSTN            LOAD STATEMENT NUMBER AS INTEGER
       JSR  PRTIN            PRINT INTEGER STATEMENT NUMBER
       MOV  =PRSNF,PROFS     POINT PAST ASTERISKS PLUS BLANK
       MOV  KVFNC,XR         GET FNCLEVEL
       MOV  =CH_LI,WA        SET LETTER I
*
*      LOOP TO GENERATE LETTER I FNCLEVEL TIMES
*
PRSN1  BZE  XR,PRSN2         JUMP IF ALL SET
       JSR  PRTCH            ELSE PRINT AN I
       DCV  XR               DECREMENT COUNTER
       BRN  PRSN1            LOOP BACK
*
*      MERGE WITH ALL LETTER I CHARACTERS GENERATED
*
PRSN2  MOV  =CH_BL,WA        GET BLANK
       JSR  PRTCH            PRINT BLANK
       MOV  PRSNA,WA         RESTORE ENTRY WA
       MOV  (XS)+,XR         RESTORE ENTRY XR
       EXI                   RETURN TO PRTSN CALLER
       ENP                   END PROCEDURE PRTSN
       EJC
*
*      PRTST -- PRINT STRING
*
*      PRTST PLACES A STRING OF CHARACTERS IN THE PRINT BUFFER
*
*      SEE PRTNL FOR GLOBAL LOCATIONS USED
*
*      NOTE THAT THE FIRST WORD OF THE BLOCK (NORMALLY B_SCL)
*      IS NOT USED AND NEED NOT BE SET CORRECTLY (SEE PRTVN)
*
*      (XR)                  STRING TO BE PRINTED
*      JSR  PRTST            CALL TO PRINT STRING
*      (PROFS)               UPDATED PAST CHARS PLACED
*
PRTST  PRC  R,0              ENTRY POINT
       BNZ  HEADP,PRST0      WERE HEADERS PRINTED
       JSR  PRTPS            NO - PRINT THEM
*
*      CALL SYSPR
*
PRST0  MOV  WA,PRSVA         SAVE WA
       MOV  WB,PRSVB         SAVE WB
       ZER  WB               SET CHARS PRINTED COUNT TO ZERO
*
*      LOOP TO PRINT SUCCESSIVE LINES FOR LONG STRING
*
PRST1  MOV  SCLEN(XR),WA     LOAD STRING LENGTH
       SUB  WB,WA            SUBTRACT COUNT OF CHARS ALREADY OUT
       BZE  WA,PRST4         JUMP TO EXIT IF NONE LEFT
       MOV  XL,-(XS)         ELSE STACK ENTRY XL
       MOV  XR,-(XS)         SAVE ARGUMENT
       MOV  XR,XL            COPY FOR EVENTUAL MOVE
       MOV  PRLEN,XR         LOAD PRINT BUFFER LENGTH
       SUB  PROFS,XR         GET CHARS LEFT IN PRINT BUFFER
       BNZ  XR,PRST2         SKIP IF ROOM LEFT ON THIS LINE
       JSR  PRTNL            ELSE PRINT THIS LINE
       MOV  PRLEN,XR         AND SET FULL WIDTH AVAILABLE
       EJC
*
*      PRTST (CONTINUED)
*
*      HERE WITH CHARS TO PRINT AND SOME ROOM IN BUFFER
*
PRST2  BLO  WA,XR,PRST3      JUMP IF ROOM FOR REST OF STRING
       MOV  XR,WA            ELSE SET TO FILL LINE
*
*      MERGE HERE WITH CHARACTER COUNT IN WA
*
PRST3  MOV  PRBUF,XR         POINT TO PRINT BUFFER
       PLC  XL,WB            POINT TO LOCATION IN STRING
       PSC  XR,PROFS         POINT TO LOCATION IN BUFFER
       ADD  WA,WB            BUMP STRING CHARS COUNT
       ADD  WA,PROFS         BUMP BUFFER POINTER
       MOV  WB,PRSVC         PRESERVE CHAR COUNTER
       MVC                   MOVE CHARACTERS TO BUFFER
       MOV  PRSVC,WB         RECOVER CHAR COUNTER
       MOV  (XS)+,XR         RESTORE ARGUMENT POINTER
       MOV  (XS)+,XL         RESTORE ENTRY XL
       BRN  PRST1            LOOP BACK TO TEST FOR MORE
*
*      HERE TO EXIT AFTER PRINTING STRING
*
PRST4  MOV  PRSVB,WB         RESTORE ENTRY WB
       MOV  PRSVA,WA         RESTORE ENTRY WA
       EXI                   RETURN TO PRTST CALLER
       ENP                   END PROCEDURE PRTST
       EJC
*
*      PRTTR -- PRINT TO TERMINAL
*
*      CALLED TO PRINT CONTENTS OF STANDARD PRINT BUFFER TO
*      ONLINE TERMINAL. CLEARS BUFFER DOWN AND RESETS PROFS.
*
*      JSR  PRTTR            CALL FOR PRINT
*      (WA,WB)               DESTROYED
*
PRTTR  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         SAVE XR
       JSR  PRTIC            PRINT BUFFER CONTENTS
       MOV  PRBUF,XR         POINT TO PRINT BFR TO CLEAR IT
       LCT  WA,PRLNW         GET BUFFER LENGTH
       ADD  *SCHAR,XR        POINT PAST SCBLK HEADER
       MOV  NULLW,WB         GET BLANKS
*
*      LOOP TO CLEAR BUFFER
*
PRTT1  MOV  WB,(XR)+         CLEAR A WORD
       BCT  WA,PRTT1         LOOP
       ZER  PROFS            RESET PROFS
       MOV  (XS)+,XR         RESTORE XR
       EXI                   RETURN
       ENP                   END PROCEDURE PRTTR
       EJC
*
*      PRTVL -- PRINT A VALUE
*
*      PRTVL PLACES AN APPROPRIATE CHARACTER REPRESENTATION OF
*      A DATA VALUE IN THE PRINT BUFFER FOR DUMP/TRACE USE.
*
*      (XR)                  VALUE TO BE PRINTED
*      JSR  PRTVL            CALL TO PRINT VALUE
*      (WA,WB,WC,RA)         DESTROYED
*
PRTVL  PRC  R,0              ENTRY POINT, RECURSIVE
       MOV  XL,-(XS)         SAVE ENTRY XL
       MOV  XR,-(XS)         SAVE ARGUMENT
       CHK                   CHECK FOR STACK OVERFLOW
*
*      LOOP BACK HERE AFTER FINDING A TRAP BLOCK (TRBLK)
*
PRV01  MOV  IDVAL(XR),PRVSI  COPY IDVAL (IF ANY)
       MOV  (XR),XL          LOAD FIRST WORD OF BLOCK
       LEI  XL               LOAD ENTRY POINT ID
       BSW  XL,BL__T,PRV02   SWITCH ON BLOCK TYPE
       IFF  BL_TR,PRV04      TRBLK
       IFF  BL_AR,PRV05      ARBLK
       IFF  BL_IC,PRV08      ICBLK
       IFF  BL_NM,PRV09      NMBLK
       IFF  BL_PD,PRV10      PDBLK
.IF    .CNRA
.ELSE
       IFF  BL_RC,PRV08      RCBLK
.FI
       IFF  BL_SC,PRV11      SCBLK
       IFF  BL_SE,PRV12      SEBLK
       IFF  BL_TB,PRV13      TBBLK
       IFF  BL_VC,PRV13      VCBLK
.IF    .CNBF
.ELSE
       IFF  BL_BC,PRV15      BCBLK
.FI
       ESW                   END OF SWITCH ON BLOCK TYPE
*
*      HERE FOR BLOCKS FOR WHICH WE JUST PRINT DATATYPE NAME
*
PRV02  JSR  DTYPE            GET DATATYPE NAME
       JSR  PRTST            PRINT DATATYPE NAME
*
*      COMMON EXIT POINT
*
PRV03  MOV  (XS)+,XR         RELOAD ARGUMENT
       MOV  (XS)+,XL         RESTORE XL
       EXI                   RETURN TO PRTVL CALLER
*
*      HERE FOR TRBLK
*
PRV04  MOV  TRVAL(XR),XR     LOAD REAL VALUE
       BRN  PRV01            AND LOOP BACK
       EJC
*
*      PRTVL (CONTINUED)
*
*      HERE FOR ARRAY (ARBLK)
*
*      PRINT ARRAY ( PROTOTYPE ) BLANK NUMBER IDVAL
*
PRV05  MOV  XR,XL            PRESERVE ARGUMENT
       MOV  =SCARR,XR        POINT TO DATATYPE NAME (ARRAY)
       JSR  PRTST            PRINT IT
       MOV  =CH_PP,WA        LOAD LEFT PAREN
       JSR  PRTCH            PRINT LEFT PAREN
       ADD  AROFS(XL),XL     POINT TO PROTOTYPE
       MOV  (XL),XR          LOAD PROTOTYPE
       JSR  PRTST            PRINT PROTOTYPE
*
*      VCBLK, TBBLK, BCBLK MERGE HERE FOR ) BLANK NUMBER IDVAL
*
PRV06  MOV  =CH_RP,WA        LOAD RIGHT PAREN
       JSR  PRTCH            PRINT RIGHT PAREN
*
*      PDBLK MERGES HERE TO PRINT BLANK NUMBER IDVAL
*
PRV07  MOV  =CH_BL,WA        LOAD BLANK
       JSR  PRTCH            PRINT IT
       MOV  =CH_NM,WA        LOAD NUMBER SIGN
       JSR  PRTCH            PRINT IT
       MTI  PRVSI            GET IDVAL
       JSR  PRTIN            PRINT ID NUMBER
       BRN  PRV03            BACK TO EXIT
*
*      HERE FOR INTEGER (ICBLK), REAL (RCBLK)
*
*      PRINT CHARACTER REPRESENTATION OF VALUE
*
PRV08  MOV  XR,-(XS)         STACK ARGUMENT FOR GTSTG
       JSR  GTSTG            CONVERT TO STRING
       PPM                   ERROR RETURN IS IMPOSSIBLE
       JSR  PRTST            PRINT THE STRING
       MOV  XR,DNAMP         DELETE GARBAGE STRING FROM STORAGE
       BRN  PRV03            BACK TO EXIT
       EJC
*
*      PRTVL (CONTINUED)
*
*      NAME (NMBLK)
*
*      FOR PSEUDO-VARIABLE, JUST PRINT DATATYPE NAME (NAME)
*      FOR ALL OTHER NAMES, PRINT DOT FOLLOWED BY NAME REP
*
PRV09  MOV  NMBAS(XR),XL     LOAD NAME BASE
       MOV  (XL),WA          LOAD FIRST WORD OF BLOCK
       BEQ  WA,=B_KVT,PRV02  JUST PRINT NAME IF KEYWORD
       BEQ  WA,=B_EVT,PRV02  JUST PRINT NAME IF EXPRESSION VAR
       MOV  =CH_DT,WA        ELSE GET DOT
       JSR  PRTCH            AND PRINT IT
       MOV  NMOFS(XR),WA     LOAD NAME OFFSET
       JSR  PRTNM            PRINT NAME
       BRN  PRV03            BACK TO EXIT
*
*      PROGRAM DATATYPE (PDBLK)
*
*      PRINT DATATYPE NAME CH_BL CH_NM IDVAL
*
PRV10  JSR  DTYPE            GET DATATYPE NAME
       JSR  PRTST            PRINT DATATYPE NAME
       BRN  PRV07            MERGE BACK TO PRINT ID
*
*      HERE FOR STRING (SCBLK)
*
*      PRINT QUOTE STRING-CHARACTERS QUOTE
*
PRV11  MOV  =CH_SQ,WA        LOAD SINGLE QUOTE
       JSR  PRTCH            PRINT QUOTE
       JSR  PRTST            PRINT STRING VALUE
       JSR  PRTCH            PRINT ANOTHER QUOTE
       BRN  PRV03            BACK TO EXIT
       EJC
*
*      PRTVL (CONTINUED)
*
*      HERE FOR SIMPLE EXPRESSION (SEBLK)
*
*      PRINT ASTERISK VARIABLE-NAME
*
PRV12  MOV  =CH_AS,WA        LOAD ASTERISK
       JSR  PRTCH            PRINT ASTERISK
       MOV  SEVAR(XR),XR     LOAD VARIABLE POINTER
       JSR  PRTVN            PRINT VARIABLE NAME
       BRN  PRV03            JUMP BACK TO EXIT
*
*      HERE FOR TABLE (TBBLK) AND ARRAY (VCBLK)
*
*      PRINT DATATYPE ( PROTOTYPE ) BLANK NUMBER IDVAL
*
PRV13  MOV  XR,XL            PRESERVE ARGUMENT
       JSR  DTYPE            GET DATATYPE NAME
       JSR  PRTST            PRINT DATATYPE NAME
       MOV  =CH_PP,WA        LOAD LEFT PAREN
       JSR  PRTCH            PRINT LEFT PAREN
       MOV  TBLEN(XL),WA     LOAD LENGTH OF BLOCK (=VCLEN)
       BTW  WA               CONVERT TO WORD COUNT
       SUB  =TBSI_,WA        ALLOW FOR STANDARD FIELDS
       BEQ  (XL),=B_TBT,PRV14 JUMP IF TABLE
       ADD  =VCTBD,WA        FOR VCBLK, ADJUST SIZE
*
*      PRINT PROTOTYPE
*
PRV14  MTI  WA               MOVE AS INTEGER
       JSR  PRTIN            PRINT INTEGER PROTOTYPE
       BRN  PRV06            MERGE BACK FOR REST
.IF    .CNBF
.ELSE
       EJC
*
*      PRTVL (CONTINUED)
*
*      HERE FOR BUFFER (BCBLK)
*
PRV15  MOV  XR,XL            PRESERVE ARGUMENT
       MOV  =SCBUF,XR        POINT TO DATATYPE NAME (BUFFER)
       JSR  PRTST            PRINT IT
       MOV  =CH_PP,WA        LOAD LEFT PAREN
       JSR  PRTCH            PRINT LEFT PAREN
       MOV  BCBUF(XL),XR     POINT TO BFBLK
       MTI  BFALC(XR)        LOAD ALLOCATION SIZE
       JSR  PRTIN            PRINT IT
       MOV  =CH_CM,WA        LOAD COMMA
       JSR  PRTCH            PRINT IT
       MTI  BCLEN(XL)        LOAD DEFINED LENGTH
       JSR  PRTIN            PRINT IT
       BRN  PRV06            MERGE TO FINISH UP
.FI
       ENP                   END PROCEDURE PRTVL
       EJC
*
*      PRTVN -- PRINT NATURAL VARIABLE NAME
*
*      PRTVN PRINTS THE NAME OF A NATURAL VARIABLE
*
*      (XR)                  POINTER TO VRBLK
*      JSR  PRTVN            CALL TO PRINT VARIABLE NAME
*
PRTVN  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         STACK VRBLK POINTER
       ADD  *VRSOF,XR        POINT TO POSSIBLE STRING NAME
       BNZ  SCLEN(XR),PRVN1  JUMP IF NOT SYSTEM VARIABLE
       MOV  VRSVO(XR),XR     POINT TO SVBLK WITH NAME
*
*      MERGE HERE WITH DUMMY SCBLK POINTER IN XR
*
PRVN1  JSR  PRTST            PRINT STRING NAME OF VARIABLE
       MOV  (XS)+,XR         RESTORE VRBLK POINTER
       EXI                   RETURN TO PRTVN CALLER
       ENP                   END PROCEDURE PRTVN
.IF    .CNRA
.ELSE
       EJC
*
*      RCBLD -- BUILD A REAL BLOCK
*
*      (RA)                  REAL VALUE FOR RCBLK
*      JSR  RCBLD            CALL TO BUILD REAL BLOCK
*      (XR)                  POINTER TO RESULT RCBLK
*      (WA)                  DESTROYED
*
RCBLD  PRC  E,0              ENTRY POINT
       MOV  DNAMP,XR         LOAD POINTER TO NEXT AVAILABLE LOC
       ADD  *RCSI_,XR        POINT PAST NEW RCBLK
       BLO  XR,DNAME,RCBL1   JUMP IF THERE IS ROOM
       MOV  *RCSI_,WA        ELSE LOAD RCBLK LENGTH
       JSR  ALLOC            USE STANDARD ALLOCATOR TO GET BLOCK
       ADD  WA,XR            POINT PAST BLOCK TO MERGE
*
*      MERGE HERE WITH XR POINTING PAST THE BLOCK OBTAINED
*
RCBL1  MOV  XR,DNAMP         SET NEW POINTER
       SUB  *RCSI_,XR        POINT BACK TO START OF BLOCK
       MOV  =B_RCL,(XR)      STORE TYPE WORD
       STR  RCVAL(XR)        STORE REAL VALUE IN RCBLK
       EXI                   RETURN TO RCBLD CALLER
       ENP                   END PROCEDURE RCBLD
.FI
       EJC
*
*      READR -- READ NEXT SOURCE IMAGE AT COMPILE TIME
*
*      READR IS USED TO READ THE NEXT SOURCE IMAGE. TO PROCESS
*      CONTINUATION CARDS PROPERLY, THE COMPILER MUST READ ONE
*      LINE AHEAD. THUS READR DOES NOT DESTROY THE CURRENT IMAGE
*      SEE ALSO THE NEXTS ROUTINE WHICH ACTUALLY GETS THE IMAGE.
*
*      JSR  READR            CALL TO READ NEXT IMAGE
*      (XR)                  PTR TO NEXT IMAGE (0 IF NONE)
*      (R_CNI)               COPY OF POINTER
*      (WA,WB,WC,XL)         DESTROYED
*
READR  PRC  E,0              ENTRY POINT
       MOV  R_CNI,XR         GET PTR TO NEXT IMAGE
       BNZ  XR,READ3         EXIT IF ALREADY READ
.IF    .CINC
       BNZ  CNIND,READA      IF WITHIN INCLUDE FILE
.FI
       BNE  STAGE,=STGIC,READ3 EXIT IF NOT INITIAL COMPILE
READA  MOV  CSWIN,WA         MAX READ LENGTH
       ZER  XL               CLEAR ANY DUD VALUE IN XL
       JSR  ALOCS            ALLOCATE BUFFER
       JSR  SYSRD            READ INPUT IMAGE
       PPM  READ4            JUMP IF EOF OR NEW FILE NAME
       ICV  RDNLN            INCREMENT NEXT LINE NUMBER
.IF    .CPOL
       DCV  POLCT            TEST IF TIME TO POLL INTERFACE
       BNZ  POLCT,READ0      NOT YET
       ZER  WA               =0 FOR POLL
       MOV  RDNLN,WB         LINE NUMBER
       JSR  SYSPL            ALLOW INTERACTIVE ACCESS
       ERR  320,User interrupt
       PPM                   SINGLE STEP
       PPM                   EXPRESSION EVALUATION
       MOV  WA,POLCS         NEW COUNTDOWN START VALUE
       MOV  WA,POLCT         NEW COUNTER VALUE
.FI
READ0  BLE  SCLEN(XR),CSWIN,READ1  USE SMALLER OF STRING LNTH ...
       MOV  CSWIN,SCLEN(XR)  ... AND XXX OF -INXXX
*
*      PERFORM THE TRIM
*
READ1  MNZ  WB               SET TRIMR TO PERFORM TRIM
       JSR  TRIMR            TRIM TRAILING BLANKS
*
*      MERGE HERE AFTER READ
*
READ2  MOV  XR,R_CNI         STORE COPY OF POINTER
*
*      MERGE HERE IF NO READ ATTEMPTED
*
READ3  EXI                   RETURN TO READR CALLER
.IF    .CSFN
*
*      HERE ON END OF FILE OR NEW SOURCE FILE NAME.
*      IF THIS IS A NEW SOURCE FILE NAME, THE R_SFN TABLE WILL
*      BE AUGMENTED WITH A NEW TABLE ENTRY CONSISTING OF THE
*      CURRENT COMPILER STATEMENT NUMBER AS SUBSCRIPT, AND THE
*      FILE NAME AS VALUE.
*
READ4  BZE  SCLEN(XR),READ5  JUMP IF TRUE END OF FILE
       ZER  WB               NEW SOURCE FILE NAME
       MOV  WB,RDNLN         RESTART LINE COUNTER FOR NEW FILE
       JSR  TRIMR            REMOVE UNUSED SPACE IN BLOCK
       JSR  NEWFN            RECORD NEW FILE NAME
       BRN  READA            NOW REISSUE READ FOR RECORD DATA
*
*      HERE ON END OF FILE
*
READ5  MOV  XR,DNAMP         POP UNUSED SCBLK
.IF    .CINC
       BZE  CNIND,READ6      JUMP IF NOT WITHIN AN INCLUDE FILE
       ZER  XL               EOF WITHIN INCLUDE FILE
       JSR  SYSIF            SWITCH STREAM BACK TO PREVIOUS FILE
       PPM
       MOV  CNIND,WA         RESTORE PREV LINE NUMBER, FILE NAME
       ADD  =VCVLB,WA        VECTOR OFFSET IN WORDS
       WTB  WA               CONVERT TO BYTES
       MOV  R_IFA,XR         FILE NAME ARRAY
       ADD  WA,XR            PTR TO ELEMENT
       MOV  (XR),R_SFC       CHANGE SOURCE FILE NAME
       MOV  =NULLS,(XR)      RELEASE SCBLK
       MOV  R_IFL,XR         LINE NUMBER ARRAY
       ADD  WA,XR            PTR TO ELEMENT
       MOV  (XR),XL          ICBLK CONTAINING SAVED LINE NUMBER
       LDI  ICVAL(XL)        LINE NUMBER INTEGER
       MFI  RDNLN            CHANGE SOURCE LINE NUMBER
       MOV  =INTON,(XR)      RELEASE ICBLK
       DCV  CNIND            DECREMENT NESTING LEVEL
       MOV  CMPSN,WB         CURRENT STATEMENT NUMBER
       ICV  WB               ANTICIPATE END OF PREVIOUS STMT
       MTI  WB               CONVERT TO INTEGER
       JSR  ICBLD            BUILD ICBLK FOR STMT NUMBER
       MOV  R_SFN,XL         FILE NAME TABLE
       MNZ  WB               LOOKUP STATEMENT NUMBER BY NAME
       JSR  TFIND            ALLOCATE NEW TEBLK
       PPM                   ALWAYS POSSIBLE TO ALLOCATE BLOCK
       MOV  R_SFC,TEVAL(XL)  RECORD FILE NAME AS ENTRY VALUE
       BEQ  STAGE,=STGIC,READA IF INITIAL COMPILE, REISSUE READ
       BNZ  CNIND,READA      STILL READING FROM INCLUDE FILE
*
*      OUTER NESTING OF EXECUTE-TIME COMPILE OF -INCLUDE
*      RESUME WITH ANY STRING REMAINING PRIOR TO -INCLUDE.
*
       MOV  R_ICI,XL         RESTORE CODE ARGUMENT STRING
       ZER  R_ICI            RELEASE ORIGINAL STRING
       MOV  CNSIL,WA         GET LENGTH OF STRING
       MOV  CNSPT,WB         OFFSET OF CHARACTERS LEFT
       SUB  WB,WA            NUMBER OF CHARACTERS LEFT
       MOV  WA,SCNIL         SET NEW SCAN LENGTH
       ZER  SCNPT            SCAN FROM START OF SUBSTRING
       JSR  SBSTR            CREATE SUBSTRING OF REMAINDER
       MOV  XR,R_CIM         SET SCAN IMAGE
       BRN  READ2            RETURN
.FI
.ELSE
*
*      HERE ON END OF FILE
*
READ4  MOV  XR,DNAMP         POP UNUSED SCBLK
.IF    .CINC
       BZE  CNIND,READ6      JUMP IF NOT WITHIN AN INCLUDE FILE
       ZER  XL               EOF WITHIN INCLUDE FILE
       JSR  SYSIF            SWITCH STREAM BACK TO PREVIOUS FILE
       PPM
       DCV  CNIND            DECREMENT NESTING LEVEL
       BRN  READA            REISSUE READ FROM PREVIOUS STREAM
.FI
.FI
READ6  ZER  XR               ZERO PTR AS RESULT
       BRN  READ2            MERGE
       ENP                   END PROCEDURE READR
       EJC
.IF    .C370
*
*      SBOOL-- SETUP FOR BOOLEAN OPERATIONS ON STRINGS
*
*      1(XS)                 FIRST ARGUMENT (IF TWO)
*      0(XS)                 SECOND ARGUMENT
*      (WB)                  NUMBER OF ARGUMENTS
*                             ZERO = ONE ARGUMENTS
*                             NON-ZERO = TWO ARGUMENTS
*      JSR  SBOOL            CALL TO PERFORM OPERATION
*      PPM  LOC              TRANSFER LOC FOR ARG1 NOT STRING
*      PPM  LOC              TRANSFER LOC FOR ARG2 NOT STRING
*      PPM  LOC              TRANSFER LOC ARG LENGTHS NOT EQUAL
*      PPM  LOC              TRANSFER LOC IF NULL STRING ARGS
*      (XS)                  ARGUMENTS POPPED, RESULT STACKED
*      (XL)                  ARG 1 CHARS TO OPERATE UPON
*      (XR)                  COPY OF ARG 2 IF TWO ARGUMENTS
*      (WA)                  NO. OF CHARACTERS TO PROCESS
*      (WC)                  NO. OF WORDS TO PROCESS (BCT READY)
*      (WB)                  DESTROYED
*
*      THE SECOND ARGUMENT STRING BLOCK IS COPIED TO A RESULT
*      BLOCK, AND POINTERS RETURNED TO ALLOW THE CALLER TO
*      PROCEED WITH THE DESIRED OPERATION IF TWO ARGUMENTS.
*
*      OPERATIONS LIKE AND/OR THAT DO NOT ALTER THE TRAILING
*      ZEROS IN THE LAST WORD OF THE STRING BLOCK CAN BE
*      PERFORMED A WORD AT A TIME.  OPERATIONS SUCH AS COMPL
*      MAY EITHER BE PERFORMED A CHARACTER AT A TIME OR WILL
*      HAVE TO ADJUST THE LAST WORD IF DONE A WORD AT A TIME.
*
SBOOL  PRC  N,3              ENTRY POINT
       JSR  GTSTG            CONVERT SECOND ARG TO STRING
       PPM  SBL05            JUMP IF SECOND ARG NOT STRING
       MOV  XR,XL            ELSE SAVE POINTER
       MOV  WA,WC            AND LENGTH
       BZE  WB,SBL01         ONLY ONE ARGUMENT IF COMPL
       JSR  GTSTG            CONVERT FIRST ARGUMENT TO STRING
       PPM  SBL04            JUMP IF NOT STRING
       BNE  WA,WC,SBL03      JUMP IF LENGTHS UNEQUAL
*
*      MERGE HERE IF ONLY ONE ARGUMENT
*
SBL01  MOV  XR,-(XS)         STACK FIRST ARGUMENT
       BZE  WC,SBL02         RETURN NULL IF NULL ARGUMENT
       JSR  ALOCS            ALLOCATE SPACE FOR COPY
       BZE  WB,SBL06         ONLY ONE ARGUMENT IF COMPL
       MOV  WC,WA            STRING LENGTH
       MOV  XR,WB            SAVE ADDRESS OF COPY
       CTB  WA,SCHAR         GET SCBLK LENGTH
       MVW                   MOVE ARG2 CONTENTS TO COPY
       MOV  WB,XR            RELOAD RESULT PTR
SBL06  MOV  (XS)+,XL         RELOAD FIRST ARGUMENT
       MOV  XR,-(XS)         STACK RESULT
       ADD  *SCHAR,XL        POINT TO CHARACTERS IN ARG 1 BLOCK
       ADD  *SCHAR,XR        POINT TO CHARACTERS IN RESULT BLOCK
       MOV  WC,WA            CHARACTER COUNT
       CTW  WC,0             NUMBER OF WORDS OF CHARACTERS
       LCT  WC,WC            PREPARE COUNTER
       EXI
*
*      HERE IF NULL ARGUMENTS
*
SBL02  EXI  4                TAKE NULL STRING EXIT
*
*      HERE IF ARGUMENT LENGTHS UNEQUAL
*
SBL03  EXI  3                TAKE UNEQUAL LENGTH ERROR EXIT
*
*      HERE IF FIRST ARG IS NOT A STRING
*
SBL04  EXI  1                TAKE BAD FIRST ARG ERROR EXIT
*
*      HERE FOR SECOND ARG NOT A STRING
*
SBL05  EXI  2                TAKE BAD SECOND ARG ERROR EXIT
       ENP                   END PROCEDURE SBOOL
       EJC
.FI
*
*      SBSTR -- BUILD A SUBSTRING
*
*      (XL)                  PTR TO SCBLK/BFBLK WITH CHARS
*      (WA)                  NUMBER OF CHARS IN SUBSTRING
*      (WB)                  OFFSET TO FIRST CHAR IN SCBLK
*      JSR  SBSTR            CALL TO BUILD SUBSTRING
*      (XR)                  PTR TO NEW SCBLK WITH SUBSTRING
*      (XL)                  ZERO
*      (WA,WB,WC,XL,IA)      DESTROYED
*
*      NOTE THAT SBSTR IS CALLED WITH A DUMMY STRING POINTER
*      (POINTING INTO A VRBLK OR SVBLK) TO COPY THE NAME OF A
*      VARIABLE AS A STANDARD STRING VALUE.
*
SBSTR  PRC  E,0              ENTRY POINT
       BZE  WA,SBST2         JUMP IF NULL SUBSTRING
       JSR  ALOCS            ELSE ALLOCATE SCBLK
       MOV  WC,WA            MOVE NUMBER OF CHARACTERS
       MOV  XR,WC            SAVE PTR TO NEW SCBLK
       PLC  XL,WB            PREPARE TO LOAD CHARS FROM OLD BLK
       PSC  XR               PREPARE TO STORE CHARS IN NEW BLK
       MVC                   MOVE CHARACTERS TO NEW STRING
       MOV  WC,XR            THEN RESTORE SCBLK POINTER
*
*      RETURN POINT
*
SBST1  ZER  XL               CLEAR GARBAGE POINTER IN XL
       EXI                   RETURN TO SBSTR CALLER
*
*      HERE FOR NULL SUBSTRING
*
SBST2  MOV  =NULLS,XR        SET NULL STRING AS RESULT
       BRN  SBST1            RETURN
       ENP                   END PROCEDURE SBSTR
       EJC
*
*      STGCC -- COMPUTE COUNTERS FOR STMT STARTUP TESTING
*
*      JSR  STGCC            CALL TO RECOMPUTE COUNTERS
*      (WA,WB)               DESTROYED
*
*      ON EXIT, STMCS AND STMCT CONTAIN THE COUNTER VALUE TO
*      TESTED IN STMGO.
*
*
STGCC  PRC  E,0
.IF    .CPOL
       MOV  POLCS,WA         ASSUME NO PROFILING OR STCOUNT TRACING
       MOV  =NUM01,WB        POLL EACH TIME POLCS EXPIRES
.ELSE
       MOV  CFP_M,WA         ASSUME NO PROFILING OR STCOUNT TRACING
.FI
       LDI  KVSTL            GET STMT LIMIT
       BNZ  KVPFL,STGC1      JUMP IF PROFILING ENABLED
       ILT  STGC3            NO STCOUNT TRACING IF NEGATIVE
       BZE  R_STC,STGC2      JUMP IF NOT STCOUNT TRACING
*
*      HERE IF PROFILING OR IF STCOUNT TRACING ENABLED
*
.IF    .CPOL
STGC1  MOV  WA,WB            COUNT POLCS TIMES WITHIN STMG
       MOV  =NUM01,WA        BREAK OUT OF STMGO ON EACH STMT
.ELSE
STGC1  MOV  =NUM01,WA        BREAK OUT OF STMGO ON EACH STMT
.FI
       BRN  STGC3
*
*      CHECK THAT STMCS DOES NOT EXCEED KVSTL
*
STGC2  MTI  WA               BREAKOUT COUNT START VALUE
       SBI  KVSTL            PROPOSED STMCS MINUS STMT LIMIT
       ILE  STGC3            JUMP IF STMT COUNT DOES NOT LIMIT
       LDI  KVSTL            STLIMIT LIMITS BREAKCOUNT COUNT
       MFI  WA               USE IT INSTEAD
*
*      RE-INITIALIZE COUNTER
*
STGC3  MOV  WA,STMCS         UPDATE BREAKOUT COUNT START VALUE
       MOV  WA,STMCT         RESET BREAKOUT COUNTER
.IF    .CPOL
       MOV  WB,POLCT
.FI
       EXI
       EJC
*
*      TFIND -- LOCATE TABLE ELEMENT
*
*      (XR)                  SUBSCRIPT VALUE FOR ELEMENT
*      (XL)                  POINTER TO TABLE
*      (WB)                  ZERO BY VALUE, NON-ZERO BY NAME
*      JSR  TFIND            CALL TO LOCATE ELEMENT
*      PPM  LOC              TRANSFER LOCATION IF ACCESS FAILS
*      (XR)                  ELEMENT VALUE (IF BY VALUE)
*      (XR)                  DESTROYED (IF BY NAME)
*      (XL,WA)               TEBLK NAME (IF BY NAME)
*      (XL,WA)               DESTROYED (IF BY VALUE)
*      (WC,RA)               DESTROYED
*
*      NOTE THAT IF A CALL BY VALUE SPECIFIES A NON-EXISTENT
*      SUBSCRIPT, THE DEFAULT VALUE IS RETURNED WITHOUT BUILDING
*      A NEW TEBLK.
*
TFIND  PRC  E,1              ENTRY POINT
       MOV  WB,-(XS)         SAVE NAME/VALUE INDICATOR
       MOV  XR,-(XS)         SAVE SUBSCRIPT VALUE
       MOV  XL,-(XS)         SAVE TABLE POINTER
       MOV  TBLEN(XL),WA     LOAD LENGTH OF TBBLK
       BTW  WA               CONVERT TO WORD COUNT
       SUB  =TBBUK,WA        GET NUMBER OF BUCKETS
       MTI  WA               CONVERT TO INTEGER VALUE
       STI  TFNSI            SAVE FOR LATER
       MOV  (XR),XL          LOAD FIRST WORD OF SUBSCRIPT
       LEI  XL               LOAD BLOCK ENTRY ID (BL_XX)
       BSW  XL,BL__D,TFN00   SWITCH ON BLOCK TYPE
       IFF  BL_IC,TFN02      JUMP IF INTEGER
.IF    .CNRA
.ELSE
       IFF  BL_RC,TFN02      REAL
.FI
       IFF  BL_P0,TFN03      JUMP IF PATTERN
       IFF  BL_P1,TFN03      JUMP IF PATTERN
       IFF  BL_P2,TFN03      JUMP IF PATTERN
       IFF  BL_NM,TFN04      JUMP IF NAME
       IFF  BL_SC,TFN05      JUMP IF STRING
       ESW                   END SWITCH ON BLOCK TYPE
*
*      HERE FOR BLOCKS FOR WHICH WE USE THE SECOND WORD OF THE
*      BLOCK AS THE HASH SOURCE (SEE BLOCK FORMATS FOR DETAILS).
*
TFN00  MOV  1(XR),WA         LOAD SECOND WORD
*
*      MERGE HERE WITH ONE WORD HASH SOURCE IN WA
*
TFN01  MTI  WA               CONVERT TO INTEGER
       BRN  TFN06            JUMP TO MERGE
       EJC
*
*      TFIND (CONTINUED)
*
*      HERE FOR INTEGER OR REAL
*      POSSIBILITY OF OVERFLOW EXIST ON TWOS COMPLEMENT
*      MACHINE IF HASH SOURCE IS MOST NEGATIVE INTEGER OR IS
*      A REAL HAVING THE SAME BIT PATTERN.
*
*
TFN02  LDI  1(XR)            LOAD VALUE AS HASH SOURCE
       IGE  TFN06            OK IF POSITIVE OR ZERO
       NGI                   MAKE POSITIVE
       IOV  TFN06            CLEAR POSSIBLE OVERFLOW
       BRN  TFN06            MERGE
*
*      FOR PATTERN, USE FIRST WORD (PCODE) AS SOURCE
*
TFN03  MOV  (XR),WA          LOAD FIRST WORD AS HASH SOURCE
       BRN  TFN01            MERGE BACK
*
*      FOR NAME, USE OFFSET AS HASH SOURCE
*
TFN04  MOV  NMOFS(XR),WA     LOAD OFFSET AS HASH SOURCE
       BRN  TFN01            MERGE BACK
*
*      HERE FOR STRING
*
TFN05  JSR  HASHS            CALL ROUTINE TO COMPUTE HASH
*
*      MERGE HERE WITH HASH SOURCE IN (IA)
*
TFN06  RMI  TFNSI            COMPUTE HASH INDEX BY REMAINDERING
       MFI  WC               GET AS ONE WORD INTEGER
       WTB  WC               CONVERT TO BYTE OFFSET
       MOV  (XS),XL          GET TABLE PTR AGAIN
       ADD  WC,XL            POINT TO PROPER BUCKET
       MOV  TBBUK(XL),XR     LOAD FIRST TEBLK POINTER
       BEQ  XR,(XS),TFN10    JUMP IF NO TEBLKS ON CHAIN
*
*      LOOP THROUGH TEBLKS ON HASH CHAIN
*
TFN07  MOV  XR,WB            SAVE TEBLK POINTER
       MOV  TESUB(XR),XR     LOAD SUBSCRIPT VALUE
       MOV  1(XS),XL         LOAD INPUT ARGUMENT SUBSCRIPT VAL
       JSR  IDENT            COMPARE THEM
       PPM  TFN08            JUMP IF EQUAL (IDENT)
*
*      HERE IF NO MATCH WITH THAT TEBLK
*
       MOV  WB,XL            RESTORE TEBLK POINTER
       MOV  TENXT(XL),XR     POINT TO NEXT TEBLK ON CHAIN
       BNE  XR,(XS),TFN07    JUMP IF THERE IS ONE
*
*      HERE IF NO MATCH WITH ANY TEBLK ON CHAIN
*
       MOV  *TENXT,WC        SET OFFSET TO LINK FIELD (XL BASE)
       BRN  TFN11            JUMP TO MERGE
       EJC
*
*      TFIND (CONTINUED)
*
*      HERE WE HAVE FOUND A MATCHING ELEMENT
*
TFN08  MOV  WB,XL            RESTORE TEBLK POINTER
       MOV  *TEVAL,WA        SET TEBLK NAME OFFSET
       MOV  2(XS),WB         RESTORE NAME/VALUE INDICATOR
       BNZ  WB,TFN09         JUMP IF CALLED BY NAME
       JSR  ACESS            ELSE GET VALUE
       PPM  TFN12            JUMP IF REFERENCE FAILS
       ZER  WB               RESTORE NAME/VALUE INDICATOR
*
*      COMMON EXIT FOR ENTRY FOUND
*
TFN09  ADD  *NUM03,XS        POP STACK ENTRIES
       EXI                   RETURN TO TFIND CALLER
*
*      HERE IF NO TEBLKS ON THE HASH CHAIN
*
TFN10  ADD  *TBBUK,WC        GET OFFSET TO BUCKET PTR
       MOV  (XS),XL          SET TBBLK PTR AS BASE
*
*      MERGE HERE WITH (XL,WC) BASE,OFFSET OF FINAL LINK
*
TFN11  MOV  (XS),XR          TBBLK POINTER
       MOV  TBINV(XR),XR     LOAD DEFAULT VALUE IN CASE
       MOV  2(XS),WB         LOAD NAME/VALUE INDICATOR
       BZE  WB,TFN09         EXIT WITH DEFAULT IF VALUE CALL
       MOV  XR,WB            COPY DEFAULT VALUE
*
*      HERE WE MUST BUILD A NEW TEBLK
*
       MOV  *TESI_,WA        SET SIZE OF TEBLK
       JSR  ALLOC            ALLOCATE TEBLK
       ADD  WC,XL            POINT TO HASH LINK
       MOV  XR,(XL)          LINK NEW TEBLK AT END OF CHAIN
       MOV  =B_TET,(XR)      STORE TYPE WORD
       MOV  WB,TEVAL(XR)     SET DEFAULT AS INITIAL VALUE
       MOV  (XS)+,TENXT(XR)  SET TBBLK PTR TO MARK END OF CHAIN
       MOV  (XS)+,TESUB(XR)  STORE SUBSCRIPT VALUE
       MOV  (XS)+,WB         RESTORE NAME/VALUE INDICATOR
       MOV  XR,XL            COPY TEBLK POINTER (NAME BASE)
       MOV  *TEVAL,WA        SET OFFSET
       EXI                   RETURN TO CALLER WITH NEW TEBLK
*
*      ACESS FAIL RETURN
*
TFN12  EXI  1                ALTERNATIVE RETURN
       ENP                   END PROCEDURE TFIND
       EJC
*
*      TMAKE -- MAKE NEW TABLE
*
*      (XL)                  INITIAL LOOKUP VALUE
*      (WC)                  NUMBER OF BUCKETS DESIRED
*      JSR  TMAKE            CALL TO MAKE NEW TABLE
*      (XR)                  NEW TABLE
*      (WA,WB)               DESTROYED
*
TMAKE  PRC  E,0
       MOV  WC,WA            COPY NUMBER OF HEADERS
       ADD  =TBSI_,WA        ADJUST FOR STANDARD FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       JSR  ALLOC            ALLOCATE SPACE FOR TBBLK
       MOV  XR,WB            COPY POINTER TO TBBLK
       MOV  =B_TBT,(XR)+     STORE TYPE WORD
       ZER  (XR)+            ZERO ID FOR THE MOMENT
       MOV  WA,(XR)+         STORE LENGTH (TBLEN)
       MOV  XL,(XR)+         STORE INITIAL LOOKUP VALUE
       LCT  WC,WC            SET LOOP COUNTER (NUM HEADERS)
*
*      LOOP TO INITIALIZE ALL BUCKET POINTERS
*
TMA01  MOV  WB,(XR)+         STORE TBBLK PTR IN BUCKET HEADER
       BCT  WC,TMA01         LOOP TILL ALL STORED
       MOV  WB,XR            RECALL POINTER TO TBBLK
       EXI
       ENP
       EJC
*
*      VMAKE -- CREATE A VECTOR
*
*      (WA)                  NUMBER OF ELEMENTS IN VECTOR
*      (XL)                  DEFAULT VALUE FOR VECTOR ELEMENTS
*      JSR  VMAKE            CALL TO CREATE VECTOR
*      PPM  LOC              IF VECTOR TOO LARGE
*
*      (XR)                  POINTER TO VCBLK
*      (WA,WB,WC,XL)         DESTROYED
*
VMAKE  PRC  E,1              ENTRY POINT
       LCT  WB,WA            COPY ELEMENTS FOR LOOP LATER ON
       ADD  =VCSI_,WA        ADD SPACE FOR STANDARD FIELDS
       WTB  WA               CONVERT LENGTH TO BYTES
       BGT  WA,MXLEN,VMAK2   FAIL IF TOO LARGE
       JSR  ALLOC            ALLOCATE SPACE FOR VCBLK
       MOV  =B_VCT,(XR)      STORE TYPE WORD
       ZER  IDVAL(XR)        INITIALIZE IDVAL
       MOV  WA,VCLEN(XR)     SET LENGTH
       MOV  XL,WC            COPY DEFAULT VALUE
       MOV  XR,XL            COPY VCBLK POINTER
       ADD  *VCVLS,XL        POINT TO FIRST ELEMENT VALUE
*
*      LOOP TO SET VECTOR ELEMENTS TO DEFAULT VALUE
*
VMAK1  MOV  WC,(XL)+         STORE ONE VALUE
       BCT  WB,VMAK1         LOOP TILL ALL STORED
       EXI                   SUCCESS RETURN
*
*      HERE IF DESIRED VECTOR SIZE TOO LARGE
*
VMAK2  EXI  1                FAIL RETURN
       ENP
       EJC
*
*      SCANE -- SCAN AN ELEMENT
*
*      SCANE IS CALLED AT COMPILE TIME (BY EXPAN ,CMPIL,CNCRD)
*      TO SCAN ONE ELEMENT FROM THE INPUT IMAGE.
*
*      (SCNCC)               NON-ZERO IF CALLED FROM CNCRD
*      JSR  SCANE            CALL TO SCAN ELEMENT
*      (XR)                  RESULT POINTER (SEE BELOW)
*      (XL)                  SYNTAX TYPE CODE (T_XXX)
*
*      THE FOLLOWING GLOBAL LOCATIONS ARE USED.
*
*      R_CIM                 POINTER TO STRING BLOCK (SCBLK)
*                            FOR CURRENT INPUT IMAGE.
*
*      R_CNI                 POINTER TO NEXT INPUT IMAGE STRING
*                            POINTER (ZERO IF NONE).
*
*      R_SCP                 SAVE POINTER (EXIT XR) FROM LAST
*                            CALL IN CASE RESCAN IS SET.
*
*      SCNBL                 THIS LOCATION IS SET NON-ZERO ON
*                            EXIT IF SCANE SCANNED PAST BLANKS
*                            BEFORE LOCATING THE CURRENT ELEMENT
*                            THE END OF A LINE COUNTS AS BLANKS.
*
*      SCNCC                 CNCRD SETS THIS NON-ZERO TO SCAN
*                            CONTROL CARD NAMES AND CLEARS IT
*                            ON RETURN
*
*      SCNIL                 LENGTH OF CURRENT INPUT IMAGE
*
*      SCNGO                 IF SET NON-ZERO ON ENTRY, F AND S
*                            ARE RETURNED AS SEPARATE SYNTAX
*                            TYPES (NOT LETTERS) (GOTO PRO-
*                            CESSING). SCNGO IS RESET ON EXIT.
*
*      SCNPT                 OFFSET TO CURRENT LOC IN R_CIM
*
*      SCNRS                 IF SET NON-ZERO ON ENTRY, SCANE
*                            RETURNS THE SAME RESULT AS ON THE
*                            LAST CALL (RESCAN). SCNRS IS RESET
*                            ON EXIT FROM ANY CALL TO SCANE.
*
*      SCNTP                 SAVE SYNTAX TYPE FROM LAST
*                            CALL (IN CASE RESCAN IS SET).
       EJC
*
*      SCANE (CONTINUED)
*
*
*
*      ELEMENT SCANNED       XL        XR
*      ---------------       --        --
*
*      CONTROL CARD NAME     0         POINTER TO SCBLK FOR NAME
*
*      UNARY OPERATOR        T_UOP     PTR TO OPERATOR DVBLK
*
*      LEFT PAREN            T_LPR     T_LPR
*
*      LEFT BRACKET          T_LBR     T_LBR
*
*      COMMA                 T_CMA     T_CMA
*
*      FUNCTION CALL         T_FNC     PTR TO FUNCTION VRBLK
*
*      VARIABLE              T_VAR     PTR TO VRBLK
*
*      STRING CONSTANT       T_CON     PTR TO SCBLK
*
*      INTEGER CONSTANT      T_CON     PTR TO ICBLK
*
.IF    .CNRA
.ELSE
*      REAL CONSTANT         T_CON     PTR TO RCBLK
*
.FI
*      BINARY OPERATOR       T_BOP     PTR TO OPERATOR DVBLK
*
*      RIGHT PAREN           T_RPR     T_RPR
*
*      RIGHT BRACKET         T_RBR     T_RBR
*
*      COLON                 T_COL     T_COL
*
*      SEMI-COLON            T_SMC     T_SMC
*
*      F (SCNGO NE 0)        T_FGO     T_FGO
*
*      S (SCNGO NE 0)        T_SGO     T_SGO
       EJC
*
*      SCANE (CONTINUED)
*
*      ENTRY POINT
*
SCANE  PRC  E,0              ENTRY POINT
       ZER  SCNBL            RESET BLANKS FLAG
       MOV  WA,SCNSA         SAVE WA
       MOV  WB,SCNSB         SAVE WB
       MOV  WC,SCNSC         SAVE WC
       BZE  SCNRS,SCN03      JUMP IF NO RESCAN
*
*      HERE FOR RESCAN REQUEST
*
       MOV  SCNTP,XL         SET PREVIOUS RETURNED SCAN TYPE
       MOV  R_SCP,XR         SET PREVIOUS RETURNED POINTER
       ZER  SCNRS            RESET RESCAN SWITCH
       BRN  SCN13            JUMP TO EXIT
*
*      COME HERE TO READ NEW IMAGE TO TEST FOR CONTINUATION
*
SCN01  JSR  READR            READ NEXT IMAGE
       MOV  *DVUBS,WB        SET WB FOR NOT READING NAME
       BZE  XR,SCN30         TREAT AS SEMI-COLON IF NONE
       PLC  XR               ELSE POINT TO FIRST CHARACTER
       LCH  WC,(XR)          LOAD FIRST CHARACTER
       BEQ  WC,=CH_DT,SCN02  JUMP IF DOT FOR CONTINUATION
       BNE  WC,=CH_PL,SCN30  ELSE TREAT AS SEMICOLON UNLESS PLUS
*
*      HERE FOR CONTINUATION LINE
*
SCN02  JSR  NEXTS            ACQUIRE NEXT SOURCE IMAGE
       MOV  =NUM01,SCNPT     SET SCAN POINTER PAST CONTINUATION
       MNZ  SCNBL            SET BLANKS FLAG
       EJC
*
*      SCANE (CONTINUED)
*
*      MERGE HERE TO SCAN NEXT ELEMENT ON CURRENT LINE
*
SCN03  MOV  SCNPT,WA         LOAD CURRENT OFFSET
       BEQ  WA,SCNIL,SCN01   CHECK CONTINUATION IF END
       MOV  R_CIM,XL         POINT TO CURRENT LINE
       PLC  XL,WA            POINT TO CURRENT CHARACTER
       MOV  WA,SCNSE         SET START OF ELEMENT LOCATION
       MOV  =OPDVS,WC        POINT TO OPERATOR DV LIST
       MOV  *DVUBS,WB        SET CONSTANT FOR OPERATOR CIRCUIT
       BRN  SCN06            START SCANNING
*
*      LOOP HERE TO IGNORE LEADING BLANKS AND TABS
*
SCN05  BZE  WB,SCN10         JUMP IF TRAILING
       ICV  SCNSE            INCREMENT START OF ELEMENT
       BEQ  WA,SCNIL,SCN01   JUMP IF END OF IMAGE
       MNZ  SCNBL            NOTE BLANKS SEEN
*
*      THE FOLLOWING JUMP IS USED REPEATEDLY FOR SCANNING OUT
*      THE CHARACTERS OF A NUMERIC CONSTANT OR VARIABLE NAME.
*      THE REGISTERS ARE USED AS FOLLOWS.
*
*      (XR)                  SCRATCH
*      (XL)                  PTR TO NEXT CHARACTER
*      (WA)                  CURRENT SCAN OFFSET
*      (WB)                  *DVUBS (0 IF SCANNING NAME,CONST)
*      (WC)                  =OPDVS (0 IF SCANNING CONSTANT)
*
SCN06  LCH  XR,(XL)+         GET NEXT CHARACTER
       ICV  WA               BUMP SCAN OFFSET
       MOV  WA,SCNPT         STORE OFFSET PAST CHAR SCANNED
.IF    .CUCF
       BSW  XR,CFP_U,SCN07   SWITCH ON SCANNED CHARACTER
.ELSE
       BSW  XR,CFP_A,SCN07   SWITCH ON SCANNED CHARACTER
.FI
*
*      SWITCH TABLE FOR SWITCH ON CHARACTER
*
       IFF  CH_BL,SCN05      BLANK
.IF    .CAHT
       IFF  CH_HT,SCN05      HORIZONTAL TAB
.FI
.IF    .CAVT
       IFF  CH_VT,SCN05      VERTICAL TAB
.FI
.IF    .CAEX
       IFF  CH_EY,SCN37      UP ARROW
.FI
       IFF  CH_D0,SCN08      DIGIT 0
       IFF  CH_D1,SCN08      DIGIT 1
       IFF  CH_D2,SCN08      DIGIT 2
       IFF  CH_D3,SCN08      DIGIT 3
       IFF  CH_D4,SCN08      DIGIT 4
       IFF  CH_D5,SCN08      DIGIT 5
       IFF  CH_D6,SCN08      DIGIT 6
       IFF  CH_D7,SCN08      DIGIT 7
       IFF  CH_D8,SCN08      DIGIT 8
       IFF  CH_D9,SCN08      DIGIT 9
       EJC
*
*      SCANE (CONTINUED)
*
       IFF  CH_LA,SCN09      LETTER A
       IFF  CH_LB,SCN09      LETTER B
       IFF  CH_LC,SCN09      LETTER C
       IFF  CH_LD,SCN09      LETTER D
       IFF  CH_LE,SCN09      LETTER E
       IFF  CH_LG,SCN09      LETTER G
       IFF  CH_LH,SCN09      LETTER H
       IFF  CH_LI,SCN09      LETTER I
       IFF  CH_LJ,SCN09      LETTER J
       IFF  CH_LK,SCN09      LETTER K
       IFF  CH_LL,SCN09      LETTER L
       IFF  CH_LM,SCN09      LETTER M
       IFF  CH_LN,SCN09      LETTER N
       IFF  CH_LO,SCN09      LETTER O
       IFF  CH_LP,SCN09      LETTER P
       IFF  CH_LQ,SCN09      LETTER Q
       IFF  CH_LR,SCN09      LETTER R
       IFF  CH_LT,SCN09      LETTER T
       IFF  CH_LU,SCN09      LETTER U
       IFF  CH_LV,SCN09      LETTER V
       IFF  CH_LW,SCN09      LETTER W
       IFF  CH_LX,SCN09      LETTER X
       IFF  CH_LY,SCN09      LETTER Y
       IFF  CH_L_,SCN09      LETTER Z
.IF    .CASL
       IFF  CH__A,SCN09      SHIFTED A
       IFF  CH__B,SCN09      SHIFTED B
       IFF  CH__C,SCN09      SHIFTED C
       IFF  CH__D,SCN09      SHIFTED D
       IFF  CH__E,SCN09      SHIFTED E
       IFF  CH__F,SCN20      SHIFTED F
       IFF  CH__G,SCN09      SHIFTED G
       IFF  CH__H,SCN09      SHIFTED H
       IFF  CH__I,SCN09      SHIFTED I
       IFF  CH__J,SCN09      SHIFTED J
       IFF  CH__K,SCN09      SHIFTED K
       IFF  CH__L,SCN09      SHIFTED L
       IFF  CH__M,SCN09      SHIFTED M
       IFF  CH__N,SCN09      SHIFTED N
       IFF  CH__O,SCN09      SHIFTED O
       IFF  CH__P,SCN09      SHIFTED P
       IFF  CH__Q,SCN09      SHIFTED Q
       IFF  CH__R,SCN09      SHIFTED R
       IFF  CH__S,SCN21      SHIFTED S
       IFF  CH__T,SCN09      SHIFTED T
       IFF  CH__U,SCN09      SHIFTED U
       IFF  CH__V,SCN09      SHIFTED V
       IFF  CH__W,SCN09      SHIFTED W
       IFF  CH__X,SCN09      SHIFTED X
       IFF  CH__Y,SCN09      SHIFTED Y
       IFF  CH___,SCN09      SHIFTED Z
.FI
       EJC
*
*      SCANE (CONTINUED)
*
       IFF  CH_SQ,SCN16      SINGLE QUOTE
       IFF  CH_DQ,SCN17      DOUBLE QUOTE
       IFF  CH_LF,SCN20      LETTER F
       IFF  CH_LS,SCN21      LETTER S
       IFF  CH_UN,SCN24      UNDERLINE
       IFF  CH_PP,SCN25      LEFT PAREN
       IFF  CH_RP,SCN26      RIGHT PAREN
       IFF  CH_RB,SCN27      RIGHT BRACKET
       IFF  CH_BB,SCN28      LEFT BRACKET
       IFF  CH_CB,SCN27      RIGHT BRACKET
       IFF  CH_OB,SCN28      LEFT BRACKET
       IFF  CH_CL,SCN29      COLON
       IFF  CH_SM,SCN30      SEMI-COLON
       IFF  CH_CM,SCN31      COMMA
       IFF  CH_DT,SCN32      DOT
       IFF  CH_PL,SCN33      PLUS
       IFF  CH_MN,SCN34      MINUS
       IFF  CH_NT,SCN35      NOT
       IFF  CH_DL,SCN36      DOLLAR
       IFF  CH_EX,SCN37      EXCLAMATION MARK
       IFF  CH_PC,SCN38      PERCENT
       IFF  CH_SL,SCN40      SLASH
       IFF  CH_NM,SCN41      NUMBER SIGN
       IFF  CH_AT,SCN42      AT
       IFF  CH_BR,SCN43      VERTICAL BAR
       IFF  CH_AM,SCN44      AMPERSAND
       IFF  CH_QU,SCN45      QUESTION MARK
       IFF  CH_EQ,SCN46      EQUAL
       IFF  CH_AS,SCN49      ASTERISK
       ESW                   END SWITCH ON CHARACTER
*
*      HERE FOR ILLEGAL CHARACTER (UNDERLINE MERGES)
*
SCN07  BZE  WB,SCN10         JUMP IF SCANNING NAME OR CONSTANT
       ERB  230,Syntax error: Illegal character
       EJC
*
*      SCANE (CONTINUED)
*
*      HERE FOR DIGITS 0-9
*
SCN08  BZE  WB,SCN09         KEEP SCANNING IF NAME/CONSTANT
       ZER  WC               ELSE SET FLAG FOR SCANNING CONSTANT
*
*      HERE FOR LETTER. LOOP HERE WHEN SCANNING NAME/CONSTANT
*
SCN09  BEQ  WA,SCNIL,SCN11   JUMP IF END OF IMAGE
       ZER  WB               SET FLAG FOR SCANNING NAME/CONST
       BRN  SCN06            MERGE BACK TO CONTINUE SCAN
*
*      COME HERE FOR DELIMITER ENDING NAME OR CONSTANT
*
SCN10  DCV  WA               RESET OFFSET TO POINT TO DELIMITER
*
*      COME HERE AFTER FINISHING SCAN OF NAME OR CONSTANT
*
SCN11  MOV  WA,SCNPT         STORE UPDATED SCAN OFFSET
       MOV  SCNSE,WB         POINT TO START OF ELEMENT
       SUB  WB,WA            GET NUMBER OF CHARACTERS
       MOV  R_CIM,XL         POINT TO LINE IMAGE
       BNZ  WC,SCN15         JUMP IF NAME
*
*      HERE AFTER SCANNING OUT NUMERIC CONSTANT
*
       JSR  SBSTR            GET STRING FOR CONSTANT
       MOV  XR,DNAMP         DELETE FROM STORAGE (NOT NEEDED)
       JSR  GTNUM            CONVERT TO NUMERIC
       PPM  SCN14            JUMP IF CONVERSION FAILURE
*
*      MERGE HERE TO EXIT WITH CONSTANT
*
SCN12  MOV  =T_CON,XL        SET RESULT TYPE OF CONSTANT
       EJC
*
*      SCANE (CONTINUED)
*
*      COMMON EXIT POINT (XR,XL) SET
*
SCN13  MOV  SCNSA,WA         RESTORE WA
       MOV  SCNSB,WB         RESTORE WB
       MOV  SCNSC,WC         RESTORE WC
       MOV  XR,R_SCP         SAVE XR IN CASE RESCAN
       MOV  XL,SCNTP         SAVE XL IN CASE RESCAN
       ZER  SCNGO            RESET POSSIBLE GOTO FLAG
       EXI                   RETURN TO SCANE CALLER
*
*      HERE IF CONVERSION ERROR ON NUMERIC ITEM
*
SCN14  ERB  231,Syntax error: Invalid numeric item
*
*      HERE AFTER SCANNING OUT VARIABLE NAME
*
SCN15  JSR  SBSTR            BUILD STRING NAME OF VARIABLE
       BNZ  SCNCC,SCN13      RETURN IF CNCRD CALL
       JSR  GTNVR            LOCATE/BUILD VRBLK
       PPM                   DUMMY (UNUSED) ERROR RETURN
       MOV  =T_VAR,XL        SET TYPE AS VARIABLE
       BRN  SCN13            BACK TO EXIT
*
*      HERE FOR SINGLE QUOTE (START OF STRING CONSTANT)
*
SCN16  BZE  WB,SCN10         TERMINATOR IF SCANNING NAME OR CNST
       MOV  =CH_SQ,WB        SET TERMINATOR AS SINGLE QUOTE
       BRN  SCN18            MERGE
*
*      HERE FOR DOUBLE QUOTE (START OF STRING CONSTANT)
*
SCN17  BZE  WB,SCN10         TERMINATOR IF SCANNING NAME OR CNST
       MOV  =CH_DQ,WB        SET DOUBLE QUOTE TERMINATOR, MERGE
*
*      LOOP TO SCAN OUT STRING CONSTANT
*
SCN18  BEQ  WA,SCNIL,SCN19   ERROR IF END OF IMAGE
       LCH  WC,(XL)+         ELSE LOAD NEXT CHARACTER
       ICV  WA               BUMP OFFSET
       BNE  WC,WB,SCN18      LOOP BACK IF NOT TERMINATOR
       EJC
*
*      SCANE (CONTINUED)
*
*      HERE AFTER SCANNING OUT STRING CONSTANT
*
       MOV  SCNPT,WB         POINT TO FIRST CHARACTER
       MOV  WA,SCNPT         SAVE OFFSET PAST FINAL QUOTE
       DCV  WA               POINT BACK PAST LAST CHARACTER
       SUB  WB,WA            GET NUMBER OF CHARACTERS
       MOV  R_CIM,XL         POINT TO INPUT IMAGE
       JSR  SBSTR            BUILD SUBSTRING VALUE
       BRN  SCN12            BACK TO EXIT WITH CONSTANT RESULT
*
*      HERE IF NO MATCHING QUOTE FOUND
*
SCN19  MOV  WA,SCNPT         SET UPDATED SCAN POINTER
       ERB  232,Syntax error: Unmatched string quote
*
*      HERE FOR F (POSSIBLE FAILURE GOTO)
*
SCN20  MOV  =T_FGO,XR        SET RETURN CODE FOR FAIL GOTO
       BRN  SCN22            JUMP TO MERGE
*
*      HERE FOR S (POSSIBLE SUCCESS GOTO)
*
SCN21  MOV  =T_SGO,XR        SET SUCCESS GOTO AS RETURN CODE
*
*      SPECIAL GOTO CASES MERGE HERE
*
SCN22  BZE  SCNGO,SCN09      TREAT AS NORMAL LETTER IF NOT GOTO
*
*      MERGE HERE FOR SPECIAL CHARACTER EXIT
*
SCN23  BZE  WB,SCN10         JUMP IF END OF NAME/CONSTANT
       MOV  XR,XL            ELSE COPY CODE
       BRN  SCN13            AND JUMP TO EXIT
*
*      HERE FOR UNDERLINE
*
SCN24  BZE  WB,SCN09         PART OF NAME IF SCANNING NAME
       BRN  SCN07            ELSE ILLEGAL
       EJC
*
*      SCANE (CONTINUED)
*
*      HERE FOR LEFT PAREN
*
SCN25  MOV  =T_LPR,XR        SET LEFT PAREN RETURN CODE
       BNZ  WB,SCN23         RETURN LEFT PAREN UNLESS NAME
       BZE  WC,SCN10         DELIMITER IF SCANNING CONSTANT
*
*      HERE FOR LEFT PAREN AFTER NAME (FUNCTION CALL)
*
       MOV  SCNSE,WB         POINT TO START OF NAME
       MOV  WA,SCNPT         SET POINTER PAST LEFT PAREN
       DCV  WA               POINT BACK PAST LAST CHAR OF NAME
       SUB  WB,WA            GET NAME LENGTH
       MOV  R_CIM,XL         POINT TO INPUT IMAGE
       JSR  SBSTR            GET STRING NAME FOR FUNCTION
       JSR  GTNVR            LOCATE/BUILD VRBLK
       PPM                   DUMMY (UNUSED) ERROR RETURN
       MOV  =T_FNC,XL        SET CODE FOR FUNCTION CALL
       BRN  SCN13            BACK TO EXIT
*
*      PROCESSING FOR SPECIAL CHARACTERS
*
SCN26  MOV  =T_RPR,XR        RIGHT PAREN, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
*
SCN27  MOV  =T_RBR,XR        RIGHT BRACKET, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
*
SCN28  MOV  =T_LBR,XR        LEFT BRACKET, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
*
SCN29  MOV  =T_COL,XR        COLON, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
*
SCN30  MOV  =T_SMC,XR        SEMI-COLON, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
*
SCN31  MOV  =T_CMA,XR        COMMA, SET CODE
       BRN  SCN23            TAKE SPECIAL CHARACTER EXIT
       EJC
*
*      SCANE (CONTINUED)
*
*      HERE FOR OPERATORS. ON ENTRY, WC POINTS TO THE TABLE OF
*      OPERATOR DOPE VECTORS AND WB IS THE INCREMENT TO STEP
*      TO THE NEXT PAIR (BINARY/UNARY) OF DOPE VECTORS IN THE
*      LIST. ON REACHING SCN46, THE POINTER HAS BEEN ADJUSTED TO
*      POINT TO THE APPROPRIATE PAIR OF DOPE VECTORS.
*      THE FIRST THREE ENTRIES ARE SPECIAL SINCE THEY CAN OCCUR
*      AS PART OF A VARIABLE NAME (.) OR CONSTANT (.+-).
*
SCN32  BZE  WB,SCN09         DOT CAN BE PART OF NAME OR CONSTANT
       ADD  WB,WC            ELSE BUMP POINTER
*
SCN33  BZE  WC,SCN09         PLUS CAN BE PART OF CONSTANT
       BZE  WB,SCN48         PLUS CANNOT BE PART OF NAME
       ADD  WB,WC            ELSE BUMP POINTER
*
SCN34  BZE  WC,SCN09         MINUS CAN BE PART OF CONSTANT
       BZE  WB,SCN48         MINUS CANNOT BE PART OF NAME
       ADD  WB,WC            ELSE BUMP POINTER
*
SCN35  ADD  WB,WC            NOT
SCN36  ADD  WB,WC            DOLLAR
SCN37  ADD  WB,WC            EXCLAMATION
SCN38  ADD  WB,WC            PERCENT
SCN39  ADD  WB,WC            ASTERISK
SCN40  ADD  WB,WC            SLASH
SCN41  ADD  WB,WC            NUMBER SIGN
SCN42  ADD  WB,WC            AT SIGN
SCN43  ADD  WB,WC            VERTICAL BAR
SCN44  ADD  WB,WC            AMPERSAND
SCN45  ADD  WB,WC            QUESTION MARK
*
*      ALL OPERATORS COME HERE (EQUAL MERGES DIRECTLY)
*      (WC) POINTS TO THE BINARY/UNARY PAIR OF OPERATOR DVBLKS.
*
SCN46  BZE  WB,SCN10         OPERATOR TERMINATES NAME/CONSTANT
       MOV  WC,XR            ELSE COPY DV POINTER
       LCH  WC,(XL)          LOAD NEXT CHARACTER
       MOV  =T_BOP,XL        SET BINARY OP IN CASE
       BEQ  WA,SCNIL,SCN47   SHOULD BE BINARY IF IMAGE END
       BEQ  WC,=CH_BL,SCN47  SHOULD BE BINARY IF FOLLOWED BY BLK
.IF    .CAHT
       BEQ  WC,=CH_HT,SCN47  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WC,=CH_VT,SCN47  JUMP IF VERTICAL TAB
.FI
       BEQ  WC,=CH_SM,SCN47  SEMICOLON CAN IMMEDIATELY FOLLOW =
       BEQ  WC,=CH_CL,SCN47  COLON CAN IMMEDIATELY FOLLOW =
       BEQ  WC,=CH_RP,SCN47  RIGHT PAREN CAN IMMEDIATELY FOLLOW =
       BEQ  WC,=CH_RB,SCN47  RIGHT BRACKET CAN IMMEDIATELY FOLLOW =
       BEQ  WC,=CH_CB,SCN47  RIGHT BRACKET CAN IMMEDIATELY FOLLOW =
*
*      HERE FOR UNARY OPERATOR
*
       ADD  *DVBS_,XR        POINT TO DV FOR UNARY OP
       MOV  =T_UOP,XL        SET TYPE FOR UNARY OPERATOR
       BLE  SCNTP,=T_UOK,SCN13 OK UNARY IF OK PRECEDING ELEMENT
       EJC
*
*      SCANE (CONTINUED)
*
*      MERGE HERE TO REQUIRE PRECEDING BLANKS
*
SCN47  BNZ  SCNBL,SCN13      ALL OK IF PRECEDING BLANKS, EXIT
*
*      FAIL OPERATOR IN THIS POSITION
*
SCN48  ERB  233,Syntax error: Invalid use of operator
*
*      HERE FOR ASTERISK, COULD BE ** SUBSTITUTE FOR EXCLAMATION
*
SCN49  BZE  WB,SCN10         END OF NAME IF SCANNING NAME
       BEQ  WA,SCNIL,SCN39   NOT ** IF * AT IMAGE END
       MOV  WA,XR            ELSE SAVE OFFSET PAST FIRST *
       MOV  WA,SCNOF         SAVE ANOTHER COPY
       LCH  WA,(XL)+         LOAD NEXT CHARACTER
       BNE  WA,=CH_AS,SCN50  NOT ** IF NEXT CHAR NOT *
       ICV  XR               ELSE STEP OFFSET PAST SECOND *
       BEQ  XR,SCNIL,SCN51   OK EXCLAM IF END OF IMAGE
       LCH  WA,(XL)          ELSE LOAD NEXT CHARACTER
       BEQ  WA,=CH_BL,SCN51  EXCLAMATION IF BLANK
.IF    .CAHT
       BEQ  WA,=CH_HT,SCN51  EXCLAMATION IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WA,=CH_VT,SCN51  EXCLAMATION IF VERTICAL TAB
.FI
*
*      UNARY *
*
SCN50  MOV  SCNOF,WA         RECOVER STORED OFFSET
       MOV  R_CIM,XL         POINT TO LINE AGAIN
       PLC  XL,WA            POINT TO CURRENT CHAR
       BRN  SCN39            MERGE WITH UNARY *
*
*      HERE FOR ** AS SUBSTITUTE FOR EXCLAMATION
*
SCN51  MOV  XR,SCNPT         SAVE SCAN POINTER PAST 2ND *
       MOV  XR,WA            COPY SCAN POINTER
       BRN  SCN37            MERGE WITH EXCLAMATION
       ENP                   END PROCEDURE SCANE
       EJC
*
*      SCNGF -- SCAN GOTO FIELD
*
*      SCNGF IS CALLED FROM CMPIL TO SCAN AND ANALYZE A GOTO
*      FIELD INCLUDING THE SURROUNDING BRACKETS OR PARENTHESES.
*      FOR A NORMAL GOTO, THE RESULT RETURNED IS EITHER A VRBLK
*      POINTER FOR A SIMPLE LABEL OPERAND, OR A POINTER TO AN
*      EXPRESSION TREE WITH A SPECIAL OUTER UNARY OPERATOR
*      (O_GOC). FOR A DIRECT GOTO, THE RESULT RETURNED IS A
*      POINTER TO AN EXPRESSION TREE WITH THE SPECIAL OUTER
*      UNARY OPERATOR O_GOD.
*
*      JSR  SCNGF            CALL TO SCAN GOTO FIELD
*      (XR)                  RESULT (SEE ABOVE)
*      (XL,WA,WB,WC)         DESTROYED
*
SCNGF  PRC  E,0              ENTRY POINT
       JSR  SCANE            SCAN INITIAL ELEMENT
       BEQ  XL,=T_LPR,SCNG1  SKIP IF LEFT PAREN (NORMAL GOTO)
       BEQ  XL,=T_LBR,SCNG2  SKIP IF LEFT BRACKET (DIRECT GOTO)
       ERB  234,Syntax error: Goto field incorrect
*
*      HERE FOR LEFT PAREN (NORMAL GOTO)
*
SCNG1  MOV  =NUM01,WB        SET EXPAN FLAG FOR NORMAL GOTO
       JSR  EXPAN            ANALYZE GOTO FIELD
       MOV  =OPDVN,WA        POINT TO OPDV FOR COMPLEX GOTO
       BLE  XR,STATB,SCNG3   JUMP IF NOT IN STATIC (SGD15)
       BLO  XR,STATE,SCNG4   JUMP TO EXIT IF SIMPLE LABEL NAME
       BRN  SCNG3            COMPLEX GOTO - MERGE
*
*      HERE FOR LEFT BRACKET (DIRECT GOTO)
*
SCNG2  MOV  =NUM02,WB        SET EXPAN FLAG FOR DIRECT GOTO
       JSR  EXPAN            SCAN GOTO FIELD
       MOV  =OPDVD,WA        SET OPDV POINTER FOR DIRECT GOTO
       EJC
*
*      SCNGF (CONTINUED)
*
*      MERGE HERE TO BUILD OUTER UNARY OPERATOR BLOCK
*
SCNG3  MOV  WA,-(XS)         STACK OPERATOR DV POINTER
       MOV  XR,-(XS)         STACK POINTER TO EXPRESSION TREE
       JSR  EXPOP            POP OPERATOR OFF
       MOV  (XS)+,XR         RELOAD NEW EXPRESSION TREE POINTER
*
*      COMMON EXIT POINT
*
SCNG4  EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE SCNGF
       EJC
*
*      SETVR -- SET VRGET,VRSTO FIELDS OF VRBLK
*
*      SETVR SETS THE PROPER VALUES IN THE VRGET AND VRSTO
*      FIELDS OF A VRBLK. IT IS CALLED WHENEVER TRBLKS ARE
*      ADDED OR SUBTRACTED (TRACE,STOPTR,INPUT,OUTPUT,DETACH)
*
*      (XR)                  POINTER TO VRBLK
*      JSR  SETVR            CALL TO SET FIELDS
*      (XL,WA)               DESTROYED
*
*      NOTE THAT SETVR IGNORES THE CALL IF XR DOES NOT POINT
*      INTO THE STATIC REGION (I.E. IS SOME OTHER NAME BASE)
*
SETVR  PRC  E,0              ENTRY POINT
       BHI  XR,STATE,SETV1   EXIT IF NOT NATURAL VARIABLE
*
*      HERE IF WE HAVE A VRBLK
*
       MOV  XR,XL            COPY VRBLK POINTER
       MOV  =B_VRL,VRGET(XR) STORE NORMAL GET VALUE
       BEQ  VRSTO(XR),=B_VRE,SETV1 SKIP IF PROTECTED VARIABLE
       MOV  =B_VRS,VRSTO(XR) STORE NORMAL STORE VALUE
       MOV  VRVAL(XL),XL     POINT TO NEXT ENTRY ON CHAIN
       BNE  (XL),=B_TRT,SETV1 JUMP IF END OF TRBLK CHAIN
       MOV  =B_VRA,VRGET(XR) STORE TRAPPED ROUTINE ADDRESS
       MOV  =B_VRV,VRSTO(XR) SET TRAPPED ROUTINE ADDRESS
*
*      MERGE HERE TO EXIT TO CALLER
*
SETV1  EXI                   RETURN TO SETVR CALLER
       ENP                   END PROCEDURE SETVR
.IF    .CNSR
.ELSE
       EJC
*
*      SORTA -- SORT ARRAY
*
*      ROUTINE TO SORT AN ARRAY OR TABLE ON SAME BASIS AS IN
*      SITBOL. A TABLE IS CONVERTED TO AN ARRAY, LEAVING TWO
*      DIMENSIONAL ARRAYS AND VECTORS AS CASES TO BE CONSIDERED.
*      WHOLE ROWS OF ARRAYS ARE PERMUTED ACCORDING TO THE
*      ORDERING OF THE KEYS THEY CONTAIN, AND THE STRIDE
*      REFERRED TO, IS THE THE LENGTH OF A ROW. IT IS ONE
*      FOR A VECTOR.
*      THE SORT USED IS HEAPSORT, FUNDAMENTALS OF DATA STRUCTURE
*      HOROWITZ AND SAHNI, PITMAN 1977, PAGE 347.
*      IT IS AN ORDER N*LOG(N) ALGORITHM. IN ORDER
*      TO MAKE IT STABLE, COMPARANDS MAY NOT COMPARE EQUAL. THIS
*      IS ACHIEVED BY SORTING A COPY ARRAY (REFERRED TO AS THE
*      SORT ARRAY) CONTAINING AT ITS HIGH ADDRESS END, BYTE
*      OFFSETS TO THE ROWS TO BE SORTED HELD IN THE ORIGINAL
*      ARRAY (REFERRED TO AS THE KEY ARRAY). SORTC, THE
*      COMPARISON ROUTINE, ACCESSES THE KEYS THROUGH THESE
*      OFFSETS AND IN THE CASE OF EQUALITY, RESOLVES IT BY
*      COMPARING THE OFFSETS THEMSELVES. THE SORT PERMUTES THE
*      OFFSETS WHICH ARE THEN USED IN A FINAL OPERATION TO COPY
*      THE ACTUAL ITEMS INTO THE NEW ARRAY IN SORTED ORDER.
*      REFERENCES TO ZEROTH ITEM ARE TO NOTIONAL ITEM
*      PRECEDING FIRST ACTUAL ITEM.
*      REVERSE SORTING FOR RSORT IS DONE BY HAVING THE LESS THAN
*      TEST FOR KEYS EFFECTIVELY BE REPLACED BY A
*      GREATER THAN TEST.
*
*      1(XS)                 FIRST ARG - ARRAY OR TABLE
*      0(XS)                 2ND ARG - INDEX OR PDTYPE NAME
*      (WA)                  0 , NON-ZERO FOR SORT , RSORT
*      JSR  SORTA            CALL TO SORT ARRAY
*      PPM  LOC              TRANSFER LOC IF TABLE IS EMPTY
*      (XR)                  SORTED ARRAY
*      (XL,WA,WB,WC)         DESTROYED
       EJC
*
*      SORTA (CONTINUED)
*
SORTA  PRC  N,1              ENTRY POINT
       MOV  WA,SRTSR         SORT/RSORT INDICATOR
       MOV  *NUM01,SRTST     DEFAULT STRIDE OF 1
       ZER  SRTOF            DEFAULT ZERO OFFSET TO SORT KEY
       MOV  =NULLS,SRTDF     CLEAR DATATYPE FIELD NAME
       MOV  (XS)+,R_SXR      UNSTACK ARGUMENT 2
       MOV  (XS)+,XR         GET FIRST ARGUMENT
       MNZ  WA               USE KEY/VALUES OF TABLE ENTRIES
       JSR  GTARR            CONVERT TO ARRAY
       PPM  SRT18            SIGNAL THAT TABLE IS EMPTY
       PPM  SRT16            ERROR IF NON-CONVERTABLE
       MOV  XR,-(XS)         STACK PTR TO RESULTING KEY ARRAY
       MOV  XR,-(XS)         ANOTHER COPY FOR COPYB
       JSR  COPYB            GET COPY ARRAY FOR SORTING INTO
       PPM                   CANT FAIL
       MOV  XR,-(XS)         STACK POINTER TO SORT ARRAY
       MOV  R_SXR,XR         GET SECOND ARG
       MOV  NUM01(XS),XL     GET PTR TO KEY ARRAY
       BNE  (XL),=B_VCT,SRT02 JUMP IF ARBLK
       BEQ  XR,=NULLS,SRT01  JUMP IF NULL SECOND ARG
       JSR  GTNVR            GET VRBLK PTR FOR IT
       ERR  257,Erroneous 2nd arg in SORT/RSORT of vector
       MOV  XR,SRTDF         STORE DATATYPE FIELD NAME VRBLK
*
*      COMPUTE N AND OFFSET TO ITEM A(0) IN VECTOR CASE
*
SRT01  MOV  *VCLEN,WC        OFFSET TO A(0)
       MOV  *VCVLS,WB        OFFSET TO FIRST ITEM
       MOV  VCLEN(XL),WA     GET BLOCK LENGTH
       SUB  *VCSI_,WA        GET NO. OF ENTRIES, N (IN BYTES)
       BRN  SRT04            MERGE
*
*      HERE FOR ARRAY
*
SRT02  LDI  ARDIM(XL)        GET POSSIBLE DIMENSION
       MFI  WA               CONVERT TO SHORT INTEGER
       WTB  WA               FURTHER CONVERT TO BAUS
       MOV  *ARVLS,WB        OFFSET TO FIRST VALUE IF ONE
       MOV  *ARPRO,WC        OFFSET BEFORE VALUES IF ONE DIM.
       BEQ  ARNDM(XL),=NUM01,SRT04 JUMP IN FACT IF ONE DIM.
       BNE  ARNDM(XL),=NUM02,SRT16  FAIL UNLESS TWO DIMENS
       LDI  ARLB2(XL)        GET LOWER BOUND 2 AS DEFAULT
       BEQ  XR,=NULLS,SRT03  JUMP IF DEFAULT SECOND ARG
       JSR  GTINT            CONVERT TO INTEGER
       PPM  SRT17            FAIL
       LDI  ICVAL(XR)        GET ACTUAL INTEGER VALUE
       EJC
*
*      SORTA (CONTINUED)
*
*      HERE WITH SORT COLUMN INDEX IN IA IN ARRAY CASE
*
SRT03  SBI  ARLB2(XL)        SUBTRACT LOW BOUND
       IOV  SRT17            FAIL IF OVERFLOW
       ILT  SRT17            FAIL IF BELOW LOW BOUND
       SBI  ARDM2(XL)        CHECK AGAINST DIMENSION
       IGE  SRT17            FAIL IF TOO LARGE
       ADI  ARDM2(XL)        RESTORE VALUE
       MFI  WA               GET AS SMALL INTEGER
       WTB  WA               OFFSET WITHIN ROW TO KEY
       MOV  WA,SRTOF         KEEP OFFSET
       LDI  ARDM2(XL)        SECOND DIMENSION IS ROW LENGTH
       MFI  WA               CONVERT TO SHORT INTEGER
       MOV  WA,XR            COPY ROW LENGTH
       WTB  WA               CONVERT TO BYTES
       MOV  WA,SRTST         STORE AS STRIDE
       LDI  ARDIM(XL)        GET NUMBER OF ROWS
       MFI  WA               AS A SHORT INTEGER
       WTB  WA               CONVERT N TO BAUS
       MOV  ARLEN(XL),WC     OFFSET PAST ARRAY END
       SUB  WA,WC            ADJUST, GIVING SPACE FOR N OFFSETS
       DCA  WC               POINT TO A(0)
       MOV  AROFS(XL),WB     OFFSET TO WORD BEFORE FIRST ITEM
       ICA  WB               OFFSET TO FIRST ITEM
*
*      SEPARATE PRE-PROCESSING FOR ARRAYS AND VECTORS DONE.
*      TO SIMPLIFY LATER KEY COMPARISONS, REMOVAL OF ANY TRBLK
*      TRAP BLOCKS FROM ENTRIES IN KEY ARRAY IS EFFECTED.
*
*      (XL) = 1(XS) = POINTER TO KEY ARRAY
*      (XS) = POINTER TO SORT ARRAY
*      WA = NUMBER OF ITEMS, N (CONVERTED TO BYTES).
*      WB = OFFSET TO FIRST ITEM OF ARRAYS.
*      WC = OFFSET TO A(0)
*
SRT04  BLE  WA,*NUM01,SRT15  RETURN IF ONLY A SINGLE ITEM
       MOV  WA,SRTSN         STORE NUMBER OF ITEMS (IN BAUS)
       MOV  WC,SRTSO         STORE OFFSET TO A(0)
       MOV  ARLEN(XL),WC     LENGTH OF ARRAY OR VEC (=VCLEN)
       ADD  XL,WC            POINT PAST END OF ARRAY OR VECTOR
       MOV  WB,SRTSF         STORE OFFSET TO FIRST ROW
       ADD  WB,XL            POINT TO FIRST ITEM IN KEY ARRAY
*
*      LOOP THROUGH ARRAY
*
SRT05  MOV  (XL),XR          GET AN ENTRY
*
*      HUNT ALONG TRBLK CHAIN
*
SRT06  BNE  (XR),=B_TRT,SRT07 JUMP OUT IF NOT TRBLK
       MOV  TRVAL(XR),XR     GET VALUE FIELD
       BRN  SRT06            LOOP
       EJC
*
*      SORTA (CONTINUED)
*
*      XR IS VALUE FROM END OF CHAIN
*
SRT07  MOV  XR,(XL)+         STORE AS ARRAY ENTRY
       BLT  XL,WC,SRT05      LOOP IF NOT DONE
       MOV  (XS),XL          GET ADRS OF SORT ARRAY
       MOV  SRTSF,XR         INITIAL OFFSET TO FIRST KEY
       MOV  SRTST,WB         GET STRIDE
       ADD  SRTSO,XL         OFFSET TO A(0)
       ICA  XL               POINT TO A(1)
       MOV  SRTSN,WC         GET N
       BTW  WC               CONVERT FROM BYTES
       MOV  WC,SRTNR         STORE AS ROW COUNT
       LCT  WC,WC            LOOP COUNTER
*
*      STORE KEY OFFSETS AT TOP OF SORT ARRAY
*
SRT08  MOV  XR,(XL)+         STORE AN OFFSET
       ADD  WB,XR            BUMP OFFSET BY STRIDE
       BCT  WC,SRT08         LOOP THROUGH ROWS
*
*      PERFORM THE SORT ON OFFSETS IN SORT ARRAY.
*
*      (SRTSN)               NUMBER OF ITEMS TO SORT, N (BYTES)
*      (SRTSO)               OFFSET TO A(0)
*
SRT09  MOV  SRTSN,WA         GET N
       MOV  SRTNR,WC         GET NUMBER OF ROWS
       RSH  WC,1             I = N / 2 (WC=I, INDEX INTO ARRAY)
       WTB  WC               CONVERT BACK TO BYTES
*
*      LOOP TO FORM INITIAL HEAP
*
SRT10  JSR  SORTH            SORTH(I,N)
       DCA  WC               I = I - 1
       BNZ  WC,SRT10         LOOP IF I GT 0
       MOV  WA,WC            I = N
*
*      SORTING LOOP. AT THIS POINT, A(1) IS THE LARGEST
*      ITEM, SINCE ALGORITHM INITIALISES IT AS, AND THEN MAINTAINS
*      IT AS, ROOT OF TREE.
*
SRT11  DCA  WC               I = I - 1 (N - 1 INITIALLY)
       BZE  WC,SRT12         JUMP IF DONE
       MOV  (XS),XR          GET SORT ARRAY ADDRESS
       ADD  SRTSO,XR         POINT TO A(0)
       MOV  XR,XL            A(0) ADDRESS
       ADD  WC,XL            A(I) ADDRESS
       MOV  NUM01(XL),WB     COPY A(I+1)
       MOV  NUM01(XR),NUM01(XL)  MOVE A(1) TO A(I+1)
       MOV  WB,NUM01(XR)     COMPLETE EXCHANGE OF A(1), A(I+1)
       MOV  WC,WA            N = I FOR SORTH
       MOV  *NUM01,WC        I = 1 FOR SORTH
       JSR  SORTH            SORTH(1,N)
       MOV  WA,WC            RESTORE WC
       BRN  SRT11            LOOP
       EJC
*
*      SORTA (CONTINUED)
*
*      OFFSETS HAVE BEEN PERMUTED INTO REQUIRED ORDER BY SORT.
*      COPY ARRAY ELEMENTS OVER THEM.
*
SRT12  MOV  (XS),XR          BASE ADRS OF KEY ARRAY
       MOV  XR,WC            COPY IT
       ADD  SRTSO,WC         OFFSET OF A(0)
       ADD  SRTSF,XR         ADRS OF FIRST ROW OF SORT ARRAY
       MOV  SRTST,WB         GET STRIDE
*
*      COPYING LOOP FOR SUCCESSIVE ITEMS. SORTED OFFSETS ARE
*      HELD AT END OF SORT ARRAY.
*
SRT13  ICA  WC               ADRS OF NEXT OF SORTED OFFSETS
       MOV  WC,XL            COPY IT FOR ACCESS
       MOV  (XL),XL          GET OFFSET
       ADD  NUM01(XS),XL     ADD KEY ARRAY BASE ADRS
       MOV  WB,WA            GET COUNT OF CHARACTERS IN ROW
       MVW                   COPY A COMPLETE ROW
       DCV  SRTNR            DECREMENT ROW COUNT
       BNZ  SRTNR,SRT13      REPEAT TILL ALL ROWS DONE
*
*      RETURN POINT
*
SRT15  MOV  (XS)+,XR         POP RESULT ARRAY PTR
       ICA  XS               POP KEY ARRAY PTR
       ZER  R_SXL            CLEAR JUNK
       ZER  R_SXR            CLEAR JUNK
       EXI                   RETURN
*
*      ERROR POINT
*
SRT16  ERB  256,SORT/RSORT 1st arg not suitable ARRAY or TABLE
SRT17  ERB  258,SORT/RSORT 2nd arg out of range or non-integer
*
*      RETURN POINT IF INPUT TABLE IS EMPTY
*
SRT18  EXI  1                RETURN INDICATION OF NULL TABLE
       ENP                   END PROCUDURE SORTA
       EJC
*
*      SORTC --  COMPARE SORT KEYS
*
*      COMPARE TWO SORT KEYS GIVEN THEIR OFFSETS. IF
*      EQUAL, COMPARE KEY OFFSETS TO GIVE STABLE SORT.
*      NOTE THAT IF SRTSR IS NON-ZERO (REQUEST FOR REVERSE
*      SORT), THE QUOTED RETURNS ARE INVERTED.
*      FOR OBJECTS OF DIFFERING DATATYPES, THE ENTRY POINT
*      IDENTIFICATIONS ARE COMPARED.
*
*      (XL)                  BASE ADRS FOR KEYS
*      (WA)                  OFFSET TO KEY 1 ITEM
*      (WB)                  OFFSET TO KEY 2 ITEM
*      (SRTSR)               ZERO/NON-ZERO FOR SORT/RSORT
*      (SRTOF)               OFFSET WITHIN ROW TO COMPARANDS
*      JSR  SORTC            CALL TO COMPARE KEYS
*      PPM  LOC              KEY1 LESS THAN KEY2
*                            NORMAL RETURN, KEY1 GT THAN KEY2
*      (XL,XR,WA,WB)         DESTROYED
*
SORTC  PRC  E,1              ENTRY POINT
       MOV  WA,SRTS1         SAVE OFFSET 1
       MOV  WB,SRTS2         SAVE OFFSET 2
       MOV  WC,SRTSC         SAVE WC
       ADD  SRTOF,XL         ADD OFFSET TO COMPARAND FIELD
       MOV  XL,XR            COPY BASE + OFFSET
       ADD  WA,XL            ADD KEY1 OFFSET
       ADD  WB,XR            ADD KEY2 OFFSET
       MOV  (XL),XL          GET KEY1
       MOV  (XR),XR          GET KEY2
       BNE  SRTDF,=NULLS,SRC12 JUMP IF DATATYPE FIELD NAME USED
       EJC
*
*      SORTC (CONTINUED)
*
*      MERGE AFTER DEALING WITH FIELD NAME. TRY FOR STRINGS.
*
SRC01  MOV  (XL),WC          GET TYPE CODE
       BNE  WC,(XR),SRC02    SKIP IF NOT SAME DATATYPE
       BEQ  WC,=B_SCL,SRC09  JUMP IF BOTH STRINGS
       BEQ  WC,=B_ICL,SRC14  JUMP IF BOTH INTEGERS
.IF    .CNBF
.ELSE
       BEQ  WC,=B_BCT,SRC09  JUMP IF BOTH BUFFERS
.FI
*
*      DATATYPES DIFFERENT.  NOW TRY FOR NUMERIC
*
SRC02  MOV  XL,R_SXL         KEEP ARG1
       MOV  XR,R_SXR         KEEP ARG2
.IF    .CNBF
.IF    .CNSC
       BEQ  WC,=B_SCL,SRC11  DO NOT ALLOW CONVERSION TO NUMBER
       BEQ  (XR),=B_SCL,SRC11 IF EITHER ARG IS A STRING
.FI
.ELSE
*
*      FIRST EXAMINE FOR STRING/BUFFER COMPARISON.  IF SO,
*      ALLOW LCOMP TO COMPARE CHARS IN STRING AND BUFFER
*      WITHOUT CONVERTING BUFFER TO A STRING.
*
       BEQ  WC,=B_SCL,SRC13  JUMP IF KEY1 IS A STRING
.IF    .CNSC
       BNE  WC,=B_BCT,SRC15  J IF KEY1 IS NOT A STRING OR BUFFER
.ELSE
       BNE  WC,=B_BCT,SRC14  TRY CONVERTING KEY 2 TO A NUMBER
.FI
*
*      HERE IF KEY1 IS A BUFFER, KEY2 KNOWN NOT TO BE A BUFFER.
*      IF KEY2 IS A STRING, THEN LCOMP CAN PROCEED.
*
       BEQ  (XR),=B_SCL,SRC09 J IF KEYS 1/2 ARE BUFFER/STRING
.IF    .CNSC
       BRN  SRC11            PREVENT CONVERT OF KEY 1 TO NUMBER
.ELSE
       BRN  SRC14            TRY CONVERTING KEY 1 TO NUMBER
.FI
*
*      HERE IF KEY1 IS A STRING, KEY2 KNOWN NOT TO BE A STRING.
*      IF KEY2 IS A BUFFER, THEN LCOMP CAN PROCEED.
*
SRC13  BEQ  (XR),=B_BCT,SRC09 J IF KEYS 1/2 ARE STRING/BUFFER
.IF    .CNSC
       BRN  SRC11            PREVENT CONVERT OF KEY 1 TO NUMBER
*
*      HERE IF KEY1 IS NOT A STRING OR BUFFER.
*      EXAMINE KEY2.  IF IT IS A STRING OR BUFFER, THEN DO NOT
*      CONVERT KEY2 TO A NUMBER.
*
SRC15  BEQ  (XR),=B_SCL,SRC11 J IF KEY 2 IS A STRING
       BEQ  (XR),=B_BCT,SRC11 J IF KEY 2 IS A BUFFER
*
*      HERE WITH KEYS 1/2 NOT STRINGS OR BUFFERS
*
.FI
.FI
SRC14  MOV  XL,-(XS)         STACK
       MOV  XR,-(XS)         ARGS
       JSR  ACOMP            COMPARE OBJECTS
       PPM  SRC10            NOT NUMERIC
       PPM  SRC10            NOT NUMERIC
       PPM  SRC03            KEY1 LESS
       PPM  SRC08            KEYS EQUAL
       PPM  SRC05            KEY1 GREATER
*
*      RETURN IF KEY1 SMALLER (SORT), GREATER (RSORT)
*
SRC03  BNZ  SRTSR,SRC06      JUMP IF RSORT
*
SRC04  MOV  SRTSC,WC         RESTORE WC
       EXI  1                RETURN
*
*      RETURN IF KEY1 GREATER (SORT), SMALLER (RSORT)
*
SRC05  BNZ  SRTSR,SRC04      JUMP IF RSORT
*
SRC06  MOV  SRTSC,WC         RESTORE WC
       EXI                   RETURN
*
*      KEYS ARE OF SAME DATATYPE
*
SRC07  BLT  XL,XR,SRC03      ITEM FIRST CREATED IS LESS
       BGT  XL,XR,SRC05      ADDRESSES RISE IN ORDER OF CREATION
*
*      DROP THROUGH OR MERGE FOR IDENTICAL OR EQUAL OBJECTS
*
SRC08  BLT  SRTS1,SRTS2,SRC04 TEST OFFSETS OR KEY ADDRSS INSTEAD
       BRN  SRC06            OFFSET 1 GREATER
       EJC
*
*      SORTC (CONTINUED)
*
.IF    .CNBF
*      STRINGS
.ELSE
*      STRINGS OR BUFFERS OR SOME COMBINATION OF SAME
.FI
*
SRC09  MOV  XL,-(XS)         STACK
       MOV  XR,-(XS)         ARGS
       JSR  LCOMP            COMPARE OBJECTS
       PPM                   CANT
       PPM                   FAIL
       PPM  SRC03            KEY1 LESS
       PPM  SRC08            KEYS EQUAL
       PPM  SRC05            KEY1 GREATER
*
*      ARITHMETIC COMPARISON FAILED - RECOVER ARGS
*
SRC10  MOV  R_SXL,XL         GET ARG1
       MOV  R_SXR,XR         GET ARG2
       MOV  (XL),WC          GET TYPE OF KEY1
       BEQ  WC,(XR),SRC07    JUMP IF KEYS OF SAME TYPE
*
*      HERE TO COMPARE DATATYPE IDS
*
SRC11  MOV  WC,XL            GET BLOCK TYPE WORD
       MOV  (XR),XR          GET BLOCK TYPE WORD
       LEI  XL               ENTRY POINT ID FOR KEY1
       LEI  XR               ENTRY POINT ID FOR KEY2
       BGT  XL,XR,SRC05      JUMP IF KEY1 GT KEY2
       BRN  SRC03            KEY1 LT KEY2
*
*      DATATYPE FIELD NAME USED
*
SRC12  JSR  SORTF            CALL ROUTINE TO FIND FIELD 1
       MOV  XL,-(XS)         STACK ITEM POINTER
       MOV  XR,XL            GET KEY2
       JSR  SORTF            FIND FIELD 2
       MOV  XL,XR            PLACE AS KEY2
       MOV  (XS)+,XL         RECOVER KEY1
       BRN  SRC01            MERGE
       ENP                   PROCEDURE SORTC
       EJC
*
*      SORTF -- FIND FIELD FOR SORTC
*
*      ROUTINE USED BY SORTC TO OBTAIN ITEM CORRESPONDING
*      TO A GIVEN FIELD NAME, IF THIS EXISTS, IN A PROGRAMMER
*      DEFINED OBJECT PASSED AS ARGUMENT.
*      IF SUCH A MATCH OCCURS, RECORD IS KEPT OF DATATYPE
*      NAME, FIELD NAME AND OFFSET TO FIELD IN ORDER TO
*      SHORT-CIRCUIT LATER SEARCHES ON SAME TYPE. NOTE THAT
*      DFBLKS ARE STORED IN STATIC AND HENCE CANNOT BE MOVED.
*
*      (SRTDF)               VRBLK POINTER OF FIELD NAME
*      (XL)                  POSSIBLE PDBLK POINTER
*      JSR  SORTF            CALL TO SEARCH FOR FIELD NAME
*      (XL)                  ITEM FOUND OR ORIGINAL PDBLK PTR
*      (WC)                  DESTROYED
*
SORTF  PRC  E,0              ENTRY POINT
       BNE  (XL),=B_PDT,SRTF3 RETURN IF NOT PDBLK
       MOV  XR,-(XS)         KEEP XR
       MOV  SRTFD,XR         GET POSSIBLE FORMER DFBLK PTR
       BZE  XR,SRTF4         JUMP IF NOT
       BNE  XR,PDDFP(XL),SRTF4 JUMP IF NOT RIGHT DATATYPE
       BNE  SRTDF,SRTFF,SRTF4 JUMP IF NOT RIGHT FIELD NAME
       ADD  SRTFO,XL         ADD OFFSET TO REQUIRED FIELD
*
*      HERE WITH XL POINTING TO FOUND FIELD
*
SRTF1  MOV  (XL),XL          GET ITEM FROM FIELD
*
*      RETURN POINT
*
SRTF2  MOV  (XS)+,XR         RESTORE XR
*
SRTF3  EXI                   RETURN
       EJC
*
*      SORTF (CONTINUED)
*
*      CONDUCT A SEARCH
*
SRTF4  MOV  XL,XR            COPY ORIGINAL POINTER
       MOV  PDDFP(XR),XR     POINT TO DFBLK
       MOV  XR,SRTFD         KEEP A COPY
       MOV  FARGS(XR),WC     GET NUMBER OF FIELDS
       WTB  WC               CONVERT TO BYTES
       ADD  DFLEN(XR),XR     POINT PAST LAST FIELD
*
*      LOOP TO FIND NAME IN PDFBLK
*
SRTF5  DCA  WC               COUNT DOWN
       DCA  XR               POINT IN FRONT
       BEQ  (XR),SRTDF,SRTF6 SKIP OUT IF FOUND
       BNZ  WC,SRTF5         LOOP
       BRN  SRTF2            RETURN - NOT FOUND
*
*      FOUND
*
SRTF6  MOV  (XR),SRTFF       KEEP FIELD NAME PTR
       ADD  *PDFLD,WC        ADD OFFSET TO FIRST FIELD
       MOV  WC,SRTFO         STORE AS FIELD OFFSET
       ADD  WC,XL            POINT TO FIELD
       BRN  SRTF1            RETURN
       ENP                   PROCEDURE SORTF
       EJC
*
*      SORTH -- HEAP ROUTINE FOR SORTA
*
*      THIS ROUTINE CONSTRUCTS A HEAP FROM ELEMENTS OF ARRAY, A.
*      IN THIS APPLICATION, THE ELEMENTS ARE OFFSETS TO KEYS IN
*      A KEY ARRAY.
*
*      (XS)                  POINTER TO SORT ARRAY BASE
*      1(XS)                 POINTER TO KEY ARRAY BASE
*      (WA)                  MAX ARRAY INDEX, N (IN BYTES)
*      (WC)                  OFFSET J IN A TO ROOT (IN *1 TO *N)
*      JSR  SORTH            CALL SORTH(J,N) TO MAKE HEAP
*      (XL,XR,WB)            DESTROYED
*
SORTH  PRC  N,0              ENTRY POINT
       MOV  WA,SRTSN         SAVE N
       MOV  WC,SRTWC         KEEP WC
       MOV  (XS),XL          SORT ARRAY BASE ADRS
       ADD  SRTSO,XL         ADD OFFSET TO A(0)
       ADD  WC,XL            POINT TO A(J)
       MOV  (XL),SRTRT       GET OFFSET TO ROOT
       ADD  WC,WC            DOUBLE J - CANT EXCEED N
*
*      LOOP TO MOVE DOWN TREE USING DOUBLED INDEX J
*
SRH01  BGT  WC,SRTSN,SRH03   DONE IF J GT N
       BEQ  WC,SRTSN,SRH02   SKIP IF J EQUALS N
       MOV  (XS),XR          SORT ARRAY BASE ADRS
       MOV  NUM01(XS),XL     KEY ARRAY BASE ADRS
       ADD  SRTSO,XR         POINT TO A(0)
       ADD  WC,XR            ADRS OF A(J)
       MOV  NUM01(XR),WA     GET A(J+1)
       MOV  (XR),WB          GET A(J)
*
*      COMPARE SONS. (WA) RIGHT SON, (WB) LEFT SON
*
       JSR  SORTC            COMPARE KEYS - LT(A(J+1),A(J))
       PPM  SRH02            A(J+1) LT A(J)
       ICA  WC               POINT TO GREATER SON, A(J+1)
       EJC
*
*      SORTH (CONTINUED)
*
*      COMPARE ROOT WITH GREATER SON
*
SRH02  MOV  NUM01(XS),XL     KEY ARRAY BASE ADRS
       MOV  (XS),XR          GET SORT ARRAY ADDRESS
       ADD  SRTSO,XR         ADRS OF A(0)
       MOV  XR,WB            COPY THIS ADRS
       ADD  WC,XR            ADRS OF GREATER SON, A(J)
       MOV  (XR),WA          GET A(J)
       MOV  WB,XR            POINT BACK TO A(0)
       MOV  SRTRT,WB         GET ROOT
       JSR  SORTC            COMPARE THEM - LT(A(J),ROOT)
       PPM  SRH03            FATHER EXCEEDS SONS - DONE
       MOV  (XS),XR          GET SORT ARRAY ADRS
       ADD  SRTSO,XR         POINT TO A(0)
       MOV  XR,XL            COPY IT
       MOV  WC,WA            COPY J
       BTW  WC               CONVERT TO WORDS
       RSH  WC,1             GET J/2
       WTB  WC               CONVERT BACK TO BYTES
       ADD  WA,XL            POINT TO A(J)
       ADD  WC,XR            ADRS OF A(J/2)
       MOV  (XL),(XR)        A(J/2) = A(J)
       MOV  WA,WC            RECOVER J
       AOV  WC,WC,SRH03      J = J*2. DONE IF TOO BIG
       BRN  SRH01            LOOP
*
*      FINISH BY COPYING ROOT OFFSET BACK INTO ARRAY
*
SRH03  BTW  WC               CONVERT TO WORDS
       RSH  WC,1             J = J/2
       WTB  WC               CONVERT BACK TO BYTES
       MOV  (XS),XR          SORT ARRAY ADRS
       ADD  SRTSO,XR         ADRS OF A(0)
       ADD  WC,XR            ADRS OF A(J/2)
       MOV  SRTRT,(XR)       A(J/2) = ROOT
       MOV  SRTSN,WA         RESTORE WA
       MOV  SRTWC,WC         RESTORE WC
       EXI                   RETURN
       ENP                   END PROCEDURE SORTH
.FI
       EJC
*
*      TRACE -- SET/RESET A TRACE ASSOCIATION
*
*      THIS PROCEDURE IS SHARED BY TRACE AND STOPTR TO
*      EITHER INITIATE OR STOP A TRACE RESPECTIVELY.
*
*      (XL)                  TRBLK PTR (TRACE) OR ZERO (STOPTR)
*      1(XS)                 FIRST ARGUMENT (NAME)
*      0(XS)                 SECOND ARGUMENT (TRACE TYPE)
*      JSR  TRACE            CALL TO SET/RESET TRACE
*      PPM  LOC              TRANSFER LOC IF 1ST ARG IS BAD NAME
*      PPM  LOC              TRANSFER LOC IF 2ND ARG IS BAD TYPE
*      (XS)                  POPPED
*      (XL,XR,WA,WB,WC,IA)   DESTROYED
*
TRACE  PRC  N,2              ENTRY POINT
       JSR  GTSTG            GET TRACE TYPE STRING
       PPM  TRC15            JUMP IF NOT STRING
       PLC  XR               ELSE POINT TO STRING
       LCH  WA,(XR)          LOAD FIRST CHARACTER
.IF    .CULC
       FLC  WA               FOLD TO UPPER CASE
.FI
       MOV  (XS),XR          LOAD NAME ARGUMENT
       MOV  XL,(XS)          STACK TRBLK PTR OR ZERO
       MOV  =TRTAC,WC        SET TRTYP FOR ACCESS TRACE
       BEQ  WA,=CH_LA,TRC10  JUMP IF A (ACCESS)
       MOV  =TRTVL,WC        SET TRTYP FOR VALUE TRACE
       BEQ  WA,=CH_LV,TRC10  JUMP IF V (VALUE)
       BEQ  WA,=CH_BL,TRC10  JUMP IF BLANK (VALUE)
*
*      HERE FOR L,K,F,C,R
*
       BEQ  WA,=CH_LF,TRC01  JUMP IF F (FUNCTION)
       BEQ  WA,=CH_LR,TRC01  JUMP IF R (RETURN)
       BEQ  WA,=CH_LL,TRC03  JUMP IF L (LABEL)
       BEQ  WA,=CH_LK,TRC06  JUMP IF K (KEYWORD)
       BNE  WA,=CH_LC,TRC15  ELSE ERROR IF NOT C (CALL)
*
*      HERE FOR F,C,R
*
TRC01  JSR  GTNVR            POINT TO VRBLK FOR NAME
       PPM  TRC16            JUMP IF BAD NAME
       ICA  XS               POP STACK
       MOV  VRFNC(XR),XR     POINT TO FUNCTION BLOCK
       BNE  (XR),=B_PFC,TRC17 ERROR IF NOT PROGRAM FUNCTION
       BEQ  WA,=CH_LR,TRC02  JUMP IF R (RETURN)
       EJC
*
*      TRACE (CONTINUED)
*
*      HERE FOR F,C TO SET/RESET CALL TRACE
*
       MOV  XL,PFCTR(XR)     SET/RESET CALL TRACE
       BEQ  WA,=CH_LC,EXNUL  EXIT WITH NULL IF C (CALL)
*
*      HERE FOR F,R TO SET/RESET RETURN TRACE
*
TRC02  MOV  XL,PFRTR(XR)     SET/RESET RETURN TRACE
       EXI                   RETURN
*
*      HERE FOR L TO SET/RESET LABEL TRACE
*
TRC03  JSR  GTNVR            POINT TO VRBLK
       PPM  TRC16            JUMP IF BAD NAME
       MOV  VRLBL(XR),XL     LOAD LABEL POINTER
       BNE  (XL),=B_TRT,TRC04 JUMP IF NO OLD TRACE
       MOV  TRLBL(XL),XL     ELSE DELETE OLD TRACE ASSOCIATION
*
*      HERE WITH OLD LABEL TRACE ASSOCIATION DELETED
*
TRC04  BEQ  XL,=STNDL,TRC16  ERROR IF UNDEFINED LABEL
       MOV  (XS)+,WB         GET TRBLK PTR AGAIN
       BZE  WB,TRC05         JUMP IF STOPTR CASE
       MOV  WB,VRLBL(XR)     ELSE SET NEW TRBLK POINTER
       MOV  =B_VRT,VRTRA(XR) SET LABEL TRACE ROUTINE ADDRESS
       MOV  WB,XR            COPY TRBLK POINTER
       MOV  XL,TRLBL(XR)     STORE REAL LABEL IN TRBLK
       EXI                   RETURN
*
*      HERE FOR STOPTR CASE FOR LABEL
*
TRC05  MOV  XL,VRLBL(XR)     STORE LABEL PTR BACK IN VRBLK
       MOV  =B_VRG,VRTRA(XR) STORE NORMAL TRANSFER ADDRESS
       EXI                   RETURN
       EJC
*
*      TRACE (CONTINUED)
*
*      HERE FOR K (KEYWORD)
*
TRC06  JSR  GTNVR            POINT TO VRBLK
       PPM  TRC16            ERROR IF NOT NATURAL VAR
       BNZ  VRLEN(XR),TRC16  ERROR IF NOT SYSTEM VAR
       ICA  XS               POP STACK
       BZE  XL,TRC07         JUMP IF STOPTR CASE
       MOV  XR,TRKVR(XL)     STORE VRBLK PTR IN TRBLK FOR KTREX
*
*      MERGE HERE WITH TRBLK SET UP IN WB (OR ZERO)
*
TRC07  MOV  VRSVP(XR),XR     POINT TO SVBLK
       BEQ  XR,=V_ERT,TRC08  JUMP IF ERRTYPE
       BEQ  XR,=V_STC,TRC09  JUMP IF STCOUNT
       BNE  XR,=V_FNC,TRC17  ELSE ERROR IF NOT FNCLEVEL
*
*      FNCLEVEL
*
       MOV  XL,R_FNC         SET/RESET FNCLEVEL TRACE
       EXI                   RETURN
*
*      ERRTYPE
*
TRC08  MOV  XL,R_ERT         SET/RESET ERRTYPE TRACE
       EXI                   RETURN
*
*      STCOUNT
*
TRC09  MOV  XL,R_STC         SET/RESET STCOUNT TRACE
       JSR  STGCC            UPDATE COUNTDOWN COUNTERS
       EXI                   RETURN
       EJC
*
*      TRACE (CONTINUED)
*
*      A,V MERGE HERE WITH TRTYP VALUE IN WC
*
TRC10  JSR  GTVAR            LOCATE VARIABLE
       PPM  TRC16            ERROR IF NOT APPROPRIATE NAME
       MOV  (XS)+,WB         GET NEW TRBLK PTR AGAIN
       ADD  XL,WA            POINT TO VARIABLE LOCATION
       MOV  WA,XR            COPY VARIABLE POINTER
*
*      LOOP TO SEARCH TRBLK CHAIN
*
TRC11  MOV  (XR),XL          POINT TO NEXT ENTRY
       BNE  (XL),=B_TRT,TRC13  JUMP IF NOT TRBLK
       BLT  WC,TRTYP(XL),TRC13 JUMP IF TOO FAR OUT ON CHAIN
       BEQ  WC,TRTYP(XL),TRC12 JUMP IF THIS MATCHES OUR TYPE
       ADD  *TRNXT,XL        ELSE POINT TO LINK FIELD
       MOV  XL,XR            COPY POINTER
       BRN  TRC11            AND LOOP BACK
*
*      HERE TO DELETE AN OLD TRBLK OF THE TYPE WE WERE GIVEN
*
TRC12  MOV  TRNXT(XL),XL     GET PTR TO NEXT BLOCK OR VALUE
       MOV  XL,(XR)          STORE TO DELETE THIS TRBLK
*
*      HERE AFTER DELETING ANY OLD ASSOCIATION OF THIS TYPE
*
TRC13  BZE  WB,TRC14         JUMP IF STOPTR CASE
       MOV  WB,(XR)          ELSE LINK NEW TRBLK IN
       MOV  WB,XR            COPY TRBLK POINTER
       MOV  XL,TRNXT(XR)     STORE FORWARD POINTER
       MOV  WC,TRTYP(XR)     STORE APPROPRIATE TRAP TYPE CODE
*
*      HERE TO MAKE SURE VRGET,VRSTO ARE SET PROPERLY
*
TRC14  MOV  WA,XR            RECALL POSSIBLE VRBLK POINTER
       SUB  *VRVAL,XR        POINT BACK TO VRBLK
       JSR  SETVR            SET FIELDS IF VRBLK
       EXI                   RETURN
*
*      HERE FOR BAD TRACE TYPE
*
TRC15  EXI  2                TAKE BAD TRACE TYPE ERROR EXIT
*
*      POP STACK BEFORE FAILING
*
TRC16  ICA  XS               POP STACK
*
*      HERE FOR BAD NAME ARGUMENT
*
TRC17  EXI  1                TAKE BAD NAME ERROR EXIT
       ENP                   END PROCEDURE TRACE
       EJC
*
*      TRBLD -- BUILD TRBLK
*
*      TRBLK IS USED BY THE INPUT, OUTPUT AND TRACE FUNCTIONS
*      TO CONSTRUCT A TRBLK (TRAP BLOCK)
*
*      (XR)                  TRTAG OR TRTER
*      (XL)                  TRFNC OR TRFPT
*      (WB)                  TRTYP
*      JSR  TRBLD            CALL TO BUILD TRBLK
*      (XR)                  POINTER TO TRBLK
*      (WA)                  DESTROYED
*
TRBLD  PRC  E,0              ENTRY POINT
       MOV  XR,-(XS)         STACK TRTAG (OR TRFNM)
       MOV  *TRSI_,WA        SET SIZE OF TRBLK
       JSR  ALLOC            ALLOCATE TRBLK
       MOV  =B_TRT,(XR)      STORE FIRST WORD
       MOV  XL,TRFNC(XR)     STORE TRFNC (OR TRFPT)
       MOV  (XS)+,TRTAG(XR)  STORE TRTAG (OR TRFNM)
       MOV  WB,TRTYP(XR)     STORE TYPE
       MOV  =NULLS,TRVAL(XR) FOR NOW, A NULL VALUE
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE TRBLD
       EJC
*
*      TRIMR -- TRIM TRAILING BLANKS
*
*      TRIMR IS PASSED A POINTER TO AN SCBLK WHICH MUST BE THE
*      LAST BLOCK IN DYNAMIC STORAGE. TRAILING BLANKS ARE
*      TRIMMED OFF AND THE DYNAMIC STORAGE POINTER RESET TO
*      THE END OF THE (POSSIBLY) SHORTENED BLOCK.
*
*      (WB)                  NON-ZERO TO TRIM TRAILING BLANKS
*      (XR)                  POINTER TO STRING TO TRIM
*      JSR  TRIMR            CALL TO TRIM STRING
*      (XR)                  POINTER TO TRIMMED STRING
*      (XL,WA,WB,WC)         DESTROYED
*
*      THE CALL WITH WB ZERO STILL PERFORMS THE END ZERO PAD
*      AND DNAMP READJUSTMENT. IT IS USED FROM ACESS IF KVTRM=0.
*
TRIMR  PRC  E,0              ENTRY POINT
       MOV  XR,XL            COPY STRING POINTER
       MOV  SCLEN(XR),WA     LOAD STRING LENGTH
       BZE  WA,TRIM2         JUMP IF NULL INPUT
       PLC  XL,WA            ELSE POINT PAST LAST CHARACTER
       BZE  WB,TRIM3         JUMP IF NO TRIM
       MOV  =CH_BL,WC        LOAD BLANK CHARACTER
*
*      LOOP THROUGH CHARACTERS FROM RIGHT TO LEFT
*
TRIM0  LCH  WB,-(XL)         LOAD NEXT CHARACTER
.IF    .CAHT
       BEQ  WB,=CH_HT,TRIM1  JUMP IF HORIZONTAL TAB
.FI
       BNE  WB,WC,TRIM3      JUMP IF NON-BLANK FOUND
TRIM1  DCV  WA               ELSE DECREMENT CHARACTER COUNT
       BNZ  WA,TRIM0         LOOP BACK IF MORE TO CHECK
*
*      HERE IF RESULT IS NULL (NULL OR ALL-BLANK INPUT)
*
TRIM2  MOV  XR,DNAMP         WIPE OUT INPUT STRING BLOCK
       MOV  =NULLS,XR        LOAD NULL RESULT
       BRN  TRIM5            MERGE TO EXIT
       EJC
*
*      TRIMR (CONTINUED)
*
*      HERE WITH NON-BLANK FOUND (MERGE FOR NO TRIM)
*
TRIM3  MOV  WA,SCLEN(XR)     SET NEW LENGTH
       MOV  XR,XL            COPY STRING POINTER
       PSC  XL,WA            READY FOR STORING BLANKS
       CTB  WA,SCHAR         GET LENGTH OF BLOCK IN BYTES
       ADD  XR,WA            POINT PAST NEW BLOCK
       MOV  WA,DNAMP         SET NEW TOP OF STORAGE POINTER
       LCT  WA,=CFP_C        GET COUNT OF CHARS IN WORD
       ZER  WC               SET ZERO CHAR
*
*      LOOP TO ZERO PAD LAST WORD OF CHARACTERS
*
TRIM4  SCH  WC,(XL)+         STORE ZERO CHARACTER
       BCT  WA,TRIM4         LOOP BACK TILL ALL STORED
       CSC  XL               COMPLETE STORE CHARACTERS
*
*      COMMON EXIT POINT
*
TRIM5  ZER  XL               CLEAR GARBAGE XL POINTER
       EXI                   RETURN TO CALLER
       ENP                   END PROCEDURE TRIMR
       EJC
*
*      TRXEQ -- EXECUTE FUNCTION TYPE TRACE
*
*      TRXEQ IS USED TO EXECUTE A TRACE WHEN A FOURTH ARGUMENT
*      HAS BEEN SUPPLIED. TRACE HAS ALREADY BEEN DECREMENTED.
*
*      (XR)                  POINTER TO TRBLK
*      (XL,WA)               NAME BASE,OFFSET FOR VARIABLE
*      JSR  TRXEQ            CALL TO EXECUTE TRACE
*      (WB,WC,RA)            DESTROYED
*
*      THE FOLLOWING STACK ENTRIES ARE MADE BEFORE PASSING
*      CONTROL TO THE TRACE FUNCTION USING THE CFUNC ROUTINE.
*
*                            TRXEQ RETURN POINT WORD(S)
*                            SAVED VALUE OF TRACE KEYWORD
*                            TRBLK POINTER
*                            NAME BASE
*                            NAME OFFSET
*                            SAVED VALUE OF R_COD
*                            SAVED CODE PTR (-R_COD)
*                            SAVED VALUE OF FLPTR
*      FLPTR --------------- ZERO (DUMMY FAIL OFFSET)
*                            NMBLK FOR VARIABLE NAME
*      XS ------------------ TRACE TAG
*
*      R_COD AND THE CODE PTR ARE SET TO DUMMY VALUES WHICH
*      CAUSE CONTROL TO RETURN TO THE TRXEQ PROCEDURE ON SUCCESS
*      OR FAILURE (TRXEQ IGNORES A FAILURE CONDITION).
*
TRXEQ  PRC  R,0              ENTRY POINT (RECURSIVE)
       MOV  R_COD,WC         LOAD CODE BLOCK POINTER
       SCP  WB               GET CURRENT CODE POINTER
       SUB  WC,WB            MAKE CODE POINTER INTO OFFSET
       MOV  KVTRA,-(XS)      STACK TRACE KEYWORD VALUE
       MOV  XR,-(XS)         STACK TRBLK POINTER
       MOV  XL,-(XS)         STACK NAME BASE
       MOV  WA,-(XS)         STACK NAME OFFSET
       MOV  WC,-(XS)         STACK CODE BLOCK POINTER
       MOV  WB,-(XS)         STACK CODE POINTER OFFSET
       MOV  FLPTR,-(XS)      STACK OLD FAILURE POINTER
       ZER  -(XS)            SET DUMMY FAIL OFFSET
       MOV  XS,FLPTR         SET NEW FAILURE POINTER
       ZER  KVTRA            RESET TRACE KEYWORD TO ZERO
       MOV  =TRXDC,WC        LOAD NEW (DUMMY) CODE BLK POINTER
       MOV  WC,R_COD         SET AS CODE BLOCK POINTER
       LCP  WC               AND NEW CODE POINTER
       EJC
*
*      TRXEQ (CONTINUED)
*
*      NOW PREPARE ARGUMENTS FOR FUNCTION
*
       MOV  WA,WB            SAVE NAME OFFSET
       MOV  *NMSI_,WA        LOAD NMBLK SIZE
       JSR  ALLOC            ALLOCATE SPACE FOR NMBLK
       MOV  =B_NML,(XR)      SET TYPE WORD
       MOV  XL,NMBAS(XR)     STORE NAME BASE
       MOV  WB,NMOFS(XR)     STORE NAME OFFSET
       MOV  6(XS),XL         RELOAD POINTER TO TRBLK
       MOV  XR,-(XS)         STACK NMBLK POINTER (1ST ARGUMENT)
       MOV  TRTAG(XL),-(XS)  STACK TRACE TAG (2ND ARGUMENT)
       MOV  TRFNC(XL),XL     LOAD TRACE VRBLK POINTER
       MOV  VRFNC(XL),XL     LOAD TRACE FUNCTION POINTER
       BEQ  XL,=STNDF,TRXQ2  JUMP IF NOT A DEFINED FUNCTION
       MOV  =NUM02,WA        SET NUMBER OF ARGUMENTS TO TWO
       BRN  CFUNC            JUMP TO CALL FUNCTION
*
*      SEE O_TXR FOR DETAILS OF RETURN TO THIS POINT
*
TRXQ1  MOV  FLPTR,XS         POINT BACK TO OUR STACK ENTRIES
       ICA  XS               POP OFF GARBAGE FAIL OFFSET
       MOV  (XS)+,FLPTR      RESTORE OLD FAILURE POINTER
       MOV  (XS)+,WB         RELOAD CODE OFFSET
       MOV  (XS)+,WC         LOAD OLD CODE BASE POINTER
       MOV  WC,XR            COPY CDBLK POINTER
       MOV  CDSTM(XR),KVSTN  RESTORE STMNT NO
       MOV  (XS)+,WA         RELOAD NAME OFFSET
       MOV  (XS)+,XL         RELOAD NAME BASE
       MOV  (XS)+,XR         RELOAD TRBLK POINTER
       MOV  (XS)+,KVTRA      RESTORE TRACE KEYWORD VALUE
       ADD  WC,WB            RECOMPUTE ABSOLUTE CODE POINTER
       LCP  WB               RESTORE CODE POINTER
       MOV  WC,R_COD         AND CODE BLOCK POINTER
       EXI                   RETURN TO TRXEQ CALLER
*
*      HERE IF THE TARGET FUNCTION IS NOT DEFINED
*
TRXQ2  ERB  197,TRACE fourth arg is not function name or null
*
       ENP                   END PROCEDURE TRXEQ
       EJC
*
*      XSCAN -- EXECUTION FUNCTION ARGUMENT SCAN
*
*      XSCAN SCANS OUT ONE TOKEN IN A PROTOTYPE ARGUMENT IN
*      ARRAY,CLEAR,DATA,DEFINE,LOAD FUNCTION CALLS. XSCAN
*      CALLS MUST BE PRECEDED BY A CALL TO THE INITIALIZATION
*      PROCEDURE XSCNI. THE FOLLOWING VARIABLES ARE USED.
*
*      R_XSC                 POINTER TO SCBLK FOR FUNCTION ARG
*      XSOFS                 OFFSET (NUM CHARS SCANNED SO FAR)
*
*      (WA)                  NON-ZERO TO SKIP AND TRIM BLANKS
*      (WC)                  DELIMITER ONE (CH_XX)
*      (XL)                  DELIMITER TWO (CH_XX)
*      JSR  XSCAN            CALL TO SCAN NEXT ITEM
*      (XR)                  POINTER TO SCBLK FOR TOKEN SCANNED
*      (WA)                  COMPLETION CODE (SEE BELOW)
*      (WC,XL)               DESTROYED
*
*      THE SCAN STARTS FROM THE CURRENT POSITION AND CONTINUES
*      UNTIL ONE OF THE FOLLOWING THREE CONDITIONS OCCURS.
*
*      1)   DELIMITER ONE IS ENCOUNTERED  (WA SET TO 1)
*
*      2)   DELIMITER TWO ENCOUNTERED  (WA SET TO 2)
*
*      3)   END OF STRING ENCOUNTERED  (WA SET TO 0)
*
*      THE RESULT IS A STRING CONTAINING ALL CHARACTERS SCANNED
*      UP TO BUT NOT INCLUDING ANY DELIMITER CHARACTER.
*      THE POINTER IS LEFT POINTING PAST THE DELIMITER.
*
*      IF ONLY ONE DELIMITER IS TO BE DETECTED, DELIMITER ONE
*      AND DELIMITER TWO SHOULD BE SET TO THE SAME VALUE.
*
*      IN THE CASE WHERE THE END OF STRING IS ENCOUNTERED, THE
*      STRING INCLUDES ALL THE CHARACTERS TO THE END OF THE
*      STRING. NO FURTHER CALLS CAN BE MADE TO XSCAN UNTIL
*      XSCNI IS CALLED TO INITIALIZE A NEW ARGUMENT SCAN
       EJC
*
*      XSCAN (CONTINUED)
*
XSCAN  PRC  E,0              ENTRY POINT
       MOV  WB,XSCWB         PRESERVE WB
       MOV  WA,-(XS)         RECORD BLANK SKIP FLAG
       MOV  WA,-(XS)         AND SECOND COPY
       MOV  R_XSC,XR         POINT TO ARGUMENT STRING
       MOV  SCLEN(XR),WA     LOAD STRING LENGTH
       MOV  XSOFS,WB         LOAD CURRENT OFFSET
       SUB  WB,WA            GET NUMBER OF REMAINING CHARACTERS
       BZE  WA,XSCN3         JUMP IF NO CHARACTERS LEFT
       PLC  XR,WB            POINT TO CURRENT CHARACTER
*
*      LOOP TO SEARCH FOR DELIMITER
*
XSCN1  LCH  WB,(XR)+         LOAD NEXT CHARACTER
       BEQ  WB,WC,XSCN4      JUMP IF DELIMITER ONE FOUND
       BEQ  WB,XL,XSCN5      JUMP IF DELIMITER TWO FOUND
       BZE  (XS),XSCN2       JUMP IF NOT SKIPPING BLANKS
       ICV  XSOFS            ASSUME BLANK AND DELETE IT
.IF    .CAHT
       BEQ  WB,=CH_HT,XSCN2  JUMP IF HORIZONTAL TAB
.FI
.IF    .CAVT
       BEQ  WB,=CH_VT,XSCN2  JUMP IF VERTICAL TAB
.FI
       BEQ  WB,=CH_BL,XSCN2  JUMP IF BLANK
       DCV  XSOFS            UNDELETE NON-BLANK CHARACTER
       ZER  (XS)             AND DISCONTINUE BLANK CHECKING
*
*      HERE AFTER PERFORMING ANY LEADING BLANK TRIMMING.
*
XSCN2  DCV  WA               DECREMENT COUNT OF CHARS LEFT
       BNZ  WA,XSCN1         LOOP BACK IF MORE CHARS TO GO
*
*      HERE FOR RUNOUT
*
XSCN3  MOV  R_XSC,XL         POINT TO STRING BLOCK
       MOV  SCLEN(XL),WA     GET STRING LENGTH
       MOV  XSOFS,WB         LOAD OFFSET
       SUB  WB,WA            GET SUBSTRING LENGTH
       ZER  R_XSC            CLEAR STRING PTR FOR COLLECTOR
       ZER  XSCRT            SET ZERO (RUNOUT) RETURN CODE
       BRN  XSCN7            JUMP TO EXIT
       EJC
*
*      XSCAN (CONTINUED)
*
*      HERE IF DELIMITER ONE FOUND
*
XSCN4  MOV  =NUM01,XSCRT     SET RETURN CODE
       BRN  XSCN6            JUMP TO MERGE
*
*      HERE IF DELIMITER TWO FOUND
*
XSCN5  MOV  =NUM02,XSCRT     SET RETURN CODE
*
*      MERGE HERE AFTER DETECTING A DELIMITER
*
XSCN6  MOV  R_XSC,XL         RELOAD POINTER TO STRING
       MOV  SCLEN(XL),WC     GET ORIGINAL LENGTH OF STRING
       SUB  WA,WC            MINUS CHARS LEFT = CHARS SCANNED
       MOV  WC,WA            MOVE TO REG FOR SBSTR
       MOV  XSOFS,WB         SET OFFSET
       SUB  WB,WA            COMPUTE LENGTH FOR SBSTR
       ICV  WC               ADJUST NEW CURSOR PAST DELIMITER
       MOV  WC,XSOFS         STORE NEW OFFSET
*
*      COMMON EXIT POINT
*
XSCN7  ZER  XR               CLEAR GARBAGE CHARACTER PTR IN XR
       JSR  SBSTR            BUILD SUB-STRING
       ICA  XS               REMOVE COPY OF BLANK FLAG
       MOV  (XS)+,WB         ORIGINAL BLANK SKIP/TRIM FLAG
       BZE  SCLEN(XR),XSCN8  CANNOT TRIM THE NULL STRING
       JSR  TRIMR            TRIM TRAILING BLANKS IF REQUESTED
*
*      FINAL EXIT POINT
*
XSCN8  MOV  XSCRT,WA         LOAD RETURN CODE
       MOV  XSCWB,WB         RESTORE WB
       EXI                   RETURN TO XSCAN CALLER
       ENP                   END PROCEDURE XSCAN
       EJC
*
*      XSCNI -- EXECUTION FUNCTION ARGUMENT SCAN
*
*      XSCNI INITIALIZES THE SCAN USED FOR PROTOTYPE ARGUMENTS
*      IN THE CLEAR, DEFINE, LOAD, DATA, ARRAY FUNCTIONS. SEE
*      XSCAN FOR THE PROCEDURE WHICH IS USED AFTER THIS CALL.
*
*      -(XS)                 ARGUMENT TO BE SCANNED (ON STACK)
*      JSR  XSCNI            CALL TO SCAN ARGUMENT
*      PPM  LOC              TRANSFER LOC IF ARG IS NOT STRING
*      PPM  LOC              TRANSFER LOC IF ARGUMENT IS NULL
*      (XS)                  POPPED
*      (XR,R_XSC)            ARGUMENT (SCBLK PTR)
*      (WA)                  ARGUMENT LENGTH
*      (IA,RA)               DESTROYED
*
XSCNI  PRC  N,2              ENTRY POINT
       JSR  GTSTG            FETCH ARGUMENT AS STRING
       PPM  XSCI1            JUMP IF NOT CONVERTIBLE
       MOV  XR,R_XSC         ELSE STORE SCBLK PTR FOR XSCAN
       ZER  XSOFS            SET OFFSET TO ZERO
       BZE  WA,XSCI2         JUMP IF NULL STRING
       EXI                   RETURN TO XSCNI CALLER
*
*      HERE IF ARGUMENT IS NOT A STRING
*
XSCI1  EXI  1                TAKE NOT-STRING ERROR EXIT
*
*      HERE FOR NULL STRING
*
XSCI2  EXI  2                TAKE NULL-STRING ERROR EXIT
       ENP                   END PROCEDURE XSCNI
       TTL  S P I T B O L -- STACK OVERFLOW SECTION
*
*      CONTROL COMES HERE IF THE MAIN STACK OVERFLOWS
*
       SEC                   START OF STACK OVERFLOW SECTION
*
       ADD  =NUM04,ERRFT     FORCE CONCLUSIVE FATAL ERROR
       MOV  FLPTR,XS         POP STACK TO AVOID MORE FAILS
       BNZ  GBCFL,STAK1      JUMP IF GARBAGE COLLECTING
       ERB  246,Stack overflow
*
*      NO CHANCE OF RECOVERY IN MID GARBAGE COLLECTION
*
STAK1  MOV  =ENDSO,XR        POINT TO MESSAGE
       ZER  KVDMP            MEMORY IS UNDUMPABLE
       BRN  STOPR            GIVE UP
       TTL  S P I T B O L -- ERROR SECTION
*
*      THIS SECTION OF CODE IS ENTERED WHENEVER A PROCEDURE
*      RETURN VIA AN ERR PARAMETER OR AN ERB OPCODE IS OBEYED.
*
*      (WA)                  IS THE ERROR CODE
*
*      THE GLOBAL VARIABLE STAGE INDICATES THE POINT AT WHICH
*      THE ERROR OCCURED AS FOLLOWS.
*
*      STAGE=STGIC           ERROR DURING INITIAL COMPILE
*
*      STAGE=STGXC           ERROR DURING COMPILE AT EXECUTE
*                            TIME (CODE, CONVERT FUNCTION CALLS)
*
*      STAGE=STGEV           ERROR DURING COMPILATION OF
*                            EXPRESSION AT EXECUTION TIME
*                            (EVAL, CONVERT FUNCTION CALL).
*
*      STAGE=STGXT           ERROR AT EXECUTE TIME. COMPILER
*                            NOT ACTIVE.
*
*      STAGE=STGCE           ERROR DURING INITIAL COMPILE AFTER
*                            SCANNING OUT THE END LINE.
*
*      STAGE=STGXE           ERROR DURING COMPILE AT EXECUTE
*                            TIME AFTER SCANNING END LINE.
*
*      STAGE=STGEE           ERROR DURING EXPRESSION EVALUATION
*
       SEC                   START OF ERROR SECTION
*
ERROR  BEQ  R_CIM,=CMLAB,CMPLE JUMP IF ERROR IN SCANNING LABEL
       MOV  WA,KVERT         SAVE ERROR CODE
       ZER  SCNRS            RESET RESCAN SWITCH FOR SCANE
       ZER  SCNGO            RESET GOTO SWITCH FOR SCANE
.IF    .CPOL
       MOV  =NUM01,POLCS     RESET POLL COUNT
       MOV  =NUM01,POLCT     RESET POLL COUNT
.FI
       MOV  STAGE,XR         LOAD CURRENT STAGE
       BSW  XR,STGNO         JUMP TO APPROPRIATE ERROR CIRCUIT
       IFF  STGIC,ERR01      INITIAL COMPILE
       IFF  STGXC,ERR04      EXECUTE TIME COMPILE
       IFF  STGEV,ERR04      EVAL COMPILING EXPR.
       IFF  STGEE,ERR04      EVAL EVALUATING EXPR
       IFF  STGXT,ERR05      EXECUTE TIME
       IFF  STGCE,ERR01      COMPILE - AFTER END
       IFF  STGXE,ERR04      XEQ COMPILE-PAST END
       ESW                   END SWITCH ON ERROR TYPE
       EJC
*
*      ERROR DURING INITIAL COMPILE
*
*      THE ERROR MESSAGE IS PRINTED AS PART OF THE COMPILER
*      OUTPUT. THIS PRINTOUT INCLUDES THE OFFENDING LINE (IF NOT
*      PRINTED ALREADY) AND AN ERROR FLAG UNDER THE APPROPRIATE
*      COLUMN AS INDICATED BY SCNSE UNLESS SCNSE IS SET TO ZERO.
*
*      AFTER PRINTING THE MESSAGE, THE GENERATED CODE IS
*      MODIFIED TO AN ERROR CALL AND CONTROL IS RETURNED TO
*      THE CMPIL PROCEDURE AFTER RESETTING THE STACK POINTER.
*
*      IF THE ERROR OCCURS AFTER THE END LINE, CONTROL RETURNS
*      IN A SLIGHTLY DIFFERENT MANNER TO ENSURE PROPER CLEANUP.
*
ERR01  MOV  CMPXS,XS         RESET STACK POINTER
       SSL  CMPSS            RESTORE S-R STACK PTR FOR CMPIL
       BNZ  ERRSP,ERR03      JUMP IF ERROR SUPPRESS FLAG SET
.IF    .CERA
.IF    .CSFN
       MOV  CMPSN,WC         CURRENT STATEMENT
       JSR  FILNM            OBTAIN FILE NAME FOR THIS STATEMENT
.FI
       MOV  SCNSE,WB         COLUMN NUMBER
       MOV  RDCLN,WC         LINE NUMBER
       MOV  STAGE,XR
       JSR  SYSEA            ADVISE SYSTEM OF ERROR
       PPM  ERRA3            IF SYSTEM DOES NOT WANT PRINT
       MOV  XR,-(XS)         SAVE ANY PROVIDED PRINT MESSAGE
.FI
       MOV  ERICH,ERLST      SET FLAG FOR LISTR
       JSR  LISTR            LIST LINE
       JSR  PRTIS            TERMINATE LISTING
       ZER  ERLST            CLEAR LISTR FLAG
       MOV  SCNSE,WA         LOAD SCAN ELEMENT OFFSET
       BZE  WA,ERR02         SKIP IF NOT SET
.IF    .CAHT
       LCT  WB,WA            LOOP COUNTER
       ICV  WA               INCREASE FOR CH_EX
       MOV  R_CIM,XL         POINT TO BAD STATEMENT
       JSR  ALOCS            STRING BLOCK FOR ERROR FLAG
       MOV  XR,WA            REMEMBER STRING PTR
       PSC  XR               READY FOR CHARACTER STORING
       PLC  XL               READY TO GET CHARS
*
*      LOOP TO REPLACE ALL CHARS BUT TABS BY BLANKS
*
ERRA1  LCH  WC,(XL)+         GET NEXT CHAR
       BEQ  WC,=CH_HT,ERRA2  SKIP IF TAB
       MOV  =CH_BL,WC        GET A BLANK
       EJC
*
*      MERGE TO STORE BLANK OR TAB IN ERROR LINE
*
ERRA2  SCH  WC,(XR)+         STORE CHAR
       BCT  WB,ERRA1         LOOP
       MOV  =CH_EX,XL        EXCLAMATION MARK
       SCH  XL,(XR)          STORE AT END OF ERROR LINE
       CSC  XR               END OF SCH LOOP
       MOV  =STNPD,PROFS     ALLOW FOR STATEMENT NUMBER
       MOV  WA,XR            POINT TO ERROR LINE
       JSR  PRTST            PRINT ERROR LINE
.ELSE
       MTI  PRLEN            GET PRINT BUFFER LENGTH
       MFI  GTNSI            STORE AS SIGNED INTEGER
       ADD  =STNPD,WA        ADJUST FOR STATEMENT NUMBER
       MTI  WA               COPY TO INTEGER ACCUMULATOR
       RMI  GTNSI            REMAINDER MODULO PRINT BFR LENGTH
       STI  PROFS            USE AS CHARACTER OFFSET
       MOV  =CH_EX,WA        GET EXCLAMATION MARK
       JSR  PRTCH            GENERATE UNDER BAD COLUMN
.FI
*
*      HERE AFTER PLACING ERROR FLAG AS REQUIRED
*
ERR02  JSR  PRTIS            PRINT BLANK LINE
.IF    .CERA
       MOV  (XS)+,XR         RESTORE ANY SYSEA MESSAGE
       BZE  XR,ERRA0         DID SYSEA PROVIDE MESSAGE TO PRINT
       JSR  PRTST            PRINT SYSEA MESSAGE
.FI
ERRA0  JSR  ERMSG            GENERATE FLAG AND ERROR MESSAGE
       ADD  =NUM03,LSTLC     BUMP PAGE CTR FOR BLANK, ERROR, BLK
ERRA3  ZER  XR               IN CASE OF FATAL ERROR
       BHI  ERRFT,=NUM03,STOPR PACK UP IF SEVERAL FATALS
*
*      COUNT ERROR, INHIBIT EXECUTION IF REQUIRED
*
       ICV  CMERC            BUMP ERROR COUNT
       ADD  CSWER,NOXEQ      INHIBIT XEQ IF -NOERRORS
       BNE  STAGE,=STGIC,CMP10  SPECIAL RETURN IF AFTER END LINE
       EJC
*
*      LOOP TO SCAN TO END OF STATEMENT
*
ERR03  MOV  R_CIM,XR         POINT TO START OF IMAGE
       PLC  XR               POINT TO FIRST CHAR
       LCH  XR,(XR)          GET FIRST CHAR
       BEQ  XR,=CH_MN,CMPCE  JUMP IF ERROR IN CONTROL CARD
       ZER  SCNRS            CLEAR RESCAN FLAG
       MNZ  ERRSP            SET ERROR SUPPRESS FLAG
       JSR  SCANE            SCAN NEXT ELEMENT
       BNE  XL,=T_SMC,ERR03  LOOP BACK IF NOT STATEMENT END
       ZER  ERRSP            CLEAR ERROR SUPPRESS FLAG
*
*      GENERATE ERROR CALL IN CODE AND RETURN TO CMPIL
*
       MOV  *CDCOD,CWCOF     RESET OFFSET IN CCBLK
       MOV  =OCER_,WA        LOAD COMPILE ERROR CALL
       JSR  CDWRD            GENERATE IT
       MOV  CWCOF,CMSOC(XS)  SET SUCCESS FILL IN OFFSET
       MNZ  CMFFC(XS)        SET FAILURE FILL IN FLAG
       JSR  CDWRD            GENERATE SUCC. FILL IN WORD
       BRN  CMPSE            MERGE TO GENERATE ERROR AS CDFAL
*
*      ERROR DURING EXECUTE TIME COMPILE OR EXPRESSION EVALUATIO
*
*      EXECUTE TIME COMPILATION IS INITIATED THROUGH GTCOD OR
*      GTEXP WHICH ARE CALLED BY COMPILE, CODE OR EVAL.
*      BEFORE CAUSING STATEMENT FAILURE THROUGH EXFAL IT IS
*      HELPFUL TO SET KEYWORD ERRTEXT AND FOR GENERALITY
*      THESE ERRORS MAY BE HANDLED BY THE SETEXIT MECHANISM.
*
ERR04  BGE  ERRFT,=NUM03,LABO1 ABORT IF TOO MANY FATAL ERRORS
.IF    .CPOL
       BEQ  KVERT,=NM320,ERR06 TREAT USER INTERRUPT SPECIALLY
.FI
       ZER  R_CCB            FORGET GARBAGE CODE BLOCK
       MOV  *CCCOD,CWCOF     SET INITIAL OFFSET (MBE CATSPAW)
       SSL  INISS            RESTORE MAIN PROG S-R STACK PTR
       JSR  ERTEX            GET FAIL MESSAGE TEXT
       DCA  XS               ENSURE STACK OK ON LOOP START
*
*      POP STACK UNTIL FIND FLPTR FOR MOST DEEPLY NESTED PROG.
*      DEFINED FUNCTION CALL OR CALL OF EVAL / CODE.
*
ERRA4  ICA  XS               POP STACK
       BEQ  XS,FLPRT,ERRC4   JUMP IF PROG DEFINED FN CALL FOUND
       BNE  XS,GTCEF,ERRA4   LOOP IF NOT EVAL OR CODE CALL YET
       MOV  =STGXT,STAGE     RE-SET STAGE FOR EXECUTE
       MOV  R_GTC,R_COD      RECOVER CODE PTR
       MOV  XS,FLPTR         RESTORE FAIL POINTER
       ZER  R_CIM            FORGET POSSIBLE IMAGE
.IF    .CINC
       ZER  CNIND            FORGET POSSIBLE INCLUDE
.FI
*
*      TEST ERRLIMIT
*
ERRB4  BNZ  KVERL,ERR07      JUMP IF ERRLIMIT NON-ZERO
       BRN  EXFAL            FAIL
*
*      RETURN FROM PROG. DEFINED FUNCTION IS OUTSTANDING
*
ERRC4  MOV  FLPTR,XS         RESTORE STACK FROM FLPTR
       BRN  ERRB4            MERGE
       EJC
*
*      ERROR AT EXECUTE TIME.
*
*      THE ACTION TAKEN ON AN ERROR IS AS FOLLOWS.
*
*      IF ERRLIMIT KEYWORD IS ZERO, AN ABORT IS SIGNALLED,
*      SEE CODING FOR SYSTEM LABEL ABORT AT L_ABO.
*
*      OTHERWISE, ERRLIMIT IS DECREMENTED AND AN ERRTYPE TRACE
*      GENERATED IF REQUIRED. CONTROL RETURNS EITHER VIA A JUMP
*      TO CONTINUE (TO TAKE THE FAILURE EXIT) OR A SPECIFIED
*      SETEXIT TRAP IS EXECUTED AND CONTROL PASSES TO THE TRAP.
*      IF 3 OR MORE FATAL ERRORS OCCUR AN ABORT IS SIGNALLED
*      REGARDLESS OF ERRLIMIT AND SETEXIT - LOOPING IS ALL TOO
*      PROBABLE OTHERWISE. FATAL ERRORS INCLUDE STACK OVERFLOW
*      AND EXCEEDING STLIMIT.
*
ERR05  SSL  INISS            RESTORE MAIN PROG S-R STACK PTR
       BNZ  DMVCH,ERR08      JUMP IF IN MID-DUMP
*
*      MERGE HERE FROM ERR08 AND ERR04 (ERROR 320)
*
ERR06  BZE  KVERL,LABO1      ABORT IF ERRLIMIT IS ZERO
       JSR  ERTEX            GET FAIL MESSAGE TEXT
*
*      MERGE FROM ERR04
*
ERR07  BGE  ERRFT,=NUM03,LABO1 ABORT IF TOO MANY FATAL ERRORS
       DCV  KVERL            DECREMENT ERRLIMIT
       MOV  R_ERT,XL         LOAD ERRTYPE TRACE POINTER
       JSR  KTREX            GENERATE ERRTYPE TRACE IF REQUIRED
       MOV  R_COD,WA         GET CURRENT CODE BLOCK
       MOV  WA,R_CNT         SET CDBLK PTR FOR CONTINUATION
       SCP  WB               CURRENT CODE POINTER
       SUB  WA,WB            OFFSET WITHIN CODE BLOCK
       MOV  WB,STXOC         SAVE CODE PTR OFFSET FOR SCONTINUE
       MOV  FLPTR,XR         SET PTR TO FAILURE OFFSET
       MOV  (XR),STXOF       SAVE FAILURE OFFSET FOR CONTINUE
       MOV  R_SXC,XR         LOAD SETEXIT CDBLK POINTER
       BZE  XR,LCNT1         CONTINUE IF NO SETEXIT TRAP
       ZER  R_SXC            ELSE RESET TRAP
       MOV  =NULLS,STXVR     RESET SETEXIT ARG TO NULL
       MOV  (XR),XL          LOAD PTR TO CODE BLOCK ROUTINE
       BRI  XL               EXECUTE FIRST TRAP STATEMENT
*
*      INTERRUPTED PARTLY THROUGH A DUMP WHILST STORE IS IN A
*      MESS SO DO A TIDY UP OPERATION. SEE DUMPR FOR DETAILS.
*
ERR08  MOV  DMVCH,XR         CHAIN HEAD FOR AFFECTED VRBLKS
       BZE  XR,ERR06         DONE IF ZERO
       MOV  (XR),DMVCH       SET NEXT LINK AS CHAIN HEAD
       JSR  SETVR            RESTORE VRGET FIELD
*
*      LABEL TO MARK END OF CODE
*
S_YYY  BRN  ERR08            LOOP THROUGH CHAIN
       TTL  S P I T B O L -- HERE ENDETH THE CODE
*
*      END OF ASSEMBLY
*
       END                   END MACRO-SPITBOL ASSEMBLY
